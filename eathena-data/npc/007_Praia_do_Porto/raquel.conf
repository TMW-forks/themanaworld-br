// This NPC makes potions and dyes
// Tradução por Fafinha

007.gat,179,25,0	script	Raquel	103,{

	// Setzer quest/monster oil quest participation
	set @SETZER_INITIAL, 0;
	set @SETZER_KNOWS_OIL, 1;
	set @SETZER_KNOWS_STINGER, 2;

	set @Q_MASK, NIBBLE_3_MASK;
	set @Q_SHIFT, NIBBLE_3_SHIFT;

	set @wants_sulphur, (QUEST_MAGIC & (NIBBLE_6_MASK | NIBBLE_7_MASK));
	set @wants_ironpowder, (((QUEST_MAGIC & (NIBBLE_6_MASK | NIBBLE_7_MASK)) >> NIBBLE_6_SHIFT) >= 7);
	set @Q_status, (QUEST_Forestbow_state & @Q_MASK) >> @Q_SHIFT;
	// End of Setzer quest/monster oil quest participation

	set @QUEST_CAN_GET_DYE_HERE, 2;
	set @QUEST_HAS_SOLVED_DYE_PUZZLE, 3;
	set @QUEST_GUESS_BASE, 4;

	set @CHOICE_IRON, 0;
	set @CHOICE_CONCENTRATION, 1;
	set @CHOICE_DYEING_INTRO, 2;
	set @CHOICE_DYE, 3;
	set @CHOICE_MONSTER_OIL, 4;
	set @CHOICE_MANA_POTION, 5;
	set @CHOICE_SULPHUR_POWDER, 6;
	set @CHOICE_IRON_POWDER, 7;
	set @CHOICE_ABORT, 8;

	setarray @menuitems$, "", "", "", "", "", "", "", "";
	set @c, 0;

	set @menuitems$[@c], "Poção Energética.";
	set @menuID[@c], @CHOICE_IRON;
	set @c, @c + 1;

	set @menuitems$[@c], "Poção de Concentração.";
	set @menuID[@c], @CHOICE_CONCENTRATION;
	set @c, @c + 1;

	if (!(MAGIC_FLAGS & MFLAG_KNOWS_MANAPOTION)) goto L_post_mana_potion_c;
	set @menuitems$[@c], "Poção de Mana.";
	set @menuID[@c], @CHOICE_MANA_POTION;
	set @c, @c + 1;

L_post_mana_potion_c:
	if (QUEST_clothdyer_knowsdye >= @QUEST_CAN_GET_DYE_HERE) goto L_skip_introducing;

	mes "[Raquel a Alquimista]";
	mes "\"Estou aprendendo a antiga ciência da alquimia.";
	mes "Eu já sei como criar algumas poções!!\"";
	next;

	mes "[Raquel a Alquimista]";
	mes "\"Você gostaria que eu criasse uma para você?\"";
	next;

	if (QUEST_clothdyer_knowsdye == 1) goto L_pre_dyeing;

	goto L_certain_condition;

L_skip_introducing:
	mes "[Raquel a Alquimista]";
	mes "\"olá, bem vindo de volta! Você está interessado em mais tintas ou em uma das minhas poções?\"";
	next;
	set @menuitems$[@c], "Tintas, por favor.";
	set @menuID[@c], @CHOICE_DYE;
	set @c, @c + 1;
	goto L_certain_condition;

L_pre_dyeing:
	set @menuitems$[@c], "Você pode mesmo fazer tintas?";
	set @menuID[@c], @CHOICE_DYEING_INTRO;
	set @c, @c + 1;

L_certain_condition:
	if (!@wants_sulphur) goto L_post_sulphur_option;
	set @menuitems$[@c], "Você pode fazer o Pó de Enxofre?";
	set @menuID[@c], @CHOICE_SULPHUR_POWDER;
	set @c, @c + 1;

L_post_sulphur_option:
	if (!@wants_ironpowder) goto L_post_ironpowder_option;
	set @menuitems$[@c], "Você pode fazer o Pó de Ferro?";
	set @menuID[@c], @CHOICE_IRON_POWDER;
	set @c, @c + 1;

L_post_ironpowder_option:
	if (@Q_status == @SETZER_INITIAL) goto L_post_monster_oil;
	set @menuitems$[@c], "Você pode fazer o [Poção de Óleo de Monstro]?";
	set @menuID[@c], @CHOICE_MONSTER_OIL;
	set @c, @c + 1;

L_post_monster_oil:
	set @menuitems$[@c], "Não, obrigado.";
	set @menuID[@c], @CHOICE_ABORT;
	set @c, @c + 1;

L_main_menu:
	menu
		@menuitems$[0], -,
		@menuitems$[1], -,
		@menuitems$[2], -,
		@menuitems$[3], -,
		@menuitems$[4], -,
		@menuitems$[5], -,
		@menuitems$[6], -,
		@menuitems$[7], -;

	set @menu, @menu - 1;

	if (@menu >= @c) goto L_abort;
	if (@menuID[@menu] == @CHOICE_IRON) goto L_iron;
	if (@menuID[@menu] == @CHOICE_CONCENTRATION) goto L_concentration;
	if (@menuID[@menu] == @CHOICE_DYEING_INTRO) goto L_dyeing_intro;
	if (@menuID[@menu] == @CHOICE_DYE) goto L_pick_colour;
	if (@menuID[@menu] == @CHOICE_MONSTER_OIL) goto L_monster_oil;
	if (@menuID[@menu] == @CHOICE_MANA_POTION) goto L_mana_potion;
	if (@menuID[@menu] == @CHOICE_SULPHUR_POWDER) goto L_sulphur;
	if (@menuID[@menu] == @CHOICE_IRON_POWDER) goto L_iron_powder;
	if (@menuID[@menu] == @CHOICE_ABORT) goto L_abort;
	goto L_abort;

//---------------------------------------------------------------------

L_iron_powder:
	mes "[Raquel a Alquimista]";
	mes "\"Ferro em pó? Hmm, eu posso extrair um pouco de pó de ferro a partir de um pedaço de minério de ferro, mas para isso vou precisar dissolvê-lo. Se você me der um pedaço de minério de ferro e 100 GP para o ácido, eu faço para você.\"";
	next;
	menu
		"Não se preocupe.", L_abort,
		"Aqui está!", -;

	if (countitem("IronOre") < 1) goto L_iron_powder_noore;
	if (zeny < 100)
		goto L_iron_powder_nozeny;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem("IronOre") > 1 && countitem("IronPowder") < 1)
		goto L_iron_powder_full;
	delitem "IronOre", 1;
	set zeny, zeny - 100;
	getitem "IronPowder", 4;
	mes "[Raquel a Alquimista]";
	mes "Raquel coloca o pedaço de minério em um recipiente de vidro estranho e então derrama o ácido sobre ele. Diante de seus olhos, o minério se dissolve.";
	next;

	mes "[Raquel a Alquimista]";
	mes "Raquel derrama outro líquido sobre a mistura, em seguida despeja a mistura sobre um pedaço de pano estendido sobre um copo. Ele remove o pó que se prendeu ao pano e o mostra em suas mãos.";
	mes "\"Isto é o melhor que posso fazer sem ajuda mecânica.\"";
	close;

L_iron_powder_noore:
	mes "[Raquel a Alquimista]";
	mes "\"Eu preciso de um pedaço de minério de ferro. Por favor, volte quando tiver um.\"";
	close;

L_iron_powder_nozeny:
	mes "[Raquel a Alquimista]";
	mes "\"Me desculpe, mas devo pedir que me pague 100 GP, o ácido que uso não sai de graça.\"";
	close;

L_iron_powder_full:
	mes "[Raquel a Alquimista]";
	mes "\"Me parece que seu inventário está cheio. Volte quando tiver espaço.\"";
	close;

//---------------------------------------------------------------------

L_sulphur:
	mes "[Raquel a Alquimista]";
	mes "\"Oh, isso é fácil. Me traga 3 pilhas de cinzas e 3 folhas de malva e eu vou extrair 12 pilhas de enxofre para você.\"";
	next;
	menu
		"Eu voltarei.", L_abort,
		"Não, eu não vou!", L_abort,
		"Aqui está!", -;

	if (countitem("PileOfAsh") < 3) goto L_sulphur_noash;
	if (countitem("MauveHerb") < 3) goto L_sulphur_nomauve;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem("SulphurPowder") == 0 && countitem("PileOfAsh") > 3 && countitem("MauveHerb") > 3)
		goto L_sulphur_nospace;

	delitem "PileOfAsh", 3;
	delitem "MauveHerb", 3;
	getitem "SulphurPowder", 12;
	mes "[Raquel a Alquimista]";
	mes "Você vê Raquel queimar as folhas de malva em meio às cinzas. Ela então dissolve o resultado e o filtra, finalmente ela aquece a mistura.";
	next;
	mes "[Raquel a Alquimista]";
	mes "Após a água ter evaporado, resta apenas um pó amarelado. Raquel o pega com cuidado.";
	mes "\"Isso deve ser precisamente 12 gramas de pó de enxofre.\"";
	close;

L_sulphur_noash:
	mes "[Raquel a Alquimista]";
	mes "\"Você não parece ter as 3 pilhas de cinzas.\"";
	close;

L_sulphur_nomauve:
	mes "[Raquel a Alquimista]";
	mes "\"Você não parece ter as 3 folhas de malva que pedi.\"";
	close;

L_sulphur_nospace:
	mes "[Raquel a Alquimista]";
	mes "\"Oh, querido. Me parece que você não tem espaço para carregar o pó de enxofre.\"";
	close;

//---------------------------------------------------------------------

L_mana_potion:
	mes "[Raquel a Alquimista]";
	mes "\"Humh... Eu acho que tenho a receita em algum lugar. Espere um momento...\"";
	mes "Raquel pega um pequeno livro em sua manga e o folheia.";
	mes "\"Aha, aqui está. Hmm, eu não estou convencido de que essa é a melhor receita...\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Vou precisar de uma pérola, uma garrafa de água, de 30 folhas de malva, 20 folhas de gamboge e 5 pilhas de cinzas.\"";
	next;

	menu
		"Aqui está!", L_get_mana_potion,
		 "Nem pensar.", L_main_menu;

	goto L_main_menu;

L_get_mana_potion:
	if(countitem("MauveHerb") < 30) goto L_mana_potion_lacking;
	if(countitem("GambogeHerb") < 20) goto L_mana_potion_lacking;
	if(countitem("PileOfAsh") < 5) goto L_mana_potion_lacking;
	if(countitem("Pearl") < 1) goto L_mana_potion_lacking;
	if(countitem("BottleOfWater") < 1) goto L_mana_potion_lacking;
	getinventorylist;
	if ((@inventorylist_count == 100)
		&& (countitem("MauveHerb") > 30)
		&& (countitem("PileOfAsh") > 5)
		&& (countitem("Pearl") > 1)
		&& (countitem("BottleOfWater") > 1)
		&& (countitem("ManaPotion") < 1))
		goto L_mana_potion_toomuch;

	delitem "MauveHerb", 30;
	delitem "GambogeHerb", 20;
	delitem "PileOfAsh", 5;
	delitem "Pearl", 1;
	delitem "BottleOfWater", 1;
	getitem "ManaPotion", 1;

	mes "[Raquel a Alquimista]";
	mes "Raquel tritura as folhas, em seguida, esmaga a pérola. Pegando um pequeno forno de carvão vegetal, ela aquece a garrafa de água e dissolve o pó de pérola, em seguida, as cinzas.";
	next;

	mes "[Raquel a Alquimista]";
	mes "Pegando a garrafa com um par de cinzas, ela derrama o líquido através de uma fina peneira, descartando o líquido";
	mes "Em seguida ela mistura a massa que ficou com as folhas, mistura a um pouco de água e coloca o resultado em um frasco separado.";
	next;

	mes "[Raquel a Alquimista]";
	mes "\"Aqui está. Espero que seja aquilo que você queria.\"";
	next;

	goto L_main_menu;

L_mana_potion_toomuch:
	mes "[Raquel a Alquimista]";
	mes "\"Você parece carregar bastante peso. Você não prefere se livrar de algumas coisas e voltar depois?\"";
	next;
	goto L_main_menu;

L_mana_potion_lacking:
	mes "[Raquel a Alquimista]";
	mes "\"Desculpe, mas eu preciso de 1[pearl], 1[bottle of water], 30[mauve leaves], 20[gamboge leaves] e 5[volcanic ash].\"";
	next;
	goto L_main_menu;

//-[ Poção Energética ]------------------------------------------------

L_iron:
	if(countitem("SmallMushroom") < 20) goto L_no_iron;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem("SmallMushroom") > 20) goto L_TooMany;
	mes "[Raquel a Alquimista]";
	mes "\"Ótimo! Você me trouxe exatamente o que preciso!";
	mes "Aqui está sua [Poção Energética].\"";
	delitem "SmallMushroom", 20;
	getitem "IronPotion", 1;
	close;

L_no_iron:
	mes "[Raquel a Alquimista]";
	mes "\"Você tem que me trazer 20 [small mushrooms].\"";
	close;

//-[ Poção de Concentração ]-------------------------------------------

L_concentration:
	if(countitem("Petal") < 20) goto L_no_concentration;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem("Petal") > 20) goto L_TooMany;
	mes "[Raquel a Alquimista]";
	mes "\"Ótimo! Você me trouxe exatamente o que preciso!";
	mes "Aqui está sua [Poção de Concentração].\"";
	delitem "Petal", 20;
	getitem "ConcentrationPotion", 1;
	close;

L_no_concentration:
	mes "[Raquel a Alquimista]";
	mes "\"Você tem que me trazer 20 [petals].\"";
	close;

//---------------------------------------------------------------------

L_dyeing_intro:
	mes "[Raquel a Alquimista]";
	mes "Raquel para por um instante.";
	mes "\"Hmm, Eu não sou muito bom nisso ... Só posso fazer tintas simples, daquelas que se usam em algodão e lã.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Para as tintas realmente boas eu precisaria de um poderoso catalizador, como um Spork Obsidian ou um Wumpus Egg...\"";
	mes "Raquel parece viajar em seus pensamentos.";
	next;
	menu
		"Eu só quero pintar roupas, embora...", -,
		"O que é um 'Wumpus Egg'?", L_wumpus_egg_intro,
		"O que é um 'Obsidian Spork'?", L_obsidian_spork_intro,
		"Er, não faz mal...", L_abort;
	mes "[Raquel a Alquimista]";
	mes "\"Oh, apenas para roupas? Isso é fácil. Eu posso fazer nas cores vermelhor, amarelo, azul, laranja, rosa, verde, azul escuro, preto e roxo, desde que você me consiga os ingredientes para isso.\"";
	next;
	set QUEST_clothdyer_knowsdye, 2;
	goto L_pick_colour;

L_wumpus_egg_intro:
	mes "[Raquel a Alquimista]";
	mes "Os olhos de Raquel brilham.";
	mes "\"Ah, o Wumpus Egg é um dos catalizadores mais poderosos que existe. Infelizmente o Wumpus está praticamente extinto atualmente, dessa forma ele é muito raro.";
	mes "Se você encontrar algum, por favor me avise. Pagarei muito bem por ele!\"";
	close;

L_obsidian_spork_intro:
	mes "[Raquel a Alquimista]";
	mes "Raquel coça a cabeça.";
	mes "\"Hmm, obsidian é um material escuro parecido com vidro, normalmente encontrado perto de vulcões. Moldálo não é muito difícil, mas fazer isso sem que ele perca suas propriedades mágicas é quase impossível.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Obsidian sporks são então muito difíceis de fazer, mas dizem que é muito útil o seu uso na alquimia.";
	mes "Se você encontrar um por favor me avise!\"";
	close;

L_pick_colour:
	menu
		"Quais ingredientes você precisa?", L_description,
		"Vermelho", L_red,
		"Amarelo", L_yellow,
		"Azul claro", L_light_blue,
		"Verde", L_green,
		"Laranja", L_orange,
		"Rosa", L_pink,
		"Azul escuro", L_dark_blue,
		"Preto", L_black,
		"Roxo", L_purple,
		"Verde escuro", L_dark_green,
		"Na verdade nada.", L_abort;
	close;

L_description:
	mes "[Raquel a Alquimista]";
	mes "\"As tintas que conheço são feitas a partir de ervas. Alizarina para o vermelho, gamboge para o amarelo, colbalto para o azul e malva como agente de ligação para as cores escuras.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Algumas das cores precisam de mais ingredientes, incluindo algumas que você não encontra aqui, então eu tenho que cobrar por eles.";
	mes "Oh, e para o azul escuro e o roxo eu também preciso de uma pérola, sem pó de pérola não fica uma cor brilhante.\"";
	next;

L_description_quick:
	menu
		"O que você precisa para a tinta vermelha?", L_d_red,
		"O que você precisa para a tinta amarelo?", L_d_yellow,
		"O que você precisa para a tinta azul claro?", L_d_light_blue,
		"O que você precisa para a tinta verde?", L_d_green,
		"O que você precisa para a tinta laranja?", L_d_orange,
		"O que você precisa para a tinta rosa?", L_d_pink,
		"O que você precisa para a tinta azul escuro?", L_d_dark_blue,
		"O que você precisa para a tinta preto?", L_d_black,
		"O que você precisa para a tinta roxa?", L_d_purple,
		"O que você precisa para a tinta verde escuro?", L_d_dark_green,
		"Atualmente...", -;

	menu
		"Você pode fazer alguma tinta para mim, por favor?", L_pick_colour,
		"Onde consigo ervas?", L_d_herbs,
		"Onde consigo pétalas?", L_d_petals,
		"Onde encontro minério de ferro?", L_d_ore,
		"Onde encontro pérolas?", L_d_pearl,
		"Onde encontro gosma de verme?", L_d_maggot_slime,
		"Estou vendo. Obrigado e um bom dia para você!", L_abort;
	close;

L_d_herbs:
	mes "[Raquel a Alquimista]";
	mes "\"Arbustos de alizarina, mauva, cobalto e gamboge crescem por ai. Eu não tenho visto aqui em volta, mas se você procurar, tenho certeza de que vai encontrar algumas.\"";
	next;
	goto L_description_quick;

L_d_petals:
	mes "[Raquel a Alquimista]";
	mes "\"Basta pegar de algumas flores por ai. Mas cuidado que elas não gostam que toquem nelas.\"";
	next;
	goto L_description_quick;

L_d_ore:
	mes "[Raquel a Alquimista]";
	mes "\"Minério de ferro pode ser encontrado em algumas minas por ai. É onde as pessoas costumam encontrar.\"";
	next;
	goto L_description_quick;

L_d_pearl:
	mes "[Raquel a Alquimista]";
	mes "\"Uma pérola ... hmm, pode ser um pouco complicado. Você só encontra no fundo do mar, mas dizem os piratas possuem algumas.\"";
	next;
	goto L_description_quick;

L_d_maggot_slime:
	mes "[Raquel a Alquimista]";
	mes "\"Como o nome sugere, gosma de verme é mais encontrado a partir de vermes. Existe outras criaturas, como o morcego, que secreta uma gosma semelhante que para a alguimia funciona igual.\"";
	next;
	goto L_description_quick;

L_intermediate:
	menu "Você pode fazer uma tinta para mim?", L_pick_colour,
		"Você pode descrever os ingredientes?", L_description_quick,
		"Nem pensar.", L_abort;
	close;

L_ok:
	mes "[Raquel a Alquimista]";
	mes "\"Aqui está a sua tinta. lembre-se de usar tudo, ou a cor pode ficar desbotada.\"";
	next;
	goto L_pick_colour;

L_red:
	if (countitem("AlizarinHerb") < 10) goto L_red_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem("AlizarinHerb") > 10) goto L_TooMany;
	delitem "AlizarinHerb", 10;
	getitem "RedDye", 1;
	goto L_ok;

L_red_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Hmm, desculpe, mas eu preciso de 10 folhas de alizarina para fazer a tinta vermelha.\"";
	next;
	goto L_intermediate;

L_d_red:
	mes "[Raquel a Alquimista]";
	mes "\"Tinta vermelha é fácil, basta me trazer 10 folhas de alizarina que eu preparo imediatamente.\"";
	next;
	goto L_intermediate;

L_yellow:
	if (countitem("GambogeHerb") < 10) goto L_yellow_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem("GambogeHerb") > 10) goto L_TooMany;
	delitem "GambogeHerb", 10;
	getitem "YellowDye", 1;
	goto L_ok;

L_yellow_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Não há folhas de gamboge o bastante, eu preciso de pelo menos 10. Não posso preparar a tinta para você agora.\"";
	next;
	goto L_intermediate;

L_d_yellow:
	mes "[Raquel a Alquimista]";
	mes "\"Tinta amarela nada mais é que o extrato da folha de gamboge. Se você me conseguir 10 folhas posso facilmente preparar para você.\"";
	next;
	goto L_intermediate;

L_light_blue:
	if (countitem("CobaltHerb") < 10) goto L_light_blue_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem("CobaltHerb") > 10) goto L_TooMany;
	delitem "CobaltHerb", 10;
	getitem "LightBlueDye", 1;
	goto L_ok;

L_light_blue_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Desculpe, mas não posso fazer isso para você por menos de 10 folhas de cobalto.\"";
	next;
	goto L_intermediate;

L_d_light_blue:
	mes "[Raquel a Alquimista]";
	mes "\"Tinta azul claro é o resultado de se deixar 10 folhas de cobalto misturadas em álcool por uma noite. Eu já tenho alguns aqui e posso trocar por 10 folhas de cobalto.\"";
	next;
	goto L_intermediate;

L_green:
	if (countitem("CobaltHerb") < 20) goto L_green_fail;
	if (countitem("GambogeHerb") < 20) goto L_green_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem("CobaltHerb") > 20 && countitem("GambogeHerb") > 20) goto L_TooMany;
	if (zeny < 1000) goto L_green_fail;
	delitem "CobaltHerb", 20;
	delitem "GambogeHerb", 20;
	set zeny, zeny - 1000;
	getitem "GreenDye", 1;
	goto L_ok;

L_green_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Não... Eu preciso de muito cobalto e gamboge para preparar a tinta.\"";
	mes "\"Vinte folhas de gamboge, 20 de cobalto e 1000 GP deve ser o suficiente.\"";
	next;
	goto L_intermediate;

L_d_green:
	mes "[Raquel a Alquimista]";
	mes "\"Tinta verde é um pouco mais complicado de se fazer, você precisa ferver as folhas de cobalto na temperatura certa em um tubo de cobalto.";
	mes "Fazer um tubo de cobalto não é fácil e sempre que tento acabo perdendo um tubo de cristal...\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Assim, vou precisar de vinte folhas de gamboge, vinte de cobalto e 1000 GP para os matériais.";
	next;
	goto L_intermediate;

L_orange:
	if (countitem("AlizarinHerb") < 10) goto L_orange_fail;
	if (countitem("GambogeHerb") < 10) goto L_orange_fail;
	if (countitem("IronOre") < 2) goto L_orange_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem("AlizarinHerb") > 10 && countitem("GambogeHerb") > 10 && countitem("IronOre") > 2) goto L_TooMany;
	if (zeny < 1000) goto L_orange_fail;
	delitem "AlizarinHerb", 10;
	delitem "GambogeHerb", 10;
	delitem "IronOre", 2;
	set zeny, zeny - 1000;
	getitem "OrangeDye", 1;
	goto L_ok;

L_orange_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Me desculpe, mas eu preciso de 1000 GP, dois pedaços de minério de ferro e 10 folhas de gamboge e alizarina, cada uma.\"";
	next;
	goto L_intermediate;

L_d_orange:
	mes "[Raquel a Alquimista]";
	mes "\"Tinta laranja é divertido de se fazer, mas requer ferro fundido e um pouco de pó de enxofre, o que não facil de se conseguir.";
	mes "\"Ainda assim se me trouxer 10 folhas de alizarina, 10 folhas de gamboge, 2 minérios de ferro e 1000 GP, eu posso preparar a tinta para você.\"";
	next;
	goto L_intermediate;

L_pink:
	if (countitem("AlizarinHerb") < 10) goto L_pink_fail;
	if (countitem("Petal") < 6) goto L_pink_fail;
	if (zeny < 1000) goto L_pink_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem("AlizarinHerb") > 10 && countitem("Petal") > 6) goto L_TooMany;
	delitem "AlizarinHerb", 10;
	delitem "Petal", 6;
	set zeny, zeny - 1000;
	getitem "PinkDye", 1;
	goto L_ok;

L_pink_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Não, você não tem tudo que preciso, que seriam 10 folhas de alizarina, 6 pétalas de flores e 1000 GP.\"";
	next;
	goto L_intermediate;

L_d_pink:
	mes "[Raquel a Alquimista]";
	mes "\"Tinta rosa .... Essa é engraçada, você não consegue um rosa usando as folhas habituais. Você precisa de 10 folhas de alizarina e além disso de 6 pétalas de flores rosas.";
	mes "Para extrair corretamente o rosa das pétalas, precisa ferver-las. Preciso cobrar 1000 GP por isso.\"";
	next;
	goto L_intermediate;

L_dark_blue:
	if (countitem("CobaltHerb") < 100) goto L_dark_blue_fail;
	if (countitem("MauveHerb") < 50) goto L_dark_blue_fail;
	if (countitem("Pearl") < 1) goto L_dark_blue_fail;
	if (zeny < 10000) goto L_dark_blue_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem("CobaltHerb") > 100 && countitem("MauveHerb") > 50 && countitem("Pearl") > 1) goto L_TooMany;
	delitem "CobaltHerb", 100;
	delitem "MauveHerb", 50;
	delitem "Pearl", 1;
	set zeny, zeny - 10000;
	getitem "DarkBlueDye", 1;
	goto L_ok;

L_dark_blue_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Vou precisar de todos os ingredientes para fazer a tinta azul escuro para você.\"";
	mes "\"Preciso de 100 folhas de cobalto, 50 folhas de malva, uma pérola e 10.000 GP.\"";
	next;
	goto L_intermediate;

L_d_dark_blue:
	mes "[Raquel a Alquimista]";
	mes "\"Azul escuro é uma das cores mais complicadas. Você precisa de um extrato concentrado de 100 folhas de cobalto e 50 folhas de malva para a ligação adequada.";
	mes "Para dar o brilho. uma pérola é indispensável, e a parte mais cara é um pó de safira que preciso cobrar 10.000 GP por ele.\"";
	next;
	goto L_intermediate;

L_purple:
	if (countitem("CobaltHerb") < 100) goto L_purple_fail;
	if (countitem("AlizarinHerb") < 100) goto L_purple_fail;
	if (countitem("MauveHerb") < 20) goto L_purple_fail;
	if (countitem("Pearl") < 1) goto L_purple_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem("CobaltHerb") > 100 && countitem("AlizarinHerb") > 100 && countitem("MauveHerb") > 20 && countitem("Pearl") > 1) goto L_TooMany;
	if (zeny < 40000) goto L_purple_fail;
	delitem "CobaltHerb", 100;
	delitem "AlizarinHerb", 100;
	delitem "MauveHerb", 20;
	delitem "Pearl", 1;
	set zeny, zeny - 40000;
	getitem "PurpleDye", 1;
	goto L_ok;

L_purple_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Não... Acho que você não tem todos os ingredientes que preciso, deixe eu ver...\"";
	mes "\"Eu preciso de 100 folhas de cobalto, 100 folhas de alizarina, 20 folhas de malva, uma pérola e de 40.000 GP.\"";
	next;
	goto L_intermediate;

L_d_purple:
	mes "[Raquel a Alquimista]";
	mes "\"Hmm, tinta roxa... alguns dos ingredientes são muito caros.";
	mes "Eu não posso lhe dizer o por que, já que são segredos da alquimia, e eu não quero discutir isso com pessoas que podem me fazer de palhaço.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Ainda assim, eu posso fazer isso para você, só preciso de 100 folhas de cobalto, 100 folhas de alizarina, 20 folhas de malva, uma pérola e 40.000 GP. O dinheiro será usado para os demais ingredientes.\"";
	next;
	goto L_intermediate;

L_black:
	if (countitem("CobaltHerb") < 40) goto L_black_fail;
	if (countitem("AlizarinHerb") < 40) goto L_black_fail;
	if (countitem("GambogeHerb") < 40) goto L_black_fail;
	if (countitem("MauveHerb") < 40) goto L_black_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem("CobaltHerb") > 40 && countitem("AlizarinHerb") > 40 && countitem("GambogeHerb") > 40 && countitem("MauveHerb") > 40) goto L_TooMany;
	if (zeny < 20000) goto L_black_fail;
	delitem "CobaltHerb", 40;
	delitem "AlizarinHerb", 40;
	delitem "GambogeHerb", 40;
	delitem "MauveHerb", 40;
	set zeny, zeny - 20000;
	getitem "BlackDye", 1;
	goto L_ok;

L_black_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Me desculpe mas você não tem todos os ingredientes que preciso para fazer a tinta preta. Eu preciso de 40 folhas de cobalto, alizarina, gamboge e malva, cada uma e mais 20.000 GP.\"";
	next;
	goto L_intermediate;

L_d_black:
	mes "[Raquel a Alquimista]";
	mes "\"Preto? Ah! Uma bela escolha, é minha cor preferida! Bem, se você também quiser ficar bonito como eu, eu ficaria feliz em lhe preparar uma tinta preta.";
	mes "Apenas me traga 40 folhas, cada uma, de cobalto, alizarina, camboge e malva.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Ah e 20.000 GP para a mistura de carvão e pó de diamante que precisa para tirar o cheiro da malva.\"";
	next;
	goto L_intermediate;
	
L_dark_green:
	if (QUEST_clothdyer_knowsdye != @QUEST_HAS_SOLVED_DYE_PUZZLE)
		goto L_d_dark_green;
	if (countitem("CobaltHerb") < 10) goto L_dark_green_fail;
	if (countitem("GambogeHerb") < 10) goto L_dark_green_fail;
	if (countitem("MauveHerb") < 10) goto L_dark_green_fail;
	if (countitem("MaggotSlime") < 1) goto L_dark_green_fail;
	if (zeny < 1000) goto L_dark_green_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem("CobaltHerb") > 10 && countitem("GambogeHerb") > 10 && countitem("MauveHerb") > 10 && countitem("MaggotSlime") > 1) goto L_TooMany;
	delitem "CobaltHerb", 10;
	delitem "MauveHerb", 10;
	delitem "GambogeHerb", 10;
	delitem "MaggotSlime", 1;
	set zeny, zeny - 1000;
	getitem "DarkGreenDye", 1;
	goto L_ok;

L_dark_green_fail:
	mes "[Raquel a Alquimista]";
	mes "Raquel a testa enquanto analisa os ingredientes.";
	mes "\"Não, isso não serve. Eu preciso de 10 folhas de gamboge, 10 de cobalto, 10 de malva, gosma de verme e 1.000 GP.\"";
	next;
	goto L_intermediate;

L_d_dark_green:
	mes "[Raquel a Alquimista]";
	if (QUEST_clothdyer_knowsdye != @QUEST_HAS_SOLVED_DYE_PUZZLE)
		goto L_d_dark_green_quest;
	mes "\"Agora que sabemos quantas gramas é necessário para você, tudo que precisamos é de 1.000 GP, uma gosma de verme e 10 folhas de malva, cobalto e gamboge, cada uma.\"";
	next;
	goto L_intermediate;
	
L_d_dark_green_quest:
	mes "\"Ah, verde escuro ... tinta verde escuro é até simples, exceto por um pequeno detalhe, a roupa desbota muito facilmente quando em contato com a pele humana. A melhor de resolver isso é usar um pó fixador, misturado com a essência extraida da gosma de verme.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Mas quanto do fixador é necessário vária de indivíduo para indivíduo. Temos que descobrir quantas gramas do fixador é preciso usar no seu caso antes de preparar a tinta verde escuro.\"";
	next;
	if (QUEST_clothdyer_knowsdye == @QUEST_CAN_GET_DYE_HERE)
		set QUEST_clothdyer_knowsdye, (rand(100) + @QUEST_GUESS_BASE);

L_d_dark_green_q_main:
	menu "Hmm... não obrigado.", L_intermediate,
		"Então ninguem mais pode usar a roupa que eu pintar?", L_dark_green_q_wear,
		"OK, o que preciso fazer?", L_dark_green_q_explain,
		"Vamos descobrir isso!", L_dark_green_q_guess_0;
	close;

L_dark_green_q_wear:
	mes "[Raquel a Alquimista]";
	mes "\"A cor estabiliza uma semana depois.";
	mes "Outras pessoas poderão utilizá-la, mas tenha certeza de que já passou o tempo para estabilizar a cor.\"";
	next;
	goto L_d_dark_green_q_main;

L_dark_green_q_explain2:
	mes "[Raquel a Alquimista]";
	mes "Raquel pisca.";
	mes "\"Muito bem. Funciona assim, você me dá uma gosma de verme e me diz quantas gramas vão entrar na mistura da tinta.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Então, tentamos descobrir se essa era a quantidade certa. Eu vou lhe dizer se acertou ou não, em caso de erro eu digo se foi para mais ou para menos.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Com bastante gosmas de verme podemos descobrir o quanto você precisa.\"";
	next;
	menu "Er... você pode explicar de novo?", L_dark_green_q_explain2,
		"Vamos lá!", L_dark_green_q_guess_0,
		"Eu não tenho tempo para isso.", L_intermediate;
	close;

L_dark_green_q_explain:
	mes "[Raquel a Alquimista]";
	mes "\"Nós temos que verificar o equilíbrio de ácidez da sua pele e usar com equilibrar a concentração de gosma na mistura.\"";
	mes "\"Serão várias tentativas e para cada nova tentativa vamos precisar de uma gosmas de verme.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Cabe a você me dizer quantas gramas do fixador pretende usar. Eu então preparo a mistura e em seguida você coloca o seu dedo dentro da mistura e vemos a reação.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Se a mistura ficar muito escura significa que colocamos muita gosma, se ficar muito claro significa que colocamos muito pouca e se ela manter sua cor, então acertamos a quantidade.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "Raquel sorri.";
	mes "\"Mas não se preocupe eu farei os testes totalmente de graça. Me divirto um pouco nessa brincadeira.\"";
	next;

	menu "Er... pode explicar isso de novo?", L_dark_green_q_explain,
		"Errr... o que?", L_dark_green_q_explain2,
		"Vamos logo com isso!", -,
		"Não tenho tempo para isso.", L_intermediate;

L_dark_green_q_guess_0:
	if (countitem("MaggotSlime") < 1)
		goto L_dark_green_q_noslime;
	delitem "MaggotSlime", 1;

	mes "[Raquel a Alquimista]";
	mes "Raquel enche uma pequena garrafa com a gosma de verme e retira uma garrafa de vidro de dentro do seu manto.";
	mes "\"Agora precisamos descobrir quantas gramas do fixador serão necessárias. Me diga quantas gramas você deseja utilizar.\"";
	next;

	menu "Nenhuma", -,
		"1 grama", -,
		"2 gramas", -,
		"3 gramas", -,
		"4 gramas", -,
		"5 gramas", -,
		"6 gramas", -,
		"7 gramas", -,
		"8 gramas", -,
		"9 gramas", -;

	set @menu, @menu - 1;
	set @guess_accumulator, @menu;

	mes "[Raquel a Alquimista]";
	if (@menu)
		mes "Raquel despeja boa parte da mistura dentro da garrafa de vidro.";
	mes "\"Temos agora" + @guess_accumulator + " gramas no total. Quanto mais você quer que eu adicione?\"";
	next;

	menu "Nada", -,
		"1 grama", -,
		"2 gramas", -,
		"3 gramas", -,
		"4 gramas", -,
		"5 gramas", -,
		"6 gramas", -,
		"7 gramas", -,
		"8 gramas", -,
		"9 gramas", -;

	set @menu, @menu - 1;
	set @guess_accumulator, @guess_accumulator + @menu;

	mes "[Raquel a Alquimista]";
	if (@menu)
	  mes "Raquel despeja mais um pouco da gosma de verme dentro da garrafa de vidro.";
	mes "\"Temos agora" + @guess_accumulator + " gramas no total. Deseja adicionar mais quanto?\"";
	next;

	menu "Nada", -,
		"1 grama", -,
		"2 gramas", -,
		"3 gramas", -,
		"4 gramas", -,
		"5 gramas", -,
		"6 gramas", -,
		"7 gramas", -,
		"8 gramas", -,
		"9 gramas", -;

	set @menu, @menu - 1;
	set @guess_accumulator, @guess_accumulator + @menu;

	mes "[Raquel a Alquimista]";
	if (@menu)
		mes "Raquel despeja mais um pouco da gosma de verme dentro da garrafa de vidro.";
	mes "Raquel mistura mais alguns ingredientes na garrafa, agita bem e em seguida o aquece.";
	next;

	mes "[Raquel a Alquimista]";
	mes "\"Por favor coloque seu dedo aqui.\"";
	mes "Hesitante, você coloca o dedo dentro da mistura. Ela está quente, mas agradável.";
	next;
	mes "[Raquel a Alquimista]";
	mes "Você retira o dedo.";

	if (@guess_accumulator + @QUEST_GUESS_BASE > QUEST_clothdyer_knowsdye)
		goto L_dark_green_q_toomuch;

	if (@guess_accumulator + @QUEST_GUESS_BASE < QUEST_clothdyer_knowsdye)
		goto L_dark_green_q_toolittle;

	// otherwise correct guess

	mes "Nada parece acontecer a mistura, os olhos de Raquel se iluminam.";
	mes "\"É isso! Você precisa exatamente de" + @guess_accumulator + " gramas. Vou anotar isso...\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "Raquel está radiante.";
	mes "\"Agora posso fazer a tinta verde escuro para você. Tudo que eu preciso é uma gosma de verme, dez folhas de gamboge, dez folhas de cobalto, 10 de malva e 1.000 GP para os demais ingredientes.\"";
	mes "[Você ganhou 20,000 pontos de experiência]";
	getexp 20000, 0;
	set QUEST_clothdyer_knowsdye, @QUEST_HAS_SOLVED_DYE_PUZZLE;
	next;
	goto L_intermediate;

L_dark_green_q_toomuch:
	mes "Após alguns segundos a mistura fica escura. Raquel fica desapontado.";
	mes "\"Isso foi muito, você precisa colocar menos de " + @guess_accumulator + " gramas, tente de novo.";
	goto L_dark_green_q_again;

L_dark_green_q_toolittle:
	mes "Quase que instantaneamente, a mistura  perde toda cor e fica transparente. Raquel suspira.";
	mes "\"Isso foi muito pouco. Tente colocar mais de " + @guess_accumulator + " gramas na próxima.";
	goto L_dark_green_q_again;

L_dark_green_q_again:
	mes "\"Vamos tentar de novo?\"";
	next;
	menu "Sim!", L_dark_green_q_guess_0,
		"Não, para mim já basta.", -;
	close;

L_dark_green_q_noslime:
	mes "[Raquel a Alquimista]";
	mes "\"Ah. você não tem nenhuma gosma de verme? Que pena, mas é fácil encontrar, consiga ao menos um e podemos continuar.\"";
	next;
	goto L_intermediate;

//-[ Poção de Óleo de Monstro ]----------------------------------------

L_monster_oil:
	mes "[Raquel a Alquimista]";
	mes "Raquel olha diretamente para você e continua num tom mais sério.";
	mes "\"Eu sei como fazer, mas você não vai conseguir de mim. É muito perigoso, mesmo o melhor dos alquimistas precisam de muitas tentativas e ainda corre o risco de explodir tudo! Eu posso lhe dar a receita se você insisti.\"";
	next;
	menu
		"Sim, me diga!", -,
		"Não, isso parece perigoso.", L_abort;
	mes "[Raquel a Alquimista]";
	mes "Você vai precisar de 1 [Pérola], 2 [Pele de Cobra], 3 [Ferrão de Escorpião Preto], 2 [Pilha de cinzas] e 1 [Ferrão de Escorpião Dourado]. Além disso, você deve ter muitas ervas para poder estabilizar a mistura.";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Pegue a pérola, os ferrões pretos, as peles de cobra, as cinzas e cozinhe rapidamente em um caldeirão. Em seguida adicione as ervas para estabilizar a mistura em uma pasta preta. Por fim, adicione o ferrão dourado e deixe ferver mais um pouco. E acabou!\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Boa sorte para não explodir tudo!\"";

	set @Q_status, @Q_status | @SETZER_KNOWS_STINGER;
	callsub L_Update_Var;
	close;

L_abort:
	close;


L_Update_Var:
	set QUEST_Forestbow_state, (QUEST_Forestbow_state & ~(@Q_MASK) | (@Q_status << @Q_SHIFT));
	return;

L_TooMany:
	mes "[Raquel a Alquimista]";
	mes "You don't have room for this.";
	close;
}
