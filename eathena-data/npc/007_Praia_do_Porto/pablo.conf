//
// @autor Diogo_RBG - http://diogorbg.blogspot.com
// @desc Pescador que adora a vida mansa... mas por um descuido ele quase se deu mau!
// #quest <desc>, <getItens>, <delItens>, <extra>
// @quest 'Presente de Casamento', '1 facao', '1 WhiteRose', ''
// @quest 'Matar Escorpiões', '780 exp', '', 'Matar 30 Escorpiões'

007.gat,20,44,0	script	Pablo	310,{
	if (QUEST_presenteCasamento==1) goto _pabloFria;
	if (QUEST_presenteCasamento==2) goto _trouxeRosa;
	if (QUEST_presenteCasamento==3) goto _premioFacao;
	if (QUEST_presenteCasamento==4) goto _pabloContente;
	if (QUEST_CorneliaVermes==1) goto _intro;

	mes "[Pablo o pescador]";
	mes "\"O mar num tá pra peixe !\"";
	close;

_intro:
	mes "[Pablo o pescador]";
	mes "\"Você me parece ser o guerreiro que minha esposa Cornélia ajudou... rss... ela te deu mesmo um Rolo de Macarrão como arma?\"";
	next;
	menu
		"Sim. O mais curioso é que me ajudou muito!", -;
	mes "[Pablo o pescador]";
	mes "\"Minha esposa tem um coração puro e admiro isso nela. Não foi a toa que me casei com ela.\"";
	mes "De repente Pablo faz uma cara completamente pálida.";
	next;
	mes "[Pablo o pescador]";
	mes "\"Ho meu Deus! Hoje é o nosso aniversário de casamento! Eu o esqueci completamente.";
	mes "E o que é pior: NÃO TENHO NENHUM PRESENTE!!!\"";
	next;
	goto _menuFlor;

_pabloFria:
	mes "[Pablo o pescador]";
	mes "\"Eu esqueci completamente meu aniversário de casamento com Cornélia... e agora?\"";
	next;
	goto _menuFlor;

_menuFlor:
	menu
		"Porque você não dá uma rosa a ela?", -;
	mes "[Pablo o pescador]";
	mes "\"É o que vou fazer... mas não posso sair correndo pra comprar uma rosa pra ela.";
	mes "Ela vai percerber que me esqueci do nosso aniversário de casamento. E ela vai ficar uma fera comigo.\"";
	set QUEST_presenteCasamento, 1;
	next;
	menu
		"Meus pêsames!", -,
		"Eu posso comprar uma rosa para você.", _comproFlor;
	mes "[Pablo o pescador]";
	mes "\"Sim é meu fim!\"";
	close;

_comproFlor:
	mes "[Pablo o pescador]";
	mes "\"Se você fizer isso irá salvar minha pele.";
	mes "Este dinheiro irá ajudar a comprar uma bela rosa para minha esposa.";
	mes "Por favor não demore.\"";
	set zeny, zeny + 100;
	set QUEST_presenteCasamento, 2;
	close;

_trouxeRosa:
	mes "[Pablo o pescador]";
	mes "\"Você trouxe a rosa que te pedi?\"";
	next;
	menu
		"Sim. Está na minha mochila.", _verificaRosa,
		"Me esqueci de trazer", -;
	mes "[Pablo o pescador]";
	mes "Por favor compre uma rosa com o dinheiro que te dei... estou dependendo de seu favor.";
	close;

_verificaRosa:
	if (countitem(662)) goto _rosaCerta;
	if (countitem(661)||                countitem(663)||countitem(664)) goto _rosaErrada;
	if (countitem(665)||countitem(666)||countitem(667)||countitem(668)) goto _rosaErrada;
	mes "[Pablo o pescador]";
	mes "\"Você não me trouxe nenhuma rosa. Compre uma rosa com o dinheiro que te dei.";
	mes "Por favor não demore... estou dependendo de seu favor.\"";
	close;

_rosaErrada:
	mes "[Pablo o pescador]";
	mes "\"Deixe-me ver a rosa que você me trouxe.\"";
	mes "Pablo olha sua mochila e fica ainda mais aflito.";
	next;
	mes "[Pablo o pescador]";
	mes "\"Essa não! Você não me trouxe a rosa correta.";
	mes "Não posso dar uma rosa a minha esposa que não seja a Rosa Branca, pois ela representa amor eterno.\"";
	close;

_rosaCerta:
	mes "[Pablo o pescador]";
	mes "\"Você me trouxe uma Rosa Branca representando amor eterno... é a rosa perfeita.";
	mes "Muito obrigado... você salvou minha pele.\"";
	delitem "WhiteRose", 1;
	set QUEST_presenteCasamento, 3;
	next;
	goto _premioFacao;

_premioFacao:
	getinventorylist;
	if( (@inventorylist_count + (countitem(3030)==0) ) > 100 ) goto _cheio;

	mes "[Pablo o pescador]";
	mes "\"Voltando ao assunto do 'Rolo de Macarrão'...";
	mes "Definitivamente um Rolo de Macarrão não é uma arma. Este Facão é que é uma arma.\"";
	next;
	mes "[Pablo o pescador]";
	mes "\"Facão que agora é seu!\"";
	getItem "facao", 1;
	set QUEST_presenteCasamento, 4;
	next;
	menu
		"Oba... um Facão!", -;
	mes "[Pablo o pescador]";
	mes "\"Como você me salvou de uma fria... o Facão era mínimo que eu poderia fazer por você.\"";
	close;

_pabloContente:
	mes "[Pablo o pescador]";
	mes "\"Heheh... assim que eu me encontrar com minha esposa darei a rosa a ela!";
	mes "E devo tudo a você!\"";
	goto _matarEscorpioes;

_cheio:
	mes "[Pablo o pescador]";
	mes "\"Pretendia lhe dar algo, mas parece que seu inventário está cheio.";
	mes "Volte mais tarde.\"";
	close;

//=====================================================================

_matarEscorpioes:
	if (QUEST_pabloEscorpioes==0) goto _mate30;
//	if (QUEST_pabloEscorpioes==1) ==> quest completada.
	if (QUEST_pabloEscorpioes>=2) goto _matando;
	close;

_mate30:
	next;
	mes "[Pablo o pescador]";
	mes "\"O que anda fazendo com o Facão que te dei?\"";
	next;
	menu
		"Matando um monstro aqui e ali.", -;
	mes "[Pablo o pescador]";
	mes "\"Porque você não mata Escorpiões? Aposto que iria aumentar e muito sua experiência.\"";
	set QUEST_pabloEscorpioes, 2;
	next;
	menu
		"Irei matá-los agora mesmo.", -;
	close;

_matando:
	next;
	mes "[Pablo o pescador]";
	mes "\"A propósito. Quantos Escopiões você já matou?\"";
	next;
	menu
		"Já matei "+(QUEST_pabloEscorpioes-2)+" Escorpiões.", -;
	if (QUEST_pabloEscorpioes>=32) goto _matou30;
	mes "[Pablo o pescador]";
	mes "\"Essa quantia é insuficiente. Você precisa matar pelo menos 30 Escorpiões se quiser aumentar sua experiência.\"";
	close;

_matou30:
	mes "[Pablo o pescador]";
	mes "\"Como eu disse. Matar muitos Escorpiões iria ajudar a aumentar sua experiência.\"";
	mes "";
	mes "* Você ganhou 780 pontos de experiência.";
	set BaseExp, BaseExp+780;
	set QUEST_pabloEscorpioes, 1;
	close;

}
