/**
 * @date domingo, 02 de janeiro de 2011
 * @author Diogo_RBG - http://diogorbg.blogspot.com/
 * @escritor Kamaitachi
 */

007.gat,90,76,0	script	Alquimista da Alma	500, {
	callfunc "alquimistaAlma";
	close;
}

//=====================================================================

function	script	alquimistaAlma	{
	setarray @meuCaro$, "minha cara", "meu caro";
	setarray @aventureiro$, "aventureira", "aventureiro";

	if (countitem(3199)>1 && countitem(3200)==0) goto _possuiFragmento;
	if (countitem(3200)>1) goto _possueAnel;

	mes "[Alquimista da Alma]";
	mes "\"A energia da alma emana dos seres assim como o seu mana. O mundo de Mana esteve muito carregado desta energia estes dias.\"";
	mes "[Alquimista da Alma]";
	mes "\"Mas não se preocupe com minhas palavras, não há nada de errado com o mundo. É apenas um fenômeno natural.\"";
	return;

//---------------------------------------------------------------------

_possuiFragmento:
	if(@AlquimistaAlma==1) goto _parte2;
	if(@AlquimistaAlma==2) goto _parte3;

	mes "[Alquimista da Alma]";
	mes "\"Oh! Espere! Sinto que você carrega algo muito valioso do ponto de vista alquímico!\"";
	next;
	menu
		"Ah?", -,
		"Do que está falando?", -;
	mes "[Alquimista da Alma]";
	mes "Neste momento o alquimista coloca uma mão espalmada próximo a face, enquanto a outra percorre espalmada pelo seu corpo, como se tentasse encontrar algo de natureza energética.";
	next;
	mes "[Alquimista da Alma]";
	mes "\"Sim... estou sentindo... Algo de muito poderoso está em suas mãos. Há algo que lhe chamou a atenção recentemente?\"";
	next;
	menu
		"Não me lembro (que cara estranho!)", -,
		"Sim (está pensando no cristal)", _encontrei;
	mes "[Alquimista da Alma]";
	mes "\"Entendo... quando encontrar algo estranho e precisar de algumas explicações poderá vir conversar comigo.\"";
	return;

_encontrei:
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Encontrei um cristal enorme em uma de minhas aventuras mundo a fora. Ele brilhava intensamente, então pensando que fosse algo semelhante ao Menir da Alma, toquei, e o que senti foi apenas um calor, como se o cristal tivesse uma energia dentro dele.\"";
	next;
	mes "[Alquimista da Alma]";
	mes "\"FASCINANTE! Você, "+@meuCaro$[sex]+" "+@aventureiro$[sex]+", acabou de encontrar um fenômeno raríssimo no nosso Mundo de Mana! Cristalização da energia das almas! Mas me diga... o que você fez depois de tocar a pedra?\"";
	next;
	menu
		"Eu quebrei um pedaço da pedra.", _quebrei,
		"Não toquei em nada.", -;
	mes "[Alquimista da Alma]";
	mes "\"Você parece não querer conversar sobre isso.. vou lhe deixar em paz.\"";
	return;

_quebrei:
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Eu...quebrei um pedaço para retirar o fragmento e procurar alguém que saiba a respeito disso...\"";
	next;
	mes "[Alquimista da Alma]";
	mes "\"Atitude digna de um verdadeiro explorador, posso dizer com certeza "+@meuCaro$[sex]+". ;-)\"";
	next;
	menu
		"Me conte mais.", -;
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Mas me explique um pouco mais sobre essa tal \"cristalização da energia das almas\".\"";
	next;
	mes "[Alquimista da Alma]";
	mes "\"Ah! claro, claro! Quase me esqueço de falar sobre este magnífico efeito natural. Raro, mas natural. Bem, não sei se você já sabe "+@aventureiro$[sex]+", mas todas as coisas deste mundo, de uma forma ou de outra possuem algo que conhecemos como espírito, alma, anima, entre outros nomes. Esta entidade que se chama alma não possui matéria, isto é, não pode ser tocado, apenas sentido.\"";
	next;
	mes "[Alquimista da Alma]";
	mes "\"Os deuses deste mundo concedem alma a todas as coisas, para que cumpram o seu papel neste mundo e depois voltem para os seus criadores. Mas, as almas as vezes se acumulam neste planeta. O motivo, ainda não sabemos, mas suspeita-se que a energia Mana, a mesma da pedra menir, e a do planeta, forcem as almas a permanecerem aqui por algum tempo.\"";
	next;
	mes "[Alquimista da Alma]";
	mes "\"Por este motivo, elas ao invés de ascenderem a essência dos elementos, onde moram os deuses, as almas vão para o centro do mundo Mana, onde são misturadas a energia mana.\"";
	next;
	mes "[Alquimista da Alma]";
	mes "\"Depois de muito tempo, essa energia das coisas vivas se acumulam tanto que se impregnam nas rochas cristalinas, e por serem não-matéria cristalizada, elas emanam a aura que provavelmente você deve ter visto, como forma de voltar ao seu estado natural e ascender ao céu.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Interessante...mas é chato também... O que se pode fazer com elas?\"";
	next;

_utilidadeFragmento:
	mes "[Alquimista da Alma]";
	mes "\"Ah! Excelente pergunta! Por serem a energia da alma no seu estado puro elas tem uma estranha tendência de absorver as almas assim que são libertadas dos seus corpos materiais, acumulando-as na forma cristalina para logo depois voltar ao seu estado etéreo.";
	next;
	mes "[Alquimista da Alma]";
	mes "\"Por exemplo, se algum animal for morto próximo a pedra, ela absorverá a alma deste animal para o cristal para logo depois vaporizar a alma para o céu.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Nossa... mas e o que aconteceria se eu matasse um animal longe da pedra original, e eu estivesse com este fragmento?\"";
	next;
	mes "[Alquimista da Alma]";
	mes "\"hum...a pedra tenderia a absorver a alma do animal, mas como ela foi separada da matriz cristalina, não fará muita coisa. Mas há um modo de fazer até um pequeno fragmento deste cristal absorver almas.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "\"E qual seria a utilidade disso?\"";
	next;
	mes "[Alquimista da Alma]";
	mes "\"É ai que está a grande importância deste fenômeno para o mundo Mana. O fragmento, se bem trabalhado faz com que o usuário absorva as almas dos mortos e incorpore a sua própria alma a energia contida nas almas que ele capturou.";
	next;
	mes "[Alquimista da Alma]";
	mes "\"Resumindo, o cristal absorve a alma, converte em energia e lhe passa para que você fique com uma capacidade inigualável. Geralmente se coloca o fragmento lapidado em um anel, por ser de fácil porte.\"";
	set @AlquimistaAlma, 1;
	next;

_parte2:
	menu
		"O que este fragmento pode fazer?", _utilidadeFragmento,
		"Como você pode me ajudar?", _ofereceAnel,
		"Preciso de um tempo para entender isso.", -;
	mes "[Alquimista da Alma]";
	mes "\"Eu entendo você. É muita informação de uma só vez.";
	mes "Mas não perca muito tempo pensando, pois este fenômeno não costuma durar muito tempo "+@meuCaro$[sex]+".\"";
	return;

_ofereceAnel:
	mes "[Alquimista da Alma]";
	mes "\"Eu posso lapidar este fragmento, trabalhar no anel e lhe ensinar como utilizá-lo.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Acho que estou no lugar certo e na hora certa!\"";
	next;
	mes "[Alquimista da Alma]";
	mes "\"Hahahaha. Nunca vi ser tão verdadeira a lei que rege o mundo: \"Para se ter algo, é preciso dar algo em troca\". Esta é a lei da troca equivalente "+@aventureiro$[sex]+". Se deseja ter poderes nunca vistos, precisará sacrificar algo em troca. Por este motivo, me traga o fragmento um anel ou aliança, além de 2.500 GP como custo inerente ao trabalho.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Me parece que nada vem de graça mesmo. Está bem para mim. Trarei os seus materiais, assim como o dinheiro.\"";
	next;
	mes "[Alquimista da Alma]";
	mes "\"Boa sorte "+@aventureiro$[sex]+".\"";
	set @AlquimistaAlma, 2;
	return;

_parte3:
	mes "[Alquimista da Alma]";
	mes "\"Veio preparado para que eu possa fazer o seu anel?\"";
	menu
		"Tenho tudo que precisa.", _tenhoTudo,
		"Estou coletando os materiais.", -;
	mes "[Alquimista da Alma]";
	mes "\"Não se esqueça do que preciso: o fragmento, um anel ou aliança e 2.500 GP.\"";
	return;

_tenhoTudo:
	if (countitem(3199)<1) goto _semFragmento;
	if (countitem(702)<1) goto _semAnel;
	if (Zeny<2500) goto _semGrana;
	getinventorylist;
	if(@inventorylist_count == 100) goto _cheio;
	mes "[Alquimista da Alma]";
	mes "\"Ah! vejo que trouxe tudo o que pedi. Farei imediatamente o seu anel com o cristal lapidado\"";
	delitem 3199, 1;
	delitem 702, 1;
	set Zeny, Zeny-2500;
	next;
	mes "[Alquimista da Alma]";
	mes "O alquimista recebe em mãos o anel entregue por você e coloca em cima de uma pequena bancada. Trabalha o metal para modelar o anel e logo depois lapida cuidadosamente o cristal para que se encaixe perfeitamente no anel modelado.";
	next;
	mes "[Alquimista da Alma]";
	mes "\"Ufa! Aqui está! Espero que consiga atingir os seus objetivos com o poder que este anel lhe dará. Se não se importa, ficarei com alguns pedaços do cristal que sobraram para um estudo mais aprofundado no assunto. Que ventos de boa sorte lhe acompanhem na sua jornada "+@aventureiro$[sex]+".\"";
	getitem 3200, 1;
	set @AlquimistaAlma, 0;
	close;

_semFragmento:
	mes "[Alquimista da Alma]";
	mes "\"Não posso lapidar sem o fragmento da Pedra de Emanar Alma.\"";
	return;

_semAnel:
	mes "[Alquimista da Alma]";
	mes "\"Eu preciso de um anel ou aliança para poder trabalhar nele.\"";
	return;

_semGrana:
	mes "[Alquimista da Alma]";
	mes "\"Você não tem os 2.500 GP para pagar os meus serviços.\"";
	return;

_cheio:
	mes "[Alquimista da Alma]";
	mes "\"Você está realmente carregando muitas coisas.\"";
	return;

//---------------------------------------------------------------------

_possueAnel:
	mes "[Alquimista da Alma]";
	mes "\"Vejo que você porta o Anel de Emanar Alma. Imagino que saiba como utilizá-lo.\"";
	next;
	menu
		"Como posso utilizá-lo?", _comoUtilizar,
		"Já sei utilizá-lo.", -;
	mes "[Alquimista da Alma]";
	mes "\"Tube bem então. Pensei ter me esquecido de explicar o modo de uso deste anel.\"";
	return;

_comoUtilizar:
	mes "[Alquimista da Alma]";
	mes "\"Acho que me esqueci de explicar como o anel deve ser utilizado.";
	mes "\"Mas eu posso lhe ensinar agora.\"";
	next;
	mes "[Alquimista da Alma]";
	mes "\"Uma pedra de emanar alma tem o poder de absorver a alma dos seres no momento em que eles morrem. Mas uma pedra deste tamanho não tem muito efeito... a não ser é claro que você saiba como utilizá-la.\"";
	next;
	mes "[Alquimista da Alma]";
	mes "\"Primeiro você precisa tocar em uma grande Pedra de Emanar Alma para que seu anel acumule uma grande quantidade de energia da pedra. Em seguida você precisa usar seu próprio mana para intensificar o poder do anel.\"";
	next;
	mes "[Alquimista da Alma]";
	mes "\"No momento em que você tocar na pedra o anel irá liberar parte da anergia da alma que ele acumulou. Com um pouco de técnica você pode se beneficiar desta energia da alma liberada pelo anel.\"";
	return;
}















