//
// Quest de Evento de Natal
// @author: Diogo_RBG

//010.gat,147,80,0	script	debug	319, {
//	set NATAL_pVerde, 0;
//	mes "resetado.";
//	close;
//}

010.gat,150,80,0	script	Duende Faísca	319, {
	set @id, 0;
	callfunc "menuFaisca";
	close;
}

//=====================================================================

function	script	menuFaisca	{
	setarray @texto$, "Quero voltar para a Ilha Fortaleza.", "Me leve ao Polo Norte.";

	if (NATAL_pVerde>16) goto _menu2;

	mes "[Duende Faísca]";
	mes "\"Feliz Natal!";
	mes "Por falar em natal... como anda o resgate dos presentes perdidos?\"";
	goto _menu;

_menu:
	menu
		@texto$[@id], _transp,
		"Veja meus presentes.", _presentes,
		"Porque te chamam de faísca?", _nome,
		"Tenho que ir.", -;
	return;

_menu2:
	mes "[Duende Faísca]";
	mes "\"Obrigado por me ajudar com os presentes.";
	mes "Fique com o restante deles para presentear seus amigos!\"";
	menu
		@texto$[@id], _transp,
		"Porque te chamam de faísca?", _nome,
		"Até mais então.", -;
	return;

_presentes:
	if (NATAL_pVerde==0) set NATAL_pVerde, 1;
	set @tmp, NATAL_pVerde*20;
	mes "[Duende Faísca]";
	mes "\"Eu estou interessado nos [Presentes Verde].";
	mes "Eu posso até te dar um [Presente Prateado] por eles.";
	mes "";
	mes "Você poderia me trazer " +@tmp+ " [Presentes Verde]?\"";
	next;
_menu3:
	menu
		"Trouxe " +countitem("GreenPresentBox")+ " [Presentes Verde].", _verde,
		"O que faço com o [Presente Rosa]?", _rosa,
		"O que faço com o [Presente Azul]?", _azul,
		"Depois eu te dou meus presentes.", -;
	return;

_rosa:
	mes "[Duende Faísca]";
	mes "\"Fale com a Lulu... pois ela quem é encarregada dos [Presentes Rosa].\"";
	next;
	goto _menu3;

_azul:
	mes "[Duende Faísca]";
	mes "\"Fale com o Tunico... pois ele quem é encarregado dos [Presentes Azul].\"";
	next;
	goto _menu3;

_verde:
	if (countitem("GreenPresentBox")<@tmp) goto _mais;
	getinventorylist;
	if( (@inventorylist_count + (countitem("presentePrateado")==0) - countitem("GreenPresentBox")<=@tmp ) > 100 ) goto _cheio;

	mes "[Duende Faísca]";
	mes "\"Obrigado pela ajuda!\"";
	mes "";
	mes "* Você ganhou um [Presente Prateado].";
	delitem "GreenPresentBox", @tmp;
	set NATAL_pVerde, NATAL_pVerde*2;
	set @mobID, 1;
	callfunc "MobPoints";
	return; //< Após chamar a função não irá executar esta linha

_mais:
	mes "[Duende Faísca]";
	mes "\"Só esses não são o suficiente... me traga " +@tmp+ " [Presentes Verde].\"";
	return;

_cheio:
	mes "[Duende Faísca]";
	mes "\"Você não tem espaço para meu presente.\"";
	return;

_transp:
	if (@id==0) goto _para008;
	callfunc "para010";
	return;

_para008:
	warp "008.gat", 0, 0;
	mes "[Duende Faísca]";
	mes "\"Eu fico confuso quando você me pede para ir para este lugar.\"";
	return;

_nome:
	mes "[Duende Faísca]";
	mes "\"Quem me deu este apelido foi a peste do Tunico... ele dizia que todas as minhas magias soltavam faísca.";
	mes "É exagero dele, porque nem todas fazem isso.\"";
	next;
	goto _menu;
}

//=====================================================================

function	script	para010	{
	setarray @pnts,
		156,82,
		135,42,
		160,45,
		129,59,
		98,72,
		130,93,
		175,99,
		159,112;
	set @tmp, rand(8);
	set @x, rand(5);
	set @y, rand(5);
	warp "010.gat", @pnts[@tmp*2]+(@x-2), @pnts[(@tmp*2)+1]+(@y-2);

	mes "[Duende Faísca]";
	if (@tmp==0) mes "\"Viu como a viagem foi tranquila!\"";
	if (@tmp!=0 && @tmp<=4) mes "\"Eu acho que fiz alguma coisa errada.\"";
	if (@tmp>4) mes "\"Foi quase... na próxima eu acerto!\"";
	return;
}
