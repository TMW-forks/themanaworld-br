//
// Tradução: Diogo_RBG - http://diogorbg.blogspot.com/

//010.gat,80,108,0	script	debug	129,{
//	set QUEST_gorroNatal, 0;
//	mes "resetado";
//	close;
//}

010.gat,83,108,0	script	Boneco de Neve	129,{
	if (QUEST_gorroNatal==1) goto _ok;

	mes "[Boneco de Neve]";
	mes "\"Olá Jóvem.";
	mes "Você gostaria de um [Gorro de Natal]?\"";
	next;

L_Menu:
	menu
		"Sim", _sim,
		"Não", -,
		"O que você precisa?", _preciso;
	mes "[Boneco de Neve]";
	mes "\"É uma pena, isso é muito ruim, mas não se esqueça de ter o espírito de Natal!\"";
	close;

_sim:
	mes "[Boneco de Neve]";
	mes "\"Hmm, deixe-me ver o que você tem.\"";
	next;
	if(countitem("Candy") < 15) goto _semItem;
	if(countitem("ChocolateBar") < 10) goto _semItem;
	if(countitem("SantaCookie") < 5) goto _semBiscoitos;
	getinventorylist;
	if (@inventorylist_count > 99) goto _cheio;
	delitem "Candy", 15;
	delitem "ChocolateBar", 10;
	delitem "SantaCookie", 5;
	mes "[Boneco de Neve]";
	mes "\"Aqui está, desfrute de seu novo gorro!\"";
	getitem "SantaHat", 1;
	set QUEST_gorroNatal, 1;
	close;

_preciso:
	mes "[Boneco de Neve]";
	mes "\"Para o gorro especial, eu preciso que você me ajude dando um pouco de sua magia.";
	mes "Brincadeira. Eu só queria mesmo era algumas guloseimas:";
	mes "15 [Balinha]s";
	mes "10 [Barra de Chocolate]s";
	mes "5 [Biscoito de Natal]s\"";
	next;
	goto L_Menu;

_semItem:
	mes "[Boneco de Neve]";
	mes "\"Estou interessado em algumas guloseimas e você não tem nenhuma para obter um gorro.\"";
	close;

_semBiscoitos:
	mes "[Boneco de Neve]";
	mes "\"Você não me trouxe os biscoitos que tanto gosto. :(\"";
	next;
	menu
		"Eu não sei onde conseguir.", _semDesculpas,
		"Eles são para o Papai Noel.", _semDesculpas,
		"Eu irei trazê-los.", -;
	close;

_semDesculpas:
	mes "[Boneco de Neve]";
	mes "\"Mas eu gosto muito destes biscoitos, por favor traga-os para mim.\"";
	close;

_ok:
	mes "[Boneco de Neve]";
	mes "\"Obrigado por me ajudar!\"";
	close;

_cheio:
	mes "[Boneco de Neve]";
	mes "\"Você não tem espaço para o gorro. Volte mais tarde.\"";
	close;
}
