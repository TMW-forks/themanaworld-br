// This NPC makes potions and dyes
// Tradução por Fafinha
// Adaptação para o server BR por alastrim

010.gat,132,57,0	script	Raquel	103,{

	set @EscolhaIntroTingimento, 0;
	set @EscolhaCorante, 1;
	set @EscolhaAbortar, 2;

	setarray @menuitems$, "", "", "", "", "";
	set @c, 0;

	set @menuitems$[@c], "Estou muito ocupado. Depois eu volto.";
	set @menuID[@c], @EscolhaAbortar;
	set @c, @c + 1;

_introducao:
	mes "[Raquel a Alquimista]";
	mes "\"Estou aprendendo a antiga ciência da alquimia. Estou estudando bastante para conseguir criar vários tipos de poções!\"";
	if (QUEST_MASK1 & MASK1_SABETINTA) goto L_skip_introducing;
	if (QUEST_tinta == 1) goto L_pre_dyeing;
	close;

L_skip_introducing:
	mes "[Raquel a Alquimista]";
	mes "\"Olá, bem vindo de volta! Você está interessado em mais tintas?\"";
	next;
	set @menuitems$[@c], "Sim. Tintas, por favor.";
	set @menuID[@c], @EscolhaCorante ;
	set @c, @c + 1;
	goto L_main_menu;

L_pre_dyeing:
	set @menuitems$[@c], "Você sabe fazer tintas?";
	set @menuID[@c], @EscolhaIntroTingimento;
	set @c, @c + 1;

L_main_menu:
	menu
		@menuitems$[0], -,
		@menuitems$[1], -,
		@menuitems$[2], -,
		@menuitems$[3], -,
		@menuitems$[4], -,
		@menuitems$[5], -;

	set @menu, @menu - 1;

	if (@menu >= @c) goto L_abort;
	if (@menuID[@menu] == @EscolhaIntroTingimento) goto L_dyeing_intro;
	if (@menuID[@menu] == @EscolhaCorante ) goto L_pick_colour;
	if (@menuID[@menu] == @EscolhaAbortar) goto L_abort;
	goto L_abort;

//---------------------------------------------------------------------

L_dyeing_intro:
	mes "[Raquel a Alquimista]";
	mes "Raquel para por um instante.";
	mes "\"Hmm, Eu não sou muito boa nisso... Só posso fazer tintas simples, daquelas que se usam em algodão e lã.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Para as tintas realmente boas eu precisaria de um poderoso catalizador, como um Spork Obsidian ou um Wumpus Egg...\"";
	mes "Raquel parece viajar em seus pensamentos.";
	next;
	menu
		"Eu só queria pintar roupas...", -,
		"O que é um 'Wumpus Egg'?", L_wumpus_egg_intro,
		"O que é um 'Obsidian Spork'?", L_obsidian_spork_intro,
		"Er, não faz mal...", L_abort;
	mes "[Raquel a Alquimista]";
	mes "\"Oh, apenas para roupas? Isso é fácil. Eu posso fazer nas cores vermelha, amarela, azul, laranja, rosa, verde, azul escura, preta e roxa, desde que você me consiga os ingredientes para isso.\"";
	next;
	set QUEST_tinta, 0;
	set QUEST_MASK1, (QUEST_MASK1 | MASK1_SABETINTA);
	goto L_pick_colour;

L_wumpus_egg_intro:
	mes "[Raquel a Alquimista]";
	mes "Os olhos de Raquel brilham.";
	mes "\"Ah, o Wumpus Egg é um dos catalizadores mais poderosos que existe. Infelizmente o Wumpus está praticamente extinto atualmente, dessa forma ele é muito raro.";
	mes "Se você encontrar algum, por favor me avise. Pagarei muito bem por ele!\"";
	close;

L_obsidian_spork_intro:
	mes "[Raquel a Alquimista]";
	mes "Raquel coça a cabeça.";
	mes "\"Hmm, obsidian é um material escuro parecido com vidro, normalmente encontrado perto de vulcões. Moldá-lo não é muito difícil, mas fazer isso sem que ele perca suas propriedades mágicas é quase impossível.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Obsidian sporks são então muito difíceis de fazer, mas dizem que é muito útil o seu uso na alquimia.";
	mes "Se você encontrar um por favor me avise!\"";
	close;

L_pick_colour:
	menu
		"Quais ingredientes você precisa?", L_description,
		"Vermelho", L_red,
		"Amarelo", L_yellow,
		"Azul claro", L_light_blue,
		"Verde", L_green,
		"Laranja", L_orange,
		"Rosa", L_pink,
		"Azul escuro", L_dark_blue,
		"Preto", L_black,
		"Roxo", L_purple,
		"Verde escuro", L_dark_green,
		"Na verdade nada.", L_abort;
	close;

L_description:
	mes "[Raquel a Alquimista]";
	mes "\"As tintas que conheço são feitas a partir de ervas. Alizarina para o vermelho, gamboge para o amarelo, colbalto para o azul e malva como agente de ligação para as cores escuras.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Algumas das cores precisam de mais ingredientes, incluindo algumas que você não encontra aqui, então eu tenho que cobrar por eles.";
	next;
	mes "[Raquel a Alquimista]";
	mes "Oh, e para o azul escuro e o roxo eu também preciso de uma pérola, sem pó de pérola não fica uma cor brilhante.\"";
	next;

L_description_quick:
	menu
		"O que você precisa para a tinta vermelha?", L_d_red,
		"O que você precisa para a tinta amarelo?", L_d_yellow,
		"O que você precisa para a tinta azul claro?", L_d_light_blue,
		"O que você precisa para a tinta verde?", L_d_green,
		"O que você precisa para a tinta laranja?", L_d_orange,
		"O que você precisa para a tinta rosa?", L_d_pink,
		"O que você precisa para a tinta azul escuro?", L_d_dark_blue,
		"O que você precisa para a tinta preto?", L_d_black,
		"O que você precisa para a tinta roxa?", L_d_purple,
		"O que você precisa para a tinta verde escuro?", L_d_dark_green,
		"Atualmente...", -;

	menu
		"Você pode fazer alguma tinta para mim, por favor?", L_pick_colour,
		"Onde consigo ervas?", L_d_herbs,
		"Onde consigo pétalas?", L_d_petals,
		"Onde encontro minério de ferro?", L_d_ore,
		"Onde encontro pérolas?", L_d_pearl,
		"Onde encontro gosma de verme?", L_d_maggot_slime,
		"Estou vendo. Obrigado e um bom dia para você!", L_abort;
	close;

L_d_herbs:
	mes "[Raquel a Alquimista]";
	mes "\"Arbustos de alizarina, mauva, cobalto e gamboge crescem por ai. Eu não tenho visto aqui em volta, mas se você procurar, tenho certeza de que vai encontrar algumas.\"";
	next;
	goto L_description_quick;

L_d_petals:
	mes "[Raquel a Alquimista]";
	mes "\"Basta pegar de algumas flores por ai. Mas cuidado que elas não gostam que toquem nelas.\"";
	next;
	goto L_description_quick;

L_d_ore:
	mes "[Raquel a Alquimista]";
	mes "\"Minério de ferro pode ser encontrado em algumas minas por ai. É onde as pessoas costumam encontrar.\"";
	next;
	goto L_description_quick;

L_d_pearl:
	mes "[Raquel a Alquimista]";
	mes "\"Uma pérola ... hmm, pode ser um pouco complicado. Você só encontra no fundo do mar, mas dizem os piratas possuem algumas.\"";
	next;
	goto L_description_quick;

L_d_maggot_slime:
	mes "[Raquel a Alquimista]";
	mes "\"Como o nome sugere, gosma de verme é mais encontrado a partir de vermes. Existe outras criaturas, como o morcego, que secreta uma gosma semelhante que para a alguimia funciona igual.\"";
	next;
	goto L_description_quick;

L_intermediate:
	menu "Você pode fazer uma tinta para mim?", L_pick_colour,
		"Você pode descrever os ingredientes?", L_description_quick,
		"Nem pensar.", L_abort;
	close;

L_ok:
	mes "[Raquel a Alquimista]";
	mes "\"Aqui está a sua tinta. lembre-se de usar tudo, ou a cor pode ficar desbotada.\"";
	next;
	goto L_pick_colour;

L_red:
	if (countitem(683) < 10) goto L_red_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem(683) > 10) goto L_TooMany;
	delitem 683, 10;
	getitem 690, 1;
	goto L_ok;

L_red_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Hmm, desculpe, mas eu preciso de 10 folhas de alizarina para fazer a tinta vermelha.\"";
	next;
	goto L_intermediate;

L_d_red:
	mes "[Raquel a Alquimista]";
	mes "\"Tinta vermelha é fácil, basta me trazer 10 folhas de alizarina que eu preparo imediatamente.\"";
	next;
	goto L_intermediate;

L_yellow:
	if (countitem(682) < 10) goto L_yellow_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem(682) > 10) goto L_TooMany;
	delitem 682, 10;
	getitem 693, 1;
	goto L_ok;

L_yellow_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Não há folhas de gamboge o bastante, eu preciso de pelo menos 10. Não posso preparar a tinta para você agora.\"";
	next;
	goto L_intermediate;

L_d_yellow:
	mes "[Raquel a Alquimista]";
	mes "\"Tinta amarela nada mais é que o extrato da folha de gamboge. Se você me conseguir 10 folhas posso facilmente preparar para você.\"";
	next;
	goto L_intermediate;

L_light_blue:
	if (countitem(681) < 10) goto L_light_blue_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem(681) > 10) goto L_TooMany;
	delitem 681, 10;
	getitem 694, 1;
	goto L_ok;

L_light_blue_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Desculpe, mas não posso fazer isso para você por menos de 10 folhas de cobalto.\"";
	next;
	goto L_intermediate;

L_d_light_blue:
	mes "[Raquel a Alquimista]";
	mes "\"Tinta azul claro é o resultado de se deixar 10 folhas de cobalto misturadas em álcool por uma noite. Eu já tenho alguns aqui e posso trocar por 10 folhas de cobalto.\"";
	next;
	goto L_intermediate;

L_green:
	if (countitem(681) < 20) goto L_green_fail;
	if (countitem(682) < 20) goto L_green_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem(681) > 20 && countitem(682) > 20) goto L_TooMany;
	if (zeny < 1000) goto L_green_fail;
	delitem 681, 20;
	delitem 682, 20;
	set zeny, zeny - 1000;
	getitem 691, 1;
	goto L_ok;

L_green_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Não... Eu preciso de muito cobalto e gamboge para preparar a tinta.\"";
	mes "\"Vinte folhas de gamboge, 20 de cobalto e 1000 GP deve ser o suficiente.\"";
	next;
	goto L_intermediate;

L_d_green:
	mes "[Raquel a Alquimista]";
	mes "\"Tinta verde é um pouco mais complicado de se fazer, você precisa ferver as folhas de cobalto na temperatura certa em um tubo de cobalto.";
	mes "Fazer um tubo de cobalto não é fácil e sempre que tento acabo perdendo um tubo de cristal...\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Assim, vou precisar de vinte folhas de gamboge, vinte de cobalto e 1000 GP para os matériais.";
	next;
	goto L_intermediate;

L_orange:
	if (countitem(683) < 10) goto L_orange_fail;
	if (countitem(682) < 10) goto L_orange_fail;
	if (countitem(640) < 2) goto L_orange_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem(683) > 10 && countitem(682) > 10 && countitem(640) > 2) goto L_TooMany;
	if (zeny < 1000) goto L_orange_fail;
	delitem 683, 10;
	delitem 682, 10;
	delitem 640, 2;
	set zeny, zeny - 1000;
	getitem 697, 1;
	goto L_ok;

L_orange_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Me desculpe, mas eu preciso de 1000 GP, dois pedaços de minério de ferro e 10 folhas de gamboge e alizarina, cada uma.\"";
	next;
	goto L_intermediate;

L_d_orange:
	mes "[Raquel a Alquimista]";
	mes "\"Tinta laranja é divertido de se fazer, mas requer ferro fundido e um pouco de pó de enxofre, o que não facil de se conseguir.";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Ainda assim se me trouxer 10 folhas de alizarina, 10 folhas de gamboge, 2 minérios de ferro e 1000 GP, eu posso preparar a tinta para você.\"";
	next;
	goto L_intermediate;

L_pink:
	if (countitem(683) < 10) goto L_pink_fail;
	if (countitem(565) < 6) goto L_pink_fail;
	if (zeny < 1000) goto L_pink_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem(683) > 10 && countitem(565) > 6) goto L_TooMany;
	delitem 683, 10;
	delitem 565, 6;
	set zeny, zeny - 1000;
	getitem 695, 1;
	goto L_ok;

L_pink_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Não, você não tem tudo que preciso, que seriam 10 folhas de alizarina, 6 pétalas de flores e 1000 GP.\"";
	next;
	goto L_intermediate;

L_d_pink:
	mes "[Raquel a Alquimista]";
	mes "\"Tinta rosa .... Essa é engraçada, você não consegue um rosa usando as folhas habituais. Você precisa de 10 folhas de alizarina e além disso de 6 pétalas de flores rosas.";
	next;
	mes "[Raquel a Alquimista]";
	mes "Para extrair corretamente o rosa das pétalas, precisa ferver-las. Preciso cobrar 1000 GP por isso.\"";
	next;
	goto L_intermediate;

L_dark_blue:
	if (countitem(681) < 100) goto L_dark_blue_fail;
	if (countitem(680) < 50) goto L_dark_blue_fail;
	if (countitem(700) < 1) goto L_dark_blue_fail;
	if (zeny < 10000) goto L_dark_blue_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem(681) > 100 && countitem(680) > 50 && countitem(700) > 1) goto L_TooMany;
	delitem 681, 100;
	delitem 680, 50;
	delitem 700, 1;
	set zeny, zeny - 10000;
	getitem 692, 1;
	goto L_ok;

L_dark_blue_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Vou precisar de todos os ingredientes para fazer a tinta azul escuro para você.\"";
	mes "\"Preciso de 100 folhas de cobalto, 50 folhas de malva, uma pérola e 10.000 GP.\"";
	next;
	goto L_intermediate;

L_d_dark_blue:
	mes "[Raquel a Alquimista]";
	mes "\"Azul escuro é uma das cores mais complicadas. Você precisa de um extrato concentrado de 100 folhas de cobalto e 50 folhas de malva para a ligação adequada.";
	next;
	mes "[Raquel a Alquimista]";
	mes "Para dar o brilho. uma pérola é indispensável, e a parte mais cara é um pó de safira que preciso cobrar 10.000 GP por ele.\"";
	next;
	goto L_intermediate;

L_purple:
	if (countitem(681) < 100) goto L_purple_fail;
	if (countitem(683) < 100) goto L_purple_fail;
	if (countitem(680) < 20) goto L_purple_fail;
	if (countitem(700) < 1) goto L_purple_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem(681) > 100 && countitem(683) > 100 && countitem(680) > 20 && countitem(700) > 1) goto L_TooMany;
	if (zeny < 40000) goto L_purple_fail;
	delitem 681, 100;
	delitem 683, 100;
	delitem 680, 20;
	delitem 700, 1;
	set zeny, zeny - 40000;
	getitem 698, 1;
	goto L_ok;

L_purple_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Não... Acho que você não tem todos os ingredientes que preciso, deixe eu ver...\"";
	mes "\"Eu preciso de 100 folhas de cobalto, 100 folhas de alizarina, 20 folhas de malva, uma pérola e de 40.000 GP.\"";
	next;
	goto L_intermediate;

L_d_purple:
	mes "[Raquel a Alquimista]";
	mes "\"Hmm, tinta roxa... alguns dos ingredientes são muito caros.";
	mes "Eu não posso lhe dizer o por que, já que são segredos da alquimia, e eu não quero discutir isso com pessoas que podem me fazer de palhaço.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Ainda assim, eu posso fazer isso para você, só preciso de 100 folhas de cobalto, 100 folhas de alizarina, 20 folhas de malva, uma pérola e 40.000 GP. O dinheiro será usado para os demais ingredientes.\"";
	next;
	goto L_intermediate;

L_black:
	if (countitem(681) < 40) goto L_black_fail;
	if (countitem(683) < 40) goto L_black_fail;
	if (countitem(682) < 40) goto L_black_fail;
	if (countitem(680) < 40) goto L_black_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem(681) > 40 && countitem(683) > 40 && countitem(682) > 40 && countitem(680) > 40) goto L_TooMany;
	if (zeny < 20000) goto L_black_fail;
	delitem 681, 40;
	delitem 683, 40;
	delitem 682, 40;
	delitem 680, 40;
	set zeny, zeny - 20000;
	getitem 696, 1;
	goto L_ok;

L_black_fail:
	mes "[Raquel a Alquimista]";
	mes "\"Me desculpe mas você não tem todos os ingredientes que preciso para fazer a tinta preta. Eu preciso de 40 folhas de cobalto, alizarina, gamboge e malva, cada uma e mais 20.000 GP.\"";
	next;
	goto L_intermediate;

L_d_black:
	mes "[Raquel a Alquimista]";
	mes "\"Preto? Ah! Uma bela escolha, é minha cor preferida! Bem, se você também quiser ficar bonito como eu, eu ficaria feliz em lhe preparar uma tinta preta. Apenas me traga 40 folhas, cada uma, de cobalto, alizarina, camboge e malva.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Ah e 20.000 GP para a mistura de carvão e pó de diamante que precisa para tirar o cheiro da malva.\"";
	next;
	goto L_intermediate;

L_dark_green:
	if (QUEST_tinta2 != 2) goto L_d_dark_green;

L_dark_green2:
	if (countitem(681) < 10) goto L_dark_green_fail;
	if (countitem(682) < 10) goto L_dark_green_fail;
	if (countitem(680) < 10) goto L_dark_green_fail;
	if (countitem(505) < 1) goto L_dark_green_fail;
	if (zeny < 1000) goto L_dark_green_fail;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem(681) > 10 && countitem(682) > 10 && countitem(680) > 10 && countitem(505) > 1) goto L_TooMany;
	delitem 681, 10;
	delitem 680, 10;
	delitem 682, 10;
	delitem 505, 1;
	set zeny, zeny - 1000;
	getitem 699, 1;
	goto L_ok;

L_dark_green_fail:
	mes "[Raquel a Alquimista]";
	mes "Raquel a testa enquanto analisa os ingredientes.";
	mes "\"Não, isso não serve. Eu preciso de 10 folhas de gamboge, 10 de cobalto, 10 de malva, gosma de verme e 1.000 GP.\"";
	next;
	goto L_intermediate;

L_d_dark_green:
	if (QUEST_MASK1 & MASK1_SABETINTA2) goto L_dark_green2;
	mes "[Raquel a Alquimista]";
	if (QUEST_tinta2 != 1) goto L_d_dark_green_quest;
	mes "\"Agora que sabemos quantas gramas é necessário para você, tudo que precisamos é de 1.000 GP, uma gosma de verme e 10 folhas de malva, cobalto e gamboge, cada uma.\"";
	next;
	goto L_intermediate;

L_d_dark_green_quest:
	mes "\"Ah, verde escuro... tinta verde escuro é até simples, exceto por um pequeno detalhe, a roupa desbota muito facilmente quando em contato com a pele humana. A melhor de resolver isso é usar um pó fixador, misturado com a essência extraida da gosma de verme.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Mas quanto do fixador é necessário vária de indivíduo para indivíduo. Temos que descobrir exatamente quantas miligramas do fixador é preciso usar no seu caso antes de preparar a tinta verde escuro.\"";
	next;
	set QUEST_clothdyer_dgreen, (rand(999));

L_d_dark_green_q_main:
	menu "Hmm... não obrigado.", L_intermediate,
		"Então ninguem mais pode usar a roupa que eu pintar?", L_dark_green_q_wear,
		"OK, o que preciso fazer?", L_dark_green_q_explain,
		"Vamos descobrir isso!", L_dark_green_q_guess_0;
	close;

L_dark_green_q_wear:
	mes "[Raquel a Alquimista]";
	mes "\"A cor estabiliza uma semana depois. Outras pessoas poderão utilizá-la, mas tenha certeza de que já passou o tempo para estabilizar a cor.\"";
	next;
	goto L_d_dark_green_q_main;

L_dark_green_q_explain2:
	mes "[Raquel a Alquimista]";
	mes "Raquel pisca.";
	mes "\"Muito bem. Funciona assim, você me dá uma gosma de verme e me diz quantas miligramas vão entrar na mistura da tinta.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Então, tentamos descobrir se essa era a quantidade certa. Eu vou lhe dizer se acertou ou não, em caso de erro eu digo se foi para mais ou para menos.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Com bastante gosmas de verme podemos descobrir o quanto você precisa.\"";
	next;
	menu "Er... você pode explicar de novo?", L_dark_green_q_explain2,
		"Vamos lá!", L_dark_green_q_guess_0,
		"Eu não tenho tempo para isso.", L_intermediate;
	close;

L_dark_green_q_explain:
	mes "[Raquel a Alquimista]";
	mes "\"Nós temos que verificar o equilíbrio de ácidez da sua pele e fazer com que a concentração de gosma na mistura seja a ideal para as suas condições.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Serão várias tentativas e para cada nova tentativa vamos precisar de uma gosma de verme.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Cabe a você me dizer em três etapas quantas miligramas do fixador pretende usar. Eu então preparo a mistura e em seguida você coloca o seu dedo dentro da mistura e vemos a reação.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Se a mistura ficar muito escura significa que colocamos muita gosma, se ficar muito claro significa que colocamos muito pouca e se ela manter sua cor, então acertamos a quantidade.\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "Raquel sorri.";
	mes "\"Mas não se preocupe eu farei os testes totalmente de graça. Me divirto um pouco nessa brincadeira.\"";
	next;

	menu "Er... pode explicar isso de novo?", L_dark_green_q_explain,
		"Errr... o que?", L_dark_green_q_explain2,
		"Vamos logo com isso!", -,
		"Não tenho tempo para isso.", L_intermediate;

L_dark_green_q_guess_0:
	if (countitem(505) < 1) goto L_dark_green_q_noslime;
	delitem 505, 1;
	mes "[Raquel a Alquimista]";
	mes "Raquel enche uma pequena garrafa com a gosma de verme e retira uma garrafa de vidro de dentro do seu manto.";
	next;
	mes "[Raquel a Alquimista]";
	mes "\"Agora precisamos descobrir exatamente quantas miligramas do fixador serão necessárias. Mas vamos por etapas. Primeiro me diga quantas miligramas você deseja utilizar.\"";
	next;

	menu "Nenhuma", -,
		"1 miligrama", -,
		"2 miligramas", -,
		"3 miligramas", -,
		"4 miligramas", -,
		"5 miligramas", -,
		"6 miligramas", -,
		"7 miligramas", -,
		"8 miligramas", -,
		"9 miligramas", -;

	set @menu, @menu - 1;
	set @guess_accumulator, @menu;

	mes "[Raquel a Alquimista]";
	if (@menu) mes "Raquel despeja boa parte da mistura dentro da garrafa de vidro.";
	mes "\"Temos agora " + @guess_accumulator + " miligramas no total. Agora me diga quantas centigramas você quer que eu adicione?\"";
	next;

	menu "Nada", -,
		"1 centigrama", -,
		"2 centigramas", -,
		"3 centigramas", -,
		"4 centigramas", -,
		"5 centigramas", -,
		"6 centigramas", -,
		"7 centigramas", -,
		"8 centigramas", -,
		"9 centigramas", -;

	set @menu, @menu - 1;
	set @guess_accumulator, @guess_accumulator + (@menu * 10);

	mes "[Raquel a Alquimista]";
	if (@menu) mes "Raquel despeja mais um pouco da gosma de verme dentro da garrafa de vidro.";
	mes "\"Temos agora um total de " + @guess_accumulator + " miligramas. Para finalizar, quantas decigramas você quer adicionar?\"";
	next;

	menu "Nada", -,
		"1 decigrama", -,
		"2 decigramas", -,
		"3 decigramas", -,
		"4 decigramas", -,
		"5 decigramas", -,
		"6 decigramas", -,
		"7 decigramas", -,
		"8 decigramas", -,
		"9 decigramas", -;

	set @menu, @menu - 1;
	set @guess_accumulator, @guess_accumulator + (@menu * 100);

	mes "[Raquel a Alquimista]";
	if (@menu) mes "Raquel despeja mais um pouco da gosma de verme dentro da garrafa de vidro.";
	mes "Raquel mistura mais alguns ingredientes na garrafa, agita bem e em seguida o aquece.";
	next;

	mes "[Raquel a Alquimista]";
	mes "\"Por favor coloque seu dedo aqui.\"";
	mes "Hesitante, você coloca o dedo dentro da mistura. Ela está quente, mas agradável.";
	next;
	mes "[Raquel a Alquimista]";
	mes "Você retira o dedo.";

	if (@guess_accumulator > QUEST_clothdyer_dgreen)
		goto L_dark_green_q_toomuch;

	if (@guess_accumulator < QUEST_clothdyer_dgreen)
		goto L_dark_green_q_toolittle;

	// otherwise correct guess

	mes "Nada parece acontecer a mistura, os olhos de Raquel se iluminam.";
	mes "\"É isso! Você precisa exatamente de " + @guess_accumulator + " miligramas. Vou anotar isso...\"";
	next;
	mes "[Raquel a Alquimista]";
	mes "Raquel está radiante.";
	mes "\"Agora posso fazer a tinta verde escuro para você. Tudo que eu preciso é uma gosma de verme, dez folhas de gamboge, dez folhas de cobalto, 10 de malva e 1.000 GP para os demais ingredientes.\"";
	mes "[Você ganhou 20,000 pontos de experiência]";
	getexp 20000, 0;
	set QUEST_tinta2, 0;
	set QUEST_clothdyer_dgreen, 0;
	set QUEST_MASK1, (QUEST_MASK1 | MASK1_SABETINTA2);
	next;
	goto L_intermediate;

L_dark_green_q_toomuch:
	mes "[Raquel a Alquimista]";
	mes "Após alguns segundos a mistura fica escura. Raquel fica desapontado.";
	mes "\"Isso foi muito, você precisa colocar menos de " + @guess_accumulator + " miligramas, tente de novo.";
	goto L_dark_green_q_again;

L_dark_green_q_toolittle:
	mes "[Raquel a Alquimista]";
	mes "Quase que instantaneamente, a mistura  perde toda cor e fica transparente. Raquel suspira.";
	mes "\"Isso foi muito pouco. Tente colocar mais de " + @guess_accumulator + " miligramas na próxima.";
	goto L_dark_green_q_again;

L_dark_green_q_again:
	mes "[Raquel a Alquimista]";
	mes "\"Vamos tentar de novo?\"";
	next;
	menu "Sim!", L_dark_green_q_guess_0,
		"Não, para mim já basta.", -;
	close;

L_dark_green_q_noslime:
	mes "[Raquel a Alquimista]";
	mes "\"Ah. você não tem nenhuma gosma de verme? Que pena, mas é fácil encontrar, consiga ao menos um e podemos continuar.\"";
	next;
	goto L_intermediate;

L_abort:
	close;

L_TooMany:
	mes "[Raquel a Alquimista]";
	mes "Você não tem espaço para carregar mais nada.";
	close;
}
