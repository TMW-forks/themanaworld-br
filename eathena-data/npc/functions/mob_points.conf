// Mob points

function	script	MobPoints	{
	if (@mobID==1) goto _presentePrateado; //< Ganha presente prateado

	if (@mobID < 1002) return;
	if (@mobID >=1302) goto _mobBR;

	setarray @points,
		1, // Maggot			[1002]
		2, // Scorpion
		20, // Red Scorpion
		10, // Green Slime
		30, // Giant Maggot
		15, // Yellow Slime
		25, // Red Slime
		45, // Black Scorpion
		50, // Snake			[1010]
		4, // Fire Goblin
		55, // Spider
		23, // Evil Mushroom
		35, // Flower
		40, // Santa Slime
		15, // Rudolph Slime
		2, // Bat
		16, // Pinkie
		17, // Shroom
		14, // Fluffy			[1020]
		25, // Cave Snake
		100, // Jack-O
		80, // Fire Skull
		80, // Poison Skull
		20, // Stumpy
		70, // Mountain Snake
		15, // Easter Fluffy
		40, // Mouboo
		0, // Mauve Plant
		0, // Gamboge Plant		[1030]
		0, // Cobalt Plant
		0, // Alizarin Plant
		20, // Sea Slime
		0, // Silk Worm
		0, // Zombie
		0, // Clover Patch
		2, // Squirrel			[1037]
		2; // Squirrel			[1038] (Não me pergunte o porque !!!)

	if (@mobID >1038) return;
	if (MPQUEST == 1) set Mobpt, Mobpt + @points[@mobID - 1002];

	// Contagem de monstros nas quests
	if (@mobID==1002 && QUEST_CorneliaVermes>1) set QUEST_CorneliaVermes, QUEST_CorneliaVermes+1;
	if (@mobID==1003 && QUEST_pabloEscorpioes>1) set QUEST_pabloEscorpioes, QUEST_pabloEscorpioes+1;

	return;

//=== TMW-BR ==========================================================

_mobBR:

	setarray @points_br,
		45, // Esqueleto			[1302]
		55, // Esqueleto Adaga		[1303]
		70, // Esqueleto Arqueiro	[1304]
		80, // Esqueleto Pirata		[1305]
		80, // Fada Furiosa			[1306]
		80, // Fada Traidora		[1307]
		180, // Dragao Verde		[1308]
		80, // Fada Guardia			[1309]
		60, // Aranha Rainha		[1310]
		0, // Cachorro				[1311]
		0, // ?						[1312]
		100, // Morte				[1313]
		30, // Cabeça de Abórora	[1314]
		50, // Cabeção de Abóbora	[1315]
		30, // Cabeça de Abórora	[1316]
		40, // Mouboo Rena			[1317]
		30, // Limo de Gelo			[1318]
		15; // Limo de Neve			[1319]

	if (MPQUEST == 1) set Mobpt, Mobpt + @points_br[@mobID - 1302];

//= Halloween =========================================================

	if ((@mobID==1318 || @mobID==1319) && NATAL_evento>0) goto _presentes;
_return1:
	//if (@mobID==1315 && NATAL_joana>1) goto _cachecolJoana;
//_return2:

	return;

//= sub rotinas =======================================================

_presentes:
	set @tmp, gettime(5);
	if (@tmp<24 || @tmp>28) goto _return1;
	set @h, gettime(3)-3;    // Fuso horário
	if (@h<0) set @h, @h+24; // Correção
	if (@h==8 || @h==14 || @h==20) goto _rand;
	goto _return1;

_rand:
	if (@h==8)  set @n, (@tmp-24)*3 +1;
	if (@h==14) set @n, (@tmp-24)*3 +2;
	if (@h==20) set @n, (@tmp-24)*3 +3;
	
	set @tmp, rand(100);
	if (@tmp<4) goto _presentePrateado;
	if (@tmp<8 && NATAL_pDourado<@n) goto _presenteDourado;
	goto _return1;

_presentePrateado:
	getitem "presentePrateado", 1;
	set NATAL_evento, NATAL_evento+1;
	message strcharinfo(0), "eba!";
	
	// Se puder entrar no final do placar || ser o primeiro a entrar na lista
	if ($NATAL_vetPlacar[$NATAL_numPlacar-1]<NATAL_evento-1 && $NATAL_numPlacar!=0) goto _pular1; //< Se não puder: _pula1
	//else
		set $NATAL_nomPlacar$[$NATAL_numPlacar], strcharinfo(0);
		set $NATAL_vetPlacar[$NATAL_numPlacar], NATAL_evento-1;
		set $NATAL_lvlPlacar[$NATAL_numPlacar], BaseLevel;
		if ($NATAL_numPlacar<15) set $NATAL_numPlacar, $NATAL_numPlacar+1;
		goto _return1;
	_pular1:
	
	// Buscar nome no placar
	set @tmp, $NATAL_numPlacar; //< O padão é o ultimo da placar
	set @i, -1;
	_loop1: set @i,@i+1; if(@i>=$NATAL_numPlacar) goto _fimLoop1;
		if ($NATAL_nomPlacar$[@i]!=strcharinfo(0)) goto _loop1;
		//else
			set $NATAL_vetPlacar[@i], NATAL_evento-1;
			set $NATAL_lvlPlacar[@i], BaseLevel;
			set @tmp, @i;
			goto _fimLoop1;
		//goto _loop1;
	_fimLoop1:
	
	// Subir no placar
	set @i, @tmp; //< Indice encontrado no loop1
	set @tmp, 0;
	_loop2: set @i,@i-1; if(@i<0) goto _fimLoop2;
		if ($NATAL_vetPlacar[@i]>=NATAL_evento-1) set @i, -1;
			if(@i<0) goto _fimLoop2;
		//else
			set @tmp, 1;
			set $NATAL_nomPlacar$[@i+1], $NATAL_nomPlacar$[@i];
			set $NATAL_vetPlacar[@i+1], $NATAL_vetPlacar[@i];
			set $NATAL_lvlPlacar[@i+1], $NATAL_lvlPlacar[@i];
			set $NATAL_nomPlacar$[@i], strcharinfo(0);
			set $NATAL_vetPlacar[@i], NATAL_evento-1;
			set $NATAL_lvlPlacar[@i], BaseLevel;
		goto _loop2;
	_fimLoop2:
	
	if (@tmp==1 && $NATAL_numPlacar<15) set $NATAL_numPlacar, $NATAL_numPlacar+1;
	
	goto _return1;

_presenteDourado:
	getitem "presenteDourado", 1;
	set NATAL_pDourado, @n;
	message strcharinfo(0), "Ooo!";
	goto _return1;

//_cachecolJoana:
//	set NATAL_joana, NATAL_joana+1;
//	if (NATAL_joana==11) getitem "cachecolJoana", 1;
//	goto _return2;

}
