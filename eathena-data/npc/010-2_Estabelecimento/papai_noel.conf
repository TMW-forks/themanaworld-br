//
// @autor Diogo_RBG - http://diogorbg.blogspot.com
// @desc O bom velhinho está te esperando em seu chalé nas montanhas.
// #quest <desc>, <getItens>, <delItens>, <extra>
// @quest 'Biscoitos de Natal', '1 presenteDourado', '9-15 santaCookie', ''
// @quest 'Gorro de Papai Noel', '1 SantaBeardHat', '1 presenteDourado', ''

//010-2.gat,90,21,0	script	debug	105, {
//	mes "resetado";
//	set QUEST_biscoitoNatal, 0;
//	close;
//}

010-2.gat,93,21,0	script	Papai Noel	105, {
	setarray @bomMenino$, "uma boa menina", "um bom menino";

	if (QUEST_biscoitoNatal==0 && countitem("santaCookie")>8) goto _biscoitos;
	if (QUEST_gorroBarba==0 && countitem("presenteDourado")) goto _presenteDourado;

	mes "[Papai Noel]";
	mes "\"How How How!!! Feliz Natal!\"";
	next;
	menu
		"PAPAI NOEL!!!", -,
		"Olá Papai Noel.", -,
		"(sem palavras!)", -;
	mes "[Papai Noel]";
	mes "\"Você foi " +@bomMenino$[sex]+ " este ano?\"";
	next;
	menu
		"Fui sim.", -,
		"Eu sou " +@bomMenino$[sex]+ ".", -,
		"Vim te ajudar com os presentes de natal.", _ajudar;
	mes "[Papai Noel]";
	mes "\"Muito bem! Continue assim.\"";
	close;

_ajudar:
	mes "[Papai Noel]";
	mes "\"Então o Faísca deve ter te contado sobre o acidente com os presentes de natal.";
	mes "Isso me preocupa demais. Minha barba até tá ficando branca de tanta preocupação!";
	mes "How How How!!!\"";
	next;
	menu
		"Mas sua barba sempre foi branca.", -,
		"Kkk... num perde o senso de humor!", -;
	mes "[Papai Noel]";
	mes "\"Sei que estamos atrasados com o natal, mas no final tudo vai ficar bem.";
	mes "E também não é o primeiro acidente que já tivemos com os presentes.";
	mes "Pois uma vez o Faísca colocou fogo no depósito de embalagens faltando um mês pro natal... foi por pouco que não atrasamos o natal!";
	mes "How How How!!!\"";
	next;
	mes "[Papai Noel]";
	mes "\"Meus duendes estão encarregados de coletar os presentes de natal. Entregue os presentes a eles.\"";
	next;
	mes "[Papai Noel]";
	mes "\"Tenha um feliz natal. How How How!!!\"";
	close;

//---------------------------------------------------------------------

_biscoitos:
	mes "[Papai Noel]";
	mes "\"Humm... que cheirinho bom!";
	mes "Você tem aqueles deliciosos biscoitos de Natal?\"";
	next;
	menu
		"Tenho alguns.", _tenho,
		"Eu estava juntando eles pra você.", _tenho,
		"Não tenho biscoitos.", -;
	mes "[Papai Noel]";
	mes "\"How How How!";
	mes "Então acho que meu nariz está me enganando.";
	mes "Ou talvez sejam alguns farelos dos que comi hoje de manhã!\"";
	close;

_tenho:
	mes "[Papai Noel]";
	mes "\"Eu sabia! Nunca me engano quanto a isso!";
	mes "...";
	mes "Papai Noel pode comer alguns biscoitos com você?\"";
	next;
	menu
		"Claro Papai Noel!", -,
		"Sabia que você ia querer!", -,
		"Eu sou " +@bomMenino$[sex]+ "!", -;
	mes "[Papai Noel]";
	mes "\"Papai Noel até vai te dar este presentinho!\"";
	next;
	if (countitem("santaCookie")<9) goto _semBiscoito;
	getinventorylist;
	if( (@inventorylist_count + (countitem("santaCookie")>15) + (countitem("presenteDourado")==0) ) > 100 ) goto _cheio;

	mes "[Papai Noel]";
	mes "CrOoOck CroOock CrOOOck!";
	mes "\"Que delícia!\"";
	mes "";
	mes "* Você ganhou um [Presente Dourado].";
	set @tmp, countitem("santaCookie");
	if (@tmp>15) set @tmp, 15; // Retira até 15 biscoitos.
	delitem "santaCookie", @tmp;
	getitem "presenteDourado", 1;
	set QUEST_biscoitoNatal, 1;
	close;

_semBiscoito:
	mes "[Papai Noel]";
	mes "\"Que pena, você não tem o suficiente para nós dois.\"";
	close;

_cheio:
	mes "[Papai Noel]";
	mes "\"Você está carregando muita coisa.\"";
	close;

//---------------------------------------------------------------------

_presenteDourado:
	mes "[Papai Noel]";
	mes "\"Ainda não abriu o seu presente dourado?\"";
	next;
	menu
		"O que tem nele?", _explica,
		"Presente dourado?", _explica,
		"Depois eu abro.", -;
	mes "[Papai Noel]";
	mes "\"Tudo bem então.\"";
	close;

_explica:
	mes "[Papai Noel]";
	mes "\"O presente dourado possui uma magia especial de natal de ocultar o presente nele guardado. Fazendo que ele sempre seja uma surpresa.\"";
	next;
	mes "[Papai Noel]";
	mes "\"As vezes ele pode te surpreender com um presente muito especial! Lhe dando o que você mais deseja caso ele sinta uma forte presensa do seu espírito de natal.";
	mes "";
	mes "Deseja abrí-lo agora?\"";
	next;
	menu
		"SIM!!!", -,
		"Vou abrir agora mesmo!", -,
		"Estou repleto do espírito do natal.", -;
	if (countitem("presenteDourado")==0) goto _semPresente;
	getinventorylist;
	if( (@inventorylist_count + (countitem("presenteDourado")>1) + 1) > 100 ) goto _cheio;

	mes "[Papai Noel]";
	mes "\"Parece que você gosta muito do Papai Noel!";
	mes "How How How!!!\"";
	mes "";
	mes "* Você ganhou um [Gorro de Papai Noel].";
	delitem "presenteDourado", 1;
	getitem "SantaBeardHat", 1;
	set QUEST_gorroBarba, 1;
	close;

_semPresente:
	mes "[Papai Noel]";
	mes "\"Não esconda seu presente.\"";
	close;
}
