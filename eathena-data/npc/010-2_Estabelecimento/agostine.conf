//
// @author Shaili and QOAL
// @translate Diogo_RBG
 
//010-2.gat,28,21,0	script	debug	137,{
//	set QUEST_agostine, 0;
//	mes "resetado.";
//	close;
//}

010-2.gat,31,21,0	script	Agostine	137,{
	setarray @meuAmigo$, "minha amiga", "meu amigo";
	// @en "my friend" "my friend"
	setarray @obrigado$, "obrigada", "obrigado";
	// @en "thanks" "thanks"
	setarray @charmoso$, "charmosa", "charmoso";
	// @en "glamourous" "glamourous"

	if (QUEST_agostine == 1) goto _estado_0_3;
	if (QUEST_agostine == 2) goto _estado_1;
	if (QUEST_agostine == 3) goto _estado_2;
	if (QUEST_agostine == 4) goto _estado_4;
	if (QUEST_agostine == 5) goto _estado_4_success;
	if (QUEST_agostine == 6) goto _estado_6;
	if (QUEST_agostine == 7) goto _estado_11;
	if (QUEST_agostine == 8) goto _estado_12;
	if (QUEST_agostine >= 9) goto _estado_13;

	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Isto vai para cima, esta vai para a esquerda...\"";
	// @en "\"This goes up, this goes left...\""
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Mmmm...?\"";
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Oh, um cliente! Deixe-me apresentar! Meu nome é Agostine!";
	// @en "\"Oh, a customer! Let me introduce myself! My name is Agostine!"
	mes "Algumas pessoas dizem que eu sou o melhor alfaiate do mundo, mas eu acho que sou o melhor do universo!";
	// @en "Some people say that I am the best tailor in the world, but I think I am the best one in the universe!"
	mes "Então, o que posso fazer por você, " +@meuAmigo$[sex]+ "?\"";
	// @en "So, What can I do for you, " "?\""
	next;
	menu
		"Eu quero algo novo para o meu guarda-roupa!", _estado_0_1,
		// @en "I want something new for my wardrobe!"
		"Oh, nada, " +@obrigado$[sex]+ "!", -;
		// @en "Oh, nothing, " "!"
	close;

// Quest #0 - Beginning - Needed: Iron Potion

_estado_0_1:
	setarray @seuGarcon$, "sua garçonete", "seu garçon";
	// @en "your waiter" "your waiter"

	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Bem.. você sabe... trabalhar tão duro que me deixa tão cansado!";
	// @en "\"Well.. you know... working so hard makes me so tired!"
	mes "Você pode me trazer algo saboroso para beber, " +@meuAmigo$[sex]+ "?\"";
	// @en "Can you bring me something tasty to drink, " "?\""
	next;
	menu
		"Claro, que tipo de bebida você gosta?", _estado_0_2,
		// @en "Sure, what kind of drink you like?"
		"Eu não sou " +@seuGarcon$[sex]+ "!", -;
		// @en "I'm not " "!"
	close;

_estado_0_2:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Aqui não temos um bom bar. Deixe-me saborear bebidas diferentes.";
	// @en "\"Here we do not have a good bar. Let me taste different beverages.";
	mes "Quero algo novo para beber.\"";
	// @en "I want something new to drink.\"";
	next;
	menu
		"Eu vou lhe trazer bebidas diferentes para experimentar!", _estado_Accept,
		// @en "I'll bring you different drinks to try!"
		"Talvez um bom bar será aberto em breve, espere-o.", -;
		// @en "Maybe a good bar will open soon, wait for it."
	close;

_estado_Accept:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Muito obrigado.\"";
	// @en "\"Thanks so much.\""
	set QUEST_agostine, 1;
	close;

_estado_0_3:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Bem, o que você me trouxe?\"";
	// @en "\"Well, what have you brought me?\""
	next;
	menu
		"Uma [Bebida de Cactus].", _estado_0_4,
		// @en "A [Bebida de Cactus]."
		"Uma [Poção de Cactus].", _estado_0_5,
		// @en "A [Poção de Cactus]."
		"Um pouco de [Leite].", _estado_0_6,
		// @en "Some [Leite]."
		"Um copo de [Cerveja].", _estado_0_7,
		// @en "A pint of [Cerveja]."
		"Uma [Poção Energética].", _estado_0_9,
		// @en "A [Poção Energética]."
		"Uma [Poção de Concentração].", _estado_0_8,
		// @en "A [Poção de Concentração]."
		"Nada, no momento.", -;
		// @en "Nothing, at the moment."
	close;

_estado_0_4:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Humm, uma [Bebida de Cactus]?\"";
	// @en "\"So, a [Bebida de Cactus]?\""
	if (countitem("CactusDrink") < 1) goto _estado_neg;
	next;
	delitem "CactusDrink", 1;
	goto _estado_bad;
	close;

_estado_0_5:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Humm, uma [Poção de Cactus]?\"";
	// @en "\"So, a [Poção de Cactus]?\""
	if (countitem("CactusPotion") < 1) goto _estado_neg;
	next;
	delitem "CactusPotion", 1;
	goto _estado_bad;
	close;

_estado_0_6:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Humm, um pouco de [Leite] para mim?\"";
	// @en "\"So, some [Leite] for me?\""
	if (countitem("Milk") < 1) goto _estado_neg;
	delitem "Milk", 1;
	goto _estado_bad;
	close;

_estado_0_7:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Humm, uma [cerveja]?\"";
	// @en "\"So, a [cerveja]?\""
	if (countitem("Beer") < 1) goto _estado_neg;
	next;
	delitem "Beer", 1;
	goto _estado_bad;
	close;

_estado_0_8:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Humm, uma [Poção de Concentração]?\"";
	// @en "\"So, a [Poção de Concentração]?\"";
	if (countitem("ConcentrationPotion") < 1) goto _estado_neg;
	next;
	delitem "ConcentrationPotion", 1;
	goto _estado_bad;
	close;

_estado_0_9:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Humm, uma [Poção Energética]?\"";
	// @en "\"So, an [Poção Energética]?\""
	if (countitem("IronPotion") < 1) goto _estado_neg;
	next;
	delitem "IronPotion", 1;
	mes "Agostine parece satisfeito pela forma que ele bebe a poção.";
	// @en "Agostine looks pleased as he drinks the potion."
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Oh! Isso é muito bom! Eu me sinto um novo homem, " +@meuAmigo$[sex]+ "!\"";
	// @en "\"Oh! This is damn good! I feel a new man, " "!\""
	next;
	set zeny, zeny + 500;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Esta é uma pequena recompensa por sua ajuda. Pegue 500 GP, " +@meuAmigo$[sex]+ "!\"";
	// @en "\"This is a little reward for your help. Take 500 GP, " "!\""
	mes "";
	mes "* Você recebeu 500 GP.";
	// @en "* You received 500 GP."
	set QUEST_agostine, 2;
	close;

_estado_neg:
	mes "";
	mes "\"Parece que você mentiu. Você não tem a bebida que você me disse.\"";
	// @en "\"Seems that you lied. You don't have the drink you told me.\""
	close;

_estado_bad:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Bem, eu não gostei muito. Traga-me algo mais.\"";
	// @en "\"Well, I don't like it so much. Bring me something else.\"";
	close;

// First Quest: edited version of Forest Bow Quest - Needed: about 30 White Furs and 15.000 GP

_estado_1:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Mmmm... Deixe-me ver... Este ano tons claros estão na moda!";
	// @en "\"Mmmm... Let me see... This year light colours are fashionable!"
	mes "Gostaria de um par de [Luvas de Inverno] branco como a neve?\"";
	// @en "Would you like a pair of snow-white [Luvas de Inverno]?\""
	next;
	menu
		"Não, " +@obrigado$[sex]+ ". Eu prefiro roupas escuras...", -,
		// @en "No, " ". I love dark clothes..."
		"Você realmente pode fazer isso por mim?", _estado_2;
		// @en "You really can do this for me?"
	close;

_estado_2:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Bem, [Luvas de Inverno] são tão elegantes por sua pele adorável!";
	// @en "\"Well, [Luvas de Inverno] are so fashionable for their lovable fur!"
	mes "Somente as melhores peles podem servir para estas luvas!";
	// @en "Only the best furs can be saw for these gloves!"
	mes "Mas, como você vê, eu sou um alfaiate, não um caçador! Então, por que você não me traz o melhor do pêlo macio que você puder encontar?\"";
	// @en "But, as you see, I am a tailor, not a hunter! So, why don't you bring me the best fluffy's fur you can see?\""
	set QUEST_agostine, 3;
	next;
	menu
		"Você está louco! Eu não vou matar nenhum animal para isso!", -,
		// @en "You are crazy! I won't kill any animal for this!"
		"Claro, eu voltarei em breve!", _estado_3;
		// @en "Sure, I will be back soon!"
	close;

_estado_3:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Lembre-se, eu quero a melhor [Pele Branca]!\"";
	// @en "\"Remember, I want the best of [Pele Branca]!\""
	set QUEST_agostine, 4;
	close;

_estado_4:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Oh, " +@meuAmigo$[sex]+ "! Você já me trouxe alguma boa [Pele Branca]?\"";
	// @en "\"Oh, " "! Have you brought me some good [Pele Branca]?\""
	if (countitem("WhiteFur") < 1) close;
	next;
	menu
		"Aqui, dê uma olhada!", _estado_4_try,
		// @en "Here, take a look!"
		"Sim, mas eu preciso dela para outra coisa.", -;
		// @en "Yes, but I need it for something else."
	close;

_estado_4_try:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Deixe-me ver, " +@meuAmigo$[sex]+ "! Ela precisa ser perfeita para minhas luvas!\"";
	// @en "\"Let me see, " "! It needs to be perfect for my gloves!\"";
	mes "";
	mes "Agostine analisa o pedaço de pele.";
	// @en "Agostine examines the patch of fur.";
	next;
	delitem "WhiteFur", 1;
	set @Temp1,rand(30);
	if (@Temp1 == 0) goto _estado_4_success;
	mes "Agostine joga fora a [Pele Branca].";
	// @en "Agostine throws away the [Pele Branca]."
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Foi terrivelmente cortada! Você deveria prestar mais atenção quando você for matar Tufos!\"";
	// @en "\"It was awfully cut! You should pay more attention when you kill Tufos!\""
	if (countitem("WhiteFur") < 1) close;
	next;
	menu
		"Aqui, tenho outra!", _estado_4_try,
		// @en "Here I have another one!"
		"Desculpe, vou tomar cuidado...", -;
		// @en "Sorry, I will be careful..."
	close;

_estado_4_success:
	mes "Agostine retorna um sorriso.";
	// @en "Agostine turns into a smile."
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"É perfeito! Perfeito! Bom trabalho, " +@meuAmigo$[sex]+ "! Vou preparar as luvas agora mesmo!\"";
	// @en "\"It's perfect! Perfect! Good job, " "! I will prepare your gloves right now!\""
	next;
	set QUEST_agostine, 5;
	goto _estado_5;

_estado_5:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Ehm, " +@meuAmigo$[sex]+ ", meu trabalho não é de graça. Eu preciso de 15.000 GP para o trabalho.\"";
	// @en "\"Ehm, " ", my work is not free. I need 15,000 GP for the work.\"";
	next;
	menu
		"Você não é um alfaiate, você é um ladrão!!", -,
		// @en "You are not a tailor, you are a thief!!"
		"Eu acho que é um preço razoável...", _estado_5_pay;
		// @en "I think it's a reasonable price..."
	close;

_estado_5_pay:
	if (zeny < 15000) goto _estado_5_nocash;
	getinventorylist;
	if (@inventorylist_count == 100) goto _semEspaco;
	set zeny, zeny - 15000;
	getitem "WinterGloves", 1;
	set QUEST_agostine, 6;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Aqui estão elas. Você vai ter as mãos mais elegantes do mundo!\"";
	// @en "\"Here they are. You will have the more fashionable hands in the world!\""
	close;

_estado_5_nocash:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Parece que você está sem dinheiro. Volte quando você tiver o dinheiro.\"";
	// @en "\"Seems like you are out of cash. Come back when you have the money.\""
	close;

_estado_6:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Oh, eu lembro de você! Suas luvas são tão elegantes, " +@meuAmigo$[sex]+ "!";
	// @en "\"Oh, I remember you! Your gloves are sooo fashionable, " "!"
	mes "Bem, eu tenho uma boa notícia para você. Nesta edição do 'TMW-BR Fashion' eu vi um bonito acessório, um par de [Botas de Inverno], " +@meuAmigo$[sex]+ ".";
	// @en "Well, I have a good news for you. In this issue of 'TMW-BR Fashion' I saw a pretty accessory, a pair of [Botas de Inverno], " "."
	mes "Eu posso fazer seu próprio par, se você quiser!\"";
	// @en "I can make your own pair, if you want!\""
	next;
	menu
		"É uma ótima idéia!", _estado_10,
		// @en "It's a great idea!"
		"Já estou bastante " +@charmoso$[sex]+ ", " +@obrigado$[sex]+ ".", -;
		// @en "I'm " " enough, " "."
	close;

// Start of Second Quest: Furry Boots - Needed: About 30 White Furs, 1 pair of Boots and 15.000 GP

_estado_10:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Perfeito! Para satisfazer o seu pedido, eu preciso de outra perfeita [Pele Branca] e um par de [Botas], como aquelas que você pode encontrar nas minas.";
	// @en "\"Perfect! For satisfying your request I need another perfect [Pele Branca] and a pair of [Botas], like those you can find in the mines."
	mes "Elas vão ficar ótimas em você, " +@meuAmigo$[sex]+ "!\"";
	// @en "They will make you lovely, " "!\""
	set QUEST_agostine, 7;
	close;

_estado_11:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Então, " +@meuAmigo$[sex]+ ", me trouxe uma correta [Pele Branca]?\"";
	// @en "\"So, " ", have you brought me the right [Pele Branca]?\""
	if (countitem("WhiteFur") < 1) close;
	next;
	menu
		"Claro, eu sou um caçador de Tufos", _estado_11_try,
		// @en "Sure, I'm a fluffy hunter!"
		"Ainda não, desculpe.", -;
		// @en "Not yet, sorry."
	close;

_estado_11_try:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Eu prefiro ver o pedaço de pele primeiro.\"";
	// @en "\"I'd rather see the patch of fur, first.\""
	mes "";
	mes "Agostine analisa o pedaço de pele.";
	// @en "Agostine examines the patch of fur."
	next;
	delitem "WhiteFur", 1;
	set @Temp2,rand(30);
	if (@Temp2 == 0) goto _estado_11_success;
	mes "Agostine rasga a [Pele Branca].";
	// @en "Agostine rips the [Pele Branca]."
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Essa pele era terrível. Eu não vou trabalhar com ela!\"";
	// @en "\"That fur was terrible. I won't work with it!\""
	if (countitem("WhiteFur") < 1) close;
	next;
	menu
		"Bem, talvez esta seja a melhor!", _estado_11_try,
		// @en "Well, maybe this is better!"
		"Vou caçar outros Tufos...", -;
		// @en "I will hunt other fluffies..."
	close;

_estado_11_success:
	mes "Agostine retorna um sorriso.";
	// @en "Agostine turns into a smile.";
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Esta é boa o suficiente, " +meuAmigo$[sex]+ ". Bom trabalho.\"";
	// @en "\"This one is good enough, " ". Good job.\""
	delitem "WhiteFur", 1;
	set QUEST_agostine, 8;
	goto _estado_12;

_estado_12:
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Eu preciso também de um par de [Botas] para o trabalho.";
	// @en "\"I need also a pair of [Botas] for the work."
	mes "E o meu pagamento, obviamente. 15.000 GP mais precisamente.\"";
	// @en "And my payment, obviously. 15,000 GP is right.\""
	next;
	menu
		"Claro, aqui está tudo.", _estado_12_pay,
		// @en "Sure, here it is all."
		"Eu perdi alguma coisa, já volto!", -;
		// @en "I missed something, I will be back soon!
	close;

_estado_12_pay:
	if (zeny < 15000) goto _estado_12_missing;
	if (countitem("Boots") < 1) goto _estado_12_missing;
	// No inventory check needed, as boots are removed, opening a slot
	set zeny, zeny - 15000;
	delitem "Boots", 1;
	getitem "FurBoots", 1;
	set QUEST_agostine, 9;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Aproveite suas novas botas, " +meuAmigo$[sex]+ "!\"";
	// @en "\"Enjoy your new boots, " "!\""
	close;

_estado_12_missing:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Parece que você esqueceu algo. Verifique o seu inventário e seus bolsos!\"";
	// @en "\"Seems you have forgotten something. Check your inventory and your pockets!\""
	close;

_estado_13:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Você está tão " +@charmoso$[sex]+ ", " +@meuAmigo$[sex]+ "!";
	// @en "\"You are so " ", " "!"
	mes "Eu tenho feito um exelente trabalho em sua roupa!\"";
	// @en "I have done a very good job on your clothes!\""
	close;

_semEspaco:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Você não tem lugar para colocá-los. Volte quando tiver.\"";
	// @en "\"You don't have anywhere to put them. Come back when you do.\""
	close;
}
