//
//#################################################################################
//#               Zeca o fazendeiro                                               #
//# Este script contém as definições para obtenção de uma foice normal.           #
//#                                                                               #
//# Requisitos: 10 Dez antenas de trasgo rosa                                     #
//#                                                                               #
//# recompensa: Foice                                                             #
//#                                                                               #
//#################################################################################
//

//004.gat,109,113,0	script	debug	142,{
//	set QUEST_foice, 0;
//	mes "resetado.";
//	close;
//}

004.gat,112,113,0	script	Zeca	142,{
_inicio_:
	if (getequipid(equip_head) == 751 && esteChapeu == 0) goto _hasteRosa;
	if (getequipid(equip_head) == 751 && esteChapeu == 1 && QUEST_foice == 2) goto _hasteTrofeu;
	if (QUEST_foice == 0) goto _inicio;
	if (QUEST_foice == 1) goto _retornoFoice;
	if (QUEST_foice == 2) goto _obrigadoFoice;

_hasteRosa:
	mes "O fazendeiro lhe espeta com um ancinho.";
	next;
	heal -50, 0;
	next;
	mes "\"Mardita Rosinha somi daqui sua disgramada!\"";
	next;
	menu
		"To saindo fora daqui.", -,
		"Opa, o que você está fazendo?", _hastePorque;
	close;

_hastePorque:
	set esteChapeu, 1;
	mes "[Zeca o fazendeiro]";
	mes "\"Disculpa ieu, é qui ocê aparecia qui nem aquelas mardita Rosinha.\"";
	next;
	if (QUEST_foice == 0) goto _ajuda;
	close;

_hasteTrofeu:
	mes "[Zeca o fazendeiro]";
	mes "\"É meió que isso na sua muringa seja um troféu.\"";
	next;
	menu
		"Claro que é.", -,
		"Isso é uma homenagem aos adoráveis Trasgos Rosas", _hasteFim;
	mes "[Zeca o fazendeiro]";
	mes "\"Que bão, brigado pela ajuda.\"";
	close;

_hasteFim:
	if (Sex == 0) set $@mardita$, "Mardita adoradora";
	if (sex == 1) set $@mardita$, "Mardito adorador";
	mes "[Zeca o fazendeiro]";
	mes "\"" + $@mardita$ + " de rosinhas vo te insiná a num mexe cum os mais velho!\"";
	mes "O fazendeiro lhe espeta com uma foice.";
	heal -250, 0;
	close;

_inicio:
	mes "[Zeca o fazendeiro]";
	mes "\"Marditas Rosinha... vão cabá com minha roça. Ieu deio elas!";
	mes "Óia se ieu fosse mais jóvi ieu cabava cum essas mardita qua minhas póprias MÂO!\""; 
	next;
	menu
		"Elas me parecem inofensivas, será que o senhor não está exagerando um pouco?", _razao,
		"Calma senhor, se continuar assim vai infartar!.", -;
	close;

_razao:
	mes "[Zeca o fazendeiro]";
	mes "\"Izagerandu? Óia, essas rosinha tão cabando cum a minha colheita, se eu não caba cum elas, ieu vô perde tudo!";
	mes "Mais o pobrema é qui ieu num tenho mais idadi para saí correno atrais delas tudo.\"";
	next;
	menu
		"O senhor quer que eu ajude?", _ajuda,
		"É meu senhor, a vida não é justa.", -;
	close;

_ajuda:
	mes "[Zeca o fazendeiro]";
	mes "\"Ié... Ocê parece qui sabe corrê, acho que ocê cunsegui caba cum umas quanta daquelas mardita rosinha, talveis elas num me incomode mais.";
	mes "Se ocê cunsegui me traiz pur favô 10 antena rosa só pra pruvá que ocê matô aquelis trasti tá??\"";
	set QUEST_foice, 1;
	next;
	menu
		"Claro, estou a caminho", -,
		"O quanto isso tem valor para o senhor?", _recompensa;
	close;

_recompensa:
	mes "[Zeca o fazendeiro]";
	mes "\"Ora ieu quero que minha colheita fique bem, mais ieu sô pobre e num posso ti dâ muita coisa, ieu posso dá procê uma foice daí ocê usa ela como arma né.\"";
	close;

_retornoFoice:
	mes "[Zeca o fazendeiro]";
	mes "\"Ocê pegô as deiz anteninha?\"";
	next;

	menu
		"Ainda não, mas não se preocupe eu vou trazer elas para o senhor.", -,
		"Claro, aqui estão!", _trocaFoice;
	close;

_trocaFoice:
	if (countitem("antenaRosa") < 10) goto _oceMingano;
	getinventorylist;
	delitem "antenaRosa", 10;
	getitem "foice", 1;
	set QUEST_foice, 2;
	mes "[Zeca o fazendeiro]";
	mes "\"Isso é bão! Tô muito feliz que ocê me deu as anteninha, acho que agora aquelas mardita vão pará de incomodá, pega a foice qui ti prumeti, ela podi sê meio disingonçadinha, mais machuca bastanti.\""; 
	close;

_oceMingano:
	mes "[Zeca o fazendeiro]";
	mes "\"Ocê mi inganô, pufavô trais as 10 anteninha e insina uma lição praquelas mardita.\"";
	close;

_obrigadoFoice:
	if (getequipid(equip_head) == 751) goto _hasteTrofeu;
	mes "[Zeca o fazendeiro]";
	mes "\"Como é bão vê ocê dinovo, obrigado pur me ajudá cum as rosinhas, aquelas mardita pararu de cume minhas verdura!\"";
	close;
}
