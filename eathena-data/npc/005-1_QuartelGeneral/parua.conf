// Parua's fight - Original from Kage
// Adapted to the brazilian server and translated to portuguese by alastrim
// Adaptado do original desenvolvido por kage


005-1.gat,38,46,0	script	Parua	307,{

if isin("005-1.gat", 28, 46, 53, 65), set @dentro, 1;
set @INDIVIDUO$, strcharinfo(0);
if (($@ARENA_STATUS_IND == 1) && (@INDIVIDUO$ == $@LUTADOR$)) goto L_inddentro;
if (($@ARENA_STATUS_IND == 1) && (@INDIVIDUO$ != $@LUTADOR$)) goto L_indfora;
if (($@ARENA_STATUS_GRUPO == 1) && (@dentro == 1)) goto L_grupodentro;
if (($@ARENA_STATUS_GRUPO == 1) && (@dentro == 0)) goto L_grupofora;
if (($@ARENA_STATUS_GRUPO == 0) | ($@ARENA_STATUS_IND == 0)) && (@dentro == 1) goto L_menudentro;

L_menu:
	mes "[Parua]";
	mes "\"Olá "+ strcharinfo(0) +"!\"";
	next;
	mes "[Parua]";
	mes "\"Você gostaria de saber se é forte o suficiente enfrentando os desafios de minha arena?\"";

	mes "["+ strcharinfo(0) +"]";
	menu
		"=S Não, Agradeço!", L_fim,
		"=] Antes eu queria entrar na arena!", L_entra,
		"=] Sim, Por favor!", L_comeca,
		"=| Qual é o maior Record?", L_record;

L_menudentro:
	mes "[Parua]";
	mes "\"Olá "+ strcharinfo(0) +"!\"";
	next;
	mes "[Parua]";
	mes "\"Agora que já deu umas voltinhas em minha arena, será que vai querer enfrentar os seus desafios?\"";

	mes "["+ strcharinfo(0) +"]";
	menu
		"=S Não, Agradeço! Mas queria ficar mais tempo aqui dentro.", L_fim,
		"=] Eu quero é sair logo deste lugar!", L_sai,
		"=] Sim, Por favor!", L_comeca,
		"=| Qual é o maior Record?", L_record;

L_comeca:
	if (getgmlevel(0) >= 20 && (@gmtemp != 1)) goto L_avisogm;
	mes "[Parua]";
	mes "\"Muito bem! Mas para aceitar meu desafio, você precisará me pagar uma quantia em dinheiro...\"";

	mes "["+ strcharinfo(0) +"]";
	menu
		"=) OK! Mas quanto preciso pagar?", -,
		"=[ Me desculpe, mas estou sem dinheiro no momento.", L_fim;

	if BaseLevel <= 10,  set @preco, 1000;
	if (BaseLevel > 10) && (BaseLevel <= 20), set @preco, 3000;
	if (BaseLevel > 20) && (BaseLevel <= 30), set @preco, 5000;
	if (BaseLevel > 30) && (BaseLevel <= 40), set @preco, 9000;
	if (BaseLevel > 40) && (BaseLevel <= 50), set @preco, 15000;
	if (BaseLevel > 50) && (BaseLevel <= 60), set @preco, 20000;
	if (BaseLevel > 60) && (BaseLevel <= 70), set @preco, 30000;
	if (BaseLevel > 70) && (BaseLevel <= 80), set @preco, 40000;
	if (BaseLevel > 80) && (BaseLevel <= 90), set @preco, 50000;
	if (BaseLevel > 90) && (BaseLevel <= 255), set @preco, 60000;

	mes "[Parua]";
	mes "\"Se você for lutar individualmente, o preço é somente " + @preco/1000 + " KGP. Mas se quiser uma disputa em grupo o preço é o total de 20KGP para um grupo com menos de 10 lutadores e 50KGP para um grupo com mais de 10 jogadores.\"";
	next;
	menu
		"=) OK! Quero lutar na modalidade individual.", L_individual,
		"=) Não vou enfrentar essa arena sem ajuda. Quero a disputa em grupo.", L_grupo,
		"=[ Me desculpe, mas realmente não tenho como pagar.", L_fim;

L_entra:
	message strcharinfo(0), "Ao colocar os pés na arena você treme ao imaginar as épicas batalhas sediadas neste lugar!";
	warp "005-1.gat", 41, 55;
	close;

L_sai:
	warp "005-1.gat", 40, 42;
	set @dentro, 0;
	close;

L_individual:
	mes "[Parua]";
	mes "\"Ótima escolha! A arena é muito simples. Eu vou sumonando os monstros e você tem que matá-los. Só isso!\"";
	mes "\"Se você não conseguir se manter de pé, ou se sair por aquele portão, o desafio acaba!\"";
	mes "\"E então, ainda está com vontade de lutar?\"";
	menu
		"=) Claro! Pode começar.", L_sozinho,
		"=[ Acho que preciso resolver algumas coisas antes!", L_fim;


L_grupo:
	mes "[Parua]";
	mes "\"Excelente... Quero ver essa arena ferver! Mas antes vou te explicar algumas coisas.\"";
	mes "\"A arena é muito simples. Eu vou sumonando os monstros e vocês têm que matá-los. Só isso!\"";
	mes "\"Outra coisa. Quando você me disser que posso começar, eu vou começar com quem estiver DENTRO da arena!\"";
	mes "\"Quem estiver do lado de fora só vai poder entrar quando o desafio terminar!\"";
	mes "\"E então, Posso começar?\"";
	menu
		"=) Claro! Pode começar.", L_desafiogrupo,
		"=[ Acho que vou chamar mais gente antes de começar!", L_fim;

L_sozinho:
	if (zeny < @preco) goto L_semgrana;
	warp "005-1.gat", 41, 55;
	if (getareausers("005-1.gat", 28, 46, 53, 65) > 1) goto L_naosozinho;
	set zeny, zeny - @preco;
	npctalk "Vamos ver como você se sai sozinho!";
	set $@LUTADOR$, @INDIVIDUO$;
	set $@ARENA_STATUS_IND, 1;
	set $@ARENA_LEVEL_IND, 1;
	set $@MORTES, PC_DIE_COUNTER;
	set $@ID_PARUA, getcharid(3);

	startnpctimer;	
	goto L_fim;

L_desafiogrupo:
	if (getareausers("005-1.gat", 28, 46, 53, 65) <= 1) goto L_naogrupo;
	mes "[Parua]";
	mes "\"Qual o nome do grupo ou clan que vai participar do desafio? Capriche no nome pois ele pode aparecer no mural da fama!\"";
	input $@GRUPO$;
	set @pessoas, getareausers("005-1.gat", 28, 46, 53, 65);
	if getareausers("005-1.gat", 28, 46, 53, 65) > 10, set @precogrupo, 50000;
	set @precogrupo, 20000;
	mes "[Parua]";
	mes "\"O custo desta batalha será de " + @precogrupo/1000 + "KGP, pois há no momento " + @pessoas + " pessoas presentes na arena.\"";
	next;
	mes "[Parua]";
	mes "\"Tem certeza que tem condições de pagar?\"";
	menu "=) Claro! ", -,
		"=[ Acho que antes vou ter que recolher a contribuição de meus amigos!", L_fim;
	if (getareausers("005-1.gat", 28, 46, 53, 65) > @pessoas) && (@precogrupo == 20000) goto L_maisgente;
	if (zeny < @precogrupo) goto L_semgrana;
	set zeny, zeny - @precogrupo;
	npctalk "Que a batalha começe!";
	set $@ARENA_STATUS_GRUPO, 1;
	set $@ARENA_LEVEL_GRUPO, 1;
	set $@ARENA_CONTA_JOGADORES, getareausers("005-1.gat", 28, 46, 53, 65);

	set $@FIGHT_CAVE_LAST, 0;

	startnpctimer;
	goto L_fim;

L_naosozinho:
	mes "[Parua]";
	mes ">=[ \"Eu estou vendo que tem mais gente aqui... Porque não pede pra eles sairem? Com educação, claro... =)\"";
	goto L_fim;

L_naogrupo:
	mes "[Parua]";
	mes ">=[ \"Hmmm... Pelo que eu saiba isso não é um grupo. Por favor, chame mais gente!\"";
	goto L_fim;

L_semgrana:
	mes "[Parua]";
	mes "=S \"Parece que você não tem dinheiro suficiente.\"";
	goto L_fim;


L_record:
	if($ARENA_RECORD_IND==0 && $ARENA_RECORD_GRUPO==0) npctalk "Não existe ninguem suficientemente corajoso neste mundo para aceitar meu desafio!";
	if($ARENA_RECORD_GRUPO>0) npctalk "O record da arena pertence ao clan/grupo " + $GRUPO_RECORD$ + ", que conseguiu a façanha de ser o primeiro a chegar ao nível " + $ARENA_RECORD_GRUPO + ".";
	if($ARENA_RECORD_IND>0) npctalk "O record individual da arena pertence a " + $RECORD_IND$ + ", que foi o primeiro a chegar ao nível " + $ARENA_RECORD_IND + ".";
	close;

OnTimer5000:
	setnpctimer 0;
	if ($@ARENA_STATUS_IND == 1) goto L_logica;
	if ($@ARENA_STATUS_GRUPO == 1) goto L_logicagrupo;

L_return1:
	areatimer "005-1.gat", 28, 46, 53, 65, 10, "Parua::onTick";
	end;

L_return1grupo:
	set $@ARENA_CONTA_JOGADORES, 0;
	areatimer "005-1.gat", 28, 46, 53, 65, 10, "Parua::onTick";
	end;

L_logica:
	attachrid($@ID_PARUA);
	if (PC_DIE_COUNTER > $@MORTES) goto L_limpa;
	if (getareausers("005-1.gat", 28, 46, 53, 65) <= 0) goto L_limpa;
	set $@ARENA_ROUND_TEMPO, $@ARENA_ROUND_TEMPO + 5; // Avança 5 segundos
	if (mobcount("005-1.gat", "Parua::onPetDeath") <= 0) goto L_proximoround;
	if ($@ARENA_ROUND_TEMPO > 60) goto L_proximoround;
	goto L_return1;

L_logicagrupo:
	set $@FIGHT_CAVE_ROUND_PEN, $@ARENA_CONTA_JOGADORES;
	if ($@ARENA_CONTA_JOGADORES <= 0) goto L_limpa;
	if ($@ARENA_CONTA_JOGADORES > 15) set $@ARENA_CONTA_JOGADORES, 15;
	set $@ARENA_ROUND_TEMPO, $@ARENA_ROUND_TEMPO + 5; // Avança 5 seconds
	if (mobcount("005-1.gat", "Parua::onPetDeath") <= 0) goto L_proximoroundgrupo;
	if ($@ARENA_ROUND_TEMPO + $@FIGHT_CAVE_ROUND_PEN >= 75) goto L_proximoroundgrupo;
	goto L_return1grupo;

L_proximoround:
	set $@ARENA_ROUND_TEMPO, 0;
	set $@ARENA_LEVEL_IND, $@ARENA_LEVEL_IND + 1;
	set $@ARENA_POINTS, $@ARENA_LEVEL_IND;
	goto L_return;

L_proximoroundgrupo:
	set $@ARENA_ROUND_TEMPO, 0;
	set $@ARENA_LEVEL_GRUPO, $@ARENA_LEVEL_GRUPO + $@ARENA_CONTA_JOGADORES + ($@ARENA_LEVEL_GRUPO / 40);
	set $@ARENA_POINTS, $@ARENA_LEVEL_GRUPO;
	goto L_return;

L_return:
	if ($@ARENA_STATUS_IND == 1) set $@ARENA_LEVEL, $@ARENA_LEVEL_IND;
	if ($@ARENA_STATUS_GRUPO == 1) set $@ARENA_LEVEL, $@ARENA_LEVEL_GRUPO;

	npctalk "Nova rodada começa! O nível desta rodada é " + $@ARENA_LEVEL;

	set $@MOB_1_SUMMON, 0;
	set $@MOB_2_SUMMON, 0;
	set $@MOB_3_SUMMON, 0;
	set $@MOB_4_SUMMON, 0;
	set $@MOB_5_SUMMON, 0;
	set $@MOB_6_SUMMON, 0;
	goto L_SUMMON;

L_SUMMON:
	if ($@ARENA_POINTS >= 64 && $@MOB_1_SUMMON < 3) goto L_MOB1;
	if ($@ARENA_POINTS >= 32 && $@MOB_2_SUMMON < 5) goto L_MOB2;
	if ($@ARENA_POINTS >= 16 && $@MOB_3_SUMMON < 10) goto L_MOB3;
	if ($@ARENA_POINTS >= 8 && $@MOB_4_SUMMON < 15) goto L_MOB4;
	if ($@ARENA_POINTS >= 4 && $@MOB_5_SUMMON < 20) goto L_MOB5;
	if ($@ARENA_POINTS >= 1 && $@MOB_6_SUMMON < 25) goto L_MOB6;
	goto L_return1;


//JackO
L_MOB1:
	set $@MOB_1_SUMMON, $@MOB_1_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 64;
	areamonster "005-1.gat", 29, 47, 52, 64, "", 1022, 1, "Parua::onPetDeath";
	goto L_SUMMON;

//Esqueleto Pirata
L_MOB2:
	set $@MOB_2_SUMMON, $@MOB_2_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 32;
	areamonster "005-1.gat", 29, 47, 52, 64, "", 1305, 1, "Parua::onPetDeath";
	goto L_SUMMON;

//Cobra da Montanha
L_MOB3:
	set $@MOB_3_SUMMON, $@MOB_3_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 16;
	areamonster "005-1.gat", 29, 47, 52, 64, "", 1026, 1, "Parua::onPetDeath";
	goto L_SUMMON;

//Esqueleto Arqueiro
L_MOB4:
	set $@MOB_4_SUMMON, $@MOB_4_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 8;
	areamonster "005-1.gat", 29, 47, 52, 64, "", 1304, 1, "Parua::onPetDeath";
	goto L_SUMMON;

//Cobra
L_MOB5:
	set $@MOB_5_SUMMON, $@MOB_5_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 2;
	areamonster "005-1.gat", 29, 47, 52, 64, "", 1010, 1, "Parua::onPetDeath";
	goto L_SUMMON;

//Limo Vermelho
L_MOB6:
	set $@MOB_6_SUMMON, $@MOB_6_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 1;
	areamonster "005-1.gat", 29, 47, 52, 64, "", 1008, 1, "Parua::onPetDeath";
	goto L_SUMMON;


// Checa se o jogador está vivo
onTick:
	if (isdead(0)) end;
	set $@ARENA_CONTA_JOGADORES, $@ARENA_CONTA_JOGADORES + 1;
	end;

onPetDeath:
	end;

onInit:
	initnpctimer;
	stopnpctimer;
	end;

L_limpa:
	npctalk "Fim de Jogo.";
	mapannounce "005-1.gat", "Parua: A arena já está livre para novos desafiantes!", 0;
	if ($@ARENA_LEVEL_IND > $ARENA_RECORD_IND) goto L_novorecordindividual;
	if ($@ARENA_LEVEL_GRUPO > $ARENA_RECORD_GRUPO) goto L_novorecordgrupo;
	set $@ARENA_STATUS_IND, 0;
	set $@ARENA_STATUS_GRUPO, 0;
	set $@ARENA_CONTA_JOGADORES, 0;
	set $@ARENA_LEVEL_IND, 1;
	set $@ARENA_LEVEL_GRUPO, 1;
	set $@ARENA_ROUND_TEMPO, 0;
	set $@MORTES, 0;
	set $@ID_PARUA, 0;
	killmonster "005-1.gat", "Parua::onPetDeath";
	setnpctimer 0;
	stopnpctimer;
	close2;
	end;

L_novorecordindividual:
	set $RECORD_IND$, $@LUTADOR$;
	set $ARENA_RECORD_IND, $@ARENA_LEVEL_IND;
	set $@ARENA_STATUS_IND, 0;
	set $@ARENA_LEVEL_IND, 1;
	set $@ARENA_ROUND_TEMPO, 0;
	set $@ARENA_CONTA_JOGADORES, 0;
	set $@MORTES, 0;
	set $@ID_PARUA, 0;
	killmonster "005-1.gat", "Parua::onPetDeath";
	stopnpctimer;
	setnpctimer 0;
	end;

L_novorecordgrupo:
	set $RECORD_GRUPO$, $@GRUPO$;
	set $ARENA_RECORD_GRUPO, $@ARENA_LEVEL_GRUPO;
	set $@ARENA_STATUS_GRUPO, 0;
	set $@ARENA_LEVEL_GRUPO, 1;
	set $@ARENA_ROUND_TEMPO, 0;
	set $@ARENA_CONTA_JOGADORES, 0;
	killmonster "005-1.gat", "Parua::onPetDeath";
	stopnpctimer;
	setnpctimer 0;
	end;

L_indfora:
	mes "[Parua]";
	mes "\"Olá, está gostando do desempenho de \"" + $@LUTADOR$ + "\"?\"";
	menu "Sim! " + $@LUTADOR$ + " é muito forte!", L_fim,
	     "Não! Não vejo a hora de chegar a minha vez!", L_fim,	
	     "Na verdade, eu queria saber mais sobre os recordes da arena.", L_record;

L_inddentro:
	npctalk "Preste atenção na luta " + $@LUTADOR$ + " !";

L_grupofora:
	mes "[Parua]";
	mes "\"Olá, está gostando do desempenho do pessoal do \"" + $@GRUPO$ + "\"?\"";
	menu "Sim! Eles são os melhores!", L_fim,
	     "Não! Não vejo a hora de chegar a minha vez!", L_fim,	
	     "Na verdade, eu queria saber mais sobre os recordes da arena.", L_record;

L_grupodentro:
	npctalk "Preste atenção na luta " + strcharinfo(0) + " ! Seu grupo depende de você!";


L_avisogm:
	set @gmtemp, 1;
	mes "[Parua]";
	mes "=S \"Oi. Eu já vi você usando uns truques estranhos por aí... Só espero que você não prejudique meu ranking com seus poderes... Isso causaria uma grande revolta nos meus clientes.\"";
	next;
	goto L_comeca;

L_maisgente:
	mes "[Parua]";
	mes "=S \"Ei... Não tente me enganar! Haviam " + @pessoas + " pessoas antes, e agora vejo um total de " + getareausers("005-1.gat", 28, 46, 53, 65) + " !\"";
	next;
	goto L_comeca;

L_fim:
	close;
	end;
	
}
