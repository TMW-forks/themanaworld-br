//
// @script Camélia a Enfermeira
// @autor Diogo_RBG - http://diogorbg.blogspot.com
// @desc Uma enfermeira pronta a lhe ajudar em tudo que puder.
// #quest <desc>, <getItens>, <delItens>, <extra>
// @quest 'Chave para abrir o baú.', '1 chaveBau',, 'Espera de 15:00'
// @quest 'Chave para abrir o baú.', '1 chaveBau',, 'Espera de 45:00'
// @quest 'Chave para abrir o baú.', '1 chaveBau', '1 SharpKnife', 'Espera de 3:00:00'

007-4.gat,97,26,0	script	Camélia	119,{
	if (Sex == 0) set @meu_minha$, "minha";
	if (Sex == 1) set @meu_minha$, "meu";

	if (QUEST_bauCamelia==0) goto _inicio;
	if (QUEST_bauCamelia==1) goto _procuraEquipamento;
	if (QUEST_bauCamelia==2) goto _abraBau;
	if (QUEST_bauCamelia==3) goto _abriuBau;
	if (QUEST_bauCamelia==5) goto _procuraEquipamento2;
	if (QUEST_bauCamelia==6) goto _abraBau;
	if (QUEST_bauCamelia==7) goto _devolveFaca;
	if (QUEST_bauCamelia==10) goto _abraBau;

	callfunc "getTime";
	if (QUEST_bauCamelia==9 && @time>=QUEST_bauCameliaTime+180*60) goto _chave3;

	mes "[Camélia a Enfermeira]";
	mes "\"Olá "+@meu_minha$+" jovem. O que posso fazer por você?\"";
	next;
	menu
		"Você poderia me curar?", _curar,
		"Nada. Obrigado.", -;
	close;

//= Inicio de conversa... fala como vc foi encontrado =================

_inicio:
	mes "[Camélia a Enfermeira]";
	mes "\"Olá "+@meu_minha$+" jovem, que bom que acordou!";
	mes "Afinal, já faziam muitos dias em que você se recuperava.\"";
	next;
	menu
		"Fazia?! E como eu vim parar aqui?", _comoPararAqui,
		"Ai... minha cabeça ainda doi.", _comRazao,
		"Você poderia me curar?", _curar;
	goto _explicacao;

_comoPararAqui:
	mes "[Camélia a Enfermeira]";
	mes "\"Você não se lembra?!";
	goto _explicacao;

_comRazao:
	mes "[Camélia a Enfermeira]";
	mes "\"Com razão !";
	goto _explicacao;

_explicacao:
	mes "Você foi encontrado, gravimente ferido, por pescadores da região e trazido para cá, pois precisava de cuidados médicos.\"";
	next;
	mes "[Camélia a Enfermeira]";
	mes "\"Você é muito forte sabia?! E teve muita sorte de ter sobrevivido.\"";
	next;
	menu
		"Espere, agora eu me lembro! Eu estava caçando quando levei uma forte pancada na cabeça. Mas não me lembro de mais nada!", -;
	next;
	menu
		"E quanto ao meu equipamento?", -;
	mes "[Camélia a Enfermeira]";
	mes "\"Você foi trazido para cá apenas com a roupa do corpo e uma faca... mais nada.";
	mes "Provavelmente você tenha sido saqueado, pois é pouco provável que alguém caçe com roupas de algodão e uma faca! É loucura! Se é que você me entende.\"";
	next;
	menu
		"Claro que é! E é por isso que preciso de meu equipamento.", -;
	mes "[Camélia a Enfermeira]";
	mes "\"Entendo. Eu vou procurar pelos pescadores que te trouxeram e perguntar isso a eles.\"";
	set QUEST_bauCamelia, 1;
	callfunc "getTime";
	set QUEST_bauCameliaTime, @time;
	close;

//= Ganha a chave para abrir o baú p/ pegar a Faca Afiada =============

_procuraEquipamento:
	mes "[Camélia a Enfermeira]";
	mes "\"Olá "+@meu_minha$+" jovem, que bom que voltou!\"";
	next;
	menu
		"Você encontrou meu equipamento?", _calcular1,
		"Você poderia me curar?", _curar,
		"Te vejo depois.", -;
	close;

_calcular1:
	callfunc "getTime";
	if (@time >= QUEST_bauCameliaTime + 15*60) goto _chave1;

	mes "[Camélia a Enfermeira]";
	mes "\"Tenha calma, pois eu irei falar com eles... prometo.";
	mes "Volte mais tarde.\"";
	close;

_chave1:
	mes "[Camélia a Enfermeira]";
	mes "\"Eu falei com os pescadores, mas eles disseram que não encontraram muita coisa no local... só uma Faca Afiada.\"";
	next;
	menu
		"Posso ver a Faca Afiada?", _ganhaChave1,
		"A faca não me interessa.", -;
	close;

_ganhaChave1:
	mes "[Camélia a Enfermeira]";
	mes "\"Claro, eu guardei ela em meu baú.";
	mes "Por favor... poderia pegar esta chave e abrir meu baú?\"";
	next;
	menu
		"Sim... pode deichar que eu faço.", -;
	getinventorylist;
	if (@inventorylist_count==100 && countitem(3022)==0) goto _lotado;
	getitem "chaveBau", 1;
	set QUEST_bauCamelia, 2;
	close;

//= Deixa ficar com a Faca Afiada. ====================================

_abraBau:
	mes "[Camélia a Enfermeira]";
	mes "\"Por favor... abra meu baú com a chave que te dei.\"";
	close;

_abriuBau:
	mes "[Camélia a Enfermeira]";
	mes "\"Olá "+@meu_minha$+" jovem. Você encontrou a Faca Afiada?\"";
	next;
	menu
		"Sim, mas ela não é minha. (verdade)", _verdadeFaca,
		"Sim, é essa mesma! (mentira)", _mentiraFaca,
		"Você poderia me curar?", _curar;
	close;

_verdadeFaca:
	mes "[Camélia a Enfermeira]";
	mes "\"Não é! Vou comentar isso com os pescadores.\"";
	next;
	mes "[Camélia a Enfermeira]";
	mes "\"Por enquanto fique com ela, já que não tem dono.\"";
	set QUEST_bauCamelia, 5;
	callfunc "getTime";
	set QUEST_bauCameliaTime, @time;
	close;

_mentiraFaca:
	mes "[Camélia a Enfermeira]";
	mes "\"Que bom que encontrou parte de seu equipamento.\"";
	set QUEST_bauCamelia, 4;
	close;

//= Ganha a chave do baú para pegar a Adaga ===========================

_procuraEquipamento2:
	mes "[Camélia a Enfermeira]";
	mes "\"Olá "+@meu_minha$+" jovem, que bom que voltou!\"";
	next;
	menu
		"Você encontrou meu equipamento?", _calcular2,
		"Você poderia me curar?", _curar,
		"Te vejo depois.", -;
	close;

_calcular2:
	callfunc "getTime";
	if(@time >= QUEST_bauCameliaTime + 45*60) goto _chave2;

	mes "[Camélia a Enfermeira]";
	mes "\"Tenha calma, pois eu irei falar com eles.";
	mes "Volte mais tarde.\"";
	close;

_chave2:
	mes "[Camélia a Enfermeira]";
	mes "\"Um pescador encontrou uma arma perto de onde você foi encontrado... pode ser que seja ela.\"";
	next;
	menu
		"Posso ver essa arma?", _ganhaChave2,
		"A faca não me interessa.", -;
	close;

_ganhaChave2:
	mes "[Camélia a Enfermeira]";
	mes "\"Sim, você pode.";
	mes "E aqui está a chave... já sabe o que fazer.\"";
	getinventorylist;
	if (@inventorylist_count==100 && countitem(3022)==0) goto _lotado;
	getitem "chaveBau", 1;
	set QUEST_bauCamelia, 6;
	close;

//= Pede a Faca Afiada de volta. ======================================

_devolveFaca:
	mes "[Camélia a Enfermeira]";
	mes "\"Então... você encontrou a arma no baú?\"";
	next;
	menu
		"Sim.. eu me lembro dessa Adaga.", -;
	mes "[Camélia a Enfermeira]";
	mes "\"Por acaso você ainda está com a Faca Afiada que te dei?\"";
	next;
	menu
		"Sim. E quero devolver.", _devolver,
		"Não. Eu já a vendi.", _vendeu,
		"Você poderia me curar?", _curar;
	close;

_vendeu:
	mes "[Camélia a Enfermeira]";
	mes "\"Desta vez você me decepcionou. Como pode vender algo que não era seu?";
	set QUEST_bauCamelia, 8;
	close;

_devolver:
	if (countitem(522)==0) goto _semFaca;

	mes "[Camélia a Enfermeira]";
	mes "\"Você me surpriende! Nem precisei pedir que devolvesse aquela faca.";
	mes "Vou continuar a perguntar sobre seu equipamento. Tenha a certeza disso.\"";
	delitem "SharpKnife", 1;
	callfunc "getTime";
	set QUEST_bauCameliaTime, @time;
	set QUEST_bauCamelia, 9;
	close;

_semFaca:
	mes "[Camélia a Enfermeira]";
	mes "\"Eu acho que você perdeu a faca que te dei.\"";
	close;

//= Ganha a chave do baú para pegar o Short de Algodão. ===============

_chave3:
	mes "[Camélia a Enfermeira]";
	mes "\"Eu tenho duas notícias para você. Uma boa e uma não tão boa.\"";
	next;
	menu
		"Começe pela notícia boa.", -,
		"Começe pela notícia não tão boa.", -; 
	mes "[Camélia a Enfermeira]";
	mes "\"Vou começar pela não tão boa:";
	mes "Os pescadores voltaram aqui e me avisaram que mais nada foi encontrado. Sem esperanças de encontrar o resto do seu equipamento.\"";
	next;
	menu
		"E a notícia boa.", -;
	mes "[Camélia a Enfermeira]";
	mes "\"A notícia boa é que eles foram solidários e lhe mandaram um presente.";
	mes "Pegue a chave de meu baú e descubra o que tem lá.\"";
	getinventorylist;
	if (@inventorylist_count==100 && countitem(3022)==0) goto _lotado;
	getitem "chaveBau", 1;
	set QUEST_bauCamelia, 10;
	close;

//= Cura e funções diversas. ==========================================

_curar:
	if (baselevel > 10) goto _temPagar;
	mes "[Camélia a Enfermeira]";
	mes "\"Você realmente não me pacere bem. Mas essa agulhada vai resolver!\"";
	next;
	message strcharinfo(0), "Ai!";
	heal 500, 500;
	close;

_temPagar:
	mes "[Camélia a Enfermeira]";
	mes "\"Sim... eu posso. Mas você terá que me ajudar também. Esta injeção é cara e exitem outras pessoas mais necessitadas.";
	mes "Mas eu poderia te curar por 500 GP.\"";
	next;
	menu
		"Tudo bem... eu pago.", _pagarOk,
		"Não mesmo!", -;
	close;

_pagarOk:
	if(zeny<500) goto _semGrana;
	set zeny, zeny - 500;
	mes "[Camélia a Enfermeira]";
	mes "\"Tudo bem então. Vou aplicar a injeção.\"";
	next;
	message strcharinfo(0), "Ai!";
	heal 500, 500;
	close;

_lotado:
	mes "[Camélia a Enfermeira]";
	mes "Você não tem espaço para minha chave. Volte mais tarde.";
	close;

_semGrana:
	mes "[Camélia a Enfermeira]";
	mes "\"Você não tem dinheiro o suficiente para pagar pela injeção.\"";
	close;

}
