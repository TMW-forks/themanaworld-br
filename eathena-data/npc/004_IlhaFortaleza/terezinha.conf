//
// @data 17/01/2010
// @autor Diogo_RBG - http://diogorbg.blogspot.com
// @desc Terezinha é mulher de Chico. Ela é muito dedicada em seu tricô.
// #quest <desc>, <getItens>, <delItens>, <extra>
// @quest 'Haste Roxa', '1 antenaRoxa', '1 hasteRoxa', ''

004.gat,120,76,0	script	Terezinha	151,{
	if (QUEST_trasgo==5) goto _hasteRoxaInicio;
	if (QUEST_trasgo==6) goto _tragaAntena;
	if (QUEST_trasgo>=7 || (QUEST_MASK1 & MASK1_TRASGO)) goto _argradicida;

	mes "[Terezinha esposa de Chico]";
	mes "A mulher, muito concentrada em seu tricô, parece não reparar em sua presença.";
	close;

//-- QUEST_trasgo == 5 --------------------------------------------

_hasteRoxaInicio:
	mes "[Terezinha esposa de Chico]";
	mes "\"Ei... ocê num é quem tava arjudando o meu marido Chico a martá as peste das beterraba?\"";
	next;
	menu
		"Sim... eu mesmo!", _sim,
		"A senhora me confundiu com outra pessoa.", -;
	close;

_sim:
	mes "[Terezinha esposa de Chico]";
	mes "\"Intão devo di argradecê ocê... purque meu marido tá dos más sartisfeto.";
	mes "Só pur perguntá... ele ti deu argum argrado?\"";
	next;
	menu
		"Não senhora.", -,
		"Não precisa.", -;
	mes "[Terezinha esposa de Chico]";
	if (@menu==1)
		mes "\"Nem credito que ele fez isso com ocê!\"";
	if (@menu==2)
		mes "\"Más é claro di qui prercisa... ocê merece um argradin!\"";
	next;
	mes "[Terezinha esposa de Chico]";
	mes "\"Vô ti dá um argradinho, más nóis semo pobre e num pode dá muita coisa...";
	mes "Más ieu já sei o que eu posso te dá.\"";
	set QUEST_trasgo, 6;
	next;
	goto _tragaAntena;

//-- QUEST_trasgo == 6 --------------------------------------------

_tragaAntena:
	mes "[Terezinha esposa de Chico]";
	mes "\"Se ocê trazê pra mim uma darquela vareta roxa que tem na cabeça daquelas peste de beterraba eu posso ti fazê uma lembrancinha.";
	mes "É coisa simpres más ocê há de gostá muito!\"";
	if (countitem("antenaRoxa")) goto _tenhoAntena;
	close;

_tenhoAntena:
	next;
	menu
		"Pegue essa [Antena Roxa].", _pegueEssa,
		"Depois te trago uma.", -;
	close;

_pegueEssa:
	if (countitem("antenaRoxa")<1) goto _cade;
	getinventorylist;
	if( (@inventorylist_count + (countitem("antenaRoxa")>1) ) == 100 ) goto _cheio;

	mes "[Terezinha esposa de Chico]";
	mes "Teresinha prente a antena em uma tira de tricô e lhe entrega.";
	mes "\"Óia só o que ieu ti fiz!\"";
	mes "";
	mes "* Você ganhou uma [Haste Roxa].";
	delitem "antenaRoxa", 1;
	getitem "hasteRoxa", 1;
	set QUEST_trasgo, 7;
	close;

_cade:
	mes "[Terezinha esposa de Chico]";
	mes "\"Uai... cadê a vareta roxa que ocê disse que tinha!\"";
	close;

_cheio:
	mes "[Terezinha esposa de Chico]";
	mes "\"Ocê tá carregando coisa pur demais.... é melhor vortá más tarte!\"";
	close;

//-- QUEST_trasgo == 7 --------------------------------------------

_argradicida:
	mes "[Terezinha esposa de Chico]";
	mes "\"Tô muito da argradicida com ocê! Argradeço ocê pur arjudá meu marido.\"";
	if (getequipid(equip_head)==3070) goto _oceGosto;
	close;

_oceGosto:
	next;
	mes "[Terezinha esposa de Chico]";
	mes "\"Que bão que ocê gostô do argrado que te fiz!\"";
	close;
}
