//#################################################################################
//#
//# this script file contains the script for the NPC which allows the
//# PCs to create potions from herbs.
//#
//# Participates in the monster oil subquest (cf. Nicholas' Setzer quest)
//# Tradução por Fafinha
//#################################################################################

005.gat,111,71,0	script	Carlos	107,{
	set @SETZER_INITIAL, 0;
	set @SETZER_KNOWS_OIL, 1;
	set @SETZER_KNOWS_STINGER, 2;
	set @SETZER_FLAG_MADE_OIL, 4;

	set @PEARL, 700;
	set @SNAKE_SKIN, 641;
	set @BLACK_STINGER, 709;
	set @ASH_PILE, 701;
	set @GOLDEN_STINGER, 706;
	set @MONSTER_OIL, 707;
	set @MONSTER_OIL_XP, 100000;

	set @Q_MASK, NIBBLE_3_MASK;
	set @Q_SHIFT, NIBBLE_3_SHIFT;

	set @MAUVE, 680;
	set @COBALT, 681;
	set @GAMBOGE, 682;
	set @ALIZARIN, 683;

	set @Q_status, (QUEST_Forestbow_state & @Q_MASK) >> @Q_SHIFT;

	mes "[Carlos]";
	mes "\"Eu posso ajudá-lo com poções de cura e ervas de malva, cobalto, gamboge e alizarina.";
	mes "Quantas poções gostaria que eu preparasse?\"";
	next;

	if (@Q_status && Inspector == 10)
		menu
			"Uma.", L_1,
			"Cinco.", L_5,
			"Dez.", L_10,
			"Cinquenta.", L_50,
			"Eu gostaria que me prepasse uma [Poção de Óleo de Monstro].", L_monster_oil_start,
			//"Atualmente...", L_NohMask, //< Sem sentido... é continuação de outra quest.
			"Nada obrigado!", L_close;
	if (@Q_status && Inspector != 10)
		menu
			"Uma.", L_1,
			"Cinco.", L_5,
			"Dez.", L_10,
			"Cinquenta.", L_50,
			"Eu gostaria que me prepasse uma [Poção de Óleo de Monstro].", L_monster_oil_start,
			"Nada obrigado!", L_close;
	if (!@Q_status && Inspector == 10)
		menu
			"Uma.", L_1,
			"Cinco.", L_5,
			"Dez.", L_10,
			"Cinquenta.", L_50,
			//"Atualmente...", L_NohMask, //< Sem sentido... é continuação de outra quest.
			"Nada obrigado!", L_close;
	if (!@Q_status && Inspector != 10)
		menu
			"Uma.", L_1,
			"Cinco.", L_5,
			"Dez.", L_10,
			"Cinquenta.", L_50,
			"Nada obrigado!", L_close;

L_1:
	set @num, 1;
	goto L_brew;

L_5:
	set @num, 5;
	goto L_brew;

L_10:
	set @num, 10;
	goto L_brew;

L_50:
	set @num, 50;
	goto L_brew;

L_brew:
	mes "[Carlos]";
	mes "\"Agora ponha " + @num + " ervas de cada espécie no caldeirão, mexa bem e concentre toda sua força mental nisso.\"";
	next;

	if (countitem(@MAUVE) < @num) goto L_NotEnough;
	if (countitem(@COBALT) < @num) goto L_NotEnough;
	if (countitem(@GAMBOGE) < @num) goto L_NotEnough;
	if (countitem(@ALIZARIN) < @num) goto L_NotEnough;

	set @a, @num;
	set @result1, 0;
	set @result2, 0;
	set @result3, 0;
	set @result4, 0;

L_CraftLoop:
	set @throw, rand(readparam(bInt));
	set @result, 1;
	if (@throw >= 5) set @result, 2;
	if (@throw >= 15) set @result, 3;
	if (@throw >= 35) set @result, 4;
	
	if (@result == 1) set @result1, @result1 + 1;
	if (@result == 2) set @result2, @result2 + 1;
	if (@result == 3) set @result3, @result3 + 1;
	if (@result == 4) set @result4, @result4 + 1;
	
	set @a, @a - 1;
	if (@a > 0) goto L_CraftLoop;

	delitem @MAUVE, @num;
	delitem @COBALT, @num;
	delitem @GAMBOGE, @num;
	delitem @ALIZARIN, @num;

	getitem 684, @result1;
	getitem 685, @result2;
	getitem 686, @result3;
	getitem 687, @result4;

	mes "[Carlos]";
	mes "\"Tudo pronto! Você fez:";
	if (@result1 > 0) mes @result1 + " tiny potions";
	if (@result2 > 0) mes @result2 + " small potions";
	if (@result3 > 0) mes @result3 + " medium potions";
	if (@result4 > 0) mes @result4 + " large potions";
	mes "Volte sempre.\"";
	close;

L_NotEnough:
	mes "[Carlos]";
	mes "\"Oh, parece que você não tem ervas o suficiente. Você pode encontrar as ervas no interior de florestas.\"";
	close;

L_monster_oil_start:
	mes "[Carlos]";
	mes "\"Óleo de monstro? Hmm, ouvi falar disso, mas eu não me lembro como se faz isso...\"";
	next;
	if (@Q_status == @SETZER_KNOWS_OIL)
		menu	"Ok, eu vou perguntar por ai, talvez alguém saiba.", L_close;
	if (@Q_status > @SETZER_KNOWS_OIL)
		menu	"Raquel me disse a receita.", L_monster_oil_knows_recipe;
	close;

L_monster_oil_knows_recipe:
	mes "[Carlos]";
	mes "\"Ah, mas é claro! Sim, devemos ser capazes de preparar isso aqui. Só que... hmm. Pêrola e três ferrões de escorpião preto? Parece perigoso conseguir isso.";
	mes "Eu receio ter de pedir que você faça isso sozinho.\"";
	next;

L_monster_oil_main_menu:
	set @main_menu, 1;
	menu
		"O que você quiz dizer com 'perigoso'?", L_monster_oil_why_dangerous,
		"O que eu preciso, mais uma vez?", L_monster_oil_ingredients,
		"Como isso funciona?", L_monster_oil_explain,
		"Vamos começar!", L_monster_oil_start_brew,
		"Onde posso conseguir um ferrão de escorpião dourado?", L_monster_oil_where_gold,
		"Eu mudei de idéia.", L_close;
	close;

L_monster_oil_why_dangerous:
	mes "[Carlos]";
	mes "\"Bem, com esses dois reagentes nessa proporção, há risco de uma explosão, possivelmente fatal. Então, é melhor estar preparado para correr caso o líquido comece a borbulhar demais. Melhor perder os ingredientes do que a cabeça!\"";
	next;
	goto L_monster_oil_main_menu;

L_monster_oil_ingredients:
	mes "[Carlos]";
	mes "\"Você vai precisar de 1 [Pérola], 2 [Pele de Cobra], 3 [Ferrão de Escorpião Preto], 2 [Pilha de cinzas] e 1 [Ferrão de Escorpião Dourado]. O ferrão dourado vai por último, após a parte perigosa, assim você não o perde caso a misture falhe.\"";
	next;
	mes "[Carlos]";
	mes "\"Você também deve ter ervas de gamboge, malva, alizarina e cobalto. Elas servirão para estabilizar a mistura.\"";
	next;
	goto L_monster_oil_main_menu;

L_monster_oil_explain:
	mes "[Carlos]";
	mes "\"Depois de colocar as partes dos monstros, exceto o ferrão dourado, a mistura ganhará uma cor aleatóriamente. Nesse momento você deve usar as ervas para estabilizar a mistura.\"";
	next;
	mes "[Carlos]";
	mes "\"É claro que, por ser uma mistura perigosa, ela mudará de cor por si só, provavelmente mais rápido que você possa estabilizar com as ervas.\"";
	next;
	mes "[Carlos]";
	mes "\"Pelo o que vejo, se deve alternar entre fazes, de acrescentar uma cor, depois remover uma cor, depois acrescentar novamente e por assim vai. Claro que se conseguir a cor que se deseja, já não se pode adicionar mais.\"";
	next;
	mes "[Carlos]";
	mes "\"Eu ainda não consegui descobrir como fazer isso mais facilmente, embora...\"";
	next;
	mes "[Carlos]";
	mes "\"ao adicionar malva, você a deixe mais escura. Alizarina adiciona vermelho, gamboge amarelo e cobalto o azul. Para deixá-la bem preta, primeiro deve chegar a um cinza escuro e depois adicionar malva.\"";
	next;
	mes "[Carlos]";
	mes "\"Infelizmente, as mudanças de cor na mistura são aleatórias, por isso as vezes você pode acabar adicionando a cor errada...\"";
	next;
	mes "[Carlos]";
	mes "\"Desde que a mistura mude de cor em primeiro lugar, você pode adicionar malva quando ela tiver qualquer cor escura e torcer para que funcione.\"";
	next;
	mes "[Carlos]";
	mes "\"Mas não adicione demais! Se você adicionar uma cor que já está lá ou tentar escurer na hora errada, porá tudo a perder.\"";
	next;
	if (@main_menu) goto L_monster_oil_main_menu;
	goto L_monster_oil_loop;

L_monster_oil_where_gold:
	mes "[Carlos]";
	mes "\"Boa pergunta. Não existe nenhum Escorpião Dourado por essas terras. Talvez você consiga de algum aventureiro ou comerciante que tenham viajado para outras terras.\"";
	next;
	goto L_monster_oil_main_menu;

L_monster_oil_start_brew:
	set @main_menu, 0;
	if (countitem (@PEARL) < 1) goto L_monster_oil_missing;
	if (countitem (@SNAKE_SKIN) < 2) goto L_monster_oil_missing;
	if (countitem (@BLACK_STINGER) < 3) goto L_monster_oil_missing;
	if (countitem (@ASH_PILE) < 2) goto L_monster_oil_missing;
	if (countitem (@GOLDEN_STINGER) < 1) goto L_monster_oil_missing_gold;

	delitem @PEARL, 1;
	delitem @SNAKE_SKIN, 2;
	delitem @BLACK_STINGER, 3;
	delitem @ASH_PILE, 2;

	set @bubble_mode, 0;
	setarray @bubble_modes$,
		"A mistura está calma.",
		"A mistura está fermentando.",
		"A mistura está fermentando violentamente!";

	set @color, 1 << (rand(3));
	setarray @colors$,
		"branco",
		"vermelho",
		"amarelo",
		"laranja",
		"azul",
		"roxo",
		"verde",
		"cinza";

	set @intensity, 1;
	setarray @intensities$,
		"claro",
		"médio",
		"escuro";

	set @auto_mode, 0;	// Start by adding a colour or going lighter

	goto L_monster_oil_loop;

L_monster_oil_missing:
	mes "[Carlos]";
	mes "\"Você não tem todos os ingredientes. lembre-se você precisa de 1 [Pérola], 2 [Pele de Cobra], 3 [Ferrão de Escorpião Preto], 2 [Pilha de cinzas] e 1 [Ferrão de Escorpião Dourado].\"";
	close;

L_monster_oil_missing_gold:
	mes "[Carlos]";
	mes "\"Você parece ter tudo, menos o ferrão dourado. Seu que é dificil mas continue tentando.\"";
	close;

L_monster_oil_loop:
	mes "[Preparando Óleo de Monstro]";
	mes @bubble_modes$[@bubble_mode];
	mes "A mistura está " + @colors$[@color] + " " + @intensities$[@intensity] + ".";
	next;
	menu
		"Adicionar folha de Alizarina", L_monster_oil_alizarin,
		"Adicionar folha de Gamboge", L_monster_oil_gamboge,
		"Adicionar folha de Cobalto", L_monster_oil_cobalt,
		"Adicionar folha de Malva", L_monster_oil_mauve,
		"Peça ao Carlos um conselho", L_monster_oil_explain,
		"Corra!", -;

	mes "[Preparando Óleo de Monstro]";
	mes "Você se atira ao chão. Segundos depois o caldeirão treme e a mistura explode.";
	mes "Carlos rasteja para fora debaixo da fumaça.";
	next;
	mes "[Carlos]";
	mes "\"Fico feliz que você tenha escolhido preparar num outro dia. Mas talvez tivesse funcionado se continuasse a mistura, ou não!\"";
	close;

L_monster_oil_out_of_leaves:
	mes "[Preparando Óleo de Monstro]";
	mes "Você não tem mais desta folha sobrando!";
	next;
	goto L_monster_oil_loop;

L_monster_oil_alizarin:
	set @use_leaf, @ALIZARIN;
	set @use_color, 1;
	goto L_monster_oil_leaf_color;

L_monster_oil_gamboge:
	set @use_leaf, @GAMBOGE;
	set @use_color, 2;
	goto L_monster_oil_leaf_color;

L_monster_oil_cobalt:
	set @use_leaf, @COBALT;
	set @use_color, 4;
	goto L_monster_oil_leaf_color;

L_monster_oil_leaf_color:
	if (countitem (@use_leaf) < 1) goto L_monster_oil_out_of_leaves;

	callsub S_monster_oil_random_move;
	if (@color & @use_color) goto L_monster_oil_pc_bad;
	set @color, @color | @use_color;
	delitem @use_leaf, 1;

	mes "A mistura muda de tom e sua folha se dissolve.";
	next;
	goto L_monster_oil_loop;

L_monster_oil_mauve:
	if (countitem(@MAUVE) < 1) goto L_monster_oil_out_of_leaves;
	delitem @MAUVE, 1;

	callsub S_monster_oil_random_move;
	if (@intensity == 2 && @color == 7) goto L_monster_oil_done;
	if (@intensity == 2) goto L_monster_oil_pc_bad;

	set @intensity, @intensity + 1;
	mes "A mistura escurece e sua folha de malva se dissolve.";
	next;
	goto L_monster_oil_loop;

S_monster_oil_random_move:
	mes "[Preparando Óleo de Monstro]";
	mes "Você joga uma folha e mexe.";

	if (@auto_mode == 1)
		goto L_monster_oil_random_1;

L_monster_oil_random_0: // mode 0:  add colour or lighten up
	set @auto_mode, 1;
	if (rand(3) == 0) goto L_monster_oil_random_0_lighten;

	set @choice, 1 << (rand(3));
	if (@color & @choice)
	goto L_monster_oil_auto_bad;
	set @color, @color | @choice;

	mes "Antes de sua folha se dissolver a mistura muda para uma tonalidade " + @colors$[@color] + ".";
	return;

L_monster_oil_random_0_lighten:
	if (@intensity == 0) goto L_monster_oil_auto_bad;
	set @intensity, @intensity - 1;
	mes "Antes de sua folha se dissolver a mistura muda para uma tonalidade de " + @colors$[@color] + ".";
	return;

L_monster_oil_random_1: // mode 1: remove colour
	set @auto_mode, 0;
	set @choice, 1 << (rand(3));
	if (!(@color & @choice)) goto L_monster_oil_auto_bad;
	set @color, @color & (~@choice);
	mes "Antes de sua folha se dissolver a mistura muda para uma tonalidade " + @colors$[@color] + ".";
	return;

L_monster_oil_pc_bad:
	if (rand(readparam(bInt) + 100) > 60) goto L_monster_oil_pc_bad_ok;
	if (@bubble_mode)
		mes "Como sua folha dissolveu, aumentou a fermentação da mistura.";
	if (!@bubble_mode)
		mes "Como sua folha dissolveu, a fermentação da mistura começou.";
	next;
	set @bubble_mode, @bubble_mode + 1;
	if (@bubble_mode > 2) goto L_monster_oil_explode;
	goto L_monster_oil_loop;

L_monster_oil_pc_bad_ok:
	mes "Sua folha se dissolve, mas parece não ter efeito.";
	next;
	goto L_monster_oil_loop;

L_monster_oil_auto_bad:
	if (rand(3) == 0) return;
	if (rand(readparam(bInt) + 100) > 70)
		return;
	mes "A mistura começa a fermentar violentamente.";
	set @bubble_mode, @bubble_mode + 1;
	if (@bubble_mode > 2) next;
	if (@bubble_mode > 2) goto L_monster_oil_explode;
	return;

L_monster_oil_explode:
	mes "[Explodindo a mistura de óleo]";
	mes "Sua mistura explode!";
	if (rand(readparam(bAgi)) > 40) goto L_monster_oil_explode_dodge;
	mes "A monstruosa explosão da mistura atingi você.";
	//next;
	itemheal (15 * rand(readparam(bVit))) - 1500, 0;
	close;

L_monster_oil_explode_dodge:
	mes "Você se atira ao chão no último momento.";
	next;
	mes "[Carlos]";
	mes "Carlos acena para você debaixo de uma cadeira.";
	mes "\"Eu lhe disse, isso é perigoso!\"";
	close;

L_monster_oil_done:
	mes "Você joga uma folha de malva, a bebida muda para um preto bem forte e se acalma.";
	next;

	mes "[Preparando Óleo de Monstro]";
	mes "Como instruido, você adiciona um ferrão dourado a mistura.";

	if (countitem (@GOLDEN_STINGER) < 1) goto L_monster_oil_no_gold_end;

	mes "A mistura fermenta mais um pouco e em seguida se acalma completamente.";
	delitem @GOLDEN_STINGER, 1;

	if (!(@Q_status & @SETZER_FLAG_MADE_OIL))
		getexp @MONSTER_OIL_XP, 0;
	if (!(@Q_status & @SETZER_FLAG_MADE_OIL))
		mes "* Você ganhou " +@MONSTER_OIL_XP+ " pontos de experiência]";

	set @Q_status, @Q_status | @SETZER_FLAG_MADE_OIL;
	callsub S_Update_Var;
	next;

	mes "[Carlos]";
	mes "Carlos se levanta debaixo da cadeira e olha para sua mistura. Ele está visivelmente impressionado.";
	mes "\"Muito bom! Isso deve ser o suficiente para uma poção de óleo de monstro. Deixe-me lhe preparar uma garrafa.\"";
	mes "Ele lhe entrega o óleo de monstro.";
	getitem @MONSTER_OIL, 1;
	close;

L_monster_oil_no_gold_end:
	mes "Mas onde está o seu ferrão dourado? Você não tinha um desde o inicio?";
	mes "Frustado, você se retira.";
	close;

L_close:
	close;

S_Update_Var:
	set QUEST_Forestbow_state, (QUEST_Forestbow_state & ~(@Q_MASK) | (@Q_status << @Q_SHIFT));
	return;

//L_NohMask:
//	mes "[Carlos]";
//	mes "\"Me desculpe, mas eu não sei. Eu vou ficar na cidade. Tente perguntar para um dos mineiros.\"";
//	close;
}
