///////////////////////////////////////////////////////////////////////////////////////////////////////
// NOME: Governanta
// ETMOLOGIA: Quem Governa a casa
// SEXO: Feminino
// SPRITE: Adefinir (Temporariamente 164)
// PROGRAMADOR: Lunovox<rui.gravata@hotmail.com>
// CRIAÇÃO: Quarta, 2010-03-05
// FUNÇÃO: Da um missão para escolher uma NomeDaPatente.
// STATUS: desenvolvendo
// RESET: Ainda não há reset! (Talvés Necessite)
///////////////////////////////////////////////////////////////////////////////////////////////////////

005.gat,155,99,0	script	Elric	153,{
	//set Elric_Capítulo,0; close;
	
	set FrascoOleoDeMonstro, 707;
	set FrascoVazio, 3023;
	set PoteDeAgua, 541;
	set AsaDeMorcego, 753;
	set PorcaoDeCinza, 701;
	set FerraoDeEscorpiaoVermeho, 517;
	set PetalaDeRosa, 565;
	set CasuloDeSeda, 718;
	set ErvaMalve, 680;
	
	if(
		countitem(FrascoVazio)>=1 && 
		countitem(PoteDeAgua)>=1 && 
		countitem(AsaDeMorcego)>=10 && 
		countitem(PorcaoDeCinza)>=20 && 
		countitem(FerraoDeEscorpiaoVermeho)>=25 && 
		countitem(PetalaDeRosa)>=30 && 
		countitem(CasuloDeSeda)>=40 &&
		countitem(ErvaMalve)>=60 &&
		Elric_Capítulo==1
	) goto _possui_tudo;

	if(
		countitem(FrascoVazio)>=1 && 
		countitem(PoteDeAgua)>=1 && 
		countitem(AsaDeMorcego)>=10 && 
		countitem(PorcaoDeCinza)>=20 && 
		countitem(FerraoDeEscorpiaoVermeho)>=25 && 
		countitem(PetalaDeRosa)>=30 && 
		countitem(CasuloDeSeda)>=40 &&
		countitem(ErvaMalve)>=60 &&
		Elric_Capítulo==2
	) goto _retorna_com_tudo;

	if(Elric_Capítulo==1) goto _Rever;
	if(Elric_Capítulo==2) goto _nova_compra;
//--------------------------------------------------------------------------------------------------
	mes "[Elric]";
	if(Sex==1) mes "=] \"Olá Andarilho!";
	if(Sex==0) mes "=] \"Olá Andarilha!";
	mes "Em que posso lhe ajudar? \"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "=] \"Olá meu nome é "+ strcharinfo(0) +".";
	mes "Procuro um Alquimista que consiga fazer Óleo de Monstro.\"";
	next;

	mes "["+ strcharinfo(0) +"]";
	menu
	"(-_-) Mas até agora não encontrei nenhum!", -,
	"(6_6) Você sabe onde posso encontrar um?", -;

	mes "[Alquimista Elric]";
	mes "=] \"Olá "+ strcharinfo(0) +"!";
	mes "Você deu sorte! Sou o Alquimista Elric.";
	mes "Eu consigo fazer Óleo de Monstro. \"";
	next;

_nova_compra:
	mes "["+ strcharinfo(0) +"]";
	menu
	"(*_*) o que é é preciso para 1 frasco Óleo de Monstro? [707]", _Regras,
	"(-_-) Você está brincando com minha cara!!", -;
	
	mes "[Alquimista Elric]";
	mes "=) Não estou brincado! Posso provar para você!";
	next;
goto _Regras;

_Regras:
	mes "[Alquimista Elric]";
	mes "=) \"Bom! Primeiro você precisa me dar os ingredientes, que não são fáceis de pegar. " +
	"E em segundo lugar, não sou relógio para trabalhar de graça, tenho que dar de comer a minhas 8 esposas. \"";
	next;
goto _PergutaPreco;

_Rever:
	mes "["+ strcharinfo(0) +"]";
	mes "(9_9) \"Olá Alquimista!\"";
	next;

	mes "[Alquimista Elric]";
	mes "=) \"Olá "+ strcharinfo(0) +"!\"";
	next;

_PergutaPreco:
	mes "["+ strcharinfo(0) +"]";
	menu
	"(¬_¬) Qual o preço e itens do Óleo de Monstro?", _listar_ingredientes,
	"(T_T) Não acredito que você seja um Alquimista!", -;

	mes "[Alquimista Elric]";
	mes "=[ \"Que pena para você "+ strcharinfo(0) +"!";
	mes "O Óleo de Monstro estava com um ótimo preço!\"";
	set Elric_Capítulo, 1;
close;

_listar_ingredientes:
	mes "[Alquimista Elric]";
	mes "Elric lhe mosta um papel com algo escrito...";
	next;
	mes "-----------------------------------------------------------";
	mes "[MATERIAIS DO ÓLEO DE MONSTRO]:";
	mes "01 Frasco Vazio [3023]";
	mes "01 Pote de Água [541]";
	mes "10 Asas de Morcego [753]";
	mes "20 Porções de Cinzas [701]";
	mes "25 Ferrões de Escorpião Vermeho [517]";
	mes "30 Petalas de Rosas [565]";
	mes "40 Casulos de Seda [718]";
	mes "60 Ervas Malve [680]";
	mes " VALOR: 100KGP (Aproveite apromoção, pois semana que vem os preços irão subir)";
	mes "-----------------------------------------------------------";
	next;
	mes "["+ strcharinfo(0) +"]";
	menu
	"(¬_¬) Deixa! Nunca vou conseguir isso mesmo!", -,
	"(T_T) Isso é um assalto a mão desarmada.", _SugerePesquisaDePreco;
close;

_SugerePesquisaDePreco:
	next;
	mes "[Alquimista Elric]";
	mes "(¬.¬) Bom! Você pode fazer pesquiza de preços por ai, para ver se encontra mais barato.";
	next;
	mes "["+ strcharinfo(0) +"]";
	menu
	"(¬_¬) Isso é exploração dos pobres!", -,
	"(T_T) Vou ver o que consigo fazer.", -;
	set Elric_Capítulo, 1;
close;
//--------------------------------------------------------------------------------------------------
_possui_tudo:
	mes "["+ strcharinfo(0) +"]";
	menu
	"(T_T) Eu trouxe tudo!", _NpcSurpreso,
	"(6.6) Tem como dar um desconto?", -;

	mes "[Alquimista Elric]";
	mes "(¬_¬) \"Pobre é a disgraça!\"";
	next;

	mes "["+ strcharinfo(0) +"]";
	menu
	"(u_u)", -,
	"(T_T)", -;

	mes "[Alquimista Elric]";
	mes ">=[ \"Não posso esse é o preço memso!!\"";
close;

_NpcSurpreso:
	mes "[Alquimista Elric]";
	if(Sex==1) mes "(O_o) \"Eita! Nem imaginei que um rapaz como você teria essa capacidade. Você deve ser bastante poderoso.\"";
	if(Sex==0) mes "(O_o) \"Eita! Nem imaginei que uma garota como você teria essa capacidade. Você deve ser bastante poderosa.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	menu
	"=] Eu faço o que posso!", -,
	"=X Deu um trabalhão conseguir tudo!", -;

	next;
	set Elric_Capítulo, 2;
	mes "[Alquimista Elric]";
	mes "=) \"Trouxe os 100 mil Galenos Portuários?\" (100kGP)";
	next;
	if(Zeny >= 100000) goto _com_grana;
goto _sem_grana;
//--------------------------------------------------------------------------------------------------
_retorna_com_tudo:
	mes "[Alquimista Elric]";
	mes "=) \"Você ainda quer trocar os itens e os 100 mil Galenos Portuários por um Frasco de Óleos de Monstro?\"";
	next;
	if(Zeny >= 100000) goto _com_grana;
goto _sem_grana;
//--------------------------------------------------------------------------------------------------
_com_grana:
	mes "["+ strcharinfo(0) +"]";
	mes "(T_T) \"Claro ou você acha que estou aqui para ver sua cara feia?\"";
	next;
	
	mes "[Alquimista Elric]";
	mes "=) \"Passa para cá a bufunfa!\"";
	next;

	mes "["+ strcharinfo(0) +"]";
	menu
	"=] Ok! Aqui esta tudo que pediu!", _AceitarDar,
	"=X Não quero lhe dar agora!", -;

	mes "[Alquimista Elric]";
	mes "=[ \"Está bem! Mas vai perder a promoção!\"";
close;

_AceitarDar:
	delitem FrascoVazio, 1;
	delitem PoteDeAgua, 1;
	delitem AsaDeMorcego, 10;
	delitem PorcaoDeCinza, 20;
	delitem FerraoDeEscorpiaoVermeho, 25;
	delitem PetalaDeRosa, 30;
	delitem CasuloDeSeda, 40;
	delitem ErvaMalve, 60;
	set Zeny, Zeny - 100000; 
	getitem FrascoOleoDeMonstro, 1;

	mes "[Alquimista Elric]";
	mes "Elric recolhe seus itens e dinheiro...";
	mes "=) \"Aqui está o seu Óleo de Monstro.\"";
	next;

	mes "["+ strcharinfo(0) +"]";
	menu
	"(O.O) Você ja tinha Óleo de Monstro?", -,
	"(¬.¬) Porque precisava dos itens, se você ja tinha?", -;

	mes "[Alquimista Elric]";
	mes "=þ \"Claro! Mas preciso dos itens para repor o meu estoque.\"";
	next;

	mes "[Alquimista Elric]";
	mes "=) \"Foi bom fazer negócio com você.\"";
	mes "Se precisar de mais óleo de monstro pode me procurar.\"";
close;
//--------------------------------------------------------------------------------------------------
_sem_grana:
	mes "["+ strcharinfo(0) +"]";
	menu
	"(O.O) Eita! Esqueci o dinheiro! Não estu com ele aqui!", -,
	"(¬.¬) Tenho que fazer um negocio urgente e já volto!", -;

_PrometeVoltarComGRana:
	mes "[Alquimista Elric]";
	if(Sex==1) mes "(T_T) \"Esse rapaz só não esquece a cabeça porque está colada no pescoço!\"";
	if(Sex==0) mes "(T_T) \"Essa garota só não esquece a cabeça porque está colada no pescoço!\"";
close;
//--------------------------------------------------------------------------------------------------
}

