//#################################################################################
//#               Gordo José                                                      #
//# Este script contém as definições para 2 quests voltadas aos jogadores         #
//#                        iniciantes e intermediários.                           #
//# Requisitos: 15 gosmas de verme e uma tiny healing potion para os n00bs e      #
//#  20 cavesnake eggs e uma medium healing potion para os médios (pode mudar)    #
//# Recompensa: 2kgp para os n00bs e 5kgp para os médios                          #
//#                                                                               #
//#################################################################################

005.gat,156,75,0 script José 117, {
	mes ".:: Modo Debug ::.";
	menu
		"Continuar", -,
		"Resetar", -;
	if(@menu==2) set QUEST_jose, 0;

	set @valor, 0;
	if (QUEST_jose == 0) goto _inicion00b;
	if (QUEST_jose == 1) goto _entregan00b;
	if (QUEST_jose == 2) goto _indigestao;
	if (QUEST_jose == 3 && baselevel > 30) goto _inicioMedio;
	if (QUEST_jose == 3) goto _curado1;
	if (QUEST_jose == 4) goto _entregaMedio;
	if (QUEST_jose == 5) goto _indigestao;
//	if (QUEST_jose == 6 && QUEST_chanceler == 1) goto _castelo; //será usado na quest do king's bow
	if (QUEST_jose == 6) goto _vcmecurou; 

_inicion00b: 
	mes "[José o Guloso]";
	mes "\"Estou com tanta fome que poderia comer um container cheio de coisas gostosas....\"";
	next;
	menu
		"Eu hein, que cara mais maluco!", -,
		"Ei, mas se tem tanta fome, porque não pede uma comida para a garçonete?", _razaon00b;
	close; 

_razaon00b:
	mes "[José o Guloso]";
	mes "\"Sabe o que é? Eles não tem o que eu quero aqui!";
	mes "EI!!! Quequequem sabe você não pode trazer para mim a comida que eu quero!?\"";
	next;
	menu
		"Claro, porque não?", _requisitosn00b,
		"Olha não sou nenhum entregador de fast-food ok?", -;
	close;

_requisitosn00b:
	mes "[José o Guloso]";
	mes "Com os olhos brilhando de alegria, José lhe fala:";
	mes "\"Por favor, me traz 15 Gosmas de Verme? EU PAGO BEM!\"";
	next;
	menu 
		"Cai na real Gordinho, vai caçar uns vermes e prepare você mesmo o seu lanche!", _voceehmau,
		"Você, definitivamente, tem um gosto diferente para comidas, mas já que vai pagar... eu trarei as Gosmas de Verme", -;
	set QUEST_jose, 1;
	close;

_voceehmau:
	mes "[José o Guloso]";
	mes "Aparentemente muito triste, José fita seus olhos e diz:";
	mes "\"Com toda certeza você não entende como é ser gordinho, eu já tive um belo corpo e hoje, a comida é tudo que me resta.\"";
	close;

_entregan00b: 
	mes "[José o Guloso]";
	mes "Babando como um animal faminto à espera de comida, José pergunta:";
	mes "\"Cade as minhas Gosmas de Verme?\"";
	next;
	menu
		"Aqui estão, faça bom proveito!", _trocan00b,
		"Eu ainda não os consegui, desculpe!", _comopode;
	close;

_comopode:
	mes "[José o Guloso]";
	mes "Como você pode ser capaz de fazer isso comigo? Eu estou com muita fome, por favor, eu lhe imploro, traga minha comida!\"";
	close; 

_trocan00b:
	if (countitem("MaggotSlime") < 15) goto _comopode; 
	mes "[José o Guloso]";
	mes "\"Maravilha! Agora eu posso comer um pouco! Obrigado!";
	mes "Ah! Já ia me esquecendo, aqui está o seu dinheiro!\"";
	set zeny, zeny + 2000;
	delitem "MaggotSlime", 15;
	set QUEST_jose, 2;
	mes "Você recebeu 2000 gp";
	close;

_inicioMedio: 
	mes "[José o Guloso]";
	mes "\"Estou com tanta fome que poderia comer uma casa feita de coisas gostosas....\"";
	next;
	menu
		"AH, Fale com a Garçonete", _razaomedio,
		"Eu hein, vou sair de perto, vai que esse gordinho pensa que eu sou comida !", -;
	close;

_razaomedio:
	mes "[José o Guloso]";
	mes "\"Estou cansado de ficar aqui, esse pessoal não tem para vender as coisas que eu gosto de comer... EI! Você! Quer ganhar um bom dinheiro?\"";
	next;
	menu
		"Claro, quem não quer?", -;
	mes "[José o Guloso]";
	mes "\"Me traga vinte ovos de cobras da caverna que eu lhe pagarei muito bem.\"";
	next;
	menu
		"Não muito trabalho por pouco!", _comopode,
		"Como dizem, pagando bem, que mal tem?", -;
	set QUEST_jose, 4;
	close;

_entregaMedio: 
	mes "[José o Guloso]";
	mes "Babando como um animal faminto à espera de comida, José pergunta:";
	mes "\"Cade meus ovos de cobras da caverna?\"";
	next;
	menu
		"Aqui estão, faça bom proveito!", _trocamedio,
		"Eu ainda não os consegui, desculpe!", _comopode;
	close;

_trocamedio:
	if (countitem("CaveSnakeEgg") < 20) goto _comopode;
	mes "[José o Guloso]";
	mes "\"Maravilha! Agora eu posso comer mais um pouco! Obrigado!";
	mes "Ah! Já ia me esquecendo, aqui está o seu dinheiro!\"";
	set zeny, zeny + 5000; 
	delitem "CaveSnakeEgg", 20; 
	set QUEST_jose, 5;
	mes "Você recebeu 5000 gp";
	close;

_indigestao:
	mes "[José o Guloso]";
	mes "Você percebe que José não parece estar passando bem";
	mes "\"AHHH, que indigestão, preciso manerar mais com a comida, caramba que fartão!\"";
	next;
	goto _menuCura;

_menuCura:
	menu
		"Examinar", _examinar,
		"Dar alguma coisa para ele", _darAlgo,
		"Deixar José descançando", -;
	close;

_examinar:
	mes "José está com o rosto pálido e seu corpo está tremendo.";
	next;
	goto _menuCura;
	close;

_darAlgo:
	menu
		"Poção de Cactus", _pcactus,
		"Poção Energética", _pener,
		"Poção de Concentração", _pcon,
		"Poção Mínima de cura", _mcura,
		"Poção Pequena de cura", _pcura,
		"Poção Média de cura", _medcura,
		"Poção Grande de cura", _gcura,
		"Ops! Acho que eu não tenho nada para ajudar o gordinho", -;
	goto _menuCura;
	close;

_pcactus:
	if (countitem("CactusPotion") < 1) goto _ops;
	delitem "CactusPotion", 1;
	goto _falhacura;

_pener:
	if (countitem("IronPotion") < 1) goto _ops;
	delitem "IronPotion", 1;
	goto _falhacura;

_pcon:
	if (countitem("ConcentrationPotion") < 1) goto _ops;
	delitem "ConcentrationPotion", 1;
	goto _falhacura;

_mcura:
	if (countitem("TinyHealingPotion") < 1) goto _ops;
	delitem "TinyHealingPotion", 1;
	set @valor, 250;
	if (QUEST_jose == 2) goto _completan00b;
	goto _falhacura;

_pcura:
	if (countitem("SmallHealingPotion") < 1) goto _ops;
	delitem "SmallHealingPotion", 1;
	set @valor, 500;
	if (QUEST_jose == 2) goto _completan00b;
	goto _falhacura;

_medcura:
	if (countitem("MediumHealingPotion") < 1) goto _ops;
	delitem "MediumHealingPotion", 1;
	set @valor, 1000;
	if (QUEST_jose == 2) goto _completan00b;
	if (QUEST_jose == 5) goto _completamedio;
	close;

_gcura:
	if (countitem("LargeHealingPotion") < 1) goto _ops;
	delitem "LargeHealingPotion", 1;
	set @valor, 2000;
	if (QUEST_jose == 2) goto _completan00b;
	if (QUEST_jose == 5) goto _completamedio;
	close;

_ops:
	mes "Ao procurar no seu inventário, você percebe que não tem a poção que pretendia dar para José.";
	mes "Instantaneamente você pensa que deveria ter se precavido.";
	next;
	goto _menuCura;

_falhacura:
	mes "[José o Guloso]";
	mes "Com certa dificuldade ele bebe a poção.";
	next;
	mes "[José o Guloso]";
	mes "Depois de um sonoro arroto, você percebe que ele não melhorou.";
	next;
	goto _menuCura;

_completan00b:
	mes "[José o Guloso]";
	mes "Com certa dificuldade ele bebe a poção.";
	next;
	mes "[José o Guloso]";
	mes "Depois de um sonoro arroto, José parece estar muito bem!";
	set QUEST_jose, 3;
	next;

_curado1:
	mes "[José o Guloso]";
	mes "\"Nossa! Estou bem, muito obrigado viu!? Talvez uma outra hora eu lhe peça mais alguma coisa!\"";
	if(@valor) goto _pagamentoCura;
	close;

_pagamentoCura:
	next;
	mes "[José o Guloso]";
	mes "\"Me sinto tão bem, que vou lhe dar mais um dinheirinho por ter me ajudado a fazer aquela dor passar.\"";
	set zeny, zeny + @valor;
	mes "Você recebeu "+ @valor +" gp";
	close;

_completamedio:
	mes "[José o Guloso]";
	mes "Com certa dificuldade ele bebe a poção.";
	next;
	mes "[José o Guloso]";
	mes "Depois de um sonoro arroto, José parece estar muito bem!";
	set QUEST_jose, 6;
	next;

_vcmecurou:
	mes "[José o Guloso]";
	mes "\"Me sinto melhor agora, muito obrigado por me ajudar!";
	mes "Daqui em diante vou comer coisas menos invasivas.\"";
	if(@valor) goto _pagamentoCura;
	close;
}
