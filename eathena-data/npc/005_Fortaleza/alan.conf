//#################################################################################
//#                                                                               #
//#  Este script contém scripts de npcs para obter o arco da floresta(é uma quest #
//#                                                                               #
//# Requisitos: até 21 rawlogs, 5000gp, um pouco de conversa e busca.             #
//#                                                                               #
//# Recompensa: Arco da Floresta                                                  #
//#################################################################################

// Mestre da Loja do Arqueiro
005.gat,38,70,0	script	Alan	125, {
	set @Q_MASK, NIBBLE_0_MASK;
	set @Q_SHIFT, NIBBLE_0_SHIFT;

        set @Q_status, ((QUEST_Forestbow_state) & @Q_MASK) >> @Q_SHIFT;

	if (@Q_status == 1) goto L_State_1;
	if (@Q_status == 2) goto L_State_2;
	if (@Q_status == 3) goto L_State_3;
	if (@Q_status == 4) goto L_State_4;
	if (@Q_status == 5) goto L_State_5;
	
	mes "[Alan]";
	mes "\"Quando quizeres comprar algo pergunte ao meu aprendiz, eu apenas trabalho com encomendas especiais.\"";
	next;
	if (Inspector == 1)
		menu
			"Muito bem, Obrigado", -,
			"Podes me fazer um arco realmente bom?", L_State_0_1,
			"Você viu algo susteito ultimamente, algo que possa estar conectado com os Roubos recentes?", L_NohMask_Answer;
	if (Inspector != 1)
		menu
			"Muito bem, obrigado", -,
			"Podes me fazer um arco realmente bom?", L_State_0_1;
	close;
	
L_State_0_1:
	mes "[Alan]";
	mes "\Estás falando dos meus Lendários Arcos da Floresta?\"";
	next;
	menu
		"Sim, esses seriam ótimos.", -;
	mes "[Alan]";
	mes "\"Desculpa não os fabrico mais.\"";
	next;
	menu
		"Ah, que pena", -,
		"O que? Qual o motivo?", L_State_0_2;
	close;

L_State_0_2:
	mes "[Alan]";
	mes "\"O problema é a falta de materiais sabe? Esses meus arcos não são feitos de madeira comum.";
	mes "Eles são feitos com uma madeira viva especial, e apenas os melhores tocos vivos, são bons o bastante.";
	mes "Eu pegava esses tocos do Jack O lenhador, mas a última vez que fui encomendar mais uma remessa ele me disse que nunca mais trabalharia com isso.\"";

	set @Q_status, 1;
        callsub S_Update_Var;

	next;
	menu
		"Que pena.", -,
		"Você perguntou para ele o motivo?", L_State_0_3;
	close;

L_State_0_3:
	mes "[Alan]";
	mes "\"Claro que sim! Ele disse para eu deixa-lo em paz. Porque não você não pergunta para ele o motivo disso?\"";
	next;
	menu
		"Claro eu vou perguntar para ele.", -,
		"Tenho certeza que ele tem seus motivos.", -;
	close;

L_State_1:
	mes "[Alan]";
	mes "\"Já perguntastes ao Jack o motivo dele não me entregar mais os tocos?\"";
	next;
	if (Inspector == 1)
		menu
			"Não tive tempo ainda", -,
			"Não o encontrei ainda.", L_State_1_1,
			"Você viu algo susteito ultimamente, algo que possa estar conectado com os Roubos recentes?", L_NohMask_Answer;
	if (Inspector != 1)
		menu
			"Não tive tempo ainda", -,
			"Não o encontrei ainda.", L_State_1_1;
	close;

L_State_1_1:
	mes "[Alan]";
	mes "\"Procure ele, tenho certeza que o encontrarás por aí! Na floresta talvez?.\"";
	close;
	
L_State_2:
	mes "[Alan]";
	mes "\"Já perguntastes ao Jack o motivo dele não me entregar mais os tocos?\"";
	next;
	if (Inspector == 1)
		menu
			"Sim, ele me comentou que as árvores se transformaram em perigosos monstros.", -,
			"Você viu algo susteito ultimamente, algo que possa estar conectado com os Roubos recentes?", L_NohMask_Answer;
	if (Inspector != 1)
		menu
			"Sim, ele me comentou que as árvores se transformaram em perigosos monstros.", -;
	mes "[Alan]";
	mes "\"Péssima notícia! Quem sabe você não toma o trabalho dele?";
	mes "Quando tu matares alguns desses monstros, me traga os seus tocos para que eu possa os analizar.";
	mes "Talvez você seja capaz de encontrar um pedaço de madeira que seja forte o bastante para tornar-se um de meus arcos.\"";

	set @Q_status, 3;
        callsub S_Update_Var;

	close;
	
L_State_3:
	mes "[Alan]";
	mes "\"Como anda a caçada, me truxestes algum toco?\"";
	if (countitem(569) < 1) close;
	next;
	if (Inspector == 1)
		menu
			"Aqui, dá uma olhada.", L_State_3_try,
			"Sim, mas eu preciso dele para outra coisa.", -,
			"Você viu algo susteito ultimamente, algo que possa estar conectado com os Roubos recentes?", L_NohMask_Answer;
	if (Inspector != 1)
		menu
			"Aqui, dê uma olhada", L_State_3_try,
			"Sim, mas eu preciso dele para outra coisa.F", -;
	close;

L_State_3_try:
	mes "[Alan]";
	mes "\"Hummmm... Me parece bom, mas será forte o bastante?\"";
	mes "Alan curva o toco contra o seu joelho";
	next;
	delitem "RawLog", 1;
	set @Temp1, rand(20);
	if (@Temp1 == 0) goto L_State_3_success;
	mes "O toco se parte com um ruído forte";
	next;
	mes "[Alan]";
	mes "\"Desculpe este toco não era forte o bastante, agora ele virou lixo.\"";
	if (countitem(569) < 1) close;
	next;
	menu
		"Claro, aqui está.", L_State_3_try,
		"OPA! Pare de quebrar minhas coisas", -;
	close;
	
L_State_3_success:
	mes "Alan faz muita força, porém o toco não dobra.";
	next;
	mes "[Alan]";
	mes "\"AAHHH esse sim! Que ótimo toco você me trouce, ele vai se transformar em um ótimo arco!\"";
	next;
	set @Q_status, 4;
        callsub S_Update_Var;
	
L_State_4:
	mes "[Alan]";
	mes "\"Com este toco, eu posso fazer um ótimo Arco da Floresta. Preciso apenas de 10000gp para os materiais e tempo de trabalho.\"";
	next;
	if (Inspector == 1)
		menu
			"10,000??? QUE FACADA!", -,
			"Claro, aqui está", L_State_4_pay,
			"Você viu algo susteito ultimamente, algo que possa estar conectado com os Roubos recentes?", L_NohMask_Answer;
	if (Inspector != 1)
		menu
			"10,000??? QUE FACADA!", -,
			"Claro, aqui está!", L_State_4_pay;
	close;

L_State_4_pay:
	if (zeny < 10000) goto L_State_4_nocash;
	getinventorylist;
	if (@inventorylist_count == 100) goto L_State_4_TooMany;
	set zeny, zeny - 10000;
	getitem "ForestBow", 1;
	set @Q_status, 5;
        callsub S_Update_Var;
	mes "[Alan]";
	mes "\"Aqui está, aproveite ele.\"";
	close;

L_State_4_nocash:
	mes "[Alan]";
	mes "\"Parece que você está sem dinheiro.\"";
	close;

L_State_4_TooMany:
	mes "[Alan]";
	mes "\"Parece que você não tem espaço para guardar o arco, volte depois.\"";
	close;

L_State_5:
	mes "[Alan]";
	mes "\"Espero que você esteja satisfeito com o Arco da Floresta, ele é realmente um dos meus melhores trabalhos.\"";
	next;

	if (Inspector == 1)
		menu
			"E realmente estou!", -,
			"Você viu algo susteito ultimamente, algo que possa estar conectado com os Roubos recentes?", L_NohMask_Answer;
	close;

L_NohMask_Answer:
	mes "[Alan]";
	mes "\"Não.\"";
	close;

S_Update_Var:
        set QUEST_Forestbow_state,
        	(QUEST_Forestbow_state & ~(@Q_MASK)
                | (@Q_status << @Q_SHIFT));
        return;
}
