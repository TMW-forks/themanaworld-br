/**
 * Avélia, a bruxa da agilidade.
 *
 * @date 17 de outubro de 2010
 * @author Diogo_RBG - http://diogorbg.blogspot.com
 */

008.gat,103,42,0	script	Avélia	167,3,2{

	if(@buffAgi!=0) goto _continuacao;

	mes "[Avélia]";
	mes "\"Como chegou até aqui? É preciso ser muito rápido para escapar dos ataques dos espectros!";
	mes "Bem... isso não importa agora. Fique dentro da Barreira Mágica e estará a salvo.\"";
	menu
		"Não estou fugindo. Vim atrás de seu poder.", -;
	mes "[Avélia]";
	mes "\"Meu poder não irá valer de nada se você não for forte o suficiente para poder usá-lo.";
	mes "Mas de qualquer forma não posso deixar que você saia lá fora sem a minha ajuda.\"";
	set @buffAgi, $@buffAgi;
	next;
	goto _continuacao;

_continuacao:
	mes "[Avélia]";
	mes "\"Venha sempre que precisar de meu poder. Entre em meu círculo mágico e desfrute dele.";
	mes "Só espere até que eu possa acumular meu poder.\"";
	close;

OnTouch:
	if(@buffAgi==0 || @buffAgi==$@buffAgi) end;

	sc_start 185,600, 60;
	//specialeffect2 3;
	set @buffAgi, $@buffAgi;
	end;

OnTimer20000:
	setnpctimer 0;
	set $@buffAgi, $@buffAgi+1;
	specialeffect 21;
	end;

OnInit:
	initnpctimer;
	set $@buffAgi, 1;
	end;

}
