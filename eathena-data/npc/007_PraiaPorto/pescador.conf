//

007.gat,131,113,0	script	Pescador	311,{
	set @raiz, 740;
	set @corda, 3217;

	//- Quest Corda Pescador - Conversar com Alan primeiro.
	if (QUEST_MASK1 & MASK1_CORDA_PESCADOR) goto _geral; //< Quest Corda Pescador concluída. Estado 3.
	if (QUEST_arcoFloresta!=1) goto _geral;

	if (QUEST_cordaPescador == 0) goto _introFloresta;
	if (QUEST_cordaPescador == 1) goto _florestaCorda4;
	if (QUEST_cordaPescador == 2) goto _florestaCorda6;
	goto _geral;

_geral:
	mes "[Pescador]";
	mes "\"Ehh, parece que hoje o mar não está pra peixe mesmo...\"";
	close;

_introFloresta:
	mes "[Pescador]";
	mes "\"É, parece que meu avô estava certo. É inútil jogar as redes quando as viglotas não estão por perto.\"";
	next;
	menu
		"Viglotas? O que é isso senhor?", -;
	next;
	mes "[Pescador]";
	mes "\"Viglota é um tipo de pássaro que voa de vez em quando sobre o mar a procura de peixes.\"";
	next;
	mes "\"Todo o pescador sabe que são as viglotas que determinam os dias de pesca. Sem elas, pescar é ir contra a corrente da natureza. Mas deixando isso de lado, o que você faz aqui?\"";
	next;
	menu
		"Me disseram que os pescadores dessa região fazem cordas fortes e bem finas.", -;
	next;
	mes "[Pescador]";
	mes "\"Hahaha! Parece que a fama dos pescadores desta ilha ultrapassam o mar-além meu amigo!\"";
	next;
	mes "\"Sim! Apesar de parecer história de pescador, nossas linhas para varas são bastante fortes e resistentes. Mas acho que você não quer uma de nossas cordas especiais para pescar não é mesmo?\"";
	next;
	menu
		"Na verdade não, preciso da corda para outra coisa.",_florestacorda1,
		"Sim, vou começar a pescar.", -;
	next;
	mes "[Pescador]";
	mes "\"Como mentiroso você se sairia um bom pescador. E para a sua informação, só os que tem o selo da Ondina têm permissão para pescar. Mas farei sua corda mesmo assim...\"";
	next;
	goto _florestacorda2;

_florestacorda1:
	next;
	mes "[Pescador]";
	mes "\"Bem, em todo o caso, farei a sua corda...\"";
	next;
	goto _florestacorda2;

_florestacorda2:
	mes "[Pescador]";
	mes "\"Basta me trazer 10 raízes de árvores e mais 10000 GPs pelo serviço deste homem do mar. O que você me diz?\"";
	next;
	menu
		"Por mim está bom o bastante.",_florestacorda3,
		"Hum...vou pensar a respeito.", -;
	mes "[Pescador]";
	mes "\"Ok, sem problemas.\"";
	close;

_florestacorda3:
	mes "[Pescador]";
	mes "\"Então estarei esperando aqui companheiro.\"";
	next;
	callsub _missaoCordaPescador;
	close;

_florestaCorda4:
	mes "[Pescador]";
	mes "\"Conseguiu as 10 raízes e os 10.000 GPs que te pedi?\"";
	next;
	menu
		"Aqui está. 10 raízes e 10.000 GPs",_checagemfloresta,
		"Falta pegar ainda.", -;
	mes "[Pescador]";
	mes "\"Não estou com pressa, quando tiver tudo, pode falar comigo.\"";
	next;
	callsub _missaoCordaPescador;
	close;

_checagemfloresta:
	if (countitem(@raiz) < 10 || Zeny < 10000) goto _florestacorda5;
	mes "[Pescador]";
	mes "\"Então vamos ao trabalho!\"";
	delitem @raiz, 10;
	set Zeny, Zeny-10000;
	set QUEST_cordaPescador, 2;
	next;
	goto _florestaCorda6;
	close;

_florestacorda5:
	mes "[Pescador]";
	mes "\"Você não trouxe tudo o que pedi. Continue procurando e volte quando tiver 10 raízes e 10.000 GP em mãos.\"";
	next;
	callsub _missaoCordaPescador;
	close;

_florestaCorda6:
	mes "O pescador guarda o dinheiro em um dos bolsos e no outro as raízes.";
	next;
	mes "Ele vai até a parte de trás da sua casa e traz um pote de conteúdo oleoso com um cheiro igual ao de peixe.";
	next;
	mes "[Pescador]";
	mes "\"Está aqui o nosso segredo: óleo dos frutos do mar.\"";
	next;
	mes "O pescador continua sua tarefa fazendo tranças com as raízes logo depois que elas foram descascadas com uma pequena faca, até formar um tipo de corda fina, mas ao mesmo tempo rústica e que poderia ser facilmente partida.";
	next;
	mes "Tendo deitado a corda em uma tábua de madeira, ele começa agora a passar o óleo, aplicando os segredos dos pescadores em sua tarefa.";
	next;
	mes "Depois de alguns minutos, a corda esta lisa e bastante resistente, o suficiente para ser usada tanto na terra quanto no mar.";
	next;
	mes "[Pescador]";
	mes "\"Prontinho, sua corda está pronta para ser usada. Foi com uma destas que quase consegui pegar a Ossuda, um esqueleto de baleia vivo que mal caberia nesta praia, mas ninguém acreditaria nesta história mesmo... Bem, está ai, faça bom proveito companheiro.\"";
	getitem @corda, 1;
	next;
	callsub _missaoCordaPescadorCompleta;
	close;

//- Missão Corda Pescador ----------------------------------------------

_missaoCordaPescador:
	if(QUEST_cordaPescador!=0) goto _else2;
		set QUEST_cordaPescador, 1;
		set MOBS_cordaPescador, 0;
_else2:
	mes "[Missão: Corda Fina]";
	mes "* Itens: " +countitem(@raiz)+ "/10 Raízes";
	mes "* Monstros: " +MOBS_cordaPescador+ "/30 Troncos Vivos";
	mes "* Recompensa: 9.000 XP";
	// Tronco Vivo: 149 XP, 149x30 ~= 9.000 XP
	// Margem Segurança lvl 39: 9.163 XP
	return;

_missaoCordaPescadorCompleta:
	mes "* Você ganhou 9.000 pontos de experiência.";
	message strcharinfo(0), "Ganhei 9.000 pontos de experiência.";
	set BaseExp, BaseExp+9000; //< Restrição lvl>=30

	set QUEST_cordaPescador, 0;
	set MOBS_cordaPescador, 0;
	set QUEST_MASK1, QUEST_MASK1 | MASK1_CORDA_PESCADOR;
	return;

}
