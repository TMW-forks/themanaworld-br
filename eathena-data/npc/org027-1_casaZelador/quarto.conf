/**
 * Quest: Tem um zumbi debaixo da minha cama !
 *
 * Quarto do Coveiro. Onde se esconde o Zumbi.
 *
 * @date sábado, 13 de novembro de 2010
 *
 * @author Diogo_RBG - http://diogorbg.blogspot.com
 * @author Alasmist
 */

org027-1.gat,27,26,0	script	#porta	0,0,1{
	if (isin("org027-1.gat", 26, 20, 32, 25)) goto _isin;
	if (quest_coveiro >= 6) end;
	if (quest_coveiro < 2) goto _fechado;
	if (quest_coveiro == 2) goto _abrir;
	if (quest_coveiro > 2) goto _abrir2;

_isin:
	if (mobcount("org027-1.gat", "#armario::OnDead") >= 0) goto _impedir;
	goto _sairQuarto;

_fechado:
	mes "Você tenta abrir a porta do quarto do coveiro, mas ela está trancada.";
	close;

_fechado2:
	mes "Você tenta abrir a porta do quarto do coveiro, mas alguém a trancou por dentro.";
	close;

_impedir:
	message strcharinfo(0), "Eu não posso fugir agora...";
	end;

_abrir:
	if (getareausers("org027-1.gat", 26, 20, 32, 25) >= 1) goto _fechado2;
	set quest_coveiro, 3;
	warp "org027-1.gat",27,24;
	message strcharinfo(0), "Que cheiro terrível! Com certeza o zumbi está em algum lugar neste quarto!";
	close;

_abrir2:
	if (getareausers("org027-1.gat", 26, 20, 32, 25) >= 1) goto _fechado2;
	killmonster "org027-1.gat", "#armario::OnDead";
	warp "org027-1.gat",27,24;
	close;

_sairQuarto:
	warp "org027-1.gat",27,28;
	close;
}

//======================================================================

org027-1.gat,27,19,0	script	[?]#janela	300,{
	if (quest_coveiro!=3 || isin("org027-1.gat", 26, 20, 32, 25)==0) end;
	mes "Você percebe sinais de arrombamento nesta janela... Provavelmente o zumbi deve ter entrado por aqui.";
	close;
}
org027-1.gat,31,20,0	script	#cama1	332,{
	if (quest_coveiro!=3 || isin("org027-1.gat", 26, 20, 32, 25)==0) end;
	mes "Você se abaixa para olhar debaixo da cama.";
	next;
	mes "Mas não há nada de especial no ponto em que você procurou";
	close;
}
org027-1.gat,32,20,0	script	#cama2	332,{
	if (quest_coveiro!=3 || isin("org027-1.gat", 26, 20, 32, 25)==0) end;
	mes "Você se abaixa para olhar debaixo da cama.";
	next;
	mes "Mas não há nada de especial no ponto em que você procurou";
	close;
}
org027-1.gat,31,21,0	script	#cama3	332,{
	if (quest_coveiro!=3 || isin("org027-1.gat", 26, 20, 32, 25)==0) end;
	mes "Você se abaixa para olhar debaixo da cama.";
	next;
	mes "Mas não há nada de especial no ponto em que você procurou";
	close;
}
org027-1.gat,32,21,0	script	#cama4	332,{
	if (quest_coveiro!=3 || isin("org027-1.gat", 26, 20, 32, 25)==0) end;
	mes "Você se abaixa para olhar debaixo da cama.";
	next;
	mes "Hmmm... Você encontrou um pequeno pedaço de tecido sujo de terra, um farrapo fedorento que provavelmente veio da roupa do zumbi!";
	getitem 777, 1; //< Farrapos.
	set quest_coveiro, 4;
	close;
}

//======================================================================

org027-1.gat,29,20,0	script	#armario	300,{
	if (quest_coveiro<3 || quest_coveiro>5 || isin("org027-1.gat", 26, 20, 32, 25)==0) end;
	if (quest_coveiro == 3) goto _abrir1;
	if (quest_coveiro == 4 || quest_coveiro == 5) goto _abrir2;
	end;

_abrir1:
	mes "Você tenta abrir o armário mas a porta parece estar emperrada.";
	close;

_abrir2:
	if(mobcount("org027-1.gat", "#armario::OnDead") >= 0) end;
	mes "Você tenta abrir o armário mas a porta parece estar emperrada.";
	next;
	mes "Hmm... Mas a porta não está trancada...";
	next;
	mes "Isso significa que... o zumbi...";
	next;
	mes "Sem pensar duas vezes você puxa a maçaneta do armário com toda a sua força!";
	close2;
	set quest_coveiro, 5;
	//set $quest_coveiro_id, getcharid(3);
	//set $nome$, strcharinfo(0);
	monster "org027-1.gat", 29, 21, "", 1036, 1, "#armario::OnDead";
	startnpctimer;
	end;

OnDead:
	message strcharinfo(0), "Ae! Dei um jeito no Zumbi! =)";
	set quest_coveiro, 6;
	end;

}
