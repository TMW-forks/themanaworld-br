/**
 * Joãozinho é um negociante nato. Não deixe ele raspar a sua grana!
 *
 * @date sábado, 15 de janeiro de 2011
 * @author Diogo_RBG - http://diogorbg.blogspot.com/
 */

008.gat,34,95,0	script	Joãozinho	309,{
	setarray @tio$, "tia", "tio";
	set QUEST_precoMapa, 0; //< Apaga a variável antiga.

	if(isin("008.gat", 27, 88, 41, 102)==0) goto _longe;
	mes "[Joãozinho]";
	mes "\"Ei " + @tio$[sex] + "!!! Quer comprar um mapa?\"";

_menu:
	menu
		"Sim", _sim,
		"Mapa de que?", _info1,
		"E onde você o encontrou?", _info2,
		"E o que tem no mapa?", _info3,
		"Não", -;
	mes "[Joãozinho]";
	mes "\"Tudo bem. Vendo pra outro!\"";
	close;

_longe:
	mes "[Joãozinho]";
	mes "\"Ei " + @tio$[sex] + "!!! Porque você está tão longe?";
	mes "Chegue mais perto para que eu possa te mostrar uma coisa.\"";
	close;

_info1:
	mes "[Joãozinho]";
	mes "\"Um mapa do tesouro... ora!";
	mes "O que você achou que fosse?\"";
	next;
	menu
		"Não acredito que seja um mapa do tesouro.", -;
	mes "[Joãozinho]";
	mes "\"É porque você não sabe ler!";
	mes "Está escrito nele: MAPA DO TESOURO.\"";
	next;
	mes "[Joãozinho]";
	mes "\"Vai querer comprar um?\"";
	goto _menu;

_info2:
	mes "[Joãozinho]";
	mes "\"Eu vi cair do bolso de um pirata bêbado.";
	mes "E que também tinha um bafo horrível.\"";
	next;
	mes "[Joãozinho]";
	mes "\"Mas você vai querer o mapa ou não?\"";
	goto _menu;

_info3:
	mes "[Joãozinho]";
	mes "\"Tem uns rabiscos e um X bem grande no meio.";
	mes "Eu bem que queria saber ler mapas do tesouro, mas não sei!\"";
	next;
	mes "[Joãozinho]";
	mes "\"Você tem cara de que sabe ler mapas do tesouro... Vai querer o mapa?\"";
	goto _menu;

_sim:
	if(@precoMapa==1) goto _negociado;
	mes "[Joãozinho]";
	mes "\"Ok, custa 15.000 GP. Vai querer comprar?\"";
	menu
		"Vou comprar por 15.000 GP.", _comprar,
		"Não mesmo!", _negociar;
	mes "[Joãozinho]";
	mes "\"Aff... isso quer dizer sim ou não?\"";
	close;

_negociar:
	mes "[Joãozinho]";
	mes "\"Quer negociar né?!";
	mes "Eu posso te vender por 10.000 GP. É uma ótima oferta!\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Até parece que vou te pagar 10.000 GP por uma cópia de mapa.";
	mes "Sua bolsa está cheia de mapas... tá pensando que vai ficar rico às minhas custas?!\"";
	next;
	mes "[Joãozinho]";
	mes "\"Ok... Ok... estou vendo que você sabe negociar!\"";
	next;
	mes "[Joãozinho]";
	mes "\"Que tal 5.000 GP no mapa? É minha ultima oferta! Vai querer comprar?\"";
	set @precoMapa, 1;
	menu
		"Por 5.000 GP eu compro.", _comprar,
		"Ainda está caro!", -;
	mes "[Joãozinho]";
	mes "\"Eu disse que não faria por menos.\"";
	close;

_negociado:
	mes "[Joãozinho]";
	mes "\"Nosso acordo era de 5.000 GP. Vai querer comprar?\"";
	menu
		"Vou comprar por 5.000 GP.", _comprar,
		"Ainda está caro!", -;
	mes "[Joãozinho]";
	mes "\"Não faço por menos que isso.\"";
	close;

_comprar:
	if(@precoMapa==0) set @tmp, 15000;
	if(@precoMapa==1) set @tmp, 5000;
	if(zeny<@tmp) goto _semGrana;
	getinventorylist;
	if( (@inventorylist_count + (countitem(3006)==0) ) > 100 ) goto _cheio;
	set zeny, zeny - @tmp;
	getitem 3006, 1; //Mapa do Tesouro
	mes "[Joãozinho]";
	mes "\"Vlw " + @tio$[sex] + "!\"";
	close;

_semGrana:
	mes "[Joãozinho]";
	mes "\"Você não tem o dinheiro!";
	mes "Só estava querendo me enganar.\"";
	close;

_cheio:
	mes "[Joãozinho]";
	mes "\"Como você pode carregar tanta coisa!";
	mes "Não vou poder te vender agora.";
	mes "Volte assim que sua bolsa estiver vazia.\"";
	close;
}
