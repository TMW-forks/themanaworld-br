//
// @autor Diogo_RBG

//prisao.gat,20,24,0	script	debug	307,{
//	mes "vc nunca foi preso!";
//	set prisEst, 0;
//	close;
//}

prisao.gat,23,24,0	script	Clóvis	307,{
	if (prisEst==1) goto _foiPreso;
	if (prisEst==2) goto _aguarde;
	if (prisEst==3) goto _deOlho;

	mes "[Clóvis o Guarda da Prisão]";
	mes "\"Você sim é um cidadão exemplar! Nunca foi preso!";
	mes "Ao contrário destes sujeitos de ficha suja.\"";
	npctalk "'"+strcharinfo(0)+"' é um cidadão exemplar! Possui a ficha limpa!";
	close;

_foiPreso:
	mes "[Clóvis o Guarda da Prisão]";
	mes "\"Outro que não respeita ou não conhece as regras do reino!\"";
	mes "";
	mes "* Não tratar mal os outros jogadores (insultando ou dizendo palavrões).";
	mes "* Proibido uso de 'bots' ou qualquer atividade que não sejam comandos do jogador.";
	mes "* Não abusar da caixa de texto com mensagens repetitivas.";
	mes "* Também não abusar da função de 'negociar' com jogadores.";
	mes "* Não pedir itens repetidamente. Pidões serão ignorados ou banidos.";
	next;
	menu
		"Como vim parar aqui?", -,
		"Sou inocente... me solte!", _inocente;
	mes "[Clóvis o Guarda da Prisão]";
	mes "\"É uma boa pergunta!\"";
	next;
	goto _explicacao;

_inocente:
	mes "[Clóvis o Guarda da Prisão]";
	mes "\"Isso é o que todos dizem!\"";
	next;
	goto _explicacao;

_explicacao:
	mes "[Clóvis o Guarda da Prisão]";
	mes "\"Você foi preso porque descumpriu umas das leis do reino.";
	mes "Por isso terá que pagar pena na prisão!\"";
	set $prisTime[prisNum], 0;
	set prisEst, 2;
	close;

_aguarde:
	if ($prisTime[prisNum]>=prisPena) goto _cumpriuPena;

	mes "[Clóvis o Guarda da Prisão]";
	set @tmp, prisPena-$prisTime[prisNum];
	if (@tmp==1) mes "\"Sua pena está quase terminando. Será libertado em 1 minuto!\"";
	if (@tmp>1)  mes "\"Sua pena ainda não terminou. Ainda faltam "+@tmp+" minutos.\"";
	mes "";
	mes "* Não tratar mal os outros jogadores (insultando ou dizendo palavrões).";
	mes "* Proibido uso de 'bots' ou qualquer atividade que não sejam comandos do jogador.";
	mes "* Não abusar da caixa de texto com mensagens repetitivas.";
	mes "* Também não abusar da função de 'negociar' com jogadores.";
	mes "* Não pedir itens repetidamente. Pidões serão ignorados ou banidos.";
	close;

_cumpriuPena:
	mes "[Clóvis o Guarda da Prisão]";
	mes "\"Você cumpriu toda a pena!";
	mes "Mas lembre-se que se tornar a cometer crimes contra o reino será enviado para prisão outra vez!\"";
	next;
	mes "[Clóvis o Guarda da Prisão]";
	mes "\"Para não ser preso novamente basta não tratar mal outros jogadores, não perturbar a paz e não usar trapassas no jogo.\"";
	mes "";
	mes "* Não tratar mal os outros jogadores (insultando ou dizendo palavrões).";
	mes "* Proibido uso de 'bots' ou qualquer atividade que não sejam comandos do jogador.";
	mes "* Não abusar da caixa de texto com mensagens repetitivas.";
	mes "* Também não abusar da função de 'negociar' com jogadores.";
	mes "* Não pedir itens repetidamente. Pidões serão ignorados ou banidos.";
	next;
	mes "[Clóvis o Guarda da Prisão]";
	mes "\"Agora você está livre!";
	mes "Faça bom uso de sua liberdade.\"";
	set @x, rand(11);
	warp "prisao.gat", 25+@x, 27;
	savepoint "prisao.gat", 25+@x, 27;
	callfunc "delPris";
	close;

_deOlho:
	mes "[Clóvis o Guarda da Prisão]";
	mes "\"Agora você está fichado. Melhor não cometer mais nenhum crime!\"";
	if (prisQtd==1) npctalk "'"+strcharinfo(0)+"' foi fichado uma vez!";
	if (prisQtd>1) npctalk "'"+strcharinfo(0)+"' foi fichado "+prisQtd+" vezes!";
	close;

//- Mostrando emotion -------------------------------------------------

OnTimer60000:
	emotion 14;
	setnpctimer 0;
	end;

OnInit:
	initnpctimer;
	end;
}

//= Vigiando prisioneiros =============================================

prisao.gat,0,0,0	script	#timerPrisao	300,{
	end;
OnTimer60000:
	set $@i, 0;
	_loop: set $@i,$@i+1; if($@i>$numPris) goto _fimLoop;
		if ($prisTime[$@i]==-1 || isloggedin($prisPlay[$@i])==0) goto _loop;
		attachrid ($prisPlay[$@i]);
		if (prisNum!=$@i) goto _loop; //> Está logado com outro char!
		set $prisTime[$@i], $prisTime[$@i]+1;
		goto _loop;
	_fimLoop:
	setnpctimer 0;
	end;

OnInit:
	initnpctimer;
	end;

}

//= Funções ===========================================================

function	script	addPris	{
	if (prisEst!=0) goto _jaFoiPreso;

	set $numPris, $numPris+1;
	set prisNum, $numPris;
	set $prisPlay[prisNum], getcharid(3);
	set $prisTime[prisNum], -1;
	set prisPena, 15;
	//resetlvl 4;        //< tirava os pontos de status
	//set SkillPoint, 0; //< Zerava a skill Basic
	setskill 2, 0; // Reseta a skill de trade.
	set prisQtd, 1;
	set prisEst, 1;
	return;

_jaFoiPreso:
	set $prisTime[prisNum], -1;
	set prisPena, 15;
	set prisQtd, prisQtd+1;
	//resetlvl 4;        //< tirava os pontos de status
	//set SkillPoint, 0; //< Zerava a skill Basic
	setskill 2, 0; // Reseta a skill de trade.
	set prisEst, 1;
	return;
}

function	script	delPris	{
	if (prisEst==0||prisEst==3) return;

	if (prisQtd>0) set prisQtd, prisQtd-1;
	set $prisTime[prisNum], -1;
	set prisEst, 3;
	return;
}
