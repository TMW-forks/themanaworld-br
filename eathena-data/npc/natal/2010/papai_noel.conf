//
// @autor Diogo_RBG - http://diogorbg.blogspot.com
// @desc O bom velhinho está te esperando em seu chalé nas montanhas.
// #quest <desc>, <getItens>, <delItens>, <extra>
// @quest 'Biscoitos de Natal', '1 presenteDourado', '? santaCookie', 'Level >= 30.'
// @quest 'Gorro de Papai Noel', '1 SantaBeardHat', '1 presenteDourado', ''

010-2.gat,90,21,0	script	debug	105, {
	mes "debug";
	menu
		"set 0", -,
		"set 3", -,
		"set 4", -,
		"set 6", -,
		"NATAL2_noelMobs+=50", -,
		"Sair", -;
	if (@menu==1) set NATAL2_noel, 0;
	if (@menu==2) set NATAL2_noel, 3;
	if (@menu==3) set NATAL2_noel, 4;
	if (@menu==4) set NATAL2_noel, 6;
	if (@menu==5) set NATAL2_noelMobs, NATAL2_noelMobs+50;
	close;
}

010-2.gat,93,21,0	script	Papai Noel	105, {
	set @itemPresenteRosa, 515;
	set @itemPresenteAzul, 516;
	set @itemPresenteVerde, 538;

	setarray @trocado$, "Menino", "Menina";
	setarray @umMenino$, "uma menina", "um menino";

	if (QUEST_biscoitoNatal==0 && countitem(635)>8 && BaseLevel>=30) goto _biscoitos;
	if (QUEST_gorroBarba==0 && countitem(3045)) goto _presenteDourado;

	if (NATAL2_noel==1 || NATAL2_noel==2) goto _semOculos;
	if (NATAL2_noel==3) goto _comOculos;
	if (NATAL2_noel==4) goto _ajudaComPresentes;
	if (NATAL2_noel==5) goto _verificaPresentes;
	if (NATAL2_noel==6) goto _ajudaComMaisPresentes;
	if (NATAL2_noel==7) goto _verificaMaisPresentes;
	if (NATAL2_noel>=8) goto _felizNatal;

//- NATAL 2010 DO TMW-BR ----------------------------------------------

	mes "[Papai Noel]";
	mes "\"How How How. Feliz natal! " +@trocado$[sex]+ ".\"";
	next;
	menu
		"Mas Papai Noel...eu sou " +@umMenino$[sex]+ "...", -;
	mes "Papai Noel faz uma tentativa de lhe ver melhor, como se não estivesse lhe enxergando direito.";
	next;
	mes "[Papai Noel]";
	mes "\"Oh! Me perdoe criança, não vi que você era " +@umMenino$[sex]+ ". Estou com sérios problemas neste natal.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Tudo bem papai Noel, mas o que aconteceu? Vejo que o senhor não está usando o seus óculos.\"";
	next;
	mes "[Papai Noel]";
	mes "\"É minha criança, estou com um problemão. Eu estava vendo as cartinhas das crianças quando o meu amigo boneco de neve veio me fazer uma visita.\"";
	next;
	mes "[Papai Noel]";
	mes "\"Tirei os óculos e deixei-o em cima da mesa. Daí algum dos meus ajudantes, enquanto eu conversava, fez uma carinha no bumbum do boneco de neve, usando os meus óculos e alguns doces. No momento eu não vi nada, mas assim que ele saiu, os danadinhos começaram a rir.\"";
	next;
	mes "[Papai Noel]";
	mes "\"Ai ai, estes meus ajudantes... Agora, como está próximo do natal, não tem como eu pedir para eles irem lá pegar os óculos novamente. Eles estão muito ocupados agora. E eu também não posso ir, pois tenho que organizar tudo. A única solução é alguém ir lá e falar com ele.\"";
	next;
	menu
		"Não se preocupe Noel, eu vou lá falar com ele.", _falarComEle,
		"Óculos no bumbum...kkk.", -;
	mes "[Papai Noel]";
	mes "\"Oh! Que indelicadeza de sua parte!\"";
	close;

_falarComEle:
	mes "[Papai Noel]";
	mes "\"Oh! Seria muito gentil de sua parte fazer isso por este velhinho. Obrigado "+strcharinfo(0)+".\"";
	set NATAL2_noel, 1;
	close;

_semOculos:
	mes "[Papai Noel]";
	mes "\"Olá "+strcharinfo(0)+", vejo que ainda não conseguiu os meus óculos. Por favor, vá até o meu amigo Boneco de Neve e peça a ele para me devolver. Caso contrário, não vou conseguir ler a lista de presentes das crianças e o natal vai ser um desastre este ano.\"";
	close;

_comOculos:
	mes "[Papai Noel]";
	mes "\"How How How! Você conseguiu pegar eles! Agora o natal está a salvo! Muito obrigado "+strcharinfo(0)+". Vou colocar o seu nome na listinha de presentes especiais, How How How! Tenha um feliz natal "+strcharinfo(0)+".\"";
	set NATAL2_noel, 4;
	close;

//- AJUDANTE DE PAPAI NOEL --------------------------------------------

_ajudaComPresentes:
	mes "[Papai Noel]";
	mes "\"Olá de novo " +strcharinfo(0)+ "! Estava precisando muito da sua ajuda.\"";
	next;
	menu
		"Claro! O que eu posso fazer desta vez papai Noel?", _ajudaComPresentes2,
		"No momento eu estou ocupado Noel, lamento.", -;
	mes "[Papai Noel]";
	mes "\"Ah...tudo bem minha criança. Acho que eu posso resolver sozinho. Tenha um feliz natal.\"";
	close;

_ajudaComPresentes2:
	mes "[Papai Noel]";
	mes "\"Oh minha criança! Muito obrigado por me ajudar. O problema é que agora que tenho os óculos, eu li a lista de presentes e vi que temos muita coisa para fazer. Não sei se os meus ajudantes dão conta de tudo. Preciso que você me ajude a pegar alguns presentes.\"";
	next;
	menu
		"Sim! Ajudo.", -;
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Mas em que lugar da casa os presentes estão?\"";
	next;
	mes "[Papai Noel]";
	mes "\"Não estão dentro da casa...Estão do lado de fora.\"";
	next;
	menu
		"O que?", -,
		"Do lado de fora?", -;
	mes "[Papai Noel]";
	mes "\"Tunico estava separando uma pilha de presentes lá fora, quando de repente alguns limos engoliram os presentes e saíram pólo norte afora. Agora, cabe a você recuperar 50 Presentes Rosa, 50 Presentes Azul e 50 Presentes Verde para que o natal seja salvo mais uma vez.\"";
	next;
	menu
		"Já estou a caminho.", -;
	callsub _missaoPresentes;
	close;

_verificaPresentes:
	if(countitem(@itemPresenteRosa)<50) goto _semTodosPresentes;
	if(countitem(@itemPresenteAzul)<50) goto _semTodosPresentes;
	if(countitem(@itemPresenteVerde)<50) goto _semTodosPresentes;
	if(NATAL2_noelMobs<100) goto _semMonstros;
	getinventorylist;
	if(@inventorylist_count == 100) goto _cheio;

	mes "[Papai Noel]";
	mes "\"How How How. Você conseguiu mais uma vez! Que bravura! Como prêmio lhe entrego esta lembrancinha para que você carregue consigo o espírito do natal em todos os dias do ano.\"";
	delitem @itemPresenteRosa, 50;
	delitem @itemPresenteAzul, 50;
	delitem @itemPresenteVerde, 50;
	getitem 3193, 1; //< [Globo Natal Mágico]
	mes "";
	callsub _missaoPresentesCompleta;
	next;
	goto _felizNatal;

_semTodosPresentes:
	mes "[Papai Noel]";
	mes "\"Oh...volte aqui quando recuperar todos os 50 Presentes Rosa, 50 Presentes Azul e 50 Presentes Verde que te pedi. Por favor, seja breve, pois o natal já está quase chegando...\"";
	next;
	callsub _missaoPresentes;
	close;

_semMonstros:
	mes "[Missão: Ajudante de Papai Noel]";
	mes "* Quantidade de monstros insuficiente.";
	next;
	callsub _missaoPresentes;
	close;

//- CONTROLE AJUDANTE DE PAPAI NOEL -----------------------------------

_missaoPresentes:
	if(NATAL2_noel!=4) goto _else;
		set NATAL2_noel, 5;
		set NATAL2_noelMobs, 0;
		set @NATAL2_noelMobs, 0;
_else:
	mes "[Missão: Ajudante de Papai Noel]";
	mes "* Monstros: "+NATAL2_noelMobs+"/100 Limo Gelo/Rodolfo/Noel";
	mes "* Itens: "+countitem(@itemPresenteRosa)+"/50 Presentes Rosa";
	mes "* Itens: "+countitem(@itemPresenteAzul)+"/50 Presentes Azul";
	mes "* Itens: "+countitem(@itemPresenteVerde)+"/50 Presentes Verde";
	callfunc "getXpMax", 60000;
	mes "* Recompensa: "+@xp+" XP";
	// Limo de Gelo: 301 XP 301*100*2 ~= 60.000 XP
	// Margem Segurança lvl 59: 66.038 XP
	return;

_missaoPresentesCompleta:
	callfunc "getXpMax", 60000;
	mes "* Você ganhou "+@xp+" pontos de experiência.";
	message strcharinfo(0), "Ganhei "+@xp+" pontos de experiência.";
	set BaseExp, BaseExp+@xp;
	set NATAL2_noel, 6;
	set NATAL2_noelMobs, 0;
	return;

//- RESGATE DE MAIS PRESENTES -----------------------------------------

_ajudaComMaisPresentes:
	mes "[Papai Noel]";
	mes "\"Olá de novo " +strcharinfo(0)+ "! O natal ainda corre perigo e preciso que você me ajude novamente.\"";
	next;
	menu
		"Claro! Sempre que precisar Papai Noel.", _ajudaComMaisPresentes2,
		"No momento eu estou ocupado Noel, lamento.", -;
	mes "[Papai Noel]";
	mes "\"Ah...tudo bem minha criança. Acho que eu posso resolver sozinho. Tenha um feliz natal.\"";
	close;

_ajudaComMaisPresentes2:
	mes "[Papai Noel]";
	mes "\"Oh minha criança! Muito obrigado por me ajudar. O problema é que eu descobri que mais presentes foram perdidos. Não foram só aqueles que eu pensava. O que me diz de recuperar mais alguns presentes perdidos?\"";
	next;
	menu
		"Conte comigo.", -;
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Mas o que aconteceu desta vez?\"";
	next;
	mes "[Papai Noel]";
	mes "\"Enquanto você resgatava os presentes perdidos eu fui conversar com o Tunico para que ele não empilhasse mais presentes lá fora, pois os limos engoliram a pilha de presentes que ele estava fazendo. Ai ele me perguntou: \"Engoliram as 3 pilhas de presentes!\". Só quando ele me disse isso que eu fui perceber o tamanho da trapalhada dele.\"";
	next;
	mes "[Papai Noel]";
	mes "\"Todos estes presentes fazem muita falta e levaria muito tempo para produzi-los novamente. A melhor opção seria você resgatar os 100 Presentes Rosa, 100 Presentes Azul e 100 Presentes Verde antes da noite de natal.\"";
	next;
	menu
		"Já estou a caminho.", -;
	callsub _missaoMaisPresentes;
	close;

_verificaMaisPresentes:
	if(countitem(@itemPresenteRosa)<100) goto _semMaisPresentes;
	if(countitem(@itemPresenteAzul)<100) goto _semMaisPresentes;
	if(countitem(@itemPresenteVerde)<100) goto _semMaisPresentes;
	if(NATAL2_noelMobs<200) goto _semMaisMonstros;
	getinventorylist;
	if(@inventorylist_count == 100) goto _cheio;

	mes "[Papai Noel]";
	mes "\"How How How. Você conseguiu mais uma vez! Que bravura! Como prêmio lhe entrego esta lembrancinha para que você carregue consigo o espírito do natal em todos os dias do ano.\"";
	delitem @itemPresenteRosa, 100;
	delitem @itemPresenteAzul, 100;
	delitem @itemPresenteVerde, 100;
	getitem 3045, 1; //< [Presente Dourado]
	mes "";
	callsub _missaoMaisPresentesCompleta;
	next;
	goto _felizNatal;

_semMaisPresentes:
	mes "[Papai Noel]";
	mes "\"Oh...volte aqui quando recuperar todos os 100 Presentes Rosa, 100 Presentes Azul e 100 Presentes Verde que te pedi. Por favor, seja breve, pois o natal já está quase chegando...\"";
	next;
	callsub _missaoMaisPresentes;
	close;

_semMaisMonstros:
	mes "[Missão: Resgate de mais presentes]";
	mes "* Quantidade de monstros insuficiente.";
	next;
	callsub _missaoMaisPresentes;
	close;

_felizNatal:
	mes "[Papai Noel]";
	mes "\"Feliz natal minha criança! Que sua generosidade dure por toda a vida. Sei que você irá longe neste mundo!\"";
	close;

//- CONTROLE RESGATE DE MAIS PRESENTES --------------------------------

_missaoMaisPresentes:
	if(NATAL2_noel!=6) goto _else2;
		set NATAL2_noel, 7;
		set NATAL2_noelMobs, 0;
		set @NATAL2_noelMobs, 0;
_else2:
	mes "[Missão: Resgate de mais presentes]";
	mes "* Monstros: "+NATAL2_noelMobs+"/200 Limo Gelo/Rodolfo/Noel";
	mes "* Itens: "+countitem(@itemPresenteRosa)+"/100 Presentes Rosa";
	mes "* Itens: "+countitem(@itemPresenteAzul)+"/100 Presentes Azul";
	mes "* Itens: "+countitem(@itemPresenteVerde)+"/100 Presentes Verde";
	callfunc "getXpMax", 60000;
	set @xp, @xp*2;
	mes "* Recompensa: "+@xp+" XP";
	// Limo de Gelo: 301 XP 301*200*2 ~= 120.000 XP
	// Margem Segurança lvl 69: 138.810 XP
	return;

_missaoMaisPresentesCompleta:
	callfunc "getXpMax", 60000;
	set @xp, @xp*2;
	mes "* Você ganhou "+@xp+" pontos de experiência.";
	message strcharinfo(0), "Ganhei "+@xp+" pontos de experiência.";
	set BaseExp, BaseExp+@xp;
	set NATAL2_noel, 8;
	set NATAL2_noelMobs, 0;
	return;

//- OS BISCOITOS PREFERIDOS DO PAPAI NOEL -----------------------------

_biscoitos:
	mes "[Papai Noel]";
	mes "\"Humm... que cheirinho bom!";
	mes "Você tem aqueles deliciosos biscoitos de Natal?\"";
	next;
	menu
		"Tenho alguns.", _tenho,
		"Eu estava juntando eles pra você.", _tenho,
		"Não tenho biscoitos.", -;
	mes "[Papai Noel]";
	mes "\"How How How!";
	mes "Então acho que meu nariz está me enganando.";
	mes "Ou talvez sejam alguns farelos dos que comi hoje de manhã!\"";
	close;

_tenho:
	mes "[Papai Noel]";
	mes "\"Eu sabia! Nunca me engano quanto a isso!";
	mes "...";
	mes "Papai Noel pode comer alguns biscoitos com você?\"";
	next;
	menu
		"Claro Papai Noel!", -,
		"Sabia que você ia querer!", -,
		"Eu sou " +@bomMenino$[sex]+ "!", -;
	mes "[Papai Noel]";
	mes "\"Papai Noel até vai te dar este presentinho!\"";
	next;
	if (countitem(635)<9) goto _semBiscoito;
	getinventorylist;
	if( (@inventorylist_count + (countitem(635)>15) + (countitem(3045)==0) ) > 100 ) goto _cheio;

	mes "[Papai Noel]";
	mes "CrOoOck CroOock CrOOOck!";
	mes "\"Que delícia!\"";
	mes "";
	mes "* Você ganhou um [Presente Dourado].";
	set @tmp, countitem(635);
	if (@tmp>15) set @tmp, 15; // Retira até 15 biscoitos.
	delitem 635, @tmp;
	getitem 3045, 1;
	set QUEST_biscoitoNatal, 1;
	close;

_semBiscoito:
	mes "[Papai Noel]";
	mes "\"Que pena, você não tem o suficiente para nós dois.\"";
	close;

_cheio:
	mes "[Papai Noel]";
	mes "\"Você está carregando muita coisa.\"";
	close;

//- ABRINDO UM PRESENTE JUNTO COM O PAPAI NOEL ------------------------

_presenteDourado:
	mes "[Papai Noel]";
	mes "\"Ainda não abriu o seu presente dourado?\"";
	next;
	menu
		"O que tem nele?", _explica,
		"Presente dourado?", _explica,
		"Depois eu abro.", -;
	mes "[Papai Noel]";
	mes "\"Tudo bem então.\"";
	close;

_explica:
	mes "[Papai Noel]";
	mes "\"O presente dourado possui uma magia especial de natal de ocultar o presente nele guardado. Fazendo que ele sempre seja uma surpresa.\"";
	next;
	mes "[Papai Noel]";
	mes "\"As vezes ele pode te surpreender com um presente muito especial! Lhe dando o que você mais deseja caso ele sinta uma forte presença do seu espírito de natal.";
	mes "";
	mes "Deseja abrí-lo agora?\"";
	next;
	menu
		"SIM!!!", -,
		"Vou abrir agora mesmo!", -,
		"Estou repleto do espírito do natal.", -;
	if (countitem(3045)==0) goto _semPresente;
	getinventorylist;
	if( (@inventorylist_count + (countitem(3045)>1) + 1) > 100 ) goto _cheio;

	mes "[Papai Noel]";
	mes "\"Parece que você gosta muito do Papai Noel!";
	mes "How How How!!!\"";
	mes "";
	mes "* Você ganhou um [Gorro de Papai Noel].";
	delitem 3045, 1;
	getitem 1206, 1;
	set QUEST_gorroBarba, 1;
	close;

_semPresente:
	mes "[Papai Noel]";
	mes "\"Não esconda seu presente.\"";
	close;
}
