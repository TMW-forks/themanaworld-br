// 009 Ilha Fortaleza mobs

009.gat,47,56,56,79	monster	Fada Traidora (1)	1307,1,0,120000,mobFada::On1307

009.gat,0,0,0	script	mobFada	-1,{
On1307:
	set @mobID, 1307;
	callfunc "MobPoints";
	callfunc "makeHorcrux";
	break;

	end;
}

//=====================================================================

function	script	makeHorcrux	{
//	message strcharinfo(0),"[IvanCap:"+ NinfaIvanize_capitulo +"]";
	if(SeFezHorcruxDeFada==1) end;
	if(NinfaIvanize_capitulo<=1) 	end;
	if (
		countitem(3021)>=80 &&
		countitem(3020)>=50 &&
		countitem(3003)>=30 &&
		countitem(3024)>=10 &&
		countitem(3016)>=1
	)goto _iniciar;
	message strcharinfo(0), "Eu não tenho os ingredientes da horcrux!";
end;
//-----------------------------------------------------------------------------
_iniciar:
//	* 80 Asas de Fadas [ID:3021]
//	* 50 Pó encantados [ID:3020]
//	* 30 Porções da morte [ID:3003]
//	* 10 Veneno de Ninfas [ID:3024]
//	* 01 Objeto Recipiente [ID:3016]

// A @Ordem$ correta da magia é: "5267134"
//		Moer todas as Asas de Fada.
//		Jogar Porções da Morte sobre as asas moidas.
//		Jogar Venenos de Nimfas sobre as asas moidas.
//		Misturar Tudo.
//		Banhar a fada morta com todo Pó Mágico.
//		Banhar a fada morta com toda Mistura. 
//		Dizer palavras mágicas

	set @Ordem$,"";

	set @SeMoeuAsas,0;
	set @SeJuntouMorte,0;
	set @SeJuntouVeneno,0;
	set @SeMisturou,0;
	set @SeBanhouPo,0;
	set @SeBanhouMistura,0;

	setarray @Passos$, 
		"Banhar a fada morta com todo Pó Mágico.", 
		"Jogar Porções da Morte sobre as asas moidas.", 
		"Banhar a fada morta com toda Mistura.", 
		"Dizer palavras mágicas.", 
		"Moer todas as Asas de Fada.", 
		"Jogar Venenos de Nimfas sobre as asas moidas.",
		"Misturar Tudo.";

_decidir:
	mes "["+ strcharinfo(0) +"]";
	mes "O que eu tenho fazer agora?";
	//mes "[" + @Ordem$ + "]";
	menu
		@Passos$[0], _BanharPoMagico,
		@Passos$[1], _AddPotionMorte,
		@Passos$[2], _BanharMistura,
		@Passos$[3], _DizerMagia,
		@Passos$[4], _MoerAsas,
		@Passos$[5], _AddPoisonNimphe,
		@Passos$[6], _MisturarTudo,
		" * Desistir e perder mirtura!", _desistir;
goto _desistir;
//-----------------------------------------------------------------------------
_desistir:
	message strcharinfo(0),strcharinfo(0) + ": Preciso de Ivanize para não perder o restos dos itens!";
close;
//-----------------------------------------------------------------------------
_JaFiz:
	mes "["+ strcharinfo(0) +"]";
	mes "Eu ja fiz isso!";	
	next;
goto _decidir;
//-----------------------------------------------------------------------------
_MoerAsas:
	if(@SeMoeuAsas==1) goto _JaFiz;
	set @SeMoeuAsas,1;
	set @Ordem$,@Ordem$ + "5";
	set @Passos$["4"],"[OK] " + @Passos$["4"];
	delitem 3021,80;
goto _decidir;
//-----------------------------------------------------------------------------
_AddPotionMorte:
	if(@SeJuntouMorte==1) goto _JaFiz;
	set @SeJuntouMorte,1;
	set @Ordem$,@Ordem$ + "2";
	set @Passos$["1"],"[OK] " + @Passos$["1"];
	delitem 3003,30;
goto _decidir;
//-----------------------------------------------------------------------------
_AddPoisonNimphe:
	if(@SeJuntouVeneno==1) goto _JaFiz;
	set @SeJuntouVeneno,1;
	set @Ordem$,@Ordem$ + "6";
	set @Passos$["5"],"[OK] " + @Passos$["5"];
	delitem 3024,10;
goto _decidir;
//-----------------------------------------------------------------------------
_MisturarTudo:
	if(@SeMisturou==1) goto _JaFiz;
	set @SeMisturou,1;
	set @Ordem$,@Ordem$ + "7";
	set @Passos$["6"],"[OK] " + @Passos$["6"];
goto _decidir;
//-----------------------------------------------------------------------------
_BanharPoMagico:
	if(@SeBanhouPo==1) goto _JaFiz;
	set @SeBanhouPo,1;
	set @Ordem$,@Ordem$ + "1";
	set @Passos$["0"],"[OK] " + @Passos$["0"];
	delitem 3020,50;
goto _decidir;
//-----------------------------------------------------------------------------
_BanharMistura:
	if(@SeBanhouMistura==1) goto _JaFiz;
	set @SeBanhouMistura,1;
	set @Ordem$,@Ordem$ + "3";
	set @Passos$["2"],"[OK] " + @Passos$["2"];
goto _decidir;
//-----------------------------------------------------------------------------
_DizerMagia:
	set @Palavras$,"";
	input @Palavras$,"";
	set @Ordem$,@Ordem$ + "4";
	if(@Palavras$=="KOMM-SEELE-NYMPHE" && @Ordem$=="5267134") goto _AcertouMagia;
	if(@Palavras$!="KOMM-SEELE-NYMPHE") goto _ErrouPalavrasMagicas;
	if(@Ordem$!="5267134") goto _ErrouMagia;
close;
//-----------------------------------------------------------------------------
_AcertouMagia:
	delitem 3016,1;
	getitem 3017,1;
	set SeFezHorcruxDeFada,1;
	mes "["+ strcharinfo(0) +"]";
	mes "Acertei!";
	mes "Agora tenho uma Horcrux!";
	mes "Só tenho que ter cuidado para não deixar ninguem portando minha Horcrux!";
close;
//-----------------------------------------------------------------------------
_ErrouPalavrasMagicas:
	mes "["+ strcharinfo(0) +"]";
	mes "Caramba! As palavras mágicas.";
	if (
		@SeMoeuAsas==1 ||
		@SeJuntouMorte==1 ||
		@SeJuntouVeneno==1 ||
		@SeBanhouPo==1
	)mes "Perdi os itens que usei neste fetiço!";
	mes "Preciso falar novamente com Ivanize!";
close;
//-----------------------------------------------------------------------------
_ErrouMagia:
	mes "["+ strcharinfo(0) +"]";
	mes "Caramba! Errei como fazer a magia.";
	if (
		@SeMoeuAsas==1 ||
		@SeJuntouMorte==1 ||
		@SeJuntouVeneno==1 ||
		@SeBanhouPo==1
	)mes "Perdi os itens que usei neste fetiço!";
	mes "Preciso falar novamente com Ivanize!";
close;

}
