//
// [Joãozinho]
// if QUEST_precoMapa==0
//  * Preço do mapa nao foi negociado. vende por 5kgp.
//  * if "Não mesmo"
//    * negocia preco do mapa
//    * set QUEST_precoMapa, 1;
// if QUEST_precoMapa==1
//  * Preço do mapa foi negociado. vende por 2.5kgp.
//

008.gat,34,95,0	script	Joãozinho	309,{
	if (Sex == 0) set @tioa$, "tia";
	if (Sex == 1) set @tioa$, "tio";

	mes "[Joãozinho]";
	mes "\"Ei " + @tioa$ + "!!! Quer comprar um mapa?\"";
	next;

_menu:
	menu
		"Sim", _sim,
		"Mapa de que?", _info1,
		"Porque você tem tantos?", _info2,
		"Não", _nao;
	goto _nao;

_nao:
	mes "[Joãozinho]";
	mes "\"Tudo bem. Vendo pra outro.\"";
	close;

_info1:
	mes "[Joãozinho]";
	mes "\"Um mapa do tesouro... ora!";
	mes "O que você achou que fosse?\"";
	next;
	menu
		"E onde você o encontrou?", -,
		"E o que tem no mapa?", _info3;
	mes "[Joãozinho]";
	mes "\"Eu vi cair do bolso de um pirata bêbado.";
	mes "E que também tinha um bafo horrível.\"";
	next;
	mes "[Joãozinho]";
	mes "\"Mas você vai querer o mapa ou não?\"";
	next;
	goto _menu;

_info2:
	mes "[Joãozinho]";
	mes "\"Tirei cópias do original.";
	mes "É mais lucrativo!\"";
	next;
	mes "[Joãozinho]";
	mes "\"Vai querer um?\"";
	next;
	goto _menu;

_info3:
	mes "[Joãozinho]";
	mes "\"Parece ser esta ilha mesmo... mas é muito estranho.";
	mes "E também, não sei como chegar até esta caverna perto destas duas cachoeiras.";
	mes "Sem contar que é muito perigoso.\"";
	next;
	mes "[Joãozinho]";
	mes "\"Mas isso você resolve depois... Vai querer o mapa?\"";
	next;
	goto _menu;

_sim:
	if(QUEST_precoMapa==1) goto _sim2;
	mes "[Joãozinho]";
	mes "\"Ok, custa 5.000 GP.\"";
	next;
	menu
		"Aqui está", _ok,
		"Não mesmo", _negociar;
	goto _ok1;

_sim2:
	mes "[Joãozinho]";
	mes "\"Posso te fazer por 2.500 GP.\"";
	next;
	menu
		"Aqui está", _ok,
		"Está caro", -;
	mes "[Joãozinho]";
	mes "\"Não faço por menos que isso.\"";
	close;

_negociar:
	mes "[Joãozinho]";
	mes "\"Quer negociar né?!";
	mes "2.500 GP. Não faço por menos.\"";
	set QUEST_precoMapa, 1;
	next;
	menu
		"Agora sim", _ok,
		"Ainda está caro", -;
	mes "[Joãozinho]";
	mes "\"Eu disse que não faria por menos.\"";
	close;

_ok:
	if(QUEST_precoMapa==0) set @tmp, 5000;
	if(QUEST_precoMapa==1) set @tmp, 2500;
	if(zeny<@tmp) goto _semGrana;
	getinventorylist;
	if( (@inventorylist_count + (countitem(3006)==0) ) > 100 ) goto _cheio;
	set zeny, zeny - @tmp;
	getitem 3006, 1; //Mapa do Tesouro
	mes "[Joãozinho]";
	mes "\"Vlw " + @tioa$ + "!\"";
	close;

_semGrana:
	mes "[Joãozinho]";
	mes "\"Você não tem o dinheiro!";
	mes "Só estava querendo me enganar.\"";
	close;

_cheio:
	mes "[Joãozinho]";
	mes "\"Como você pode carregar tanta coisa!";
	mes "Não vou poder te vender agora.";
	mes "Volte assim que sua bolsa estiver vazia.\"";
	close;
}
