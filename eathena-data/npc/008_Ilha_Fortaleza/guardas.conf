//

008.gat,96,25,0	script	Elias	307,{
	if (HALLOWEEN_elias==2) goto _prove;
	if (HALLOWEEN_elias==3) goto _provado;
	if (HALLOWEEN_elias>3 || HALLOWEEN_elias2>1) goto _contagem;
	if (HALLOWEEN_elias==1) goto _obrigado;

	mes "[Elias o Guarda da Torre]";
	mes "\"Vá para a cidade. Por enquanto lá está seguro.\"";
	next;
	menu
		"Estou indo!", -,
		"Não mesmo, eu vim lutar!", _lutar;
	close;

_lutar:
	mes "[Elias o Guarda da Torre]";
	mes "\"É meu dever protejer a cidade.";
	mes "E você só iria me atrapalhar... vá logo para a cidade.\"";
	next;
	menu
		"Já disse que vim para lutar.", _lutar2,
		"Grosso!", -;
	close;

_lutar2:
	mes "[Elias o Guarda da Torre]";
	mes "\"Muito bem. Então mate um destes monstros menores para provar que pode fazer isso.\"";
	next;
	menu
		"Você verá como eu não estou brincando.", -,
		"Só um! Moleza!", -;
	set HALLOWEEN_elias, 2;
	close;

_prove:
	mes "[Elias o Guarda da Torre]";
	mes "\"Você disse que poderia matar um destes monstros... prove isso!\"";
	next;
	menu
		"Vou provar!", -;
	close;

_provado:
	mes "[Elias o Guarda da Torre]";
	mes "\"Você matou mesmo aquele monstro?\"";
	next;
	menu
		"Eu disse que poderia matar.", -,
		"Eu posso matar ainda mais monstros.", -;
	mes "[Elias o Guarda da Torre]";
	mes "\"Realmente você pode me ajudar a impedir esta invasão. Vou precisar que você mate:";
	mes "* 100 daqueles monstros menores.";
	mes "* 50 daqueles monstros maiores.\"";
	next;
	menu
		"Eu esperava que fossem menos monstros!", -,
		"Considere todos mortos.", _ok,
		"Nossa! Não vou matar tantos!", -;
	mes "[Elias o Guarda da Torre]";
	mes "\"Veja todos estes monstros. Como posso defender a cidade se não matar todos? Se quiser me ajudar terá que matar a quantidade de monstros que te falei.\"";
	next;
	menu
		"Neste caso tudo bem!", _ok,
		"Você está abusando de minha ajuda.", -;
	close;

_ok:
	mes "[Elias o Guarda da Torre]";
	mes "\"Você realmente não é como os outros. É corajoso.\"";
	set HALLOWEEN_elias, 4;
	set HALLOWEEN_elias2, 2;
	close;

_contagem:
	mes "[Elias o Guarda da Torre]";
	mes "\"Já matou quantos monstros?\"";
	next;
	if (HALLOWEEN_elias2==1) goto _menu1;
	if (HALLOWEEN_elias==1) goto _menu2;
	menu
		"Já matei "+(HALLOWEEN_elias-4)+" Cabeça de Abóbora.", _monstro1,
		"Já matei "+(HALLOWEEN_elias2-2)+" Cabeção de Abóbora.", _monstro2;
	close;

_menu1:
	menu
		"Já matei "+(HALLOWEEN_elias-4)+" 'Cabeça de Abóbora'.", _monstro1;
	close;

_menu2:
	menu
		"Já matei "+(HALLOWEEN_elias2-2)+" 'Cabeção de Abóbora'.", _monstro2;
	close;

_monstro1:
	if (HALLOWEEN_elias>=104) goto _matou100;
	mes "[Elias o Guarda da Torre]";
	mes "\"Muito bem!";
	mes "Continue matando até chegar nos 100 monstros que te pedi.\"";
	close;

_monstro2:
	if (HALLOWEEN_elias2>=52) goto _matou50;
	mes "[Elias o Guarda da Torre]";
	mes "\"Muito bem!";
	mes "Continue matando até chegar nos 50 monstros que te pedi.\"";
	close;

_matou100:
	mes "[Elias o Guarda da Torre]";
	mes "\"Ótimo. Estes já não vão mais ser problema!\"";
	mes "";
	mes "* Você ganhou 59.600 pontos de experiência.";
	set BaseExp, BaseExp+59600; // Equivale a 200 'Cabeça de Abóbora'.
	set HALLOWEEN_elias, 1;
	close;

_matou50:
	mes "[Elias o Guarda da Torre]";
	mes "\"Ótimo. Estes já não vão mais ser problema!\"";
	mes "";
	mes "* Você ganhou 102.900 pontos de experiência.";
	set BaseExp, BaseExp+102900; // Equivale a 100 'Cabeçao de Abóbora'.
	set HALLOWEEN_elias2, 1;
	close;

_obrigado:
	mes "[Elias o Guarda da Torre]";
	mes "\"Obrigado por nos ajudar a conter a invasão destes monstros.";
	mes "Agora ficou fácil cuidar dos que sobraram!\"";
	close;

}

008.gat,103,25,0	script	Roger	308,{
	mes "[Roger o Guarda da Torre]";
	mes "\"Não vê que estamos sendo atacados... vá para a cidade!\"";
	next;
	menu
		"Já disse que vim lutar", _lutar,
		"Tá bom! Já to indo.", -;
	close;

_lutar:
	mes "[Roger o Guarda da Torre]";
	mes "\"Não posso permitir que você faça isso. Vá para a cidade e se proteja.\"";
	next;
	menu
		"Já vi que não adianta falar com você.", -;
	close;

}

//= debug =============================================================

//008.gat,93,25,0	script	debug	307,{
//	mes HALLOWEEN_elias;
//	mes HALLOWEEN_elias2;
//	menu
//		"set 99 'Cabeça de Abóbora'.", -,
//		"set 49 'Cabeçao de Abóbora'.", -,
//		"set para 2'.", -,
//		"resetar'.", -;
//	if (@menu==1) set HALLOWEEN_elias, 103;
//	if (@menu==2) set HALLOWEEN_elias2, 51;
//	if (@menu==3) set HALLOWEEN_elias, 2;
//	if (@menu==4) set HALLOWEEN_elias, 0;
//	close;
//}
