//
// Script de Halloween
// @autor Diogo_RBG - http://diogorbg.blogspot.com

//008.gat,27,61,0	script	Joana	163,{ //< Posição anterior
007-3.gat,35,29,0	script	Joana	163,{
	if (HALLOWEEN_joana==1) goto _obrigada;
	if (HALLOWEEN_joana>=2) goto _matando;

	menu
		"Joana! O que você faz aqui?", -;
	mes "[Joana]";
	mes "\"Eu estava colhendo maçãs para minha prima Cornélia, mas algo estranho aconteceu.";
	mes "Ramas de abóbora começaram a se espalhar por toda parte com grande rapidez e em pouco tempo eu estava cercada.\"";
	next;
	mes "[Joana]";
	mes "\"Nessas ramas cresciam abóboras gigantes... e pode parecer loucura, mas uma delas me perseguia!";
	mes "Ela lançou suas ramas em minha direção e me apanhou pelos pés.\"";
	next;
	menu
		"E o que aconteceu depois?", -;
	mes "[Joana]";
	mes "\"Por sorte eu consegui me soltar e escapar pelo único lugar que ainda não haviam ramas: a ponte.\"";
	next;
	mes "[Joana]";
	mes "\"Tenho medo de que este monstro ainda possa estar me perseguindo.\"";
	next;
	menu
		"Não se preocupe. Vou acabar com este monstro.", -,
		"Infelizmente não vou poder de ajudar.", _close;
	mes "[Joana]";
	mes "\"Isso me deixa mais aliviada!\"";
	next;
	mes "[Joana]";
	mes "\"Este monstro deve estar com meu cachecol vermelho, pois eu o perdi quando estava presa nas ramas de abóbora.\"";
	set HALLOWEEN_joana, 2;
	close;

_matando:
	mes "[Joana]";
	mes "\"Já matou o monstro que me perseguia e recuperou meu cachecol?\"";
	next;
	menu
		"Já matei "+(HALLOWEEN_joana-2)+" monstros.", -;
	if (HALLOWEEN_joana>=11) goto _achouCachecol;
	menu
		"Mas nenhum estava com seu cachecol.", -;
	mes "[Joana]";
	mes "\"Então este monstro ainda deve estar por aí e com o meu cachecol.\"";
	close;

_achouCachecol:
	menu
		"E um deles estava com seu cachecol.", -;
	if (countitem(3034)<1) goto _semCachecol;

	mes "[Joana]";
	mes "\"Se este é meu cachecol significa que o monstro que me atacou deve der morrido.";
	mes "Agora sim eu estou alivida. Obrigada.\"";
	mes "";
	mes "* Você ganhou 18.525 pontos de experiência.";
	delitem "cachecolJoana", 1;
	set BaseExp, BaseExp+18525; // Equivale a 18 Cabeção de Abóbora.
	set HALLOWEEN_joana, 1;
	close;

_obrigada:
	mes "[Joana]";
	mes "\"Ainda bem que você deu um jeito naquele monstro que me atacou.";
	mes "Obrigada!\"";
	close;

_semCachecol:
	mes "[Joana]";
	mes "\"Mas onde está meu cachecol?\"";
	next;
	menu
		"Me desculpe. Estava comigo a alguns instantes!", -;
	close;

_close:
	close;

}

//= debug =============================================================

//007-3.gat,32,29,0	script	reset	163,{
//	set HALLOWEEN_joana, 10;
//	mes "Resetado.";
//	close;
//}
