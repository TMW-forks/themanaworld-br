//
// Script de Halloween
// @autor Diogo_RBG - http://diogorbg.blogspot.com

007-1.gat,102,52,0	script	Juliana	114,{
	if (HALLOWEEN_juliana==1) goto _aindaBem;
	if (HALLOWEEN_juliana>=2) goto _matando;

	mes "[Juliana]";
	mes "Ao se aproximar de Juliana você percebe que ela está chorando.";
	next;
	menu
		"O que foi menina? Porque você está chorando?", -;
	mes "[Juliana]";
	mes "\"Lá fora está cheio de monstros assustadores e eu estou com muito medo deles.\"";
	next;
	menu
		"Não se preocupe com eles. Vou lá fora mandar todos embora.", -;
	menu
		"Ficará tudo bem se eu fizer isso?", -;
	mes "[Juliana]";
	mes "Juliana enxuga sua lágrimas e concorda com a idéia.";
	mes "\"Sim. Mas você tem que matar todos.\"";
	set HALLOWEEN_juliana, 2;
	next;
	menu
		"E quantos monstros você viu lá fora?", -;
	mes "[Juliana]";
	mes "\"Tinha mais de 10.\"";
	close;

_matando:
	mes "[Juliana]";
	mes "\"Quantos monstros você matou lá fora?\"";
	next;
	menu
		"Já matei "+(HALLOWEEN_juliana-2)+" monstros.", -;
	if (HALLOWEEN_juliana>=13) goto _matou11;
	mes "[Juliana]";
	mes "Juliana faz um bico e choraminga:";
	mes "\"Eu disse pra você que eram mais de 10 monstros.\"";
	next;
	menu
		"Volto lá e espantos o resto deles.", -,
		"Que menina birrenta!", -;
	close;

_matou11:
	mes "[Juliana]";
	mes "\"Agora sim eu não estou mais com medo. Obrigada.\"";
	mes "";
	mes "* Você ganhou 6.575 pontos de experiência.";
	set BaseExp, BaseExp+6575; // Equivale a 22 Cabeça de Abóbora.
	set HALLOWEEN_juliana, 1;
	close;

_aindaBem:
	mes "[Juliana]";
	mes "\"Ainda bem que você espantou aqueles monstros assustadores.";
	mes "Obrigada!\"";
	close;
}
