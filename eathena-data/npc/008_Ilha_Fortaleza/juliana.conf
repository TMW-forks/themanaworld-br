//#################################################################################
//#                                                                               #
//# Este script contém três quests todas usando a juliana.                        #
//#                                                                               #
//# Quest1:                                                                       #
//# Requisitos: 30 patas de inseto                                                #
//# Recompensa: arco e 500 flechas normais.                                       #
//#                                                                               #
//# Quest2:                                                                       #
//# Requisitos: 5 pétalas                                                         #
//# Recompensa: short bow, 500 flechas de ferro e 1000 GP.                        #
//#                                                                               #
//# Quest3:                                                                       #
//# Requisitos 10 Ferrões de escorpião e 5 ferrões de escorpião vermelho.         #
//# Recompensa: 30 Poções energéticas e 10 poções de concentração.                #
//#                                                                               #
//#################################################################################

008.gat,45,81,0	script	Juliana	114,{
	if (Sex == 0) set @tioa$, "tia";
	if (Sex == 1) set @tioa$, "tio";
//	if (QUEST_chaceler == 1 && QUEST_juliarco == 2 && QUEST_juliflor == 2 && QUEST_juliferrao == 2) goto _chanceler; "parte da quest do king's bow!	
	set @tmp,rand(5);
	if(@tmp==0) goto _1;
	if(@tmp==1) goto _2;
	if(@tmp==2) goto _3;
	if(@tmp==3) goto _4;
	if(@tmp==4) goto _5;

_1:
	if (QUEST_juliarco == 1) goto _arco;
	if (QUEST_juliarco == 2) goto _completo;
	mes "[Juliana]";
	mes "\"Este é meu jardim secreto.\"";
	next;	
	menu
		"Podes me mostrar alguma coisa dele?", -,
		"Legal, bom para você.", _abortar;
	mes "[Juliana]";
	mes "\"Eu disse que ele é secreto! Se eu lhe mostrar coisas, deixará de ser secreto!\"";
	menu
		"É que eu quero explorar! Não tem nem uma dica para me ajudar?", -,	
		"Isso é egoísmo menininha!", _abortar;
	mes "[Juliana]";
	mes "\"Já sei " + @tioa$ + "! Se você me trouxer 30 patinhas de inseto eu posso dar para ti uma coisa que vai ajudar na exploração!\"";
	set QUEST_juliarco, 1;
	close;
_2:	
	if (QUEST_juliflor == 1) goto _flor;
	if (QUEST_juliflor == 2) goto _completo;
	mes "[Juliana]";
	mes "\"Eu adoro sentir o aroma das flores, mas parece que elas não estão muito amigáveis.\"";
	next;	
	menu
		"É mesmo!? Porque você não pega apenas algumas pétalas então?", -,
		"É uma pena mesmo, tomara que elas se acostumem contigo.", _abortar;
	mes "[Juliana]";
	mes "\"Bem que você poderia me trazer umas pétalas né?\"";
	menu
		"Acho que sim, quem sabe assim você fica mais alegre!", -,
		"Não, eu tenho medo daquelas flores", _abortar;
	mes "[Juliana]";
	mes "\"Me traga cinco pétalas então, acho que com isso vou sentir o aroma das flores por um bom tempo!\"";
	mes "\"Por favor " + @tioa$ + "!\"";
	set QUEST_juliflor, 1;
	close;
_3:
	mes "[Juliana]";
	mes "\"Cuide bem da natureza e ela te recompensará em dobro.\"";
	close;
_4:
	if (QUEST_juliferrao == 1) goto _ferrao;
	if (QUEST_juliferrao == 2) goto _completo;
	mes "[Juliana]";
	mes "\"Uma vez eu vi um Escorpião Vermelho!\"";
	next;	
	menu
		"E você ficou com medo?", -,
		"Ah! Medrosa! Eu mato desses a hora que quiser.", _abortar;	
	mes "[Juliana]";
	mes "\"Não, mas descobri que poderia ficar com o ferrão dele, só que eu não sou boa em pegar os ferrões!\"";
	menu 
		"Que legal! Já que descobriu, você deve estar com vontade de colecionar não é?", -,
		"Ah, meus parabéns!", _abortar;
	mes "[Juliana]";
	mes "\"Sim eu descobri exatamente isso e quero fazer coleção!\"";
	mes "\"Tive uma idéia " + @tioa$ + ", quem sabe você não me consegue 10 ferrões normais e 1 vermelho?\"";
	set QUEST_juliferrao, 1;
	close;
_5:
	mes "[Juliana]";
	mes "\"A brisa daqui é muito agradável.\"";
	close;

_abortar:
	close;

_arco:
	mes "[Juliana]";
	mes "\"Me lembrei que você prometeu me trazer 30 patinhas de inseto, você está com elas?\"";
	menu
		"Sim, aqui estão.", _charco,
		"Eu me esqueci de pegar as patas.", _esqueceu;
	close;

_charco:
	if (countitem("BugLeg") < 10) goto _esqueceu;
	mes "[Juliana]";
	mes "\"Que bom " + @tioa$ + ", você me trouxe as patinhas!";
	getinventorylist;
	if ((@inventorylist_count - (countitem("BugLeg") == 30)) > 99 - (countitem("Arrow") == 0)) goto _semlugar;
	delitem "BugLeg", 30;
	getitem "Bow", 1;
	getitem "Arrow", 500;
	set QUEST_juliarco, 2;
	mes "\"Esses, presentes, vão te ajudar a explorar melhor por aí!\"";
	close;

_flor:
	mes "[Juliana]";
	mes "\"Me lembrei que você prometeu me trazer cinco pétalas de flores, você está com elas?\"";
	menu
		"Sim, aqui estão.", _chflor,
		"Eu me esqueci de pegar as patas.", _esqueceu;
	close;

_chflor:
	if (countitem("Petal") < 5) goto _esqueceu;
	mes "[Juliana]";
	mes "\"Que bom " + @tioa$ + ", você me trouxe as pétalas!";
	getinventorylist;
	if ((@inventorylist_count - (countitem("Petal") == 30)) > 99 - (countitem("IronArrow") == 0)) goto _semlugar;
	delitem "Petal", 5;
	getitem "ShortBow", 1;
	getitem "IronArrow", 500;
	set zeny, zeny + 1000;
	set QUEST_juliflor, 2;
	mes "\"Esses, presentes, vão te ajudar a explorar melhor por aí!\"";
	close;

_ferrao:
	mes "[Juliana]";
	mes "\"Me lembrei que você prometeu me trazer 10 ferrões normais e 5 ferrões vermelhos, você está com tudo aí?\"";
	menu
		"Sim, aqui estão.", _chferrao,
		"Eu me esqueci de pegar as patas.", _esqueceu;
	close;

_chferrao:
	if (countitem("ScorpionStinger") < 10) goto _esqueceu;
	if (countitem("RedScorpionStinger") < 5) goto _esqueceu;
	mes "[Juliana]";
	mes "\"Maravilha!";
	mes "Você me trouxe todos os ferrões " + @tioa$ + "!\"";
	getinventorylist;
	if ((@inventorylist_count - (countitem("ScorpionStinger") == 10) - (countitem("RedScorpionStinger") == 5)) > 99 - (countitem("ConcentrationPotion") == 0) - (countitem("IronPotion") == 0)) goto _semlugar;
	delitem "ScorpionStinger", 10;
	delitem "RedScorpionStinger", 5;
	getitem "ConcentrationPotion", 10;
	getitem "IronPotion", 30;
	mes "\"Esses presentes vão ajudar a explorar melhor por aí!\"";
	set QUEST_juliferrao, 2;
	close;

_esqueceu:
	mes "[Juliana]";
	mes "\"Eu não esqueci da sua promessa, mas parece que você esqueceu, por favor traz para mim o que eu pedi!\"";
	close;

_semlugar:
	mes "[Juliana]";
	mes "\"Puxa vida, parece que você deixou seu inventário muito cheio, volta depois que esvaziar ele um pouco!\"";
	close;
_completo:
	mes "[Juliana]";
	mes "\" E ai " + @tioa$ + ", como anda a exploração do meu jardim secreto, está aproveitando os presentes que te dei?\"";
	close;
}
