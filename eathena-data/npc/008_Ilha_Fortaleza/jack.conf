//#################################################################################
//#                                                                               #
//# Este script contém duas quests "Arco da Floresta e escudo de madeira:         #
//#                                                                               #
//# Quest1: Obtendo arco da floresta                                              #
//# Requisitos: Até 20 tocos, 10000gp, conversa e busca                           #
//# Recompensa: Arco da floresta                                                  #
//#                                                                               #
//# Quest2: Obtendo o escudo de madeira                                           #
//# Requisitos: 40 tocos, 5000gp                                                  #
//# Recompensa: Escudo de madeira e 2500exp                                       #
//#                                                                               #
//# Variáveis usadas:  @QUEST_Forestbow_state, nibbles 0 and 1			  #
//#################################################################################

// Lumberjack
008.gat,38,69,0	script	Jack	141, {
	set @RAWLOGS_AMOUNT, 40;
	set @SHIELD_COST, 5000;
	set @QUEST_SHIELD_EXP, 2500;

        set @Q_Forestbow_MASK, NIBBLE_0_MASK;
        set @Q_Forestbow_SHIFT, NIBBLE_0_SHIFT;

        set @Q_Woodenshield_MASK, NIBBLE_1_MASK;
        set @Q_Woodenshield_SHIFT, NIBBLE_1_SHIFT;

        set @Q_Forestbow, ((QUEST_Forestbow_state & @Q_Forestbow_MASK) >> @Q_Forestbow_SHIFT);
        set @Q_Woodenshield, ((QUEST_Forestbow_state & @Q_Woodenshield_MASK) >> @Q_Woodenshield_SHIFT);

	if (@Q_Woodenshield >= 3) goto L_Shield_state_3;
	if (@Q_Woodenshield == 2) goto L_Shield_state_2;
	if (@Q_Woodenshield == 1) goto L_Shield_state_1;
	if (@Q_Forestbow > 1) goto L_Bow_state_2;

	mes "[Jack O lenhador]";
	mes "\"Olá meu nome é Jack O lenhador, o inimigo de todas as árvores. Se você precisar de lenha me avisa.\"";
	if (Inspector == 1) callsub S_NohMask_Ask;
	next;
	if (@Q_Forestbow < 1) close;

	menu
		"Ouvi falar que o senhor não está mais entregando tocos vivos. Porque isso?", -;

	mes "[Jack O lenhador]";
	mes "\"PORQUE?! Valorizo a minha vida ora bolas!.\"";

	next;
	menu
		"O que o senhor quer dizer?", -;

	mes "[Jack O lenhador]";
	mes "\"Uma semana atrás eu fui derrubar uma daquelas árvorezinhas bem aqui na floresta. As mesmas árvores que eu sempre tirava os tocos vivos dos quais voce se refere. Eu estava lá, derrubando elas com o meu machado, e o que acontece? Um dos galhos daquela árvore me bateu! Na hora eu pensei que tinha caído ou que o vento tinha feito ele se mover. DOEU! Eu joguei o galho longe e continuei a cortar de repente outro galho me bateu! Fique furioso e cortei todos os galhos que estavam próximos do chão para que aquilo não se repitisse. Mas, eu ainda simplesmente não acredito no que aconteceu depois.\"";

	next;
	menu
		"O que aconteceu? ", -;

	mes "[Jack O lenhador]";
	mes "\"Você vai pensar que esse velho está ficando senil.\"";

	next;
	menu
		"Não, isso nunca!.", -;

	mes "[Jack O lenhador]";
	mes "\"Está bem. Depois que eu cortei alguns galhos a arvore inteira se mexeu! As raízes simplesmente saíram do solo e os galhos pareciam balancar e apareceu um rosto no tronco. A árvore inteira ficou viva! Aquilo foi loucura!\"";
	next;
	menu
		"Você fugiu??", -,
		"Você lutou com ela?", -;

	mes "[Jack O lenhador]";
	mes "\"Eu lutei contra a árvore! Peguei meu maxado e parti para cima daquela besta! Ela me bateu aqui *aponta para um hematoma no ombro*, aqui *mostra a canela com outro hematoma* e aqui *levanta a blusa revelando lesões piores*. Mesmo assim eu não desisti. Eu derrubei e derrubei, galho depois de galho, até que no final após cortar a raiz aquele monstro caiu no chão para não levantar mais.\"";

	next;
	menu
		"Então você acabou com esse monstro? Porque você está com tanto medo?", -;

	mes "[Jack O lenhador]";
	mes "\"Eu estava exausto e precisava descançar. Quando percebi me vi cercado de dezenas dessas criaturas.\"";

	next;
	menu
		"Você lutou contra as outras também?", -;

	mes "[Jack O lenhador]";
	mes "\"Tá maluco é? Mal consegui destruir uma daquelas bestas. Eu não tinha a mínima condição de lutar novamente. Então eu corri o mais rápido possível, para minha sorte aqueles monstrengos são lentos por causa das raízes pequeninas em suas pernas. HÁ!!\"";

	next;
	menu
		"Então agora, você não corta mais árvores?", -;

	mes "[Jack O lenhador]";
	mes "\"Eu ainda as derrubo, afinal é meu trabalho. Apenas não derrubo mais essa árvores que tem o toco vivo. Eu sei que o mestre arqueiro quer mais tocos delas, mas eu não estou nem aí para isso. Não vou arriscar minha vida por algumas peças de ouro. Se você quer, vá lá pegue-as elas estão espalhadas por aí na floresta, mas quanto a mim, nunca mais me aproximo dessas árvorezinhas.\"";

	next;
	menu
		"Na floresta? Muito bem, obrigado.", -;

	set @Q_Forestbow, 2;
        callsub S_Update_Mask;

	close;

L_Bow_state_2:
	// Bow state 4: O jogador encontrou uma peça perfeita de madeira.
	if (@Q_Forestbow > 3) goto L_Bow_state_4;

	mes "\"Boa sorte na caçada daqueles monstrengos, você vai precisar.\"";
	if (Inspector == 1) callsub S_NohMask_Ask;
	close;

L_Bow_state_4:
	mes "[Jack O lenhador]";
	mes "\"Então você finalmente encontrou um toco perfeito para o arco da floresta não é?\"";

	next;
	menu
		"Mesmo assim não pude comprar o arco...", L_Shield_state_0,
		"Bem, eu ainda estou procurando.", L_Bow_state_4_reaffirm,
		"Sim, agora já estou com o arco.", -,
		"Meu estilo de luta é corpo a corpo, não preciso de um arco .", L_Shield_state_0;

	// Confere se o jogador está falando a verdade (continue on in any case)
	if (@Q_Forestbow > 4) goto L_Shield_state_0;

	mes "[Jack O lenhador]";
	mes "\"Hrmph. Eu sou uma pessoa que respeita aqueles que falam a verdade.\"";

	next;
	goto L_Shield_state_0;

L_Bow_state_4_reaffirm:
	mes "[Jack O lenhador]";
	mes "\"Deverias falar com o Alan novamente.\"";
	close;

L_Shield_state_0:
	set @Q_Woodenshield, 1;
        callsub S_Update_Mask;

	if (Sex == 0) set @child_of_jack$, "minha própria filha";
	if (Sex == 1) set @child_of_jack$, "meu próprio filho";

	mes "[Jack O lenhador]";
	mes "\"Sem dúvidas você se lembra de como eu, lutando por minha vida, batalhei com aquelas feras. Tudo aquilo me assustou, aquelas coisas com sete galhos me assustaram!  Nunca mais cheguei perto daquelas bestas.  Agora, na minha frente, aparece você cortando uma após outra.  Sinto orgulho de você por ter coragem de desafiar aquelas bestas. Você é forte como se fosse " + @child_of_jack$ + "!\"";
	next;
	goto L_Shield_state_1;

L_Shield_state_1:
	mes "[Jack O lenhador]";
	mes "\"Tenho uma idéia, o que me diz de ganhar um escudo novo?\"";
	next;

	if (Inspector == 1)
		menu
			"Não precisa, de qualquer forma te agradesso.", L_End,
			"Sim, por favor!", -,
			"Na verdade eu gostaria de saber se você viu algo estranho na cidade.", L_NohMask_Answer;
	if (Inspector != 1)
		menu
			"Não precisa, de qualquer forma te agradesso.", L_End,
			"Sim, por favor!", -;

	mes "[Jack O lenhador]";
	mes "\"Eu preciso de " + @RAWLOGS_AMOUNT + " tocos vivos. E não precisa ser aqueles tão bons quanto os do Alan, na verdade qualquer toco que tu achar serve.   Ah, também vou precisar de " + @SHIELD_COST + " GP para os outros materiais.\"";
	next;

	set @Q_Woodenshield, 2;
        callsub S_Update_Mask;
	close;

L_End:
	close;

L_Shield_state_2:
	mes "[Jack O lenhador]";
	mes "\"Vejamos se você tem tudo que eu preciso.\"";
	next;

	if (countitem(569) < @RAWLOGS_AMOUNT) goto L_Not_enough_logs;
	if (zeny < @SHIELD_COST) goto L_Not_enough_money;
	getinventorylist;
	if (@inventorylist_count == 100) goto L_TooMany;

	delitem 569, @RAWLOGS_AMOUNT;
	set zeny, zeny - @SHIELD_COST;
	getexp @QUEST_SHIELD_EXP, 0;
	getitem 602, 1;

	set @Q_Woodenshield, 3;
        callsub S_Update_Mask;
	mes "[Jack O lenhador]";
        mes "\"Sente-se por favor.\"";
        mes "Jack serra os tocos em pedaços e os lixa até ficarem macios ao toque.";
        mes "Aplicando um líquido de forte cheiro, ele os deixa com padrões mais escuros.";
        next;

	mes "[Jack O lenhador]";
        mes "Ele pega um pedaço e o molda em um formato arredondado, então repete com uma segunda peça o que lhe parece ser a empunhadura do escudo.";
        next;

	mes "[Jack O lenhador]";
        mes "Enquanto isso o sol secou as outras peças.  Jack as coloca juntas, adiciona camadas e prega tudo.";
        mes "O resultado lhe parece um escudo usável, porém Jack adiciona mais um líquido e o deixa secando.";
        next;

	mes "[Jack O lenhador]";
        mes "Finalmente, ele lhe entrega o escudo.";
	mes "\"Aproveite seu escudo!\"";
        mes "[pontos de experiência: " + @QUEST_SHIELD_EXP + "]";
	next;
	goto L_End;

L_Not_enough_logs:
	mes "[Jack O lenhador]";
	mes "\"Você não tem madeira suficiente para eu fazer o escudo, precisas de  " + @RAWLOGS_AMOUNT + " tocos.\"";
	close;

L_Not_enough_money:
	mes "[Jack O lenhador]";
	mes "\"Não tens ouro suficiente, precisas de " + @SHIELD_COST + " GP para que eu termine o escudo.\"";
	close;

L_TooMany:
	mes "[Jack O lenhador]";
	mes "\"Não tem espaço no seu inventário para o escudo, volte mais tarde.\"";
	close;

L_Shield_state_3:
	mes "[Jack O lenhador]";
	mes "\"Espero que meu escudo lhe sirva bem!\"";
	next;

	if (Inspector == 1)
		menu
			"Eu também!", -,
			"Have you seen anything that might be connected to the recent robberies in town?", L_NohMask_Answer;
	close;

S_NohMask_Ask:
	next;
	menu
		"I'll keep that in mind.", -,
		"Você viu algo susteito ultimamente, algo que possa estar conectado com os Roubos recentes?", L_NohMask_Answer;
	return;

L_NohMask_Answer:
	mes "[Jack O lenhador]";
	mes "\"Desculpe, mas não.\"";
	close;

S_Update_Mask:
        set QUEST_Forestbow_state,
        	(QUEST_Forestbow_state & ~(@Q_Forestbow_MASK | @Q_Woodenshield_MASK))
                | (@Q_Forestbow << @Q_Forestbow_SHIFT)
                | (@Q_Woodenshield << @Q_Woodenshield_SHIFT);
        return;
}
