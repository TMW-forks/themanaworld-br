//
// [#warpSecreto]
// if QUEST_warpSecreto==0
//  * if mapaTesouro==0
//    * pede ajuda
//  * if mapaTesouro
//    * pede ajuda e fala do mapa
// if QUEST_warpSecreto==1
//  * quase acha a saída
//  * set QUEST_warpSecreto, 2;
// if QUEST_warpSecreto==2
//  * quase acha a saída
// if QUEST_warpSecreto==3
//  * warp "passagem secreta"
//  * diz: finalmete !
//  * set QUEST_warpSecreto, 4;
// if QUEST_warpSecreto==3
//  * warp "passagem secreta"
//
// [Velho Eremita]
// if QUEST_warpSecreto==0
//  * if mapaTesouro
//    * fica com o mapa e Ensina o caminho errado
//    * set QUEST_warpSecreto, 1;
// if QUEST_warpSecreto==1
//  * diz: faça o que eu te disse.
// if QUEST_warpSecreto==2
//  * ensina o caminho certo
//  * set QUEST_warpSecreto, 3;
// if QUEST_warpSecreto==3
//  * diz: Agora eu tenho certeza
// if QUEST_warpSecreto==2
//  * Fala um pouco da Caverna dos Tesouros.
//

007-1.gat,95,30,0	script	Velho Eremita	116,{
//	mes ".:: Modo Debug ::.";
//	menu
//		"Continuar", -,
//		"Resetar", -;
//	if(@menu==2) set QUEST_warpSecreto, 0;

	if(QUEST_warpSecreto==1) goto _sigaErrado;
	if(QUEST_warpSecreto==2) goto _caminhoCerto;
	if(QUEST_warpSecreto==3) goto _sigaCerto;
	if(QUEST_warpSecreto==4) goto _maisInfo;
	if(QUEST_warpSecreto>=5) goto _obrigado;

	mes "[Velho Eremita]";
	mes "\"Não existe ninguém que conheça melhor estas cavernas que eu.\"";

	getinventorylist;
	if(countitem(3006)>0) goto _comMapa;
	close;

_comMapa:
	next;
	menu
		"Pode me ajudar com este mapa?", -;
	mes "[Velho Eremita]";
	mes "\"Deixe-me ver o seu mapa.\"";
	next;
	mes "Então o velho tomou o mapa de suas mãos.";
	delitem 3006, 1; //Mapa do Tesouro
	set QUEST_warpSecreto, 1;
	next;
	mes "[Velho Eremita]";
	mes "\"Sim, eu posso... Estas realmente são as minhas cavernas!\"";
	next;
	mes "[Velho Eremita]";
	mes "\"Para chegar neste salão, basta seguir pela passagem da cachoeira.";
	mes "Mas tenha cuidado ! Lá é muito perigoso. Eu sempre ouço barulhos horripilantes vindos de lá.\"";
	next;
	menu
		"Eu vou me lembrar disso!", -;
	mes "[Velho Eremita]";
	mes "\"Eu posso ficar com seu mapa?";
	mes "Eu sempre quis ter um mapa de minhas cavernas.\"";
	next;
	menu
		"Pode sim", -;
	mes "[Velho Eremita]";
	mes "\"Muito obrigado.\"";
	close;

_sigaErrado:
	mes "[Velho Eremita]";
	mes "\"É como eu te disse. Basta seguir pela passagem da cachoeira.\"";
	close;

_caminhoCerto:
	mes "[Velho Eremita]";
	mes "\"Você conseguiu encontrar o caminho?\"";
	next;
	menu
		"NÃO!", -;
	mes "[Velho Eremita]";
	mes "\"Calma, não se zangue.";
	mes "Eu cometi um pequeno erro ao analisar o mapa, mas já sei o caminho correto.\"";
	next;
	mes "[Velho Eremita]";
	mes "\"Depois que você saiu eu ví que não bastava seguir pela passagem da cachoeira, mas que também era preciso entrar por uma passagem secreta que fica exatamente atrás da cachoeira.\"";
	set QUEST_warpSecreto, 3;
	close;

_sigaCerto:
	mes "[Velho Eremita]";
	mes "\"Agora eu tenho certeza. Basta seguir pela passagem da cachoeira e entrar por uma passagem secreta que fica exatamente atrás da cachoeira.\"";
	close;

_maisInfo:
	mes "[Velho Eremita]";
	mes "\"Então. Você conseguiu?\"";
	next;
	menu
		"Sim, e tem esqueletos por lá", -;
	mes "[Velho Eremita]";
	mes "\"Então os boatos da maldição são verdadeiros.\"";
	next;
	menu
		"Que maldição?", -;
	mes "[Velho Eremita]";
	mes "\"Não sei muito a respeito.";
	mes "Só que piratas puseram a mão no tesouro errado e que agora estão amaldiçoados.\"";
	next;
	mes "[Velho Eremita]";
	mes "\"E juntando os novos fatos, vejo que seu mapa é autêntico.\"";
	next;
	menu
		"Mas é só uma cópia", -;
	mes "[Velho Eremita]";
	mes "\"Sim. Mas o tesouro é verdadeiro.";
	mes "Além das perigosas cavernas existe muito tesouro escondido. E não se trata do tesouro amaldiçoado.\"";
	next;
	mes "[Velho Eremita]";
	mes "\"Se estas cavernas não fossem tão perigosas eu mesmo ira atrás deste tesouro.\"";
	set QUEST_warpSecreto, 5;
	close;

_obrigado:
	mes "[Velho Eremita]";
	mes "\"Se eu fosse você eu iria atrás deste tesouro.\"";
	next;
	mes "[Velho Eremita]";
	mes "\"Obrigado pelo mapa.\"";
	close;

}
