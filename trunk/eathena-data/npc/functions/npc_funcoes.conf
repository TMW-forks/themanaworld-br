/**
 * Concentração da maior parte dos scripts de NPCs.
 */

/**
 * Cogumelo capaz de teletransportar o jogador para um outro local do mesmo mapa.
 * @var arg0 Nome do mapa.
 * @date quarta, 02 de março de 2011
 * @author Diogo_RBG - http://diogorbg.blogspot.com
 */
function	script	cogumeloTeleporte	{
	percentheal -5, -5;
	warp getarg(0), 0, 0;
	if(@falaCogumelo==0) goto _falas;
	set @tmp, rand(3);
	if(@tmp==0) message strcharinfo(0), "Tô ligado!";
	if(@tmp==1) message strcharinfo(0), "Só manow!";
	if(@tmp==2) message strcharinfo(0), "Mó viagem!";
	end;

_falas:
	set @falaCogumelo, 1;
	mes "Você volta à sua consciência sem lembrar de nada... só se lembra que o mundo começou a girar no momento em que tocou aquele cogumelo.";
	mes "";
	mes "Talvez este seja um cogumelo venenoso.";
	close;
}

/**
 * Armadilha capaz de ferir o jogador em 51% se estiver armada e disparar.
 * @var arg0 Nome do mapa.
 * @var arg1 coordenada x do NPC.
 * @var arg2 coordenada y do NPC.
 * @var arg3 Nome do NPC.
 * @date sábado, 05 de março de 2011
 * @author Diogo_RBG - http://diogorbg.blogspot.com
 */
function	script	armadilhaAco	{
	set @x, getarg(1);
	set @y, getarg(2);
	if( isin(getarg(0), @x, @y, @x, @y)==0 ) goto _longe;
	if(getnpctimer(0)==0) goto _armar;
	if(getnpctimer(0)<5000) goto _disparar;
	if(getnpctimer(0)<10250) goto _afetar; //< Um 2º jogador logo atrás do primeiro que foi atingido.
	return;

_longe:
	mes "Há algo errado com isto! ";
	close;

_armar:
	initnpctimer;
	fakenpcname getarg(3), "#armada", 341;
	return;

_disparar:
	setnpctimer 10000;
	fakenpcname getarg(3), "#disparada", 342;
_afetar:
	percentheal -51, 0;
	specialeffect2 28;
	return;
}
