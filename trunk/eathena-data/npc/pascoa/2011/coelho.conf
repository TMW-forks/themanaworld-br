// Normalmente eu só mando o script pro repositório quando está tudo pronto, mas estou sem tempo e achei melhor ir mandando aos poucos. Acho que fica melhor para vocês revisarem os erros ou acrescentar suas ideias.

012.gat,155,153,0	script	debug	173,{
	callfunc "debug"; //< Só habilitado para GMs
	menu
		"set PASCOA2_esconde 0", -,
		"set PASCOA2_esconde 4", -,
		"Achar +5 ovos", -,
		"Sair", -;
	if (@menu == 1) set PASCOA2_esconde, 0;
	if (@menu == 1) set PASCOA2_escondeCont, 0;
	if (@menu == 2) set PASCOA2_esconde, 4;
	if (@menu == 2) set PASCOA2_escondeCont, 0;
	if (@menu == 3) set PASCOA2_escondeCont, PASCOA2_escondeCont + 5;
	close;
}

012.gat,157,153,0	script	Coelho	173,{
	set @hatCascaOvo, 1256;

	if (PASCOA2_esconde == 0) goto _inicio;
	if (PASCOA2_esconde == 1) goto _voltou;
	if (PASCOA2_esconde == 2) goto _retorno1;     //< Procurando 15 ovos
	if (PASCOA2_esconde == 3) goto _devePremio1;  //< Ganha um chapéu Casca de Ovo
	if (PASCOA2_esconde == 4) goto _desafio2;
	if (PASCOA2_esconde == 5) goto _menuEsconderOvos;

	//- Caso não seja nenhuma das opções a quest não poderá ser iniciada novamente.
	mes "[Coelho]";
	mes "Feliz Páscoa!";
	close;

//- Encontrar 10 ovos de páscoa ----------------------------------------

_inicio:
	mes "[Coelho]";
	mes "\"Olá! Posso te ajudar?\"";
	menu
		"Estou entediado... Existe algo de interessante por aqui?", -;
	mes "[Coelho]";
	mes "\"Bem... Um monte de coelhos e lebres! Quer coisa mais interessante do que isso? ;)\"";
	next;
	mes "[Coelho]";
	mes "\"Brincadeira... Eu entendi o que você quis dizer... Você parece ser uma pessoa que gosta de desafios. Acho que tenho a proposta ideal para você!\"";
	next;
	menu
		"É disso que eu gosto! Qual seria a proposta?", -;
_deNovo:
	mes "[Coelho]";
	mes "\"É bem simples! Eu escondi vários ovos de chocolate por todo esse lugar. Mas como eu escondi muito bem, acho bem difícil alguém encontrá-los. Por isso resolvi te dar essa chance de mostrar sua sagacidade. Encontre 15 ovos de chocolate e fale comigo.\"";
	next;
	mes "[Coelho]";
	mes "\"Isso pode ser difícil, mas é apenas para começar o desafio... Mas não se preocupe, se você for bem eu vou te recompensar muito bem!\"";
	set PASCOA2_esconde, 1;
	next;
	menu
		"Ótimo! Mas por onde eu começo a procurar?", _aceitou,
		"Ahh, estou sem paciência agora. Talvez depois eu volte.", _sair1;
	goto _sair1;

_voltou:
	mes "[Coelho]";
	mes "\"Olá! Decidiu voltar para aceitar meu desafio?\"";
	menu
		"Sim! Agora estou pronto. Mas por onde devo começar?", _aceitou,
		"Tenho dúvidas. Pode me explicar de novo?", _deNovo,
		"Não, ainda estou sem paciência para esconde-esconde...", _sair1;
	goto _sair1;

_aceitou:
	mes "[Coelho]";
	mes "\"Use a sua imaginação! Pode ser em QUALQUER lugar. Dentro de um lago, atrás de uma árvore, no meio de arbustos... qualquer lugar!\"";
	set PASCOA2_esconde, 2;
	set PASCOA2_escondeCont, 0;
	close;

_retorno1:
	if (PASCOA2_escondeCont == 0) goto _nenhumOvo;
	if (PASCOA2_escondeCont >= 15) goto _encontrouOvos;

	mes "[Coelho]";
	mes "\"Olá! Como vai a procura? Encontrou alguma coisa?\"";
	if (PASCOA2_escondeCont == 1)
		menu
			"Está muito difícil! Só achei um ate agora!", -;
	if (PASCOA2_escondeCont > 1 && PASCOA2_escondeCont < 6)
		menu
			"Está difícil, mas já achei alguns ovos!", -;
	if (PASCOA2_escondeCont >= 6 && PASCOA2_escondeCont < 10)
		menu
			"Nossa, você escondeu muito bem! Mas não vou desistir!", -;
	if (PASCOA2_escondeCont >= 10 && PASCOA2_escondeCont < 15)
		menu
			"Hah! Já achei um monte! Estou quase lá!", -;
	mes "[Coelho]";
	mes "\"Esse é o espírito! Continue procurando...\"";
	close;

_nenhumOvo:
	mes "[Coelho]";
	mes "\"Estou vendo que você não encontrou nenhum ovo... Talvez eu tenha superestimado suas habilidades intelectuais... hahaha\"";
	next;
	mes "[Coelho]";
	mes "\"Lembre-se de procurar os todos os lugares possíveis. Dentro de um lago, atrás de uma árvore, no meio de arbustos... qualquer lugar!\"";
	close;

_encontrouOvos:
	mes "[Coelho]";
	mes "\"UAU!!! Você encontrou mesmo todos os ovos! Estou impressionado...\"";
	menu
		"Foi fácil! Você achou mesmo que eu não encontraria os ovos?", -;
	mes "[Coelho]";
	mes "\"Sim... Mas não tão rápido. Mas já que você foi tão bem neste desafio, o que acha de uma nova tarefa, mas desta vez muito mais difícil?\"";
	next;
	menu
		"Claro. Mas você não está esquecendo de uma coisa coelhinho? $D ", -;
	mes "[Coelho]";
	mes "\"Hahaha, eu sabia que você ia chegar neste assunto. Você merece uma boa recompensa... Deixe-me pensar...\"";
	next;
	mes "[Coelho]";
	mes "\"Já sei qual será a sua recompensa. E acho que você irá gostar muito pois é algo muito divertido. Tão divertido quanto foi a brincadeira de encontrar ovos de páscoa... sem contar que tem tudo a ver!\"";
	next;
	mes "[Coelho]";
	mes "\"Mas primeiro feche os olhos para não estragar a surpresa!\"";
	next;
	menu
		"Tudo bem!", -,
		"Adoro surpresas!", -,
		"Nossa... quanto mistério!", -;
	mes "Você fecha os olhos e então pode apenas escutar o coelho batendo em um objeto oco, mas não faz a menor ideia do que seja...";
	next;
	mes "E quanto mais ele demora em seu trabalho mais ansioso você fica...";
	next;

	set PASCOA2_esconde, 3;
	getinventorylist;
	if( @inventorylist_count >= 100) goto _cheio;

	mes "Mas ele finalmente termina o trabalho!";
	mes "";
	goto _darPremio1;

_devePremio1:
	mes "[Coelho]";
	mes "\"Me lembro de você... estou de tevendo um prêmio!\"";
	next;
	getinventorylist;
	if( @inventorylist_count >= 100) goto _cheio;
	goto _darPremio1;

_darPremio1:
	mes "[Coelho]";
	mes "\"Veja só o que tenho para você! Espero que goste!\"";
	getitem @hatCascaOvo, 1;
	set PASCOA2_esconde, 4;
	set PASCOA2_escondeCont, 0;
	close;

//- Esconder 30 ovos de páscoa -----------------------------------------

_desafio2:
	mes "[Coelho]";
	mes "\"Muito bem! Agora vamos ao novo desafio! Esse é bem mais difícil, pois você vai ter que vencer a MINHA astúcia!\"";
	next;
	mes "[Coelho]";
	mes "\"A ideia é a mesma do desafio anterior. Só que quem vai esconder os ovos é você, e eu vou ter que encontrá-los!\"";
	next;
	menu
		"OK. Mas eu não queria gastar ovos de chocolate assim! Pode ser umas bolotas?", -;
	mes "[Coelho]";
	mes "\"Bolotas? Nem pensar! Tem que ser ovos de chocolate. Mas não se preocupe, se seus ovos de chocolate acabarem eu posso fazer alguns para você.\"";
	next;
	mes "\"Só vou precisar de alguns ingredientes e de tinta guache para enfeitar.\"";
	set PASCOA2_esconde, 5;
	next;
	goto _menuIngredienteOvo;

_menuIngredienteOvo:
	menu
		"Você tem uma lista com os ingredientes?", _ingredientes1,
		"Onde consigo os ingredientes e a tinta?", _ondeIngredientes1,
		"Ok... vou procurar pelos materiais.", -;
	mes "[Coelho]";
	mes "\"Quando tiver materiais basta trazê-los para mim.\"";
	close;

//- permite voltar ao '_menuIngredienteOvo' depois de ver os '_ingredientes'.
_ingredientes1:
	callsub _ingredientes;
	goto _menuIngredienteOvo;

//- permite voltar ao '_menuIngredienteOvo' depois de ver '_ondeIngredientes'.
_ondeIngredientes1:
	callsub _ondeIngredientes;
	goto _menuIngredienteOvo;

_menuEsconderOvos:
	if( PASCOA2_escondeCont>=30 ) goto _escondeuTodos;

	set @tmp, rand(2);
	mes "[Coelho]";
	if(@tmp==0) mes "\"Então?! Já escondeu todos os ovos de páscoa?\"";
	if(@tmp==1) mes "\"Olá novamente! Conseguiu muitos ingredientes?\"";
	next;
	menu
		"Falta esconder alguns ovos.", -,
		"Quero fazer um [Ovo de Páscoa].", _menuOvo,
		"Vixi... tenho que ir!", -;
	mes "[Coelho]";
	mes "\"Mal posso esperar para procurar por ovos de páscoa!\"";
	close;

_escondeuTodos:
	mes "[Coelho]";
	mes "\"É o que estou pensando? Você já escondeu os 30 ovos para que eu possa procurá-los?\"";
	menu
		"Tudo certo! Escondi todos os ovos.", -,
		"Sim! Só falta você ir procurá-los... ou tentar... kkk!", -,
		"Quero fazer outro [Ovo de Páscoa].", _menuOvo;
	mes "[Coelho]";
	mes "\"Muito bem, você fez um ótimo trabalho!\"";
	close;

	//< recompensa orelhas de lebre

//- Fazer [Ovo de Páscoa] ----------------------------------------------

_menuOvo:
	set @idOvoPascoa,   1208;
	set @idChocolate,   3071;
	set @idCastanha,    3075;
	set @idTintaGuache, 3080;

	//if (countitem("cupulateEsp")>=10 && countitem("castanhaPara")>=5) set @cupulate, 1;
	//if (countitem("chocolateEsp")>=10 && countitem("amendoim")>=5) set @chocoesp, 1;
	//if (countitem("chocolateAmargoEsp")>=10 && countitem("castanhaCaju")>=5) set @amargo, 1;
	//if (countitem("chocolateBrancoEsp")>=10 && countitem("coco")>=5)  set @branco, 1;

	mes "[Coelho]";
	mes "\"Qual tipo de ovo de páscoa você quer fazer.";
	menu
		"Sabor Cuputale Especial.", _fazerOvos,
		"Sabor Chocolate Especial.", _fazerOvos,
		"Sabor Chocolate Amargo Especial.", _fazerOvos,
		"Sabor Chocolate Branco Especial.", _fazerOvos,
		"Quero rever os ingredientes.", _ingredientes2,
		"Onde consigo os ingredientes?", _ondeIngredientes2,
		"Desistir.", -;
	close;

//- permite voltar ao _menuOvo depois de ver os ingredientes.
_ingredientes2:
	callsub _ingredientes;
	goto _menuOvo;

//- permite voltar ao _menuIngredienteOvo depois de ver '_ondeIngredientes'.
_ondeIngredientes2:
	callsub _ondeIngredientes;
	goto _menuOvo;

_fazerOvos:
	if (@menu<=0 || @menu>4) close;
	set @ovotipo, @menu-1;
	mes "[Guia de Páscoa]";
	mes "\"Me dê seus ingredientes.\"";
	next;
	menu
		"Aqui estão.", -,
		"Ops... Acabaram meus ingredientes.", -;
	if (@menu!=1) close;
	if (countitem(@idChocolate+@ovotipo)<10 || countitem(@idCastanha+@ovotipo)<5) goto _semIngredientes;
	goto _misturando;

_semIngredientes:
	mes "[Guia de Páscoa]";
	mes "\"Você não tem ingredientes suficiente para fazer o ovo de páscoa que escolheu.";
	goto _menuOvo;

_semTinta:
	mes "[Guia de Páscoa]";
	mes "\"Eu acho que você não tem essa cor de tinta.\"";
	next;
	goto _menuCor;

_misturando:
	mes "[Guia de Páscoa]";
	mes "\"Ótimo, vamos começar a trabalhar...\"";
	mes "";
	mes "O coelho pega os ingredientes e mistura tudo com um pouco de leite em uma vasilha ao fogo. Espera um pouco até que o chocolate derreta e então o coloca em duas fôrmas ovais.";
	next;
	goto _fazerOvo3;

_fazerOvo3:
	mes "[Guia de Páscoa]";
	mes "\"Enquanto o chocolate endurece...";
	mes "Que cor gostaria de pintar seu ovo de páscoa? Que cor de [Tinta Guache] você tem ai?!\"";
	next;
	goto _menuCor;

_menuCor:
	menu
		"Vermelho", -,
		"Verde", -,
		"Azul Escuro", -,
		"Amarelo", -,
		"Azul Claro", -,
		"Rosa", -,
		"Não tenho nenhuma Tinta Guache.", -;
	if (@menu<=0 || @menu>6) close;

	set @ovocor, @menu-1;
	if (countitem(@idTintaGuache+@ovocor)<1) goto _semTinta;
	goto _fazerOvo4;

_fazerOvo4:
	mes "[Guia de Páscoa]";
	mes "\"Já temos o chocolate, a tinta... deixe-me ver o que mais está faltando...\"";
	next;
	mes "";
	mes "\"Acho que temos tudo...\"";
	next;
	mes "[Guia de Páscoa]";
	mes "\"Vamos decorar o ovo de páscoa.\"";
	mes "";
	mes "O coelho encaixa os dois lados do ovo e o enrola em uma folha de alumínio. Recorta algumas tiras de papel e também as enrola em volta do ovo passando um pouco de cola nas pontas. Em seguida molha a ponta do pincel com tinta e começa a pintar o ovo.";
	next;
	mes "[Guia de Páscoa]";
	mes "\"Quase terminando...\"";
	mes "";
	mes "Depois de mais alguns acabamentos ele finalmente o termina.";
	next;
	goto _fazerOvo6;

_fazerOvo6:
	getinventorylist;
	if( @inventorylist_count >= 100 ) goto _cheio;
	mes "[Guia de Páscoa]";
	mes "\"Aqui está seu ovo de páscoa. Agora você já pode participar da brincadeira!\"";
	delitem @idChocolate+@ovotipo, 10;
	delitem  @idCastanha+@ovotipo, 5;
	delitem @idTintaGuache+@ovocor, 1;
	getitem @idOvoPascoa+@ovocor, 1;
	close;

//----------------------------------------------------------------------

_ingredientes:
	mes "[Coelho]";
	mes "\"Para fazer um delicioso ovo de páscoa de 340g precisaremos de 250g de chocolate especial e 90g de castanhas.";
	mes "Mas não pode ser qualquer tipo de chocolate especial e nem qualquer tipo de castanha. Para cada sabor de chocolate especial temos que utilizar um tipo diferente de castanha.\"";
	mes "";
	mes "\"Veja a lista.\"";
	next;
	mes "[Lista de Ingredientes]";
	mes "Ovo de páscoa sabor Cuputale Especial:";
	mes "- 10 [Barra de Cupulate Especial]";
	mes "- 5 [Castanha do Pará]";
	mes "Ovo de páscoa sabor Chocolate Especial:";
	mes "- 10 [Barra de Chocolate Especial]";
	mes "- 5 [Amendoim]";
	mes "Ovo de páscoa sabor Chocolate Amargo Especial:";
	mes "- 10 [Barra de Chocolate Amargo Especial]";
	mes "- 5 [Castanha de Caju]";
	mes "Ovo de páscoa sabor Chocolate Branco Especial:";
	mes "- 10 [Barra de Chocolate Branco Especial]";
	mes "- 5 [Coco]";
	next;
	return; //- Retorna para ao callsub.

_ondeIngredientes:
	mes "[Coelho]";
	mes "\"Coelhos adoram chocolate e castanhas... aposto que você vai conseguir todos os ingredientes com eles. Mas tenha cuidado para não machucá-los. Já a tinta guache você deve encontrar fácil com algum vendedor por aí...\"";
	next;
	return; //- Retorna para ao callsub.

//----------------------------------------------------------------------

_cheio:
	mes "[Coelho]";
	mes "\"Espere um momento... como pretende carregar o meu presente com tanta bagagem?";
	mes "Seria uma boa ideia você me procurar mais tarde.";
	close;

_sair1:
	set PASCOA2_esconde, 1;
	mes "[Coelho]";
	mes "\"Tudo bem! Sinta-se à vontade para voltar quando quiser.\"";
	close;

}
