//
// @data 09/04/2010
// @autor Diogo_RBG - http://diogorbg.blogspot.com
// @desc O coelho responsável em ajudar no Evento de Páscoa. Ele pode te ajudar a fazer deliciosos ovos de páscoa.

//004.gat,133,22,0	script	debug	173,{
//	mes "Reset variável para:";
//	menu
//		"0", -,
//		"1", -,
//		"2", -,
//		"3", -,
//		"4", -,
//		"5", -,
//		"sair", -;
//	if(@menu<1 || @menu>6) close;
//	set PASCOA_questOvo, @menu-1;
//	set PASCOA_questOvoPapel, 0;
//	close;
//}

floresta.gat,157,153,0	script	Guia de Páscoa	173,{
	mes "[Guia de Páscoa]";
	mes "\Feliz Páscoa... como posso te ajudar?!\"";
	goto _menu;

_menu:
	menu
		"Como participar do Evento de Páscoa?", _comoParticipar,
		"Onde posso conseguir um [Ovo de Páscoa].", _ovoPascoa,
		"Quero fazer um [Ovo de Páscoa].", _fazerOvo,
		"Como faço para presentear alguém?", _comoPresentar,
		"Os ovos tem data de validade?", _dataValidade,
		"Sair.", -;
	close;

_comoParticipar:
	mes "[Guia de Páscoa]";
	mes "\"Que bom que perguntou!";
	mes "* O Evento de Páscoa é um grande evento de confraternização, onde você poderá presentear (ou ser presenteado) todos os seus amigos com ovos de páscoa.";
	mes "* A melhor parte de ser presenteado por um amigo é que você poderá ganhar de 1% a 10% de experiência ao comer o Ovo de Páscoa que ele de deu! A quantia de experiência vai depender de seu lvl. Em um lvl mais alto você receberá mais experiência... embora pareca fazer um efeito menor na barra de experiência.";
	mes "Mas não seja guloso, pois só os primeiros 10 ovos tem esse efeito!\"";
	next;
	mes "[Guia de Páscoa]";
	mes "\"Adoro a Páscoa !!!\"";
	next;
	goto _menu;

_ovoPascoa:
	mes "[Guia de Páscoa]";
	mes "\"Eu até tinhas alguns ovos de páscoa comigo, mas já acabaram todos. :(";
	mes "Mas eu posso fazer um ovo de páscoa para você se você me trouxer os ingredientes.\"";
	next;
	goto _menu2;

_comoPresentar:
	mes "[Guia de Páscoa]";
	mes "\"Que bom que você entendeu como funciona o espírito da páscoa. ;)";
	mes "Para dar um ovo de páscoa para um amigo basta digitar: #felizpascoa <nome>";
	mes "Te explico melhor... imagine que seu amigo se chama joao. Então você digita: #felizpascoa joao";
	mes "Espero que tenha entendido como fazer.\"";
	next;
	mes "[Guia de Páscoa]";
	mes "\"Mas nem preciso dizer que antes de dar um ovo de páscoa a um amigo você precisa fazer um primeiro!\"";
	next;
	goto _menu;

_menu2:
	menu
		"Quais são os ingredientes?", _ingredientes,
		"Onde consigo estes ingredientes?", _ondeConsigo,
		"Irei trazer os ingredientes.", _trazerIngredientes,
		"Quero fazer um [Ovo de Páscoa].", _fazerOvo,
		"Sair.", -;
	close;

_ingredientes:
	mes "[Guia de Páscoa]";
	mes "\"Para fazer um delicioso ovo de páscoa de 500g precisaremos de 400g de chocolate especial e 100g de castanhas.";
	mes "Mas não pode ser qualquer tipo de chocolate especial e nem qualquer tipo de castanha. Para cada sabor de chocolate especial temos que utilizar um tipo diferente de castanha.\"";
	mes "";
	mes "\"Veja esta lista.\"";
	next;
	mes "[Lista de Ingredientes]";
	mes "Ovo de páscoa sabor Cuputale Especial:";
	mes "- 16 [Barra de Cupulate Especial]";
	mes "- 6 [Castanha do Pará]";
	mes "Ovo de páscoa sabor Chocolate Especial:";
	mes "- 16 [Barra de Chocolate Especial]";
	mes "- 6 [Amendoim]";
	mes "Ovo de páscoa sabor Chocolate Amargo Especial:";
	mes "- 16 [Barra de Chocolate Amargo Especial]";
	mes "- 6 [Castanha de Caju]";
	mes "Ovo de páscoa sabor Chocolate Branco Especial:";
	mes "- 16 [Barra de Chocolate Branco Especial]";
	mes "- 6 [Coco]";
	next;
	goto _menu2;

_ondeConsigo:
	mes "[Guia de Páscoa]";
	mes "\"Coelhos adoram chocolate e castanhas... aposto que você vai conseguir todo estes ingredientes com eles.";
	mes "Mas tenha cuidado para não machucá-los.\"";
	next;
	goto _menu2;

_trazerIngredientes:
	mes "[Guia de Páscoa]";
	mes "\"Então está combinado... estarei esperando por seus ingredientes.\"";
	close;

_dataValidade:
	mes "[Guia de Páscoa]";
	mes "\"Sim... os ovos de páscoa tem data de validade. Eles costumam durar pouco mais de uma semana, ou seja, vencem depois do dia 18/04/2010.\"";
	mes "";
	mes "* \"Não deixe para comê-los depois do dia 18/04/2010... eu falo sério.\"";
	next;
	goto _menu;

//======================================================================
//= Fazendo ovo de páscoa ==============================================
//======================================================================

_fazerOvo:
	set @idOvoPascoa,   1208;
	set @idChocolate,   3071;
	set @idCastanha,    3075;
	set @idTintaGuache, 3080;

	if (PASCOA_questOvo==1) goto _fazerOvo3;
	if (PASCOA_questOvo==2) goto _fazerOvo4;
	if (PASCOA_questOvo==3) goto _menuPapel;
	if (PASCOA_questOvo==4) goto _fazerOvo5;
	if (PASCOA_questOvo==5) goto _fazerOvo6;

	if (countitem("cupulateEsp")>=16 && countitem("castanhaPara")>=6) goto _fazerOvo2;
	if (countitem("chocolateEsp")>=16 && countitem("amendoim")>=6) goto _fazerOvo2;
	if (countitem("chocolateAmargoEsp")>=16 && countitem("castanhaCaju")>=6) goto _fazerOvo2;
	if (countitem("chocolateBrancoEsp")>=16 && countitem("coco")>=6) goto _fazerOvo2;

	mes "[Guia de Páscoa]";
	mes "\"Você não tem ingredientes suficiente para fazer um ovo de páscoa.";
	mes "Lembre-se que preciso de 16 barras de chocolate especial e 6 castanhas para cada tipo de chocolate.\"";
	close;

_fazerOvo2:
	mes "[Guia de Páscoa]";
	mes "\"Você tem ingredientes para fazer que sabor de ovo de páscoa?\"";
	next;
	goto _menu3;

_menu3:
	menu
		"Sabor Cuputale Especial.", -,
		"Sabor Chocolate Especial.", -,
		"Sabor Chocolate Amargo Especial.", -,
		"Sabor Chocolate Branco Especial.", -,
		"Meus ingredientes acabaram.", -;

	if (@menu<=0 || @menu>4) close;
	set PASCOA_questOvoTipo, @menu-1;
	mes "[Guia de Páscoa]";
	mes "\"Me dê seus ingredientes.\"";
	next;
	menu
		"Aqui estão.", -,
		"Ops... Acabaram meus ingredientes.", -;
	if (@menu!=1) close;

	if (countitem(@idChocolate+PASCOA_questOvoTipo)<16 || countitem(@idCastanha+PASCOA_questOvoTipo)<6) goto _semIngredientes;
	delitem @idChocolate+PASCOA_questOvoTipo, 16;
	delitem  @idCastanha+PASCOA_questOvoTipo, 6;
	set PASCOA_questOvo, 1;
	goto _misturando;

_misturando:
	mes "[Guia de Páscoa]";
	mes "\"Ótimo, vamos começar a trabalhar...\"";
	mes "";
	mes "O coelho pega os ingredientes e mistura tudo com um pouco de leite em uma vasilha ao fogo. Espera um pouco para que o chocolate derreta e então o coloca em duas fôrmas ovais.";
	next;
	goto _fazerOvo3;

_fazerOvo3:
	mes "[Guia de Páscoa]";
	mes "\"Enquanto o chocolate endurece...";
	mes "Que cor gostaria de pintar seu ovo de páscoa? Que cor de [Tinta Guache] você tem ai?!\"";
	next;
	goto _menuCor;

_menuCor:
	menu
		"Vermelho", -,
		"Verde", -,
		"Azul Escuro", -,
		"Amarelo", -,
		"Azul Claro", -,
		"Rosa", -,
		"Não tenho nenhuma Tinta Guache.", -;
	if (@menu<=0 || @menu>6) close;

	set PASCOA_questOvoCor, @menu-1;
	if (countitem(@idTintaGuache+PASCOA_questOvoCor)<1) goto _semTinta;
	delitem @idTintaGuache+PASCOA_questOvoCor, 1;
	set PASCOA_questOvo, 2;
	goto _fazerOvo4;

_fazerOvo4:
	mes "[Guia de Páscoa]";
	mes "\"Já temos o chocolate, a tinta... deixe-me ver o que mais está faltando...\"";

	if (PASCOA_questOvoPapel==0) set PASCOA_questOvoPapel, rand(16)+1; //< Pode ou não pedir Papel de Embrulho.
	if (PASCOA_questOvoPapel==1) goto _precisaPapel;

	mes "";
	mes "\"Acho que temos tudo...\"";
	next;
	goto _fazerOvo5;

_precisaPapel:
	set PASCOA_questOvo, 3;
	mes "";
	mes "\"Essa não! Meu [Rolo de Papel de Embrulho] acabou e sem ele não poderei decorar os ovos.\"";
	next;
	goto _menuPapel;

_menuPapel:
	mes "[Guia de Páscoa]";
	mes "\"Por favor me traga um [Rolo de Papel de Embrulho] para que eu possa decorar seu ovo de páscoa.\"";
	next;
	menu
		"Eu tenho... pegue este.", -,
		"Eu também não tenho nenhum.", -;
	if (@menu!=1) close;
	if (countitem("roloPapel")<1) goto _semPapel;
	delitem "roloPapel", 1;
	set PASCOA_questOvo, 4;
	mes "[Guia de Páscoa]";
	mes "\"Agora sim temos tudo...\"";
	next;
	goto _fazerOvo5;

_fazerOvo5:
	mes "[Guia de Páscoa]";
	mes "\"Vamos decorar o ovo de páscoa.\"";
	mes "";
	mes "O coelho encaixa os dois lados do ovo e o enrola em uma folha de alumínio. Recorta algumas tiras de papel e também as enrola em volta do ovo passando um pouco de cola nas pontas. Em seguida molha a ponta do pincel com tinta e começa a pintar o ovo.";
	next;
	mes "[Guia de Páscoa]";
	mes "\"Quase terminando...\"";
	mes "";
	mes "Depois de mais alguns acabamentos ele finalmente o termina.";
	set PASCOA_questOvo, 5;
	next;
	goto _fazerOvo6;

_fazerOvo6:
	getinventorylist;
	if( @inventorylist_count == 100 ) goto _cheio;

	mes "[Guia de Páscoa]";
	mes "\"Aqui está seu ovo de páscoa. Agora você já pode presentear um amigo!\"";
	getitem @idOvoPascoa+PASCOA_questOvoCor, 1;
	//set PASCOA_questOvoTem, PASCOA_questOvoTem+1;
	skill 398, getskilllv(398) + 1; //< para poder ser lida na SPELL.
	set PASCOA_questOvo, 0;      //< pra poder fazer outro
	set PASCOA_questOvoPapel, 0; //< pra poder pedir Papel de Embrulho.
	close;

_cheio:
	mes "[Guia de Páscoa]";
	mes "\"Espere... você não pode carregar um ovo de páscoa em meio a tantos items, pois você irá quebrá-lo.";
	mes "Volte quanto puder carregá-lo.\"";
	close;

_semPapel:
	mes "[Guia de Páscoa]";
	mes "\"Você também não tem um [Rolo de Papel de Embrulho].\"";
	close;

_semTinta:
	mes "[Guia de Páscoa]";
	mes "\"Eu acho que você não tem essa cor de tinta.\"";
	next;
	goto _menuCor;

_semIngredientes:
	mes "[Guia de Páscoa]";
	mes "\"Eu acho que você não tem mais ingredientes para este sabor de chocolate.\"";
	next;
	goto _menu3;

}
