/**
 * Este ursinho é muito fofo. Ele com certeza vai ganhar a sua simpatia.
 *
 * @date domingo, 16 de janeiro de 2011
 * @author Diogo_RBG - http://diogorbg.blogspot.com/
 */

//008-6.gat,162,64,0	script	debug	327,{
//	callfunc "debug";
//	menu
//		"set 0", -,
//		"Matar 500 abelhas", -,
//		"sair", -;
//	if(@menu==1) set QUEST_ursinho, 0;
//	if(@menu==1) set QUEST_MASK1, QUEST_MASK1 & ~MASK1_URSINHO; //< Seta 0 no bit da quest
//	if(@menu==2) set QUEST_ursinhoAbelhas, QUEST_ursinhoAbelhas + 500;
//	close;
//}

008-6.gat,164,64,0	script	Ursinho	327,{
	set @favoDeMel, 3149;
	set @ferraoAbelha, 3150;
	set @retalhoPelucia, 3218;

	if(QUEST_MASK1 & MASK1_URSINHO) goto _novoAmigo; //< Quest Ursinho Faminto concluída. Estado 3.

	if(QUEST_ursinho==1) goto _oferecerMel;
	if(QUEST_ursinho>=2) goto _presente;
	if(BaseLevel>=39) goto _inicio;

	mes "Você vê um ursinho muito fofo e pequenino sentado à sua frente. Que lindinho!";
	next;
	mes "Ele parece estar dormindo... vamos deixá-lo descansar em paz.";
	close;

_inicio:
	mes "Você vê um ursinho muito fofo e pequenino sentado à sua frente e olhando fixamente para seu potinho. Tão concentrado que nem percebe a sua presença.";
	mes "Você acha a cena interessante e resolve observar o ursinho por mais alguns instantes.";
	next;
	mes "E é ai que você presencia o ursinho abrir e fechar a tampa de seu potinho repetidas vezes. Como se estivesse procurando por algo.";
	next;
	mes "Você se pergunta: O que o ursinho está procurando? Como posso ajudá-lo?";
	next;
	goto _menu;

_menu:
	menu
		"Cantar uma música para alegrá-lo.", _cantarMusica,
		"Acariciar o seu lindo pelo macio.", _acariciar,
		"Aproximar de mansinho.", _aproximarMansinho,
		"Oferecer comida a ele.", _oferecerComida,
		"Deixar o coitadinho em paz.", -;
	mes "Você deixa o ursinho a sós com seu potinho.";
	close;

_cantarMusica:
	mes "Você começa a cantar uma linda música para o ursinho e isso o deixa muito contente.";
	mes "Você pode perceber isso ao ver claramente os grandes olhos redondos e brilhantes no olhar do ursinho.";
	next;
	mes "Você consegue distraí-lo por um momento, mas depois de um tempo o ursinho volta para o seu pequeno ritual com o potinho.";
	next;
	mes "Pobre ursinho, nem uma bela música consegue confortá-lo por muito tempo. O que fazer agora?";
	next;
	goto _menu;

_acariciar:
	mes "Você abre um grande sorriso no rosto e estende sua mão na direção do ursinho. Mas o pequenino se assusta com o movimento e começa a se afastar de você.";
	next;
	mes "Ao ver a reação do ursinho você desiste de tentar acariciá-lo.";
	next;
	mes "Você vai ter que resistir à tentação de apertar a bolinha de pelos. Tem alguma outra idéia?";
	next;
	goto _menu;

_aproximarMansinho:
	mes "Primeiro você tira a sua bolsa e a coloca no chão. Se abaixa o máximo que pode e começa a se aproximar lentamente do ursinho.";
	next;
	mes "Por sua vez o ursinho percebe a sua aproximação e se afasta. Ele não parece assustado, mas também não permite a sua aproximação.";
	next;
	if( countitem(@ferraoAbelha)>0 ) goto _comFerrao;
	if( countitem(@favoDeMel)>0 ) goto _comMel;
	mes "Você insiste na aproximação, mas o ursinho não permite. Não seria melhor tentar conquistar a confiança do ursinho primeiro?";
	next;
	goto _menu;

//- Falas se tiver mel ou ferrão.
_cheiroBom:
	mes "Então o ursinho sente um cheiro doce vindo de sua bolsa que o deixa muito interessado. Por um momento você desiste da aproximação e se põe a observar o ursinho.";
	next;
	if(@lembrancaFerrao==0) return;
	mes "O ursinho vai seguindo o cheiro doce até a sua bolsa, mas quando chega bem perto ele se lembra da ferroada no focinho e se afasta da bolsa.";
	next;
	mes "Coitadinho do ursinho. Seu focinho ainda deve estar doendo muito!";
	next;
	goto _menu;

_comFerrao:
	callsub _cheiroBom;
	mes "O ursinho vai seguindo o cheiro até a sua bolsa e sem nenhuma cerimônia começa a fuçar em suas coisas. E você apenas o observa para saber o que ele tanto procura em sua bolsa.";
	next;
	mes "Depois de muito fuçar o ursinho encontra algo realmente desagradável. Atraído pelo cheiro doce ele espeta o seu focinho em um ferrão de abelha. (coitadinho)";
	delitem @ferraoAbelha, 1;
	set @lembrancaFerrao, 1;
	next;
	mes "O ursinho não fica nada satisfeito e se afasta de você e de sua bolsa. Que pena! Quase ganhamos a confiança do ursinho.";
	next;
	goto _menu;

_comMel:
	callsub _cheiroBom;
	mes "O ursinho vai seguindo o cheiro até a sua bolsa e sem nenhuma cerimônia começa a fuçar em suas coisas. E você apenas o observa para saber o que ele tanto procura em sua bolsa.";
	next;
	mes "Depois de muito fuçar o ursinho encontra algo que o deixa realmente feliz. Ele encontra um favo de mel! E não existe nada que um urso goste mais do que mel.";
	delitem @favoDeMel, 1;
	callsub _missaoUrsinho, 0; //< Ativa variável.
	next;
	mes "O ursinho pega o favo de mel e sai correndo com ele até um cantinho escuro da caverna para saboreá-lo em paz.";
	next;
	mes "Agora sim você sabe como agradar o nosso amiguinho. Favos de mel! Porque não pensamos isto antes?!";
	next;
	callsub _missaoUrsinho, 1; //< Mostra informaçãoes.
	close;

//- OFERECENDO COMIDA AO URSINHO --------------------------------------

_oferecerComida:
	mes "Será que o ursinho tem fome? O que será que podemos oferecer a ele?";
	next;

_menuComida:
	menu
		"Oferecer uma maçã.", _maca,
		"Oferecer uma laranja.", _laranja,
		"Oferecer uma bolota.", _bolota,
		"Oferecer um cogumelo.", _cogumelo,
		"Oferecer ovo de cobra.", _ovoCobra,
		"Oferecer uma coxa de frango.", _coxaFrango,
		"Oferecer um filé cru.", _fileCru,
		"Oferecer um pouco de leite", _leite,
		"Oferecer uma cerveja.", _cerveja,
		"Sem mais ideias...", -,
		"Não oferecer nada.", -;
	mes "Quem diria ser tão difícil agradar um ursinho!";
	mes "Ele não gostou de nada do que foi oferecido... ou não está com fome.";
	close;

_maca:
	if(countitem(535)<1) goto _semComida;
	mes "Você coloca a maçã no chão e se distancia.";
	mes "O ursinho se aproxima e cheira a maçã, mas não se interessa por ela.";
	delitem 535, 1;
	next;
	getitem 535, 1;
	goto _menuComida;

_laranja:
	if(countitem(657)<1) goto _semComida;
	mes "Você coloca a laranja no chão e se distancia.";
	mes "O ursinho se aproxima e cheira a laranja, mas não se interessa por ela.";
	delitem 657, 1;
	next;
	getitem 657, 1;
	goto _menuComida;

_bolota:
	if(countitem(743)<1) goto _semComida;
	mes "Você coloca a bolota no chão e se distancia.";
	mes "O ursinho se aproxima e cheira a bolota, mas não se interessa por ela.";
	delitem 743, 1;
	next;
	getitem 743, 1;
	goto _menuComida;

_cogumelo:
	if(countitem(566)<1) goto _semComida;
	mes "Você coloca o cogumelo no chão e se distancia.";
	mes "O ursinho se aproxima e cheira o cogumelo, mas não se interessa por ele.";
	delitem 566, 1;
	next;
	getitem 566, 1;
	goto _menuComida;

_ovoCobra:
	if(countitem(714)<1) goto _semComida;
	mes "Você coloca o ovo de cobra no chão e se distancia.";
	mes "O ursinho se aproxima e cheira o ovo de cobra, mas não se interessa por ele.";
	delitem 714, 1;
	next;
	getitem 714, 1;
	goto _menuComida;

_coxaFrango:
	if(countitem(562)<1) goto _semComida;
	mes "Você coloca a coxa de frango no chão e se distancia.";
	mes "O ursinho se aproxima e cheira a coxa de frango, mas não se interessa por ela.";
	delitem 562, 1;
	next;
	getitem 562, 1;
	goto _menuComida;

_fileCru:
	if(countitem(676)<1) goto _semComida;
	mes "Você coloca o filé cru no chão e se distancia.";
	mes "O ursinho se aproxima e cheira o filé cru, mas não se interessa por ele.";
	delitem 676, 1;
	next;
	getitem 676, 1;
	goto _menuComida;

_leite:
	if(countitem(527)<1) goto _semComida;
	mes "Você coloca o leite no chão e se distancia.";
	mes "O ursinho se aproxima e cheira o leite, mas não se interessa por ele.";
	delitem 527, 1;
	next;
	getitem 527, 1;
	goto _menuComida;

_cerveja:
	mes "Não acredito que você seria capaz de oferecer bebida alcoólica a um ursinho! Que coisa feia.";
	mes "Melhor pensar em outra coisa.";
	next;
	goto _menuComida;

_semComida:
	mes "Você não tem este alimento em sua bolsa!";
	next;
	goto _menuComida;

//- OFERECENDO MEL AO URSINHO -----------------------------------------

_oferecerMel:
	mes "Nosso amiguinho sabe o que é realmente doce nessa vida. Ele gosta muito de mel.";
	mes "E pela maneira com que ele olha para você neste momento ele parece querer mais.";
	next;
	mes "Que lindinho... Ele está com aquela carinha de pidão! Ele com certeza deve estar querendo mais mel.";
	mes "Você não vai negar mel a um ursinho... não é! Ou vai?";
	next;
	goto _menuOferecerMel;

_menuOferecerMel:
	set @favos, countitem(@favoDeMel);
	if(@favos<1) goto _semMel;
	if(@favos<=3) menu
		"Oferecer todos ("+@favos+" favos de mel).", _oferecerMel2,
		"Não oferecer mel.", _nenhumFavo;
	if(@favos<=3) goto _nenhumFavo;
	if(@favos<=6) menu
		"Oferecer 3 favos de mel.", _3favos,
		"Oferecer todos ("+@favos+" favos de mel).", _oferecerMel2,
		"Não oferecer mel.", _nenhumFavo;
	if(@favos<=6) goto _nenhumFavo;
	if(@favos<=9) menu
		"Oferecer 3 favos de mel.", _3favos,
		"Oferecer 6 favos de mel.", _6favos,
		"Oferecer todos ("+@favos+" favos de mel).", _oferecerMel2,
		"Não oferecer mel.", _nenhumFavo;
	if(@favos<=9) goto _nenhumFavo;
	if(@favos>9) menu
		"Oferecer 3 favos de mel.", _3favos,
		"Oferecer 6 favos de mel.", _6favos,
		"Oferecer 9 favos de mel.", _9favos,
		"Oferecer todos ("+@favos+" favos de mel).", _oferecerMel2,
		"Não oferecer mel.", _nenhumFavo;
	goto _nenhumFavo;

_3favos:
	set @favos, 3;
	goto _oferecerMel2;
_6favos:
	set @favos, 6;
	goto _oferecerMel2;
_9favos:
	set @favos, 9;
	goto _oferecerMel2;

_oferecerMel2:
	//- Teste que elimina a possibilidade do jogador ter retirado o mel na pausa do menu.
	if(countitem(@favoDeMel) < @favos) set @favos, countitem(@favoDeMel);
	if(@favos<1) goto _semMel;

	delitem @favoDeMel, @favos;
	set QUEST_ursinhoFavos, QUEST_ursinhoFavos + @favos;
	mes "Você pega "+@favos+" favos de mel e os coloca no potinho do ursinho.";
	mes "Enquanto isso nosso amiguinho o observa de uma distância segura.";
	next;
	if(@favos>22) goto _enchePotinho;
	if(QUEST_ursinhoFavos>30) goto _encheUrsinho;
	mes "Assim que você volta 2 passos o ursinho vai até o potinho e se delicia com o mel.";
	mes "Em poucos instantes não há mais mel em seu potinho. É... ele realmente estava faminto!";
	next;
	mes "O ursinho parece querer mais. O que fazer?";
	next;
	goto _menuOferecerMel;

_enchePotinho:
	mes "Assim que você volta 2 passos o ursinho vai até o potinho e o encontra completamente cheio... quase transbordando!";
	mes "O ursinho se enche de felicidade e também de muito mel! Come até ficar satisfeito.";
	set QUEST_ursinho, 2;
	next;
	goto _presente;

_encheUrsinho:
	mes "Assim que você volta 2 passos o ursinho vai até o potinho e se delicia com o mel.";
	mes "Mas desta vez ele não consegue comer todo o mel. Finalmente nosso amiguinho está satisfeito!";
	set QUEST_ursinho, 2;
	next;
	goto _presente;

_presente:
	mes "Assim que termina sua refeição, o ursinho vai até um canto escuro da caverna e pega um pedaço de pelúcia que estava escondido.";
	mes "Todo contente, o ursinho trás o pedaço de pelúcia e o deixa exatamente à sua frente.";
	next;
	mes "Você não entende exatamente o porque do ursinho trazer aquele pedaço de pelúcia para você!";
	mes "Mas pela cara do ursinho podemos dizer que aquele pedaço de pelúcia é um presente para você.";
	next;
	getinventorylist;
	if( @inventorylist_count >= 100) goto _cheio;
	if(QUEST_ursinhoAbelhas<1500) goto _semAbelhas;
	mes "Se nosso novo amiguinho nos oferece um presente é claro que iremos aceitá-lo!";
	mes "";
	getitem @retalhoPelucia, 1;
	callsub _missaoUrsinhoCompleta;
	close;

_cheio:
	mes "Você não tem espaço para rebecer o presente do ursinho.";
	close;

_nenhumFavo:
	mes "Que maldade com o ursinho. Assim você o deixa triste.";
	next;
	callsub _missaoUrsinho, 1; //< Mostra informaçãoes.
	close;

_semAbelhas:
	mes "É preciso matar pelo menos 1.125 abelhas para completar a missão.";
	next;
	callsub _missaoUrsinho, 1; //< Mostra informaçãoes.
	close;

_semMel:
	mes "Você não tem mais favos de mel. Infelizmente terá que desapontar nosso novo amiguinho.";
	next;
	callsub _missaoUrsinho, 1; //< Mostra informaçãoes.
	close;

_novoAmigo:
	mes "Dessa vez, ao reencontrar o ursinho, é ele que vem ao seu encontro para receber seu carinho.";
	mes "Você com certeza ganhou um novo amiguinho!";
	if(countitem(@retalhoPelucia)>0) goto _utilidadeRetalho;
	close;

_utilidadeRetalho:
	next;
	mes "Neste momento você se lembra do retalho de pelúcia macio e fofinho que ganhou do ursinho e se pergunta: \"O que posso fazer com um pedaço de pelúcia fofinho e quente?\"";
	close;

//- Missão Ursinho Faminto ---------------------------------------------

_missaoUrsinho:
	if(QUEST_ursinho>0) goto _else;
		set QUEST_ursinho, 1;
		set QUEST_ursinhoFavos, 0;
		set QUEST_ursinhoAbelhas, 0;
_else:
	set @listar, getarg(0);
	if(@listar) mes "[Missão: Ursinho Faminto]";
	if(@listar) mes "* Monstros: " +QUEST_ursinhoAbelhas+ "/1125 Abelhas";
	if(@listar) mes "* Recompensa: 15.750 XP";
	// Abelha: 7 XP, 7x1.125 = 15.750 XP
	// Margem Segurança lvl 39: 9.163 XP (15K é 2x maior... mas não tem preblema)
	return;

_missaoUrsinhoCompleta:
	mes "* Você ganhou 15.750 pontos de experiência.";
	message strcharinfo(0), "Ganhei 15.750 pontos de experiência.";
	set BaseExp, BaseExp+15750; //< Só para lvl>=41

	set QUEST_ursinho, 0;
	set QUEST_ursinhoFavos, 0;
	set QUEST_ursinhoAbelhas, 0;
	set QUEST_MASK1, QUEST_MASK1 | MASK1_URSINHO;
	return;

}
