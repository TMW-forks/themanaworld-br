// Parua's fight - Original from Kage
// Adapted to the brazilian server and translated to portuguese by alastrim
// Traduzido e Adaptado do original desenvolvido por kage

//-------------------------------------------------------------------------------------------------------------------------
// Coisas a fazer ou não fazer: 
// 1) Não colocar o NPC em um mapa grande demais. Isso é para evitar que os jogadores simplesmente saiam correndo dos monstros enquanto os lvls vão aumentando.(Sim, eles aumentam sozinho com o tempo também).
// 2) Colocar um script-barreira no mapa que dá acesso à arena, de modo que durante uma "luta", somente os que estão lutando podem permanecer no mapa. Quando todos morrerem o mapa é liberado novamente.
// 3) Fazer as devidas modificações no script para que seja usado no BR. 
// 4) Testar e verificar se o desafio está devidamente equilibrado.
//-------------------------------------------------------------------------------------------------------------------------

005-1.gat,38,46,0	script	Parua	121,{
	if ($@ARENA_STATUS != 0) goto L_comeca;
	mes "[Parua]";
	mes "\"Olá.\"";
	next;
	mes "[Parua]";
	mes "\"Você gostaria de enfrentar os desafios de minha arena?\"";

	menu "Não.", L_fim,
	     "Gostaria de conhecer o RECORD desta arena e o grupo que o conseguiu.", L_record,
	     "Sim.", -;

	mes "[Parua]";
	mes "\"Muito bem, mas eu cobro 20k GP para ativar a arena...\"";
	menu "Não, isso é um roubo!", L_fim,
	     "OK, aqui está.", -;

//--------------------------------------------------------------------------------
// Abaixo você pode mudar o preço cobrado para ativar os spawns do NPC
//--------------------------------------------------------------------------------
	if (zeny < 20000) goto L_semgrana;
	if ($@ARENA_STATUS != 0) goto L_jacomecou;

//--------------------------------------------------------------------------------
// Abaixo você pode colocar um limite mínimo de jogadores para participar
//--------------------------------------------------------------------------------
	if (getareausers("005-1.gat", 28, 46, 53, 65) < 1) goto L_poucosjogadores;

//-------------------------------------------------------------------------------------------------------------------------
// Abaixo está a parte em que o jogador escolhe o nome do grupo. Quem pagar pelo evento pode escolher o nome do grupo que vai participar. O nome deste grupo pode ser o de um clan, ou qualquer outra coisa que vier na cabeça de quem estiver pagando.
//-------------------------------------------------------------------------------------------------------------------------

	mes "[Parua]";
	mes "\"OK, agora eu só preciso que você dê o nome do grupo que vai participar desta rodada. Assim eu posso anotar seu desempenho no meu registro e comparar com os outros grupos. Escolha bem o nome, pois se você for o melhor ele aparecerá no rol da fama desta arena:\"";

//--- LUNOVOX -------------------------------------------------------------------------------------------------------------
// É preciso fazer o teste novamente para evitar que o jogador esvazie os bolsos de granda enquanto tiver pondo o nome do Grupo. 
// (Isso evita que o jogador participe sem pagar...)
//-------------------------------------------------------------------------------------------------------------------------
	input $@GRUPO$;


	if (zeny < 20000) goto L_semgrana;

	npctalk "Muito bem, vamos ver como o grupo " + $@GRUPO$ + " se sai em minha arena!";
	next;
	set zeny, zeny - 20000;
	npctalk "Que a luta começe!";
	set $@ARENA_RECORD, 1;
	set $@ARENA_STATUS, 1;
	set $@ARENA_LEVEL, 1;
	set $@ARENA_CONTA_JOGADORES, getareausers("005-1.gat", 28, 46, 53, 65);

	set $@ARENA_RESTA, 0;

	startnpctimer;
	goto L_fim;

L_comeca:
	mes "[Parua]";
	mes "\"Que começe a luta!\"";
	goto L_fim;

L_semgrana:
	mes "[Parua]";
	mes "\"Parece que você não tem o dinheiro.\"";
	goto L_fim;

L_jacomecou:
	mes "[Parua]";
	mes "\"Não precisa, algum dos seus parceiros já me deu o dinheiro.\"";
	goto L_fim;

L_poucosjogadores:
	mes "[Parua]";
	mes "\"Acho melhor você chamar mais alguns amigos.... as coisas tendem a ficar feias por aqui!\"";
	goto L_fim;

L_record:
	npctalk "O record da arena pertence ao grupo " + $@GRUPO_RECORD$ + ", que conseguiu a façanha de ser o primeiro a chegar ao nível " + $@ARENA_RECORD; 

L_fim:
	close;
	end;

// Fight logic
OnTimer5000:
	setnpctimer 0;
	if ($@ARENA_STATUS != 0) goto L_logicadaarena;
L_Return_1:
	set $@ARENA_CONTA_JOGADORES, 0;
	areatimer "005-1.gat", 28, 46, 53, 65, 10, "Parua::onTick";
	end;

L_logicadaarena:
	set $@ARENA_ROUND_PEN, $@ARENA_CONTA_JOGADORES;
	if ($@ARENA_ROUND_PEN > 60) set $@ARENA_ROUND_PEN, 60;
	if ($@ARENA_CONTA_JOGADORES <= 0) goto L_limpa;
	set $@ARENA_ROUND_TEMPO, $@ARENA_ROUND_TEMPO + 5; // Advance 5 seconds
	if (mobcount("005-1.gat", "Parua::onPetDeath") <= 0) goto L_proximoround;
	if ($@ARENA_ROUND_TEMPO + $@ARENA_ROUND_PEN >= 120) goto L_proximoround;
	goto L_Return_1;

L_proximoround:
	set $@ARENA_ROUND_TEMPO, 0;

	set $@ARENA_LEVEL, $@ARENA_LEVEL + $@ARENA_CONTA_JOGADORES + ($@ARENA_LEVEL / 40);
	if ($@ARENA_LEVEL >= 3000) goto L_ganhou;
	set $@ARENA_POINTS, $@ARENA_LEVEL;

	if ($@ARENA_RESTA + 30  < $@ARENA_LEVEL) goto L_anuncia;
L_Return_2:
	npctalk "Novo round começando! O nível deste round é " + $@ARENA_LEVEL;

	set $@MOB_1_SUMMON, 0;
	set $@MOB_2_SUMMON, 0;
	set $@MOB_3_SUMMON, 0;
	set $@MOB_4_SUMMON, 0;
	set $@MOB_5_SUMMON, 0;
	set $@MOB_6_SUMMON, 0;
L_Summon:
	if ($@ARENA_POINTS >= 243 && $@MOB_1_SUMMON < 3) goto L_MOB1;
	if ($@ARENA_POINTS >= 81 && $@MOB_2_SUMMON < 10) goto L_MOB2;
	if ($@ARENA_POINTS >= 27 && $@MOB_3_SUMMON < 10) goto L_MOB3;
	if ($@ARENA_POINTS >= 9 && $@MOB_4_SUMMON < 10) goto L_MOB4;
	if ($@ARENA_POINTS >= 3 && $@MOB_5_SUMMON < 20) goto L_MOB5;
	if ($@ARENA_POINTS >= 1 && $@MOB_6_SUMMON < 25) goto L_MOB6;
	goto L_Return_1;

//----------------------------------------------------------------------------------------------------
// Abaixo está a mensagem que o pessoal do lado de fora vai ver sobre o que está acontecendo na arena.
//----------------------------------------------------------------------------------------------------

L_anuncia:
	mapannounce "005-1.gat", "O grupo " + $@GRUPO$ + " está lutando neste instante na arena. Está começando o nível " + $@ARENA_LEVEL + " e há neste momento " + $@ARENA_CONTA_JOGADORES + " jogador(es) vivo(s)." , 0;
	set $@ARENA_RESTA, $@ARENA_RESTA + 30;
	goto L_Return_2;

//----------------------------------------------------------------------------------------------------
// Abaixo está definido os diferentes tipos de monstros que o NPC cria. Sinta-se livre para adaptar à realidade do server. A parte mais difícil nesse script é chegar a um ponto equilibrado, ao mesmo tempo divertido e desafiador.
//----------------------------------------------------------------------------------------------------

L_MOB1:
	set $@MOB_1_SUMMON, $@MOB_1_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 243;
	areamonster "005-1.gat", 28, 46, 53, 65, "", 1022, 1, "Parua::onPetDeath";
	goto L_Summon;

L_MOB2:
	set $@MOB_2_SUMMON, $@MOB_2_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 81;
	areamonster "005-1.gat", 28, 46, 53, 65, "", 1313, 1, "Parua::onPetDeath";
	goto L_Summon;

L_MOB3:
	set $@MOB_3_SUMMON, $@MOB_3_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 27;
	areamonster "005-1.gat", 28, 46, 53, 65, "", 1305, 1, "Parua::onPetDeath";
	goto L_Summon;

L_MOB4:
	set $@MOB_4_SUMMON, $@MOB_4_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 9;
	areamonster "005-1.gat", 28, 46, 53, 65, "", 1304, 1, "Parua::onPetDeath";
	goto L_Summon;

L_MOB5:
	set $@MOB_5_SUMMON, $@MOB_5_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 3;
	areamonster "005-1.gat", 28, 46, 53, 65, "", 1010, 1, "Parua::onPetDeath";
	goto L_Summon;

L_MOB6:
	set $@MOB_6_SUMMON, $@MOB_6_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 1;
	areamonster "005-1.gat", 28, 46, 53, 65, "", 1008, 1, "Parua::onPetDeath";
	goto L_Summon;

// Called on each player once every 5 seconds
onTick:
	if (isdead(0)) end;
	set $@ARENA_CONTA_JOGADORES, $@ARENA_CONTA_JOGADORES + 1;
	end;

onPetDeath:
	end;

onInit:
	initnpctimer;
	stopnpctimer;
L_limpa:
	npctalk "Fim de Jogo.";
	mapannounce "005-1.gat", "Tudo preparado para novos grupos.", 0;
	set $@ARENA_STATUS, 0;
	set $@ARENA_CONTA_JOGADORES, 0;
	if ($@ARENA_LEVEL > $@ARENA_RECORD) set $@ARENA_RECORD, $@ARENA_LEVEL;
	if ($@ARENA_LEVEL > $@ARENA_RECORD) set $@GRUPO_RECORD$, $@GRUPO$;
	set $@ARENA_LEVEL, 1;
	set $@ARENA_ROUND_TEMPO, 0;
	killmonster "005-1.gat", "Parua::onPetDeath";
	stopnpctimer;
	setnpctimer 0;
	end;

L_ganhou:
	// Coisas que podem ser feitas:
	// Parabeniza os jogadores
	// Faz um anuncio global
	// Spawn de um boss com grande recompensa em XP
	// Inclusão em uma lista dos grupos ganhadores, talvez com o nome dos participantes.
	
}
