//

009.gat,87,64,0	script	#pertoLobo	0,1,1,{
	set DIST_loboSolitario, 1;
	close;
}

009.gat,90,64,0	script	#longeLobo	0,1,1,{
	set DIST_loboSolitario, 0;
	close;
}

// Ajudinha para dropar ambar
function	script	drop_ambar_pedraAguia	{
	if(QUEST_pedraAguia!=1 || DROP_pedraAguia>15) return;

	set @tmp, rand(100);
	if(@tmp<5) getitem 3215, 1; //< Ambar
	if(@tmp<5) set DROP_pedraAguia, DROP_pedraAguia+1;
	return;
}

//009.gat,78,63,0	script	debug	143,{
//	callfunc "debug";
//	menu
//		"set 0", -,
//		"Matar 60 mobs", -,
//		"set 1 PEDRA_AGUIA", -,
//		"set 0 PEDRA_AGUIA", -,
//		"sair", -;
//	if(@menu==1) set QUEST_colarAguia, 1;
//	if(@menu==1) set QUEST_pedraAguia, 0;
//	if(@menu==2) set MOBS_pedraAguia, MOBS_pedraAguia+60;
//	if(@menu==3) set QUEST_MASK1, QUEST_MASK1 | MASK1_PEDRA_AGUIA;
//	if(@menu==4) set QUEST_MASK1, QUEST_MASK1 & ~MASK1_PEDRA_AGUIA; //< seta apenas MASK1_PEDRA_AGUIA como 0.
//	close;
//}

009.gat,80,63,0	script	Lobo Solitário	143,{
	set @LEATHER_PATCH, 708;
	set @SNAKE_SKIN, 641;
	set @raiz, 740;
	set @pedraAmbar, 3215;
	set @tocoMadeira, 569;
	set @garrafaVazia, 540;
	set @pedraOlhoAguia, 3216;

	set @LEATHER_PATCH_PRICE, 300;

	set @wants_leather_patch, QUEST_Forestbow_state & NIBBLE_4_MASK;

	if (!DIST_loboSolitario) goto _muitoLonge;

	//- Quest Pedra Olho de Aguia - Fazer Quest Arco da Floresta primeiro.
	if (QUEST_MASK1 & MASK1_PEDRA_AGUIA) goto _inicio; //< Quest Pedra Olho de Aguia concluída. Estado 3.
	if (QUEST_colarAguia != 1) goto _inicio;

	if (QUEST_pedraAguia == 0) goto _questpedra;
	if (QUEST_pedraAguia == 1) goto _retornapedra;
	if (QUEST_pedraAguia == 2) goto _criarPedraAguia;
	goto _inicio;

_inicio:
	mes "[Lobo Solitário]";
	mes "\"How!\"";
	next;

	if (getequipid(equip_head) == 643 || getequipid(equip_head) == 644) goto L_WearingCowboy;
	if (getequipid(equip_legs) == 642) goto L_WearingChaps;

	mes "[Lobo Solitário]";
	mes "\"Por gerações, minha tribo têm criado roupas especiais de diferentes itens.\"";
	goto L_Check_Shops;

_muitoLonge:
	mes "[Lobo Solitário]";
	mes "\"Não posso te ouvir direito dessa distância.\"";
	close;

L_Check_Shops:
	if ((countitem(610) > 0 && countitem(@SNAKE_SKIN) > 9) && (countitem(524) > 0 && countitem(@SNAKE_SKIN) > 1)) goto L_Super_store;
	if (countitem(610) > 0 && countitem(@SNAKE_SKIN) > 9) goto L_Chaps_store;
	if (countitem(524) > 0 && countitem(@SNAKE_SKIN) > 1) goto L_Cowboy_store;

	mes "[Lobo Solitário]";
	mes "\"Talvez se você me trouxer o material certo, eu possa fazer algo para você.\"";
	next;

	if (!@wants_leather_patch)
		menu
			"Você é um índio?! Me conte mais sobre você.", _historia_indio,
			"OK, tchau.", -;
	if (@wants_leather_patch)
		menu
			"Você é um índio?! Me conte mais sobre você.", _historia_indio,
			"Espere, você pode fazer um retalho de couro para mim?", L_leather_patch,
			"OK, tchau.", -;
	close;

_historia_indio:
	mes "[Lobo Solitário]";
	mes "\"Sim, eu sou índio.";
	mes "Meu pai é o grande Chefe Monte Brilhante e minha mãe a bela Lua do Grande Lago.";
	mes "A muito tempo vivo distante de minha tribo e criar itens utilizando técnicas aperfeiçoadas por várias gerações de minha tribo é uma forma de me sentir mais próximo dela.\"";
	close;

L_CheckStuff:
	mes "[Lobo Solitário]";
	mes "\"Deixe-me ver o que você tem aí.\"";
	next;
	return;

L_Super_store:
	callsub L_CheckStuff;
	mes "[Lobo Solitário]";
	mes "\"Ahh você tem muitos artigos bons para se trabalhar.\"";
	mes "";
	mes "\"Com eles posso fazer se você quiser um chapéu de vaqueiro ou uma calça de couro de cobra.\"";
	next;
	if (@wants_leather_patch)
		menu
			"Chapéu de vaqueiro, por favor.", L_BuyCowboy,
			"Calça de couro de cobra soa bem.", L_BuyChaps,
			"Você pode fazer um retalho de couro?", L_leather_patch,
			"Não agora, talvez depois.", -;
	if (!@wants_leather_patch)
		menu
			"Chapéu de vaqueiro, por favor.", L_BuyCowboy,
			"Calça de couro de cobra soa bem.", L_BuyChaps,
			"Não agora, talvez depois.", -;
	goto L_NoDeal;

L_Cowboy_store:
	callsub L_CheckStuff;
	mes "[Lobo Solitário]";
	mes "\"Para lhe fazer um chapéu de vaqueiro vou precisar de:";
	mes "1 [Chapéu Fantasia]";
	mes "2 [Pele de Cobra da Montanha]";
	mes "5.000 GP\"";
	mes "";
	mes "\"Nós temos um acordo?\"";
	next;
	if (@wants_leather_patch)
		menu
			"Sim, tudo bem.", L_BuyCowboy,
			"Você pode fazer um retalho de couro?", L_leather_patch,
			"Pensando bem, talvez mais tarde.", -;
	if (!@wants_leather_patch)
		menu
			"Sim, tudo bem.", L_BuyCowboy,
			"Pensando bem, talvez mais tarde.", -;
	goto L_NoDeal;

L_Chaps_store:
	callsub L_CheckStuff;
	mes "[Lobo Solitário]";
	mes "\"Para lhe fazer uma calça de couro de cobra vou precisar de:";
	mes "1 [Short Jeans]";
	mes "10 [Pele de Cobra da Montanha]";
	mes "10.000 GP\"";
	mes "";
	mes "\"Nós temos um acordo?\"";
	next;
	if (!@wants_leather_patch)
		menu
			"Sim, tudo bem.", L_BuyChaps,
			"Pensando bem, talvez mais tarde.", -;
	if (@wants_leather_patch)
		menu
			"Sim, tudo bem.", L_BuyChaps,
			"Você pode fazer um retalho de couro?", L_leather_patch,
			"Pensando bem, talvez mais tarde.", -;
	goto L_NoDeal;

L_BuyChaps:
	if (zeny < 10000) goto L_NoMoney;
	if (countitem(610) < 1) goto L_NoJeans;
	if (countitem(@SNAKE_SKIN) < 10) goto L_NoSkin;
	set zeny, zeny - 10000;
	delitem @SNAKE_SKIN, 10;
	delitem 610, 1;
	getitem 642, 1;
	goto L_DealDone;

L_BuyCowboy:
	if (zeny < 5000) goto L_NoMoney;
	if (countitem(524) < 1) goto L_NoFancy;
	if (countitem(@SNAKE_SKIN) < 2) goto L_NoSkin;
	set zeny, zeny - 5000;
	delitem @SNAKE_SKIN, 2;
	delitem 524, 1;
	set @temp,rand(2);
	if(@temp == 0) goto L_Cowboy_white;
	goto L_Cowboy_black;

L_Cowboy_white:
	getitem 643, 1;
	goto L_DealDone;

L_Cowboy_black:
	getitem 644, 1;
	goto L_DealDone;

L_leather_patch:
	mes "[Lobo Solitário]";
	mes "\"Se você quiser apenas uma retalho de couro, então sim, posso fazer isso. Traga-me um [Pele de Cobra da Montanha] e " + @LEATHER_PATCH_PRICE + " GP.\"";
	next;
	menu
		"Aqui está!", -,
		"OK, Eu volto mais tarde.", L_end,
		"Isso é muito caro!.", L_NoDeal;

	if (countitem(@SNAKE_SKIN) < 1) goto L_NoSkins;
	if (zeny < @LEATHER_PATCH_PRICE) goto L_NoMoney;

	set zeny, zeny - @LEATHER_PATCH_PRICE;
	delitem @SNAKE_SKIN, 1;
	getitem @LEATHER_PATCH, 1;
	goto L_DealDone;

L_DealDone:
	mes "[Lobo Solitário]";
	mes "\"Aqui está!\"";
	mes "";
	mes "\"Volte quando quiser.\"";
	close;

L_NoDeal:
	mes "[Lobo Solitário]";
	mes "\"Certo, mas você não vai conseguir melhor negócio em qualquer outro lugar!\"";
	close;

L_NoMoney:
	mes "[Lobo Solitário]";
	mes "\"Oh, parece que você não tem dinheiro suficiente.\"";
	close;

L_NoJeans:
	mes "[Lobo Solitário]";
	mes "\"Oh, parece que você não tem bastante Short Jeans.\"";
	close;

L_NoFancy:
	mes "[Lobo Solitário]";
	mes "\"Oh, parece que você não tem bastante Chapéu Fantasia.\"";
	close;

L_NoSkins:
	mes "[Lobo Solitário]";
	mes "\"Oh, parece que você não tem bastante Pele de Cobra da Montanha.\"";
	close;

L_WearingCowboy:
	mes "[Lobo Solitário]";
	mes "\"Ah, vejo que você está usando um chapéu feito com os métodos antigos da minha tribo.\"";
	next;
	goto L_Check_Shops;

L_WearingChaps:
	mes "[Lobo Solitário]";
	mes "\"Ah, vejo que você está vestindo calças feitas por minha tribo.\"";
	next;
	goto L_Check_Shops;

L_end:
	close;

//= Missão Pedra Olho de Águia =========================================

_questpedra:
	mes "[Lobo Solitário]";
	mes "\"How!\"";
	menu
		"Soube que há um índio nesta ilha que sabe fazer uma pedra olho de águia.", -;
	next;
	mes "[Lobo Solitário]";
	mes "\"Minha tribo fazia a pedra olho de águia há muitas gerações para os caçadores da aldeia conseguirem uma visão semelhante à das águias do céu.\"";
	next;
	menu
		"Você poderia fazer uma destas pedras para mim?", -;
	next;
	mes "[Lobo Solitário]";
	mes "\"Sim, posso. Mas o poder de ver como as águias do céu só poderia ser dado pelo pajé da tribo. Se eu for fazer a pedra ela não terá poder algum. Quer mesmo assim?\"";
	next;
	menu
		"Sim, gostaria que fizesse a pedra",_querpedra,
		"Ah...se é só decorativa, acho que não vou querer...", -;

	mes "[Lobo Solitário]";
	mes "\"Só o pajé tinha o poder de chamar o espírito da grande águia para a pedra. Se não quer mais a pedra olho de águia, tudo bem.\"";
	close;

_querpedra:
	mes "[Lobo Solitário]";
	mes "\"A pedra é feita de um material amarelo-dourado encontrado em algumas árvores. O nome do material é seiva, mas quando endurecido recebe o nome de âmbar. Traga-me 25 pedras de âmbar, 20 tocos de madeira, 20 raízes, 5 garrafas vazias. Além disso vou cobrar 20.000 gp.\"";
	next;
	menu
		"Vou até a floresta procurar nas árvores o âmbar.", -;
	next;
	callsub _missaoPedraAguia;
	close;

_retornapedra:
	mes "[Lobo Solitário]";
	mes "\"Oh, parece que você realmente quer a pedra olho de águia. Trouxe tudo o que te pedi? São 25 pedras de âmbar, 20 tocos de madeira, 20 raízes, 5 garrafas vazias e 20.000 gp.\"";
	menu
		"Sim, aqui estão", _checagemPedra,
		"Não, ainda falta pegar algumas coisas.", -;
	mes "[Lobo Solitário]";
	mes "\"Então, quando tiver tudo o que lhe pedi, volte e farei a sua pedra.\"";
	next;
	callsub _missaoPedraAguia;
	close;

_checagemPedra:
	if (countitem(@pedraAmbar) < 25 || countitem(@tocoMadeira) < 20) goto _naoTemMateriais;
	if (countitem(@raiz) < 20 || countitem(@garrafaVazia) < 5) goto _naoTemMateriais;
	if (Zeny < 20000) goto _naoTemGrana;
	if (MOBS_pedraAguia<200) goto _naoMatouMobs;
	mes "[Lobo Solitário]";
	mes "\"Pois bem, farei a sua pedra imediatamente.\"";
	delitem @pedraAmbar, 25;
	delitem @tocoMadeira, 20;
	delitem @raiz, 20;
	delitem @garrafaVazia, 5;
	set Zeny, Zeny-20000;
	set QUEST_pedraAguia, 2;
	next;
	goto _criarPedraAguia;
	close;

_naoTemMateriais:
	mes "[Lobo Solitário]";
	mes "\"Você não tem tudo o que eu pedi. Volte quando estiver com tudo.\"";
	next;
	callsub _missaoPedraAguia;
	close;

_naoTemGrana:
	mes "[Lobo Solitário]";
	mes "\"Você não tem todo o dinheiro que eu pedi. Eu preciso de 20.000 GPs.\"";
	next;
	callsub _missaoPedraAguia;
	close;

_naoMatouMobs:
	mes "É preciso matar pelo menos 200 Troncos Vivos para completar a missão.";
	next;
	callsub _missaoPedraAguia;
	close;

_criarPedraAguia:
	mes "Neste momento o índio pega as pedras de âmbar e coloca numa pequena panela, acende uma fogueira, e quando as pedras começam a derreter, ele mistura de forma a ficar um líquido dourado, muito parecido com o mel.";
	next;
	mes "Logo depois, derrama o âmbar derretido num recipiente que mais parece uma bola de vidro. Espera esfriar o âmbar e depois começa a esculpir os detalhes na pedra que agora tem a forma esférica.";
	next;
	mes "[Lobo Solitário]";
	mes "\"Aqui está a sua pedra, faça bom proveito.\"";
	getitem @pedraOlhoAguia, 1;
	mes "";
	callsub _missaoPedraAguiaCompleta;
	close;

//- Missão Pedra Olho de Águia -----------------------------------------

_missaoPedraAguia:
	if(QUEST_pedraAguia!=0) goto _else;
		set QUEST_pedraAguia, 1;
		set MOBS_pedraAguia, 0;
		set DROP_pedraAguia, 0;
_else:
	mes "[Missão: Pedra Olho de Águia]";
	mes "* Itens: " +countitem(@pedraAmbar)+ "/25 Pedras de Âmbar";
	mes "* Itens: " +countitem(@tocoMadeira)+ "/20 Tocos de Madeira";
	mes "* Itens: " +countitem(@raiz)+ "/20 Raízes";
	mes "* Itens: " +countitem(@garrafaVazia)+ "/5 Garrafas Vazia";
	mes "* Monstros: " +MOBS_pedraAguia+ "/200 Troncos Vivos";
	mes "* Recompensa: 60.000 XP";
	// Tronco Vivo: 149 XP, 149x200 ~= 60.000 XP
	// Margem Segurança lvl 51: 30.886 XP
	// Margem Segurança lvl 59: 66.038 XP
	return;

_missaoPedraAguiaCompleta:
	mes "* Você ganhou 60.000 pontos de experiência.";
	message strcharinfo(0), "Ganhei 60.000 pontos de experiência.";
	set BaseExp, BaseExp+60000; //< Restrição lvl>=30

	set QUEST_pedraAguia, 0;
	set MOBS_pedraAguia, 0;
	set DROP_pedraAguia, 0;
	set QUEST_MASK1, QUEST_MASK1 | MASK1_PEDRA_AGUIA;
	return;

}
