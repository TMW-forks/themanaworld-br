//
// Tradução: Diogo_RBG - http://diogorbg.blogspot.com/

010.gat,152,42,0	script	debug	129,{
	mes "debug";
	menu
		"set 1", -,
		"set 3", -,
		"set bneve 0", -,
		"set bneve 2", -,
		"sair", -;
	if(@menu==1) set NATAL2_noel, 1;
	if(@menu==2) set NATAL2_noel, 3;
	if(@menu<=3) set NATAL2_bneve, 0;
	if(@menu==4) set NATAL2_bneve, 2;
	close;
}

010.gat,155,42,0	script	Boneco de Neve	129,{

	set @itemBengala1, 514;
	set @itemBengala2, 506;
	set @itemBalinha, 510;
	set @itemChocolate, 509;
	set @itemBiscoito, 512;
	set @itemBoloNatal, 508;

	if (NATAL2_noel==1) goto _procurandoOculos;
	if (NATAL2_noel==2) goto _retornoDoces;
	if (NATAL2_noel>=3 && NATAL2_bneve==0) goto _maisDoces;
	if (NATAL2_bneve==1) goto _retornoMaisDoces;
	if (QUEST_gorroNatal==1) goto _ok;

//- GORRO DE NATAL DO BONECO DE NEVE ----------------------------------

	mes "[Boneco de Neve]";
	mes "\"Olá Jovem.";
	mes "Você gostaria de um [Gorro de Natal]?\"";
	next;

_menu:
	menu
		"Sim", _sim,
		"Não", -,
		"O que você precisa?", _preciso;
	mes "[Boneco de Neve]";
	mes "\"É uma pena, isso é muito ruim, mas não se esqueça de ter o espírito de Natal!\"";
	close;

_sim:
	mes "[Boneco de Neve]";
	mes "\"Hmm, deixe-me ver o que você tem.\"";
	next;
	if(countitem(@itemBalinha) < 15) goto _semItem;
	if(countitem(@itemChocolate) < 10) goto _semItem;
	if(countitem(@itemBiscoito) < 5) goto _semItem;
	getinventorylist;
	if (@inventorylist_count > 99) goto _cheio;
	delitem 510, 15;
	delitem 509, 10;
	delitem 512, 5;
	mes "[Boneco de Neve]";
	mes "\"Aqui está, desfrute de seu novo gorro!\"";
	getitem 511, 1;
	set QUEST_gorroNatal, 1;
	close;

_preciso:
	mes "[Boneco de Neve]";
	mes "\"Para o gorro especial, eu preciso que você me ajude dando um pouco de sua magia.";
	mes "Brincadeira. Eu só queria mesmo era algumas guloseimas:";
	mes "15 [Balinha]s";
	mes "10 [Barra de Chocolate]s";
	mes "5 [Biscoito de Gengibre]s\"";
	next;
	goto _menu;

_semItem:
	mes "[Boneco de Neve]";
	mes "\"Estou interessado em algumas guloseimas e você não tem nenhuma para obter um gorro.\"";
	close;

_ok:
	mes "[Boneco de Neve]";
	mes "\"Obrigado por me ajudar!\"";
	close;

_cheio:
	mes "[Boneco de Neve]";
	mes "\"Você não tem espaço para o gorro. Volte mais tarde.\"";
	close;

//- NATAL 2010 DO TMW-BR ----------------------------------------------

_procurandoOculos:
	setarray @aventureiro$, "aventureira", "aventureiro";

	mes "[Boneco de Neve]";
	mes "\"Olá "+@aventureiro$[sex]+"! Não gostaria de ajudar os mais necessitados neste natal?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Eh...eu até ajudaria, mas antes eu vim pegar os óculos do Papai Noel, que está atrás de você...\"";
	next;
	mes "[Boneco de Neve]";
	mes "\"Atrás de mim? Onde?\"";
	mes "";
	mes "Neste momento, o boneco de neve tenta olhar para trás, virando constantemente para ver onde estão os óculos. Quando ele está de costas para você, você percebe um rosto com óculos, uma pinha como nariz e uma bengala doce como boca.";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Eh...senhor boneco...\"";
	next;
	mes "Ele fica de frente para você novamente.";
	mes "";
	mes "[Boneco de Neve]";
	mes "\"Oh! Eu não vi nada... E se não vejo, não posso alcançar. Vamos fazer assim, você me traz alguns doces para as criancinhas do mundo Mana e eu procuro os óculos que você supostamente diz que estão atrás de mim.\"";
	next;
	menu
		"Ah, ok então. Trarei os seus doces.", _trareiDoces,
		"Hum, vou pensar no seu caso.", -;
	mes "[Boneco de Neve]";
	mes "\"Tudo bem então. Tenha um feliz natal.\"";
	close;

_trareiDoces:
	mes "[Boneco de Neve]";
	mes "\"Oh! Esplêndido! Estarei esperando pelos doces, vou aproveitar sua ausência e fazer alguns embrulhos para colocar os doces.\"";
	next;
	callsub _missaoDoces;
	close;

_retornoDoces:
	if(countitem(@itemBengala1)<5 || countitem(@itemBengala2)<10) goto _semDoces;
	if(countitem(@itemBalinha)<15 || countitem(@itemChocolate)<20 || countitem(@itemBiscoito)<25) goto _semDoces;
	if(NATAL2_bneveMobs<50) goto _semMonstros;

	mes "[Boneco de Neve]";
	mes "\"Que bom que você voltou! Vejo que trouxe tudo o que eu pedi. Ah! encontrei os óculos! Você estava certo afinal.";
	mes "Toma, aqui está.\"";
	delitem @itemBengala1, 5;
	delitem @itemBengala2, 10;
	delitem @itemBalinha, 15;
	delitem @itemChocolate, 20;
	delitem @itemBiscoito, 25;
	mes "";
	callsub _missaoDocesCompleta;
	next;
	mes "[Boneco de Neve]";
	mes "\"Mas me diga. Como eles vieram parar aqui?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Foram os duendes do papai Noel, que resolveram pregar uma peça.\"";
	next;
	mes "[Boneco de Neve]";
	mes "\"Ah! Danadinhos...Mas isso não importa muito. O importante é que você trouxe os doces, e agora as crianças necessitadas poderão ter um natal mais feliz. Obrigado pela ajuda "+@aventureiro$[sex]+"/"+strcharinfo(0)+".\"";
	close;

_semDoces:
	mes "[Boneco de Neve]";
	mes "\"Que bom que você voltou! Mas vejo que você está sem os doces que eu te pedi.";
	mes "Traga: 5 Doces Bengala Natalina, 10 Doces Bengala, 15 Balinhas, 20 Barras de Chocolate e 25 Biscoitos de Gengibre para que as crianças tenham um natal feliz.\"";
	next;
	callsub _missaoDoces;
	close;

_semMonstros:
	mes "[Missão: Doces para as criancinhas]";
	mes "* Quantidade de monstros insuficiente.";
	next;
	callsub _missaoDoces;
	close;

//- CONTROLE NATAL 2010 DO TMW-BR -------------------------------------

_missaoDoces:
	if(NATAL2_noel!=1) goto _else;
		set NATAL2_noel, 2;
		set NATAL2_bneveMobs, 0;
		set @NATAL2_bneveMobs, 0;
_else:
	mes "[Missão: Doces para as criancinhas]";
	mes "* Monstros: "+NATAL2_bneveMobs+"/50 Limo Neve/Gelo/Rodolfo";
	mes "* Itens: "+countitem(@itemBengala1)+"/5 Doces Bengala Natalina";
	mes "* Itens: "+countitem(@itemBengala2)+"/10 Doces Bengala";
	mes "* Itens: "+countitem(@itemBalinha)+"/15 Balinhas";
	mes "* Itens: "+countitem(@itemChocolate)+"/20 Barras de Chocolate";
	mes "* Itens: "+countitem(@itemBiscoito)+"/25 Biscoitos de Gengibre";
	callfunc "getXpMax", 10000;
	mes "* Recompensa: "+@xp+" XP";
	// Limo de Neve: 122 XP 122*50*2 ~= 10.000 XP
	// Margem Segurança lvl 40: 10.093 XP
	return;

_missaoDocesCompleta:
	callfunc "getXpMax", 10000;
	mes "* Você ganhou "+@xp+" pontos de experiência.";
	message strcharinfo(0), "Ganhei "+@xp+" pontos de experiência.";
	set BaseExp, BaseExp+@xp;
	set NATAL2_noel, 3;
	set NATAL2_bneveMobs, 0;
	return;

//- QUEST MAIS DOCES PARA O BONECO DE NEVE ----------------------------

_maisDoces: //< pede o dobro de itens e dá o dobro de XP.
	mes "[Boneco de Neve]";
	mes "\"O que me diz da ideia de trazer ainda mais doces para alegrar o natal de ainda mais criancinhas?\"";
	next;
	menu
		"É uma ótima ideia.", _trareiMaisDoces,
		"Isso me parece muito difícil.", -,
		"Estou ajudando o Papai Noel.", -;
	mes "[Boneco de Neve]";
	mes "\"Tudo bem então. Tenha um feliz natal.\"";
	close;

_trareiMaisDoces:
	mes "[Boneco de Neve]";
	mes "\"Ótimo! Estarei esperando por mais doces.";
	mes "Vou aproveitar sua ausência e fazer mais embrulhos para colocar os doces.\"";
	next;
	callsub _missaoMaisDoces;
	close;

_retornoMaisDoces:
	if(countitem(@itemBengala1)<10 || countitem(@itemBengala2)<20 || countitem(@itemBalinha)<30) goto _semMaisDoces;
	if(countitem(@itemChocolate)<40 || countitem(@itemBiscoito)<50 || countitem(@itemBoloNatal)<60) goto _semMaisDoces;
	if(NATAL2_bneveMobs<100) goto _semMaisMonstros;
	getinventorylist;
	if(@inventorylist_count == 100) goto _cheioMais;

	mes "[Boneco de Neve]";
	mes "\"Que bom que você voltou! Vejo que trouxe tudo o que eu pedi.";
	mes "Estou tão feliz com a sua ajuda que lhe darei esta lembrancinha para o natal.\"";
	delitem @itemBengala1, 10;
	delitem @itemBengala2, 20;
	delitem @itemBalinha, 30;
	delitem @itemChocolate, 40;
	delitem @itemBiscoito, 50;
	delitem @itemBoloNatal, 60;
	getitem 3192, 1; //< [Globo Boneco de Neve]
	mes "";
	callsub _missaoMaisDocesCompleta;
	next;
	mes "[Boneco de Neve]";
	mes "\"Vejo que você carrega o espírito de natal.";
	mes "Compartilhe deste espírito com todos e verá que terá cada vez mais do espírito de natal.\"";
	close;

_semMaisDoces:
	mes "[Boneco de Neve]";
	mes "\"Que bom que você voltou! Mas vejo que você está sem os doces que eu te pedi.";
	mes "Traga: 10 Doces Bengala Natalina, 20 Doces Bengala, 30 Balinhas, 40 Barras de Chocolate, 50 Biscoitos de Gengibre e 60 Bolos de Natal para que as crianças tenham um natal feliz.\"";
	next;
	callsub _missaoMaisDoces;
	close;

_semMaisMonstros:
	mes "[Missão: Mais doces, mais criancinhas]";
	mes "* Quantidade de monstros insuficiente.";
	next;
	callsub _missaoMaisDoces;
	close;

_cheioMais:
	mes "[Boneco de Neve]";
	mes "\"Você não tem espaço para meu presente. Volte mais tarde.\"";
	close;

//- CONTROLE QUEST MAIS DOCES PARA O BONECO DE NEVE -------------------

_missaoMaisDoces:
	if(NATAL2_bneve!=0) goto _else2;
		set NATAL2_bneve, 1;
		set NATAL2_bneveMobs, 0;
		set @NATAL2_bneveMobs, 0;
_else2:
	mes "[Missão: Mais doces, mais criancinhas]";
	mes "* Monstros: "+NATAL2_bneveMobs+"/100 Limo Neve/Gelo/Rodolfo";
	mes "* Itens: "+countitem(@itemBengala1)+"/10 Doces Bengala Natalina";
	mes "* Itens: "+countitem(@itemBengala2)+"/20 Doces Bengala";
	mes "* Itens: "+countitem(@itemBalinha)+"/30 Balinhas";
	mes "* Itens: "+countitem(@itemChocolate)+"/40 Barras de Chocolate";
	mes "* Itens: "+countitem(@itemBiscoito)+"/50 Biscoitos de Gengibre";
	mes "* Itens: "+countitem(@itemBoloNatal)+"/60 Bolos de Natal";
	callfunc "getXpMax", 10000;
	set @xp, @xp*2;
	mes "* Recompensa: "+@xp+" XP";
	// Limo de Neve: 122 XP 122*100*2 ~= 20.000 XP
	// Margem Segurança lvl 48: 19.373 XP
	return;

_missaoMaisDocesCompleta:
	callfunc "getXpMax", 10000;
	set @xp, @xp*2;
	mes "* Você ganhou "+@xp+" pontos de experiência.";
	message strcharinfo(0), "Ganhei "+@xp+" pontos de experiência.";
	set BaseExp, BaseExp+@xp;
	set NATAL2_bneve, 2;
	set NATAL2_bneveMobs, 0;
	return;

}
