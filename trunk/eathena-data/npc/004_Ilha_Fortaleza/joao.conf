////////////////////////////////////////////////////////////////////////
//  PROGRAMADOR: twisterbk
//  REVISÃO: Alastrim
//  REVISÃO: Diogo_RBG
//  CRIAÇÃO: 01/09/2010 7:39 PM
//  UTILIDADE DO SCRIPT:
//    * Requisitos: 6 Cogumelos Pequenos dropados por Monstros Cogumelo
//    * Premio: Espada Curta
////////////////////////////////////////////////////////////////////////

// Script debug para testar o funcionamento do NPC João.
//004.gat,99,25,0	script	debug	160,{
//	mes "MASK1_TRASGO:"+(QUEST_MASK1&MASK1_TRASGO!=0)+" MASK1_COGUMELO:" +(QUEST_MASK1&MASK1_COGUMELO!=0);
//	menu
//		"Reset", _reset,
//		"Ganhar Itens", _ganhar,
//		"Sair", -;
//	close;
//_reset:
//	set QUEST_cogumelo, 1;
//	set QUEST_MASK1, QUEST_MASK1 | MASK1_TRASGO;    //< seta apenas MASK1_TRASGO como 1.
//	set QUEST_MASK1, QUEST_MASK1 & ~MASK1_COGUMELO; //< seta apenas MASK1_COGUMELO como 0.
//	close;
//_ganhar:
//	getitem 566, 6;
//	close;
//}

004.gat,101,25,0	script	João	160,{

	set @itemCongumeloPequeno, 566;
	set @itemEspadaCurta, 536;
	set @itemChapeuCogumelo, 630;

	if (getequipid(equip_head) == @itemChapeuCogumelo) goto _soco;
	if (QUEST_cogumelo == 1) goto _inicio;
	if (QUEST_cogumelo == 2) goto _retornoCogumelo;
	if (QUEST_MASK1 & MASK1_COGUMELO) goto _obrigado;
	goto _inicio;

_falaPadrao:
	mes "[João]";
	mes "\"Que arrependimento... Quando comprei essa casinha era tudo tão tranquilo...\"";
	next;
	mes "[João]";
	mes "\"Mas agora os cogumelos tomaram conta do território... Mal dá para sair de casa sem ser atacado!\"";
	close;

_inicio:
	mes "[João]";
	mes "\"Malditos Cogumelos... Já cheguei no meu limite! Minha filha não consegue nem ir à escola!";
	mes "E o duro é que eu não posso fazer nada, eles são perigosos demais!\"";
	next;
	menu
		"Eles me parecem meio bobos, o senhor esta realmente com medo?", _razao,
		"Não tenho tempo para estas coisas!.", -;
	close;

_soco:
	mes "[João]";
	mes "\"Malditos Cogumelos! Não deixam a Ilha Fortaleza em paz!\"";
	next;
	mes "João lhe da um soco bem dado. Você consegue até ver passarinhos em volta de ti.";
	heal -250, 0;
	menu
		"Eu heim, esse sujeito é meio louco. Vou sair fora daqui!", -,
		"Opa, o que você está fazendo?!", _porque,
	close;

_porque:
	mes "[João]";
	mes "\"Desculpa, é que estou estressado com esses cogumelos e quando vi o seu chapéu acabei agindo por instinto para me defender.\"";
	close;

_razao:
	mes "[João]";
	mes "\"Bobos? Bobos porque você não passou perto deles ainda!\"";
	mes "\"Se você apenas encostar em um deles todos os outros vem te atacar ao mesmo tempo!\"";
	next;
	menu
		"Você quer que eu te ajude com eles?", _ajuda,
		"É meu senhor, não posso te ajudar.", -;
	close;

_ajuda:
	mes "[João]";
	mes "\"É claro! Tudo o que eu quero é que minha vida volte a ser como era antes.";
	mes "Se você conseguir 6 Cogumelos Pequenos já vai me ajudar bastante.\"";
	mes "";
	callsub _missaoCogumelos;
	next;
	menu
		"Claro, já estou indo", -,
		"Para que o senhor precisa dos cogumelos pequenos?", _cogumelos;
	close;

_cogumelos:
	mes "[João]";
	mes "\"É que se eu mostrar os 6 cogumelos à minha filha ela vai ter mais confiança para ir à escola.";
	mes "Ela vai ficar convencida de que fui eu que dei uma lição neles e que eu posso protegê-la dos cogumelos.\"";
	next;
	mes "[João]";
	mes "\"Ah, e como recompensa eu posso te dar um bom presente!\"";
	close;

_retornoCogumelo:
	mes "[João]";
	mes "\"Você pegou os Cogumelos Pequenos?\"";
	next;
	menu
		"Ainda não, mas não se preocupe eu vou traze-los para você.", -,
		"Claro, aqui estão!", _trocaCogumelo;
	mes "[João]";
	mes "\"Por favor não demore. Estou contando com este favor.\"";
	mes "";
	callsub _missaoCogumelos;
	close;

_trocaCogumelo:
	if (countitem(@itemCongumeloPequeno) < 6) goto _enganou;
	if (MOBS_cogumelo < 40) goto _enganou2;
	getinventorylist;
	if( (@inventorylist_count + 1) >= 100) goto _cheio;
	mes "[João]";
	mes "\"Que bom que você me trouxe os Cogumelos Pequenos, agora eu posso te dar a recompensa. Aceite esta faca que ganhei de meu pai quando tinha 11 anos para você lembrar que foi corajoso o suficiente para derrotar aqueles Cogumelos Malditos.\"";
	mes "";
	delitem @itemCongumeloPequeno, 6;
	getitem @itemEspadaCurta, 1;
	//getitem @itemChapeuCogumelo, 1; //< Como sinal de que a menina perdeu o medo ela mesma poderá dar o chapéu a ele.
	callsub _missaoCogumeloCompleta;
	close;

_enganou:
	mes "[João]";
	mes "\"Você mentiu, me traga os Cogumelos Pequenos e dê logo uma lição naqueles Cogumelos Malditos.\"";
	mes "";
	callsub _missaoCogumelos;
	close;

_enganou2:
	mes "[João]";
	mes "\"Você trouxe os Cogumelos Pequenos, mas não parece ter matado os Cogumelos Malignos.";
	mes "Como irei dar coragem para minha com tantos monstros lá fora?";
	mes  "Dê uma lição naqueles Cogumelos Malditos.\"";
	mes "";
	callsub _missaoCogumelos;
	close;

_cheio:
	mes "[João]";
	mes "\"Você não tem espaço para carregar os meus presentes!\"";
	close;

_obrigado:
	mes "[João]";
	mes "\"Como é bom ver você de novo. Minha vida ainda está difícil, mas minha filha está bem mais confiante!\"";
	menu
		"Não precisava agradecer amigão!",-;
	close;

//- Missão Cogumelos Malignos -----------------------------------------

_missaoCogumelos:
	if(QUEST_cogumelo!=1) goto _else;
		set QUEST_cogumelo, 2;
		set MOBS_cogumelo, 0;
		set @MOBS_cogumelo, 0;
_else:
	mes "[Missão: Cogumelos Malignos]";
	mes "* Monstros: " +MOBS_cogumelo+ "/40 Cogumelo Maligno";
	mes "* Itens: " +countitem(@itemCongumeloPequeno)+ "/6 Cogumelo Pequeno";
	// Cogumelo Maligno: 233XP, 233x40 = 9.320 XP
	// Margem Segurança lvl 40: 10.093 XP
	// RECOMPENSA >> 9.500 XP
	return;

_missaoCogumeloCompleta:
	mes "* Você ganhou 9.500 pontos de experiência.";
	message strcharinfo(0), "Ganhei 9.500 pontos de experiência.";
	set BaseExp, BaseExp+9500; //< Só para lvl>=40

	set QUEST_cogumelo, 0;
	set MOBS_cogumelo, 0;
	set @MOBS_cogumelo, 0;
	set QUEST_MASK1, QUEST_MASK1 | MASK1_COGUMELO;
	return;

}
