// NPC Jack:
// 1) Participação na QUEST do arco da floresta. Ver comentários no NPC Alan para mais detalhes.
// 2) QUEST do Escudo de Madeira. Requisitos: Lvl 30 + 40 tocos de madeira. Prêmio: 1 Escudo de Madeira + 5000 EXP

008.gat,36,69,0	script	debug	141, {
	callfunc "debug";
	menu
		"set 0", -,
		"Matar 40 mobs", -,
		"sair", -;
	if(@menu==1) set QUEST_madeiraResist, 0;
	if(@menu==2) set QUEST_madeiraResistMobs, QUEST_madeiraResistMobs+40;
	close;
}

008.gat,38,69,0	script	Jack	141, {
	set @tocoMadeira, 569;
	set @madeiraResist, 3214;

	if (Baselevel < 30) goto _geral;
	if (QUEST_arcoFloresta > 1 || QUEST_madeiraResist == 2) goto _checagem;
	if (QUEST_arcoFloresta == 1 && QUEST_madeiraResist == 1) goto _voltafloresta;
	if (QUEST_arcoFloresta == 1) goto _introfloresta;

_checagem:
	if (QUEST_escudoMadeira == 1) goto _escudo2;
	if (QUEST_escudoMadeira < 1) goto _escudo0;
	if (QUEST_escudoMadeira > 1) goto _geral;

_geral:
	mes "O lenhador Jack está descansando. Pela quantidade de suor em seu rosto dá para perceber que ele deve ter trabalhado muito... Talvez seja melhor deixar ele descansar em paz.";
	close;

_introfloresta:
	mes "Jack está descansando, aproveita, tira um lenço do bolso e limpa o suor do rosto.";
	next;
	mes "Quando guarda o lenço, jack percebe a sua presença.";
	next;
	mes "[Jack]";
	mes "\"Olá, o que posso fazer por você?\"";
	next;
	menu
		"Me disseram que você conhece bem os tipos de madeira destas florestas.", -;
	next;
	mes "[Jack]";
	mes "\"Ah! Se há alguma coisa nesta vida que eu sei é trabalhar com a madeira. Mas me diga, o que exatamente você procura?\"";
	next;
	menu
		"Eu preciso de um pedaço de madeira bastante resistente.", -;
	next;
	mes "Jack coça a cabeça tentando se lembrar de algo.";
	next;
	mes "[Jack]";
	mes "\"Resistente...resistente... Ah! Tenho exatamente o que você precisa! Porém...\"";
	next;
	menu
		"Porém...?", -;
	mes "[Jack]";
	mes "\"Porém estou cansado. Faz horas que estou cortando lenha. Se você quiser o pedaço de madeira resistente, eu posso trocá-lo por 30 tocos de madeira, que você pode encontrar facilmente nos troncos vivos próximos daqui.\"";
	next;
	menu
		"Fazer o que né? Vamos lá.", -;
	mes "[Jack]";
	mes "\"Ótimo! Estarei aqui descansando um pouco enquanto você vai caçar. Boa sorte amigo!\"";
	next;
	callsub _missaoMadeiraResistente;
	close;

_voltafloresta:
	mes "[Jack]";
	mes "\"Conseguiu os 30 tocos de madeira que te pedi?\"";
	menu
		"Sim, aqui estão eles.", _checagemfloresta,
		"Ainda falta pegar.", -;
	mes "[Jack]";
	mes "\"Pois quando conseguir tudo, pode voltar aqui, que eu trocarei os seus tocos pelo pedaço de madeira que você deseja.\"";
	next;
	callsub _missaoMadeiraResistente;
	close;

_checagemfloresta:
	if (countitem(@tocoMadeira)<30) goto _naotemtocos;
	if (QUEST_madeiraResistMobs<100) goto _naoMatouMobs;
	goto _temtocos;
	close;

_naotemtocos:
	mes "[Jack]";
	mes "\"Parece que você não tem todos os tocos que lhe pedi. Volte quando estiver com os 30 tocos de madeira, ok?\"";
	next;
	callsub _missaoMadeiraResistente;
	close;

_naoMatouMobs:
	mes "É preciso matar pelo menos 100 Troncos Vivos para completar a missão.";
	next;
	callsub _missaoMadeiraResistente;
	close;

_temtocos:
	mes "[Jack]";
	mes "\"Hum, parece que estão todos aqui.\"";
	next;
	mes "Neste momento Jack tira um bloco de madeira debaixo de um pano grosso que cobre uma pilha de vários tipos de toras e aproveita a ocasião para guardar os tocos que você pegou para ele.";
	next;
	mes "[Jack]";
	mes "\"Bem, como promessa é dívida, aqui está o seu pedaço de madeira. Faça bom proveito.\"";
	mes "";
	delitem @tocoMadeira, 30;
	getitem @madeiraResist, 1;
	callsub _missaoMadeiraResistenteCompleta;
	close;

//- Missão Madeira Resistente ------------------------------------------

_missaoMadeiraResistente:
	if(QUEST_madeiraResist!=0) goto _else;
		set QUEST_madeiraResist, 1;
		set QUEST_madeiraResistMobs, 0;
_else:
	mes "[Missão: Madeira resistente]";
	mes "* Itens: " +countitem(@tocoMadeira)+ "/30 Tocos de Madeira";
	mes "* Monstros: " +QUEST_madeiraResistMobs+ "/100 Troncos Vivos";
	mes "* Recompensa: 30.000 XP";
	// Recomendada ao lvl 40, mas só de matar 100 troncos já sai do 30 para beirando os 40.
	// Tronco Vivo: 149 XP, 149x100 ~= 30.000 XP
	// Margem Segurança lvl 45: 15.773 XP
	// Margem Segurança lvl 51: 30.886 XP
	return;

_missaoMadeiraResistenteCompleta:
	mes "* Você ganhou 30.000 pontos de experiência.";
	message strcharinfo(0), "Ganhei 30.000 pontos de experiência.";
	set BaseExp, BaseExp+30000; //< Restrição lvl>=30

	set QUEST_madeiraResist, 0;
	set QUEST_madeiraResistMobs, 0;
	set QUEST_MASK1, QUEST_MASK1 | MASK1_MADEIRA_RESIST;
	return;

//= MISSÃO ESCUDO DE MADEIRA ===========================================

_escudo0:
	mes "[Jack]";
	mes "\"Ora, ora... Vejo que você parece ter muita força! Se você quisesse poderia se dar bem no meu ramo... Para fazer o que eu faço, força é essencial...\"";
	next;
	mes "\"Você me faz lembrar de minha juventude, de como eu passava horas sem parar coletando madeira, lutando contra aqueles troncos vivos!\"";
	next;
	menu
		"Obrigado pelo elogio, mas essa não é bem minha área.", -;
	next;
	mes "[Jack]";
	mes "\"É, eu sei... Já percebi que o que te atrai são as batalhas...\"";
	next;
	mes "\"Hmmm...\"";
	next;
	mes "\"Bem... Já que o nobre ofício dos lenhadores não te atrai, você por acaso gostaria de um resistente escudo de madeira?\"";
	next;
	menu
		"Claro! Porque não?", _escudo1,
		"Não, obrigado", -;
	mes "[Jack]";
	mes "\"Que pena, um escudo seria muito útil para você...\"";
	close;

_escudo1:
	mes "[Jack]";
	mes "\"Ótimo! Eu posso fazer um excelente escudo com apenas 40 tocos de madeira e 5.000 GP para cobrir os gastos... O que me diz?\"";
	menu
		"Parece justo. Vou pegar os materiais para você!", _escudo1a,
		"Isso é trabalho demais para um simples escudo...", -;
	mes "[Jack]";
	mes "\"Se você visse a qualidade do produto não falaria isso... Se você mudar de idéia pode vir falar comigo.\"";
	close;

_escudo1a:
	mes "[Jack]";
	mes "\"Muito bem... Estarei esperando!\"";
	set QUEST_escudoMadeira, 1;
	close;


_escudo2:
	mes "[Jack]";
	mes "\"Trouxe os materiais para fazer o escudo?\"";
	next;
	menu
		"Sim, está tudo aqui.", _checagemescudo,
		"Ainda não...", -;
	mes "[Jack]";
	mes "\"Tudo bem... Coletar madeira é um trabalho que exige paciência.\"";
	close;

_checagemescudo:
	if (countitem(@tocoMadeira)<40) goto _escudo3;
	goto _escudo4;

_escudo3:
	mes "[Jack]";
	mes "\"Você não tem tudo o que te pedi.\"";
	close;

_escudo4:
	mes "[Jack]";
	mes "\"Excelente! Sente-se por favor.\"";
	next;
	mes "Jack serra os tocos em pedaços e os lixa até ficarem macios ao toque.";
	mes "Aplicando um líquido de forte cheiro, ele os deixa com padrões mais escuros.";
	next;
	mes "Ele pega um pedaço e o molda em um formato arredondado, então repete com uma segunda peça que lhe parece ser a empunhadura do escudo.";
	next;
	mes "Enquanto isso o sol secou as outras peças. Jack as coloca juntas, adiciona camadas e prega tudo.";
	mes "O resultado lhe parece um escudo usável, porém Jack adiciona mais um líquido e o deixa secando.";
	next;
	mes "[Jack]";
	mes "\"Finalmente está pronto! Aproveite seu escudo!\"";
	delitem 740, 10;
	getitem 602, 1;
	set QUEST_escudoMadeira, 2;
	set BaseExp, BaseExp+5000;
	message strcharinfo(0), "Você ganhou 5000 pontos de experiência.";
	close;

}
