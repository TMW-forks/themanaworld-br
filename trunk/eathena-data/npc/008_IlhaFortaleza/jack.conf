// NPC Jack:
// 1) Participação na QUEST do arco da floresta. Ver comentários no NPC Alan para mais detalhes.
// 2) QUEST do Escudo de Madeira. Requisitos: Lvl 30 + 40 tocos de madeira. Prêmio: 1 Escudo de Madeira + 39.000 EXP

008.gat,36,69,0	script	debug	141, {
	callfunc "debug";
	mes "MASK1_MADEIRA_RESIST: " + (MASK1 & MASK1_MADEIRA_RESIST);
	mes "QUEST_arcoFloresta: " + QUEST_arcoFloresta;
	mes "QUEST_madeiraResist: " + QUEST_madeiraResist;
	mes "QUEST_escudoMadeira: " + QUEST_escudoMadeira;
	menu
		"set 0", -,
		"Matar 40 mobs", -,
		"set 1 MADEIRA_RESIST", -,
		"sair", -;
	if(@menu==1) set QUEST_madeiraResist, 0;
	if(@menu==2) set QUEST_madeiraResistMobs, QUEST_madeiraResistMobs+40;
	if(@menu==3) set QUEST_MASK1, QUEST_MASK1 | MASK1_MADEIRA_RESIST;
	close;
}

008.gat,38,69,0	script	Jack	141, {
	set @tocoMadeira, 569;
	set @madeiraResist, 3214;
	set @escudoMadeira, 602;

	if (Baselevel < 30 || QUEST_arcoFloresta != 1) goto _geral;
	if (MASK1 & MASK1_MADEIRA_RESIST) goto _quest2; //< Quest Madeira Resistente concluída. Estado 3.

	if (QUEST_madeiraResist == 0) goto _introfloresta;
	if (QUEST_madeiraResist == 1) goto _voltafloresta;
	if (QUEST_madeiraResist == 2) goto _ganhaMadResist;
	goto _geral;

_quest2:
	//- Quest Escudo de Madeira - Fazer Quest Madeira Resistente primeiro.
	if (MASK1 & MASK1_ESCUDO_MADEIRA) goto _geral; //< Quest Escudo de Madeira concluída. Estado 3.

	if (QUEST_escudoMadeira == 0) goto _escudo0;
	if (QUEST_escudoMadeira == 1) goto _escudo2;
	if (QUEST_escudoMadeira == 2) goto _criarEscudo;
	goto _geral;

_geral:
	mes "O lenhador Jack está descansando. Pela quantidade de suor em seu rosto dá para perceber que ele deve ter trabalhado muito... Talvez seja melhor deixar ele descansar em paz.";
	close;

_introfloresta:
	mes "Jack está descansando, aproveita, tira um lenço do bolso e limpa o suor do rosto.";
	next;
	mes "Quando guarda o lenço, jack percebe a sua presença.";
	next;
	mes "[Jack]";
	mes "\"Olá, o que posso fazer por você?\"";
	next;
	menu
		"Me disseram que você conhece bem os tipos de madeira destas florestas.", -;
	next;
	mes "[Jack]";
	mes "\"Ah! Se há alguma coisa nesta vida que eu sei é trabalhar com a madeira. Mas me diga, o que exatamente você procura?\"";
	next;
	menu
		"Eu preciso de um pedaço de madeira bastante resistente.", -;
	next;
	mes "Jack coça a cabeça tentando se lembrar de algo.";
	next;
	mes "[Jack]";
	mes "\"Resistente...resistente... Ah! Tenho exatamente o que você precisa! Porém...\"";
	next;
	menu
		"Porém...?", -;
	mes "[Jack]";
	mes "\"Porém estou cansado. Faz horas que estou cortando lenha. Se você quiser o pedaço de madeira resistente, eu posso trocá-lo por 30 tocos de madeira, que você pode encontrar facilmente nos troncos vivos próximos daqui.\"";
	next;
	menu
		"Fazer o que né? Vamos lá.", -;
	mes "[Jack]";
	mes "\"Ótimo! Estarei aqui descansando um pouco enquanto você vai caçar. Boa sorte amigo!\"";
	next;
	callsub _missaoMadeiraResistente;
	close;

_voltafloresta:
	mes "[Jack]";
	mes "\"Conseguiu os 30 tocos de madeira que te pedi?\"";
	menu
		"Sim, aqui estão eles.", _checagemfloresta,
		"Ainda falta pegar.", -;
	mes "[Jack]";
	mes "\"Pois quando conseguir tudo, pode voltar aqui, que eu trocarei os seus tocos pelo pedaço de madeira que você deseja.\"";
	next;
	callsub _missaoMadeiraResistente;
	close;

_checagemfloresta:
	if (countitem(@tocoMadeira)<30) goto _naotemtocos;
	if (QUEST_madeiraResistMobs<100) goto _naoMatouMobs;
	mes "[Jack]";
	mes "\"Hum, parece que estão todos aqui.\"";
	delitem @tocoMadeira, 30;
	set QUEST_madeiraResist, 2;
	next;
	goto _ganhaMadResist;
	close;

_naotemtocos:
	mes "[Jack]";
	mes "\"Parece que você não tem todos os tocos que lhe pedi. Volte quando estiver com os 30 tocos de madeira, ok?\"";
	next;
	callsub _missaoMadeiraResistente;
	close;

_naoMatouMobs:
	mes "É preciso matar pelo menos 100 Troncos Vivos para completar a missão.";
	next;
	callsub _missaoMadeiraResistente;
	close;

_ganhaMadResist:
	mes "Neste momento Jack tira um bloco de madeira debaixo de um pano grosso que cobre uma pilha de vários tipos de toras e aproveita a ocasião para guardar os tocos que você pegou para ele.";
	next;
	mes "[Jack]";
	mes "\"Bem, como promessa é dívida, aqui está o seu pedaço de madeira. Faça bom proveito.\"";
	mes "";
	getitem @madeiraResist, 1;
	callsub _missaoMadeiraResistenteCompleta;
	close;

//- Missão Madeira Resistente ------------------------------------------

_missaoMadeiraResistente:
	if(QUEST_madeiraResist!=0) goto _else;
		set QUEST_madeiraResist, 1;
		set QUEST_madeiraResistMobs, 0;
_else:
	mes "[Missão: Madeira resistente]";
	mes "* Itens: " +countitem(@tocoMadeira)+ "/30 Tocos de Madeira";
	mes "* Monstros: " +QUEST_madeiraResistMobs+ "/100 Troncos Vivos";
	mes "* Recompensa: 30.000 XP";
	// Recomendada ao lvl 40, mas só de matar 100 troncos já sai do 30 para beirando os 40.
	// Tronco Vivo: 149 XP, 149x100 ~= 30.000 XP
	// Margem Segurança lvl 45: 15.773 XP
	// Margem Segurança lvl 51: 30.886 XP
	return;

_missaoMadeiraResistenteCompleta:
	mes "* Você ganhou 30.000 pontos de experiência.";
	message strcharinfo(0), "Ganhei 30.000 pontos de experiência.";
	set BaseExp, BaseExp+30000; //< Restrição lvl>=30

	set QUEST_madeiraResist, 0;
	set QUEST_madeiraResistMobs, 0;
	set QUEST_MASK1, QUEST_MASK1 | MASK1_MADEIRA_RESIST;
	return;

//= MISSÃO ESCUDO DE MADEIRA ===========================================

_escudo0:
	mes "[Jack]";
	mes "\"Ora, ora... Vejo que você parece ter muita força! Se você quisesse poderia se dar bem no meu ramo... Para fazer o que eu faço, força é essencial...\"";
	next;
	mes "\"Você me faz lembrar de minha juventude, de como eu passava horas sem parar coletando madeira, lutando contra aqueles troncos vivos!\"";
	next;
	menu
		"Obrigado pelo elogio, mas essa não é bem minha área.", -;
	next;
	mes "[Jack]";
	mes "\"É, eu sei... Já percebi que o que te atrai são as batalhas...\"";
	next;
	mes "\"Hmmm...\"";
	next;
	mes "\"Bem... Já que o nobre ofício dos lenhadores não te atrai, você por acaso gostaria de um resistente escudo de madeira?\"";
	next;
	menu
		"Claro! Porque não?", _escudo1,
		"Não, obrigado", -;
	mes "[Jack]";
	mes "\"Que pena, um escudo seria muito útil para você...\"";
	close;

_escudo1:
	mes "[Jack]";
	mes "\"Ótimo! Eu posso fazer um excelente escudo com apenas 40 tocos de madeira e 5.000 GP para cobrir os gastos... O que me diz?\"";
	menu
		"Parece justo. Vou pegar os materiais para você!", _escudo1a,
		"Isso é trabalho demais para um simples escudo...", -;
	mes "[Jack]";
	mes "\"Se você visse a qualidade do produto não falaria isso... Se você mudar de idéia pode vir falar comigo.\"";
	close;

_escudo1a:
	mes "[Jack]";
	mes "\"Muito bem... Estarei esperando!\"";
	mes "";
	callsub _missaoEscudoMadeira;
	close;

_escudo2:
	mes "[Jack]";
	mes "\"Trouxe os materiais para fazer o escudo?\"";
	next;
	menu
		"Sim, está tudo aqui.", _checagemEscudo,
		"Ainda não...", -;
	mes "[Jack]";
	mes "\"Tudo bem... Coletar madeira é um trabalho que exige paciência.\"";
	next;
	callsub _missaoEscudoMadeira;
	close;

_checagemEscudo:
	if (countitem(@tocoMadeira)<40) goto _semMadeira2;
	if (QUEST_escudoMadeira<130) goto _naoMatouMobs2;
	delitem @tocoMadeira, 40;
	set QUEST_escudoMadeira, 2;
	mes "[Jack]";
	mes "\"Excelente! Sente-se por favor.\"";
	next;
	goto _criarEscudo;

_semMadeira2:
	mes "[Jack]";
	mes "\"Você não tem tudo o que te pedi.\"";
	next;
	callsub _missaoEscudoMadeira;
	close;

_naoMatouMobs2:
	mes "É preciso matar pelo menos 130 Troncos Vivos para completar a missão.";
	next;
	callsub _missaoEscudoMadeira;
	close;

_criarEscudo:
	mes "Jack serra os tocos em pedaços e os lixa até ficarem macios ao toque.";
	mes "Aplicando um líquido de forte cheiro, ele os deixa com padrões mais escuros.";
	next;
	mes "Ele pega um pedaço e o molda em um formato arredondado, então repete com uma segunda peça que lhe parece ser a empunhadura do escudo.";
	next;
	mes "Enquanto isso o sol secou as outras peças. Jack as coloca juntas, adiciona camadas e prega tudo.";
	mes "O resultado lhe parece um escudo usável, porém Jack adiciona mais um líquido e o deixa secando.";
	next;
	mes "[Jack]";
	mes "\"Finalmente está pronto! Aproveite seu escudo!\"";
	getitem @escudoMadeira, 1;
	mes "";
	callsub _missaoEscudoMadeiraCompleta;
	close;

//- Missão Escudo de Madeira -------------------------------------------

_missaoEscudoMadeira:
	if(QUEST_escudoMadeira!=0) goto _else2;
		set QUEST_escudoMadeira, 1;
		set QUEST_escudoMadeiraMobs, 0;
_else2:
	mes "[Missão: Madeira resistente]";
	mes "* Itens: " +countitem(@tocoMadeira)+ "/40 Tocos de Madeira";
	mes "* Monstros: " +QUEST_escudoMadeira+ "/130 Troncos Vivos";
	mes "* Recompensa: 39.000 XP";
	// Tronco Vivo: 149 XP, 149x130 ~= 39.000 XP
	// Margem Segurança lvl 45: 15.773 XP
	// Margem Segurança lvl 54: 40.091 XP
	return;

_missaoEscudoMadeiraCompleta:
	mes "* Você ganhou 39.000 pontos de experiência.";
	message strcharinfo(0), "Ganhei 39.000 pontos de experiência.";
	set BaseExp, BaseExp+39000; //< Restrição lvl>=30

	set QUEST_escudoMadeira, 0;
	set QUEST_escudoMadeiraMobs, 0;
	set QUEST_MASK1, QUEST_MASK1 | MASK1_ESCUDO_MADEIRA;
	return;

}
