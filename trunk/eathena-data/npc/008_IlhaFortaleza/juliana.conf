//#################################################################################
//#                                                                               #
//# Este script contém quatro quests todas usando a juliana.                      #
//#                                                                               #
//# Quest1:                                                                       #
//# Requisitos: 30 patas de inseto e lvl 10                                       #
//# Recompensa: arco, 500 flechas normais e Expbase de lvl 10                     #
//#                                                                               #
//# Quest2:                                                                       #
//# Requisitos 10 Ferrões de escorpião e 5 ferrões de escorpião vermelho e lvl 20.#
//# Recompensa: 30 P. energéticas, 10 P. de concentração e Expbase de lvl 20.     #
//#                                                                               #
//# Quest3:                                                                       #
//# Requisitos: 5 pétalas e lv 25                                                 #
//# Recompensa: short bow, 500 flechas de ferro, 1000 GP e Expbase de level 25.   #
//#                                                                               #
//# Quest4:                                                                       #
//# Requisitos: 10 ferrões pretos e lvl 30                                        #
//# Recompensa: Gorro de dormir e Exp Base level 30.	                          #
//#                                                                               #
//#################################################################################

008.gat,45,81,0	script	Juliana	114,{

	if (Sex == 0) set @tioa$, "tia";
	if (Sex == 1) set @tioa$, "tio";

	set @lvl1, 10;
	set @lvl2, 20;
	set @lvl3, 25;
	set @lvl4, 30;
// A base usada para o prêmio em XP é o level mínimo. Assim, quanto mais perto do lvl mínimo for completada a quest, melhor para o jogador.

//#################################################################################
//	mes ".:: Modo Debug ::.";
//	menu
//		"Continuar", -,
//		"Resetar", -,
//		"Patas inseto", _d1,
//		"Ferrão e ferrão vermelho", _d2,
//		"Pétalas", _d3,
//		"Ferrão preto", _d4,
//		"sair",-;
//	if(@menu==1) goto _random;
//	if(@menu==2) set QUEST_juliana, 0;
//	close;
//
//_d1:
//	if (BaseLevel >= 10 && QUEST_juliana==1) goto _arco;
//	set QUEST_juliana, 0;
//	if (BaseLevel < 10) set BaseLevel, 10;
//	goto _1;
//	close;
//_d2:
//	if (BaseLevel >= 20 && QUEST_juliana==3) goto _ferrao1;
//	set QUEST_juliana, 2;
//	if (BaseLevel < 20) set BaseLevel, 20;
//	goto _4;
//	close;
//_d3:
//	if (BaseLevel >= 25 && QUEST_juliana==5) goto _flor2;
//	set QUEST_juliana, 4;
//	if (BaseLevel < 25) set BaseLevel, 25;
//	goto _2;
//	close;
//_d4:
//	if (BaseLevel >= 30 && QUEST_juliana==7) goto _ferrao3;
//	set QUEST_juliana, 6;
//	if (BaseLevel < 30) set BaseLevel, 30;
//	goto _4;
//	close;
//#################################################################################

_random:
//	if (QUEST_chaceler == x && QUEST_juliana == x) goto _chanceler; "parte da quest do king's bow!"
	set @tmp,rand(5);
	if(@tmp==0) goto _1;
	if(@tmp==1) goto _2;
	if(@tmp==2) goto _3;
	if(@tmp==3) goto _4;
	if(@tmp==4) goto _5;

_1:
	if (QUEST_juliana == 0 && BaseLevel >= 10) goto _inicio;
	if (QUEST_juliana == 1) goto _arco;
	if (QUEST_juliana == 2) goto _completoarco;
	mes "Juliana olha com muita concentração para uma pequena quantidade de terra...";
	close;

_inicio:
	mes "[Juliana]";
	mes "\"Este é meu jardim secreto.\"";
	next;
	menu
		"Pode me mostrar alguma coisa dele?", -,
		"Legal, bom para você.", _abortar;
	mes "[Juliana]";
	mes "\"Eu disse que ele é secreto! Se eu lhe mostrar coisas, deixará de ser secreto!\"";
	next;
	menu
		"É que eu quero explorar! Não tem nem uma dica para me ajudar?", -,
		"Isso é egoísmo menininha!", _abortar;
	mes "[Juliana]";
	mes "\"Já sei " + @tioa$ + "! Se você me trouxer 30 patinhas de inseto eu posso dar para ti uma coisa que vai ajudar na exploração!\"";
	set QUEST_juliana, 1;
	close;

_2:
	if (QUEST_juliana == 4 && BaseLevel >= 25) goto _flor;
	if (QUEST_juliana == 5) goto _flor2;
	mes "[Juliana]";
	mes "\"Eu adoro sentir o aroma das flores, mas parece que algumas não são muito amigáveis.\"";
	close;

_3:
	mes "[Juliana]";
	mes "\"Cuide bem da natureza e ela te recompensará em dobro.\"";
	close;

_4:
	if (QUEST_juliana == 2 && BaseLevel >= 20) goto _ferrao;
	if (QUEST_juliana == 3) goto _ferrao1;
	if (QUEST_juliana == 6 && BaseLevel >= 30) goto _ferrao2;
	if (QUEST_juliana == 7) goto _ferrao3;

	mes "[Juliana]";
	mes "Eu adoro colecionar ferrões! Mas eu odeio escorpiões! É uma pena eu não conseguir lutar contra aquelas criaturas venenosas...";
	close;

_5:
	mes "[Juliana]";
	mes "\"A brisa daqui é muito agradável.\"";
	close;

_abortar:
	close;

_arco:
	mes "[Juliana]";
	mes "\"Me lembrei que você prometeu me trazer 30 patinhas de inseto, você está com elas?\"";
	next;
	menu
		"Sim, aqui estão.", _charco,
		"Eu me esqueci de pegar as patas.", _esqueceu;
	close;

_charco:
	if (countitem(518) < 30) goto _esqueceu;
	mes "[Juliana]";
	mes "\"Que bom " + @tioa$ + ", você me trouxe as patinhas!\"";
	getinventorylist;
	if ((@inventorylist_count - (countitem(518) == 30)) > 99 - (countitem(1199) == 0)) goto _semlugar;
	delitem 518, 30;
	getitem 1200, 1;
	getitem 1199, 500;
	set @baseexp, @lvl1;
	callfunc "checaxp";
	set QUEST_juliana, 2;
	next;
	mes "[Juliana]";
	mes "\"Esses, presentes, vão te ajudar a explorar melhor por aí!\"";
	close;

_flor:
	mes "[Juliana]";
	mes "\"Ei, " + @tioa$ + " ? Posso te pedir um outro favor?\"";
	menu
		"Claro! O que está acontecendo?",-,
		"Não. Já te ajudei demais por hoje.", _abortar;

	mes "[Juliana]";
	mes "\"É que as flores do meu jardim secreto começaram a me atacar!\"";
	next;
	mes "[Juliana]";
	mes "\"Na verdade, a maioria das flores que eu vejo parecem explodir de raiva quando alguém tenta tocá-las!\"";
	menu
		"É mesmo!? Você tem alguma idéia do motivo?", -,
		"É uma pena mesmo, mas não tenho nada com isso.", _abortar;

	mes "[Juliana]";
	mes "\"Minha mãe me disse que algum necromante deve ter amaldiçoado as flores. Ela também disse que eles são inimigos de tudo o que é belo.\"";
	next;
	mes "[Juliana]";
	mes "\"Ela entende muito de magia. No momento ela está trabalhando para criar uma poção para acalmar as flores. \"";
	next;
	mes "[Juliana]";
	mes "\"Mas deve demorar muito a ficar pronto... E além do mais, esta muito difícil conseguir o principal ingrediente, as pétalas das flores.\"";
	menu
		"Hmm...  Então o favor era conseguir pétalas?", -,
		"Se acha que vou arriscar minha vida com aqueles monstros, pode desistir!", _abortar;
	mes "[Juliana]";
	mes "\"Sim! Mas que esperteza! Ouvi minha mãe dizer que precisava de 5 pétalas por enquanto.\"";
	mes "\"Você acha que é capaz de me trazer 5 pétalas " + @tioa$ + "?\"";
	menu
		"Claro! Pode ficar tranquila que já volto com as pétalas.", -,
		"Não... ainda estou muito fraco!", _abortar;
	set QUEST_juliana, 5;
	close;

_flor2:

	mes "[Juliana]";
	mes "\" Você já voltou? E as 5 pétalas de flores, você está com elas?\"";
	next;
	menu
		"Sim, aqui estão.", _chflor,
		"Eu me esqueci de pegar as pétalas.", _esqueceu;
	close;

_chflor:
	if (countitem(565) < 5) goto _esqueceu;
	mes "[Juliana]";
	mes "\"Que bom " + @tioa$ + ", você me trouxe as pétalas!\"";
	getinventorylist;
	if ((@inventorylist_count - (countitem(565) == 5)) > 99 - (countitem(529) == 0)) goto _semlugar;
	delitem 565, 5;
	getitem 530, 1;
	getitem 529, 500;
	set zeny, zeny + 1000;
	set @baseexp, @lvl3;
	callfunc "checaxp";
	set QUEST_juliana, 6;
	next;
	mes "[Juliana]";
	mes "\"Esses presentes vão te ajudar a explorar melhor por aí!\"";
	close;

_ferrao:
	mes "[Juliana]";
	mes "\"Uma vez eu vi um Escorpião Vermelho!\"";
	next;
	menu
		"E você ficou com medo?", -,
		"Ah! Medrosa! Eu mato desses a hora que quiser.", _abortar;
	mes "[Juliana]";
	mes "\"Não, mas descobri que poderia ficar com o ferrão dele. Só que eu não consigo lutar contra os escorpiões... Eles são muito fortes!\"";
	next;
	menu
		"Eu não acho que pegar ferrões seja tão difícil assim.", -,
		"Ah, que pena!", _abortar;
	mes "[Juliana]";
	mes "\"Que inveja! Se eu fosse você eu estaria agora juntando uma grande coleção!";
	next;
	mes "Tive uma idéia " + @tioa$ + ", quem sabe você não me consegue 10 ferrões normais e 5 vermelhos?\"";
	mes "Seria um ótimo início para minha própria coleção, e como recompensa eu vou te dar um presente! O que me diz?\"";
	menu
		"Pode ser... vai ser moleza!", -,
		"Eu não tenho tempo para ficar colecionando itens para os outros!", _abortar;
	set QUEST_juliana, 3;
	close;

_ferrao1:
	mes "[Juliana]";
	mes "\"Oi " + @tioa$ + ", lembra que você me prometeu trazer 10 ferrões normais e 5 ferrões vermelhos? Você está com tudo aí?\"";
	next;
	menu
		"Sim, aqui estão.", _chferrao,
		"Eu me esqueci de pegar os ferrões.", _esqueceu;
	close;

_ferrao2:
	mes "[Juliana]";
	mes "Puxa... Você realmente deu uma lição naqueles escorpiões! Estou impressionada!";
	next;
	menu
		"Não foi nada... Estou sempre às ordens para ajudar.",-;
	mes "[Juliana]";
	mes "Será que você não gostaria de me ajudar a aumentar ainda mais a minha coleção de ferrões?";
	next;
	menu
		"Claro, eu vivo matando esses bichos mesmo...",-,
		"Não. Você é muito folgadinha...", _fim;
	mes "[Juliana]";
	mes "É que agora eu precisava de alguns ferrões pretos... Aí sim minha coleção vai ficar uma maravilha!";
	next;
	mes "[Juliana]";
	mes "Por favor, consiga 10 ferrões pretos para mim e eu te darei uma bela recompensa!";
	set QUEST_juliana, 7;
	close;

_ferrao3:
	mes "\"Olá " + @tioa$ + "! Você trouxe os 10 ferrões pretos?\"";
	next;
	menu
		"Sim, aqui estão eles.", _chferrao2,
		"Ainda não.",-;
	close;


_chferrao:
	if (countitem(507) < 10) goto _esqueceu;
	if (countitem(517) < 5) goto _esqueceu;
	mes "[Juliana]";
	mes "\"Maravilha!";
	mes "Você me trouxe todos os ferrões " + @tioa$ + "!\"";
	getinventorylist;
	if ((@inventorylist_count - (countitem(507) == 10) - (countitem(517) == 5)) > 100 - (countitem(568) == 0) - (countitem(567) == 0)) goto _semlugar;
	delitem 507, 10;
	delitem 517, 5;
	getitem 568, 10;
	getitem 567, 30;
	set @baseexp, @lvl2;
	callfunc "checaxp";
	set QUEST_juliana, 4;
	next;
	mes "[Juliana]";
	mes "\"Esses presentes vão te ajudar a ficar mais forte e com mais velocidade!\"";
	close;

_chferrao2:
	if (countitem(709) < 10) goto _esqueceu;
	mes "[Juliana]";
	mes "\"Oba!";
	mes "Você trouxe tudo mesmo!\"";
	getinventorylist;
	if (@inventorylist_count == 100) && (countitem(709) > 10) goto _semlugar;
	delitem 709, 10;
	getitem 628, 1;
	set @baseexp, @lvl4;
	callfunc "checaxp";
	set QUEST_juliana, 8;
	next;
	mes "[Juliana]";
	mes "\"Este gorro de dormir é o que tenho de mais precioso! Você me ajudou tanto que isso é o mínimo que posso fazer por você.\"";
	close;

_esqueceu:
	mes "[Juliana]";
	mes "\"Eu não esqueci da sua promessa, mas parece que você esqueceu, por favor traz para mim o que eu pedi!\"";
	close;

_semlugar:
	next;
	mes "[Juliana]";
	mes "\"Puxa vida, parece que você deixou seu inventário muito cheio, volta depois que esvaziar ele um pouco!\"";
	close;

_completoarco:
	mes "[Juliana]";
	mes "\"E ai " + @tioa$ + ", como anda a exploração do meu jardim secreto? Está aproveitando o arco que te dei?\"";
	close;

_naotrouxe:
	mes "[Juliana]";
	mes "\"Você não trouxe o que eu pedi! Porque faz isso comigo? =( \"";
	close;

_fim:
	close;
}
