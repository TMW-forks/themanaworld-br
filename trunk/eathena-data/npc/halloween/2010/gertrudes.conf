/**
 * Gertrudes, a bruxa da força.
 *
 * @date 17 de outubro de 2010
 * @author Diogo_RBG - http://diogorbg.blogspot.com
 */

008.gat,91,42,0	script	Gertrudes	167,3,2{

	if(@buffStr!=0) goto _continuacao;

	mes "[Gertrudes]";
	mes "\"O que faz aqui se não tem forças para lutar?";
	mes "Abrigue-se em um local seguro enquanto minhas irmãs e eu fazemos o trabalho.\"";
	menu
		"Mas tenho muita força.", -,
		"Não duvide da minha força.", -;
	mes "[Gertrudes]";
	mes "\"Hahah... quanta insolência! És um sujeito de sorte, pois hoje não estou com tempo para insolências.";
	mes "Vou provar que você não sabe o que é a verdadeira força com um pouco do meu poder.\"";
	set @buffStr, $@buffStr;
	next;
	goto _continuacao;

_continuacao:
	mes "[Gertrudes]";
	mes "\"Se queres força, entre no círculo mágico para que possa lhe conceder.";
	mes "Venha quantas vezes quiser... mas lembre-se de que não é minha força que irá impedir sua morte!\"";
	close;

OnTouch:
	if(@buffStr==0 || @buffStr==$@buffStr) end;

	sc_start 37,600, 30;
	//specialeffect2 3;
	set @buffStr, $@buffStr;
	end;

OnTimer20000:
	setnpctimer 0;
	set $@buffStr, $@buffStr+1;
	specialeffect 21;
	end;

OnInit:
	initnpctimer;
	set $@buffStr, 1;
	end;

}
