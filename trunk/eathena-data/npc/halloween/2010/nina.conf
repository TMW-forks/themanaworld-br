/**
 * Quest da menina fantasma - Nina - Halloween 2010.
 *
 * HALLOWEEN2_nina==2: Quando matar o Limo de Gelo.
 *
 * @date 23 de outubro de 2010
 * @author Kamaitachi
 * @programador Alastrim
 */

// Drop da boneca de pano: testado e aprovado.
//010.gat,100,75,0	script	debug	335,{
//	mes "debug";
//	menu
//		"set 0", -,
//		"set 1", -,
//		"set 2", -,
//		"set 3", -,
//		"sair", -;
//	if(@menu<1||@menu>4)
//		close;
//	set HALLOWEEN2_nina, @menu-1;
//	close;
//}

005.gat,147,39,0	script	Nina	335,{

	if ( (HALLOWEEN2_nina==1 || HALLOWEEN2_nina==2) && countitem(3159) <= 0) goto _retornoSem;
	if ( (HALLOWEEN2_nina==1 || HALLOWEEN2_nina==2) && countitem(3159) > 0) goto _retornoCom;
	if (HALLOWEEN2_nina==3) goto _retornoFim;

	mes "A sua frente, está o espírito de uma menina que parece triste com algo.";
	next;
	mes "[Nina]";
	mes "\"Oi, não tenha medo de mim. Não vou machucar você.\"";
	menu
		"Chegar mais perto e arriscar contato.", _contato,
		"Correr desesperadamente de medo.", -;
	close2;
	warp "005.gat", 142, 46;
	message strcharinfo(0), "Nossa! Que susto eu tomei!";
	end;

_contato:
	mes "[Nina]";
	mes "\"Oi, meu nome é Nina, e infelizmente sou o que sobrou de uma garotinha que vivia nesta ilha de fortaleza há muitos anos atrás.\"";
	next;
	mes "[Nina]";
	mes "\"Meu pai era soldado do castelo e tinha me dado de presente de aniversário uma linda bonequinha. Só que houve uma guerra e papai teve que lutar em terras distantes.\"";
	next;
	mes "[Nina]";
	mes "\"Dei a ele a minha bonequinha para que ele não sentisse tantas saudades de mim. Mas infelizmente papai morreu na guerra e eu também morri quando os homens maus vieram à ilha.\"";
	next;
	mes "[Nina]";
	mes "\"Estou triste porque não encontrei o meu pai no outro mundo, mas soube que minha boneca está neste mundo. Se por acaso eu tivesse minha boneca novamente, sentiria menos saudades do meu pai. Você pode me ajudar?\"";
	next;
	menu
		"Ajudar Nina.", _ajuda,
		"Recusar-se ajudar Nina.", -;
	mes "[" + strcharinfo(0) + "]: Desculpe Nina, mas não vou poder te ajudar...";
	mes "[Nina]";
	mes "\"Ah...Você me deixou mais triste. Espero que o Jack O Lanterna venha lhe pegar!\"";
	close;

_ajuda:
	mes "[" + strcharinfo(0) + "]: Eu te ajudo Nina. Onde está sua boneca?";
	next;
	mes "[Nina]";
	mes "\"Ah..Isso eu não sei. Mas papai foi a guerra na cavalaria, dizem que ele lutou bravamente na guerra do leste. Foi ferido em batalha e levaram ele para se curar em um lugar branco e frio.\"";
	next;
	mes "[Nina]";
	mes "\"Lá ele não resistiu e morreu. Só sei disso. Talvez minha boneca esteja onde papai morreu. Procure ela para mim...\"";
	next;
	menu
		"Estou a caminho!", -;
	set HALLOWEEN2_nina, 1;
	close;

_retornoSem:
	mes "[Nina]";
	mes "\"Você ainda não encontrou minha boneca...Procura ela onde a terra é sempre fria e branquinha, foi lá que papai morreu...\"";
	close;

_retornoCom:
	mes "[Nina]";
	mes "\"Oh! Você conseguiu! Estou muito feliz agora!\"";
	next;
	mes "Neste momento a menina fica estática, como se tivesse visto um fantasma";
	next;
	menu
		"Nina? O que está acontecendo? O que há com você?",-;
	mes "Lágrimas escorrem pela face de Nina";
	mes "[Nina]";
	mes "\"Papai falou comigo. Disse que agora tudo estava bem. Me falou que o espírito dele não estava no outro mundo, ele ainda estava nesse, esperando para me encontrar novamente.\"";
	next;
	mes "[Nina]";
	mes "\"Me falou também que o único lugar que ele poderia ficar era na boneca, pois queria ficar perto daquilo que eu mais amava.\"";
	next;
	mes "[Nina]";
	mes "\"Obrigado " + strcharinfo(0) + ", por ter me ajudado! Agora podemos, papai e eu, vivermos felizes na outra vida.\"";
	next;
	if (countitem(3159) <= 0) goto _retornoSem;
	delitem 3159, 1;
	getitem "medalhavelha", 1;
	set HALLOWEEN2_nina, 3;
	close;

_retornoFim:
	mes "Nina olha para você enquanto abraça alegremente a boneca e diz:";
	next;
	mes "[Nina]";
	mes "\"Obrigada por tudo " + strcharinfo(0) + ", agora recuperei minha bonequinha amada e encontrei meu pai.\"";
	close;

}
