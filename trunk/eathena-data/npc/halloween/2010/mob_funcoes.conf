/**
 * Concentração da maior parte dos scripts de Halloween para monstros.
 */

// Script quando encontrar o limo de neve que tem a boneca.
function	script	bonecaLimoGuloso	{
	if(HALLOWEEN2_nina!=1) return;

	getinventorylist;
	if( @inventorylist_count == 100 ) goto _cheio;

	getitem 3159, 1; //- Boneca de Pano
	set HALLOWEEN2_nina, 2;
	message strcharinfo(0), "Encontrei a boneca!";
	return;

_cheio:
	message strcharinfo(0), "Inventório cheio!";
	return;
}

// Script quando matar um matar um espectro em horário de evento.
// Inclui também o script pro placar dos 15+
function	script	dropHitodama2	{
	if(@evento==0) return;

	set @tmp, rand(1000);
	if(@mobID==1337 && @tmp>60) return;
	if(@mobID==1338 && @tmp>65) return;
	if(@mobID==1339 && @tmp>70) return;
	if(@mobID==1340 && @tmp>75) return;
	if(@mobID==1341 && @tmp>80) return;
	if(@mobID==1342 && @tmp>85) return;
	getitem 3157, 1;
	set HALLOWEEN2_item, HALLOWEEN2_item+1;
	message strcharinfo(0), "Aeee!";

	// Se puder entrar no final do placar || ser o primeiro a entrar na lista
	if ($HLW2_vetPlacar[$HLW2_numPlacar-1]<HALLOWEEN2_item && $HLW2_numPlacar!=0) goto _pular1; //< Se não puder: _pula1
	//else
		set $HLW2_nomPlacar$[$HLW2_numPlacar], strcharinfo(0);
		set $HLW2_vetPlacar[$HLW2_numPlacar], HALLOWEEN2_item;
		set $HLW2_lvlPlacar[$HLW2_numPlacar], BaseLevel;
		if ($HLW2_numPlacar<15) set $HLW2_numPlacar, $HLW2_numPlacar+1;
		return;
	_pular1:

	// Buscar nome no placar
	set @tmp, $HLW2_numPlacar; //< O padão é o ultimo do placar
	set @i, -1;
	_loop1: set @i,@i+1; if(@i>=$HLW2_numPlacar) goto _fimLoop1;
		if ($HLW2_nomPlacar$[@i]!=strcharinfo(0)) goto _loop1;
		//else
			set $HLW2_vetPlacar[@i], HALLOWEEN2_item;
			set $HLW2_lvlPlacar[@i], BaseLevel;
			set @tmp, @i;
			goto _fimLoop1;
		//goto _loop1;
	_fimLoop1:

	// Subir no placar
	set @i, @tmp; //< Indice encontrado no loop1
	set @tmp, 0;
	_loop2: set @i,@i-1; if(@i<0) goto _fimLoop2;
		if ($HLW2_vetPlacar[@i]>=HALLOWEEN2_item) set @i, -1;
			if(@i<0) goto _fimLoop2;
		//else
			set @tmp, 1;
			set $HLW2_nomPlacar$[@i+1], $HLW2_nomPlacar$[@i];
			set $HLW2_vetPlacar[@i+1], $HLW2_vetPlacar[@i];
			set $HLW2_lvlPlacar[@i+1], $HLW2_lvlPlacar[@i];
			set $HLW2_nomPlacar$[@i], strcharinfo(0);
			set $HLW2_vetPlacar[@i], HALLOWEEN2_item;
			set $HLW2_lvlPlacar[@i], BaseLevel;
		goto _loop2;
	_fimLoop2:

	if (@tmp==1 && $HLW2_numPlacar<15) set $HLW2_numPlacar, $HLW2_numPlacar+1;
	return;
}

// Script quando matar um espectro pra missão da morte de férias.
function	script	dropHitodama	{
	set @hitodama, getarg(0);

	if (@hitodama == 3151) goto _verde;
	if (@hitodama == 3152) goto _azul;
	if (@hitodama == 3153) goto _amarela;
	if (@hitodama == 3154) goto _vermelha;
	if (@hitodama == 3155) goto _violeta;
	if (@hitodama == 3156) goto _negra;
	return;

_verde:
	if (morteferias_contagem2 >= 200) return;
	getitem @hitodama, 1;
	set morteferias_contagem2, morteferias_contagem2 + 1;
	return;

_azul:
	set @item, Rand(100);
	if (@item > 49) return;
	if (morteferias_contagem2 >= 100) return;
	getitem @hitodama, 1;
	set morteferias_contagem2, morteferias_contagem2 + 1;
	return;

_amarela:
	set @item, Rand(100);
	if (@item > 24) return;
	if (morteferias_contagem2 >= 50) return;
	getitem @hitodama, 1;
	set morteferias_contagem2, morteferias_contagem2 + 1;
	return;

_vermelha:
	set @item, Rand(100);
	if (@item > 11) return;
	if (morteferias_contagem2 >= 25) return;
	getitem @hitodama, 1;
	set morteferias_contagem2, morteferias_contagem2 + 1;
	return;

_violeta:
	set @item, Rand(100);
	if (@item > 0) return;
	if (morteferias_contagem2 >= 15) return;
	getitem @hitodama, 1;
	set morteferias_contagem2, morteferias_contagem2 + 1;
	return;

_negra:
	set @item, Rand(200);
	if (@item > 0) return;
	if (morteferias_contagem2 >= 5) return;
	getitem @hitodama, 1;
	set morteferias_contagem2, morteferias_contagem2 + 1;
	return;

}
